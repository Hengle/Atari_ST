MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	BSR	PREP_ECR
	BSR	PREP_ZOOMS

	SF	FLAG_CONT_ZOOM
;	BSR	PREP_NEXT_ZOOM
	MOVE.W	#$777,$FFFF8242.W

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

IT_VBL	;BSR	VSYNC
	TST.B	FLAG_CONT_ZOOM
	BNE.S	.NO_CREATION
	BSR	PREP_NEXT_ZOOM
.NO_CREATION	BRA	IT_VBL

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

GEST_ZOOM	LEA	COLORS_ZOOMS,A0
	MOVEQ	#0,D0
	MOVE.W	NUMER,D0
	ADD.W	D0,D0
	MOVE.W	(A0,D0.W),COLOR_ACTU
	ADD.W	D0,D0
	LEA	ADR_ZOOMS,A1
	MOVE.L	(A1,D0.W),A1
	LEA	TABLE_COD_CARRêS,A2
	LEA	64*99(A2),A2
	LEA	TABLE_COD_CARRêS_CLIP_1,A5
	LEA	64*99(A5),A5
	LEA	TABLE_COD_CARRêS_CLIP_2,A6
	LEA	64*99(A6),A6
	LSL.L	#4,D0
	SUBA.L	D0,A2
	SUBA.L	D0,A5
	SUBA.L	D0,A6
	MOVEQ	#7,D6
	MOVEQ	#0,D5
.RECOM_AFF_2	MOVE.L	SCREEN2,A4
	ADDA.W	(A1)+,A4	;A1=ADRESSE ECRAN AU Y
	MOVE.W	(A1)+,D7	;NB DE CARRêS SUR LA LIGNE -1
	BLT.S	.CONTINUE
.RECOM_AFF_1	MOVE.L	A4,A0
	MOVEQ	#0,D5
	MOVE.B	(A1)+,D5	;DECALAGE
	ADD.W	D5,D5
	ADD.W	D5,D5
	BTST	#8,D5
	BEQ.S	.NO_1
	ANDI.W	#$FF,D5
	MOVE.L	(A5,D5.W),A3	;ADRESSE DE LA ROUTINE D'AFFICHAGE DU CARRê
	ADDQ.W	#1,A1
	BRA.S	.G
.NO_1	BTST	#9,D5
	BEQ.S	.NO_2
	ANDI.W	#$FF,D5
	MOVE.L	(A6,D5.W),A3	;ADRESSE DE LA ROUTINE D'AFFICHAGE DU CARRê
	ADDQ.W	#1,A1
	MOVE.W	#152,D1
	BRA.S	.H
.NO_2	MOVE.L	(A2,D5.W),A3	;ADRESSE DE LA ROUTINE D'AFFICHAGE DU CARRê
.F	MOVEQ	#0,D1
	MOVE.B	(A1)+,D1	;OFFSET ECRAN
.H	ADDA.W	D1,A0
.G	JSR	(A3)
	DBRA	D7,.RECOM_AFF_1
.CONTINUE	DBRA	D6,.RECOM_AFF_2

	MOVE.W	NUMER,D0
	ADDQ.W	#1,D0
	CMPI.W	#100,D0
	BLT.S	.FUCK
	SF	FLAG_CONT_ZOOM
	MOVEQ	#0,D0
.FUCK	MOVE.W	D0,NUMER
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	MOVEM.L	D0-A6,-(SP)
	MOVE.W	COLOR_ACTU,$FFFF8242.W
	MOVE.L	SCREEN2,A0
	LEA	30*160(A0),A0
	MOVEQ	#0,D0
N	SET	0
	REPT	118
	MOVE.W	D0,N(A0)
	MOVE.W	D0,8+N(A0)
	MOVE.W	D0,16+N(A0)
	MOVE.W	D0,24+N(A0)
	MOVE.W	D0,32+N(A0)
	MOVE.W	D0,40+N(A0)
	MOVE.W	D0,48+N(A0)
	MOVE.W	D0,56+N(A0)
	MOVE.W	D0,64+N(A0)
	MOVE.W	D0,72+N(A0)
	MOVE.W	D0,80+N(A0)
	MOVE.W	D0,88+N(A0)
	MOVE.W	D0,96+N(A0)
	MOVE.W	D0,104+N(A0)
	MOVE.W	D0,112+N(A0)
	MOVE.W	D0,120+N(A0)
	MOVE.W	D0,128+N(A0)
	MOVE.W	D0,136+N(A0)
	MOVE.W	D0,144+N(A0)
	MOVE.W	D0,152+N(A0)
N	SET	N+160
	ENDR
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.G
	MOVE.W	#$300,$FFFF8240.W
.G	CMPI.B	#1,$FFFFFC02.W
	BEQ.S	.FFF+6
	TST.B	FLAG_CONT_ZOOM
	BEQ.S	.FFF
	BSR	GEST_ZOOM
.FFF	BSR	SWAPEC
	CLR.W	$FFFF8240.W
	MOVEM.L	(SP)+,D0-A6
	RTE

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREP_NEXT_ZOOM	MOVE.L	PT_MESS,A0
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0	;TYPE DE FONTE ( 0,1 )
	CMPI.B	#-1,D0
	BNE.S	.NOT_THE_END
	LEA	MESSAGE(PC),A0
	MOVE.B	(A0)+,D0
.NOT_THE_END	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	TYPES_FONTS(PC),A1
	MOVE.L	(A1,D0.W),A1	;ADRESSE DES FONTES
	MOVEQ	#0,D7
	MOVE.B	(A0)+,D7	;D7=NB DE LETTRES
	MOVE.L	D7,D2
	LSL.W	#7,D2	;*128=*8*16
	MOVE.W	#1536,D3	;1536=8*16*12
	SUB.W	D2,D3	;(8*12*16)-(N*16)
	LSR.W	#1,D3	;/2
	SUBI.W	#1536/2,D3	;ON RECENTRE
	;D3=X ORIGINE
	LEA	BUF_ANNEX,A4
	SUBQ.W	#1,D7
	MOVEQ	#-64,D5	;Y0
	CLR.W	ADDIT
	MOVEQ	#7,D6
.NEXT_Y	MOVE.W	D3,D4
	SUB.L	A5,A5
	LEA	4(A4),A3
	MOVE.W	D5,(A4)+	;ON POSE UNE FOIS LE Y
	MOVE.W	D7,D1	;NB DE LETTRES-1
	MOVE.L	A0,-(SP)
.NEXT_LETTRE	MOVE.W	D1,A6
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0	;CODE ASCII DE LA LETTRE
	SUBI.B	#32,D0	;-32
	LSL.W	#3,D0	;*8
	MOVE.L	A1,A2
	ADDA.W	D0,A2	;ADRESSE DE LA FONTE
ADDIT = *+2
	ADDA.W	#$1234,A2
	MOVE.B	(A2)+,D0
	MOVEQ	#7,D1
.RECOM_TST_BIT	BTST	D1,D0
	BEQ.S	.NOTHING
	MOVE.W	D4,(A3)+	;ON POSE LE X
	ADDQ.W	#1,A5
.NOTHING	ADDI.W	#16,D4	;X=X+16
	DBRA	D1,.RECOM_TST_BIT
	MOVE.W	A6,D1
	DBRA	D1,.NEXT_LETTRE
	MOVE.L	A0,PT_MESS
	ADDI.W	#16,D5	;Y=Y+16
	ADDQ.W	#1,ADDIT
	MOVE.L	(SP)+,A0
	SUBQ.W	#1,A5
	MOVE.W	A5,(A4)
	MOVE.L	A3,A4
	DBRA	D6,.NEXT_Y	

	LEA	BUF_ZOOMS,A1
	LEA	COEFF+1600(PC),A2
	LEA	BUF_LONG+198,A3
	LEA	ADR_ZOOMS,A4
	MOVEQ	#99,D6
.RECOM_3	LEA	BUF_ANNEX,A0
	MOVE.L	A1,(A4)+
	MOVE.W	(A2),D0	;COEFF 3D
	MOVEQ	#7,D5
.RECOM_2	MOVE.W	(A0)+,D1	;Y
	ADD.W	D1,D1
	MULS.W	D0,D1
	SWAP	D1
	ADDI.W	#100,D1	;Y ECRAN
	MULU.W	#160,D1
	MOVE.W	D1,(A1)+	;Y*160
	MOVE.W	(A0)+,D2	;(NB DE CARRêS SUR LE LIGNE)-1
	SUBA.L	A6,A6
	MOVE.L	A1,A5
	ADDQ.W	#2,A1
	TST.W	D2
	BLT.S	.CONTINUE
.RECOM_1	MOVE.W	(A3),D1	;LONGUEUR
	MOVEQ	#0,D7
	MOVE.W	(A0)+,D3	;X
	ADD.W	D3,D3
	MULS.W	D0,D3
	SWAP	D3
	ADDI.W	#160,D3	;D3=X ECRAN
	CMPI.W	#320,D3
	BGE.S	.NEXT_PT
	MOVE.W	D3,D4
	ADD.W	D1,D4
;	SUBQ.W	#1,D4
	CMPI.W	#320,D4
	BLT.S	.PAS_CLIP_D
	MOVE.W	#$80,D7	;FLAG CLIP DROIT
.PAS_CLIP_D	TST.W	D3
	BGE.S	.PAS_CLIP_G
	MOVE.W	D3,D4
	ADD.W	D1,D4
	TST.W	D4
	BLT.S	.NEXT_PT
	MOVEQ	#$40,D7	;FLAG CLIP GAUCHE
.PAS_CLIP_G	MOVE.W	D3,D4
	ANDI.W	#15,D3
	OR.B	D7,D3
	MOVE.B	D3,(A1)+	;DECALAGE
	ANDI.W	#$FFF0,D4
	LSR.W	#1,D4
	MOVE.B	D4,(A1)+	;OFFSET ECRAN
	ADDQ.W	#1,A6
.NEXT_PT	DBRA	D2,.RECOM_1
.CONTINUE	SUBQ.W	#1,A6
	MOVE.W	A6,(A5)
	DBRA	D5,.RECOM_2
	LEA	-16(A2),A2
	SUBQ.W	#2,A3
	DBRA	D6,.RECOM_3
	ST	FLAG_CONT_ZOOM
 	RTS

PREP_ZOOMS	LEA	COEFF+16(PC),A0
	LEA	CARRêS(PC),A1
	LEA	BUF_COD_CARRêS,A2
	LEA	TABLE_COD_CARRêS,A3
	LEA	BUF_LONG,A4
	MOVE.W	#16,(A4)+
	MOVEQ	#0,D0
	MOVE.W	#-1,D0
	MOVEQ	#15,D1
	BSR	MAK_COD_CARRê
	MOVE.W	#99-1,D6
.FUCK	MOVEQ	#16*2,D0
	MULU.W	(A0),D0
	SWAP	D0	;LONGUEUR
	MOVE.W	D0,(A4)+
	MOVE.W	D0,D1
	ADD.W	D0,D0
	MOVE.W	(A1,D0.W),D0	;MOTIF
	SWAP	D0
	CLR.W	D0
	SWAP	D0
;	SUBQ.W	#1,D1	;HAUTEUR-1
	BSR	MAK_COD_CARRê
	LEA	16(A0),A0	;TOUT LES 8 Zs
	DBRA	D6,.FUCK

	LEA	COEFF+16(PC),A0
	LEA	CARRêS(PC),A1
	LEA	TABLE_COD_CARRêS_CLIP_1,A3
	MOVEQ	#0,D0
	MOVE.W	#-1,D0
	MOVEQ	#15,D1
	BSR	MAK_COD_CARRê_CLIP_1
	MOVE.W	#99-1,D6
.FUCK2	MOVEQ	#16*2,D0
	MULU.W	(A0),D0
	SWAP	D0	;LONGUEUR
	MOVE.W	D0,D1
	ADD.W	D0,D0
	MOVE.W	(A1,D0.W),D0	;MOTIF
	SWAP	D0
	CLR.W	D0
	SWAP	D0
;	SUBQ.W	#1,D1	;HAUTEUR-1
	BSR	MAK_COD_CARRê_CLIP_1
	LEA	16(A0),A0	;TOUT LES 8 Zs
	DBRA	D6,.FUCK2

	LEA	COEFF+16(PC),A0
	LEA	CARRêS(PC),A1
	LEA	TABLE_COD_CARRêS_CLIP_2,A3
	MOVEQ	#0,D0
	MOVE.W	#-1,D0
	MOVEQ	#15,D1
	BSR	MAK_COD_CARRê_CLIP_2
	MOVE.W	#99-1,D6
.FUCK3	MOVEQ	#16*2,D0
	MULU.W	(A0),D0
	SWAP	D0	;LONGUEUR
	MOVE.W	D0,D1
	ADD.W	D0,D0
	MOVE.W	(A1,D0.W),D0	;MOTIF
	SWAP	D0
	CLR.W	D0
	SWAP	D0
;	SUBQ.W	#1,D1	;HAUTEUR-1
	BSR	MAK_COD_CARRê_CLIP_2
	LEA	16(A0),A0	;TOUT LES 8 Zs
	DBRA	D6,.FUCK3

	MOVE.L	#MESSAGE,PT_MESS
	ST	FLAG_CONT_ZOOM
	MOVE.W	#0,NUMER
	RTS

MAK_COD_CARRê	MOVEQ	#15,D7
.ONLY_ONE	MOVE.L	A2,(A3)+
	MOVE.W	#160,D4
	MOVE.W	D1,D2
	MOVE.W	#$303C,(A2)+	;MOVE.W #$Imm,D0
	MOVE.W	D0,(A2)+
	MOVE.W	#$8150,(A2)+	;OR.W D0,(A0)
	SUBQ.W	#1,D2
.F	MOVE.W	#$8168,(A2)+
	MOVE.W	D4,(A2)+
	ADDI.W	#160,D4
	DBRA	D2,.F
	MOVE.W	#$4E75,(A2)+
	SUBQ.W	#1,D7
.RECOM_DEC	ROR.L	#1,D0
	SWAP	D0
	MOVE.W	D0,D3
	SWAP	D0
	TST.W	D3
	B