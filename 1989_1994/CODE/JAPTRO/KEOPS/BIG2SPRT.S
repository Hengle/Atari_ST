	CLR.L	-(SP)
	MOVE	#$20,-(SP)
	TRAP	#1
	ADDQ	#6,SP
	
	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	BSR	PREP_ECR

	MOVEM.L	IMG+2,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	
	BSR	CODE_MOI
	MOVE	#$101,$FFFF8240.W

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBL_SPR,$70.W
	MOVE.W	#$2300,SR

IT_VBL	BSR	VSYNC
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NOTI
	ST	$FFFF8240.W
.NOTI	**********
	MOVE.L	PT_REBOND_MOI,A4
	MOVE.W	(A4)+,D5	;Y*160
	SUBI.W	#30*160,D5
	CMPI.W	#$1234,(A4)
	BNE.S	.FUCK_TOUT
	LEA	REBOND_MOI,A4
.FUCK_TOUT	MOVE.L	A4,PT_REBOND_MOI

	MOVE.L	PT_MOUVEMENT,A5
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVE.B	(A5)+,D0	;DECALAGE(0-->3)
	MOVE.B	(A5)+,D1	;OFFSET ECRAN
	MOVE.B	(A5)+,D2	;NB DE COLONNES A AFFICHER
	MOVE.B	(A5)+,D3	;MODE : 0=JSR CODE, ON AUTOMODIFIE UN RTS
			;       1=JSR CODE+NB_COLONNES*X
	BEQ.S	.MODE_0
.MODE_1	EXT.W	D1
	ASL.W	#3,D1
	LEA	BUF,A0
	MULU.W	#16000,D0
	ADDA.L	D0,A0
	MOVE.L	SCREEN2,A1
	ADDA.W	D1,A1
	ADDA.W	D5,A1
	LEA	CODE,A2
	MULU.W	#1200,D2
	ADDA.W	D2,A2
	JSR	(A2)
	BRA.S	.LET_US_GO
.MODE_0	LEA	BUF,A0
	MULU.W	#16000,D0
	ADDA.L	D0,A0	;ADRESSE SOURCE
	MOVE.L	SCREEN2,A1
	ADDA.W	D1,A1	;ADRESSE DESTINATION
	ADDA.W	D5,A1
	LEA	CODE,A2
	MULU.W	#1200,D2
	MOVE.W	(A2,D2.W),MODIF
	MOVE.W	#$4E75,(A2,D2.W)
	JSR	(A2)
MODIF = *+2
	MOVE.W	#$1234,(A2,D2.W)

.LET_US_GO	CMPI.B	#-1,(A5)
	BNE.S	.FUCK
	LEA	MOUVEMENT,A5
.FUCK	MOVE.L	A5,PT_MOUVEMENT
	MOVE.L	SCREEN2,A0
	ADDA.W	D5,A0
	LEA	200*160(A0),A0
	MOVEQ	#5,D7
	MOVEQ	#0,D6
.EFF	MOVE.L	D6,(A0)
	MOVE.L	D6,8(A0)
	MOVE.L	D6,16(A0)
	MOVE.L	D6,24(A0)
	MOVE.L	D6,32(A0)
	MOVE.L	D6,40(A0)
	MOVE.L	D6,48(A0)
	MOVE.L	D6,56(A0)
	MOVE.L	D6,64(A0)
	MOVE.L	D6,72(A0)
	MOVE.L	D6,80(A0)
	MOVE.L	D6,88(A0)
	MOVE.L	D6,96(A0)
	MOVE.L	D6,104(A0)
	MOVE.L	D6,112(A0)
	MOVE.L	D6,120(A0)
	MOVE.L	D6,128(A0)
	MOVE.L	D6,136(A0)
	MOVE.L	D6,144(A0)
	MOVE.L	D6,152(A0)
	LEA	160(A0),A0
	DBRA	D7,.EFF
	**********
	BSR	SWAPEC
	MOVE.W	#$101,$FFFF8240.W
	BRA	IT_VBL

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBL_SPR	ADDQ.W	#1,NB_VBL
	RTE

SWAPEC	MOVE.L	SCREEN1,D7
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D7,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREP_ECR	MOVE.L	#BUFFER+5120,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADD.L	#32000+5120,D0
	MOVE.L	D0,SCREEN2
	RTS

CODE_MOI	LEA	IMG+34,A0
	LEA	CODE,A1
	MOVE	#3999,D0
AF	MOVE.L	(A0),(A1)+
	ADDQ	#8,A0
	DBF	D0,AF

	LEA	BUF,A6
	MOVE	#4-1,D7
AGAIN	LEA	CODE,A5
	MOVE	#3999,D6
COP	MOVE.L	(A5)+,(A6)+
	DBF	D6,COP
	LEA	CODE,A0
	JSR	DêCALE_BIG_SPRITE
	LEA	CODE,A0
	JSR	DêCALE_BIG_SPRITE
	LEA	CODE,A0
	JSR	DêCALE_BIG_SPRITE
	LEA	CODE,A0
	JSR	DêCALE_BIG_SPRITE
	LEA	CODE,A0
	CLR.L	(A0)+
	CLR.L	(A0)+
	DBF	D7,AGAIN

	LEA	CODE,A0
	MOVEQ	#0,D2
	MOVEQ	#0,D4
	MOVE	#20-1,D3
DO2	MOVE.L	D2,D0
	MOVE.L	D4,D5
	MOVE	#199,D1
DO	MOVE	#$2368,(A0)+
	MOVE	D5,(A0)+
	MOVE	D0,(A0)+
	ADD	#160,D0
	ADD	#80,D5
	DBF	D1,DO
	ADDQ	#8,D2
	ADDQ	#4,D4
	DBF	D3,DO2
	MOVE	#$4E75,(A0)
	MOVE.L	#MOUVEMENT,PT_MOUVEMENT
	MOVE.L	#REBOND_MOI,PT_REBOND_MOI
	RTS

DêCALE_BIG_SPRITE
	MOVE	#200-1,D1
	MOVE.L	A0,A1
.Z
	MOVE	#2-1,D3
.E
	MOVE.L	A1,A0
N	SET	0
	REPT	20
	ROXR	N(A0)
N	SET	N+4
	ENDR
	ADDQ	#2,A1
	DBF	D3,.E
	LEA	80-2*2(A1),A1
	DBF	D1,.Z	
	RTS

MOUVEMENT	
N	SET	152
	REPT	19
	DC.B	3,N,(160-N)/8,0
	DC.B	2,N,(160-N)/8,0
	DC.B	1,N,(160-N)/8,0
	DC.B	0,N,(160-N)/8,0
N	SET	N-8
	ENDR
	REPT	20
	DC.B	3,N/8,-N/8,1
	DC.B	2,N/8,-N/8,1
	DC.B	1,N/8,-N/8,1
	DC.B	0,N/8,-N/8,1
N	SET	N-8
	ENDR
	DC.B	-1
	EVEN

REBOND_MOI	INCBIN	REBONDME.CRB
	DC.W	$1234

IMG	INCBIN	ZAPPY.PI1
	BSS
NB_VBL	DS.W	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.B	64000+160*32*3
PT_MOUVEMENT	DS.L	1
PT_REBOND_MOI	DS.L	1
BUF	DS.B	16000*4
CODE	DS.B	6*4000+2