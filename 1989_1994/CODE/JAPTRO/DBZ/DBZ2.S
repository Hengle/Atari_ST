DEBUT:	
	LEA.L 	D_PILE,A7
	PEA	0.W
	MOVE 	#$20,-(SP)
	TRAP 	#1
	ADDQ.L 	#6,SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W

	MOVE.W	#$2700,SR
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE.W	#$8800,A0
	MOVEQ	#$A,D0
SET_YM1	MOVE.B	D1,(A0)	;ON REMET LES 10 REGISTRES SONORES A ZERO
	CLR.B	2(A0)
	ADDQ.W	#1,D1
	DBRA	D0,SET_YM1

	MOVE.B	#7,(A0)
	MOVE.B	#$FF,2(A0)
	MOVE.L	#BEAT,BEAT1

	LEA	BEAT,A0
	;MOVE.W	#((TEST_FIN-BEAT)/2)-1,D7
.CONT	MOVE.B	(A0),D0
	ADDI.B	#$80,D0
	MOVE.B	D0,(A0)+
	MOVE.B	(A0),D0
	ADDI.B	#$80,D0
	MOVE.B	D0,(A0)+
	CMP.L	#FINI,A0
	BNE.S	.CONT
	;DBRA	D7,.CONT

	LEA	BEAT,A0
	LEA	TEST_FIN,A1
	MOVE.W	#255,D7
.A	MOVE.B	(A0)+,(A1)+
	DBRA	D7,.A
	MOVE.W	#$2300,SR
	
	MOVE.L	#INTER_RTE,$68.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W

	CLR.W 	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE 	#5,-(SP)
	TRAP 	#14
	LEA.L 	12(SP),SP

	move.l	#BUFFERSCR,d0
	CLR.B	D0
	move.l	d0,SCREEN1		
	add.l	#64000,d0		
	move.l	d0,SCREEN2

	move.l	SCREEN1,d0	
	lsr.l	#8,d0		
	move.l	#$ffff8201,a0
	movep	d0,(a0)

******
	LEA	PART1+34,A0
	MOVE.L	SCREEN1,A1
	LEA	160(A1),A1
	MOVE	#127,D7
AFF_Y	MOVE	#(112/4)-1,D6
AFF_X	MOVE.L	(A0)+,(A1)+
	DBF	D6,AFF_X
	LEA	48(A0),A0
	LEA	70+48(A1),A1
	DBF	D7,AFF_Y
******
	LEA	PART2+34,A0
	MOVE.L	SCREEN1,A1
	LEA	160+112(A1),A1
	MOVE	#127,D7
AFF_Y2	MOVE	#(112/4)-1,D6
AFF_X2	MOVE.L	(A0)+,(A1)+
	DBF	D6,AFF_X2
	LEA	48(A0),A0
	LEA	70+48(A1),A1
	DBF	D7,AFF_Y2
******
	LEA	PART3+34,A0
	MOVE.L	SCREEN1,A1
	LEA	160(A1),A1
	LEA	230*100(A1),A1
	LEA	230*28(A1),A1
	MOVE	#127,D7
AFF_Y3	MOVE	#(112/4)-1,D6
AFF_X3	MOVE.L	(A0)+,(A1)+
	DBF	D6,AFF_X3
	LEA	48(A0),A0
	LEA	70+48(A1),A1
	DBF	D7,AFF_Y3
******
	LEA	PART4+34,A0
	MOVE.L	SCREEN1,A1
	LEA	160+112(A1),A1
	LEA	230*100(A1),A1
	LEA	230*28(A1),A1
	MOVE	#127,D7
AFF_Y4	MOVE	#(112/4)-1,D6
AFF_X4	MOVE.L	(A0)+,(A1)+
	DBF	D6,AFF_X4
	LEA	48(A0),A0
	LEA	70+48(A1),A1
	DBF	D7,AFF_Y4
******
	MOVE.L	BEAT1,A0
	MOVE.L	#VBLR_IR,$70.W
	MOVE.L	#TB0_IR,$120.W
	
	MOVE.B	#$12,$FFFFFC02.W
	
	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W

BOUCLE	BRA.S	BOUCLE
VBLR_IR
	CLR.B	$FFFFFA1B.W
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W
	MOVEM.L	PART1+2,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.B	#0,$FFFFFA19.W
	MOVE.B	#99,$FFFFFA1F.W		; BORDER HAUT
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W

	CMPA.L	#TEST_FIN,A0
	BLE.S	.NO
	LEA	BEAT,A0
.NO	MOVE.W	#$8800,A1
	;LEA	$FFFF8800.W,A1
	LEA	CODE,A2
	REPT	32
	JSR	PLAY
	DCB	128-29-7,$4E71
	ENDR
INTER_RTE	RTE

INTER_TMA:	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE	#$2100,SR
full
	LEA	$FFFF8209.W,A3
	LEA	$FFFF8260.W,A6
	LEA	$FFFF820A.W,A5

	MOVEQ	#0,D0
	MOVEQ	#2,D1

	STOP	#$2100

	MOVE	#$2700,SR
	MOVE	#$2300,(SP)

	MOVEQ	#29,D2
SYNCHROA:	DBF	D2,SYNCHROA
	NOP

	MOVE.B	D0,(A5)
	REPT	6
	NOP
	ENDR
	MOVE.B	D1,(A5)

*******
	LEA	$FFFF8209.W,A6
	LEA	$FFFF8260.W,A4
	LEA	$FFFF820A.W,A3
	LEA	DEGRADE,A5
	MOVEQ	#0,D1		
	MOVEQ	#$10,D6
	MOVE.L	#(226/2)-1,D0

SYNCHRO	MOVE.B	(A6),D7
	BEQ.S	SYNCHRO
	SUB.W	D7,D6		
	LSL.W	D6,D1	
	JSR	PLAY	
	DCB	97-4-29-7,$4E71
	MOVE	(A5)+,$FFFF8252.W
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	JSR	PLAY
	DCB	88-29-7,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-1,$4E71
	
FULLSCREEN	NOP
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	JSR	PLAY
;	DCB	29+7,$4E71
	DCB	88-29-7,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71
	MOVE	(A5)+,$FFFF8252.W
*
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	JSR	PLAY
	DCB	88-29-7,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71

	DBF	D0,FULLSCREEN

	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	JSR	PLAY
	DCB	88-29-7,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	;	ICI=116	NOPS

	CLR.B	$FFFF820A.W 
	DCB	8,$4E71

	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE	A4,(A3)
	JSR	PLAY
	DCB	88-2-29-7,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12,$4E71
	
	REPT	48
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	JSR	PLAY
	DCB	88-29-7,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12,$4E71

	ENDR
SUITE
;OKAY	move.l	SCREEN1,d0
;	move.l	SCREEN2,SCREEN1		
;	move.l	d0,SCREEN2

	move.l	SCREEN1,d0
	ADD.L	D1,D0
	lsr.l	#8,d0
	LEA	$FFFF8201.W,A4
	movep	d0,(a4)

	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
Y_VALUE	DC.L	0

TB0_IR	RTE

PLAY
	MOVEQ	#0,D5	;1
	MOVE.B	(A0)+,D5	;2
	LSL.W	#3,D5	;3
	MOVE.L	0(A2,D5.W),D6	;5
	MOVE.W	4(A2,D5.W),D7	;4
	MOVEP.L	D6,(A1)	;6
	MOVEP.W	D7,(A1)	;4     ;=25 NOPS
;	dcb	4,$4e71
;	dcb	6,$4e71
	RTS	;4    ;=29 NOPS

FIN:	MOVE.L	4.W,A0
	JMP	(A0)

	DATA
PART1	INCBIN	COL1.PI1
PART2	INCBIN	COL2.PI1
PART3	INCBIN	COL3.PI1
PART4	INCBIN	COL4.PI1

DEGRADE	DC	$010,$010,$020,$020,$030,$030,$040,$040,$050,$050,$060,$060,$070,$070
	DC	$071,$071,$072,$072,$073,$073,$074,$074,$075,$075,$076,$076,$077,$077
	DC	$177,$177,$277,$277,$377,$377,$477,$477,$577,$577,$677,$677,$777,$777
	DCB	7,$777
	DC	$101
	DC	$101,$202,$202,$303,$303,$404,$404
	DCB	4,$403
	DC	$303,$203,$103,$102,$101,$001
	DC	$101,$102,$103,$203,$303
	DCB	30,$403
	DC	$404,$405,$406,$407,$406,$405,$404
	DCB	256,$403
	
BEAT1	DS.L	1
BEAT	INCBIN	B:\CHA_LA.SPL
TEST_FIN	DCB.B	256,0
FINI
CODE	INCBIN	TABLEREP.BIN

 	SECTION BSS

F_PILE	DS.L 	128	
D_PILE	DS.L 	1	
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFERSCR	DS.B	64000
			