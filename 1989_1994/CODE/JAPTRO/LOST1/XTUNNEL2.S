NB_CERCLES = 60
	;PENSER A VIRER LA TABLE D'ADRESSE POUR LE CODE

BORD_GAUCHE	MACRO
	MOVE.W	A5,(A5)	;$8260
	MOVE.B	#0,(A5)
	ENDM

BORD_DROIT	MACRO
	MOVE.B	#0,(A6)	;$820A
	MOVE.W	A5,(A6)
	ENDM

STAB	MACRO
	MOVE.W	A5,(A5)	;$8260
	MOVE.B	#0,(A5)
	ENDM

NP	MACRO
	REPT	\1
	NOP
	ENDR
	ENDM

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

;	MOVEQ	#1,D0
;	JSR	ZIK
	;MOVE.L	A0,PLAY_ZIK
	;MOVE.L	A0,PLAY_ZIK2
	;MOVE.L	A0,PLAY_ZIK3
	;MOVE.L	A1,GOTO_FIN

	LEA	DEB_BSS,A0
.CONT_FUN	CMPA.L	#FIN_BSS,A0
	BGE.S	.FIN_FUN
	CLR.W	(A0)+
	BRA.S	.CONT_FUN
.FIN_FUN

	MOVE.L	#$01010101,$FFFF8240.W
	JSR	PREP_ECR
	LEA	BUFFER2,A0
	LEA	MSG_SPE,A1
	MOVE	#(24002/2)-1,D0
.DSQ	MOVE	(A1)+,(A0)+
	DBF	D0,.DSQ
	MOVEQ	#0,D1
	LEA	MSG_SPE,A1
	MOVE	#(24002/2)-1,D0
.DSQA	MOVE	D1,(A1)+
	DBF	D0,.DSQA

	MOVE.L	#VBL_INIT,$70.W
	BSR	PREPARE_ALL
	BSR	PROCEDURE_CON

;	MOVE.L	SCREEN1,A0
;	MOVE.L	SCREEN2,A1
;	MOVE.L	#$AAAAAAAA,D0
;	MOVE.L	#15999,D7
;.FILL	MOVE.L	D0,(A0)+
;	MOVE.L	D0,(A1)+
;	DBRA	D7,.FILL

BARRAGE	BRA.S	BARRAGE
	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	MOVEQ	#0,D0
	MOVE	#7999,D1
KILLIT	MOVE.L	D0,(A0)+
	MOVE.L	D0,(A1)+
	DBF	D1,KILLIT

	MOVE.W	#$2700,SR
	MOVE.L	#INTER_RTE,$68.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

BOUCLE
JMP	BRA.S	JMP
GOTO_FIN = *+2
	JMP	$12345678

ROUTY	LEA	MSG,A0
MODDEP	EQU	*+2
	ADDA.L	#0,A0
	CMPI	#$9999,(A0)
	BNE.S	.Z
	MOVE.L	#VBL_UNE,$70.W
	RTS
.Z	MOVE.L	SCREEN1,A1
	MOVE	#200-1,D0
AFY2	MOVE	#20-1,D1
AFX2	MOVE	(A0)+,(A1)
	ADDQ	#8,A1
	DBF	D1,AFX2
	DBF	D0,AFY2
	RTS

TIME	DC	0
VBL_UNE	MOVE	#$4E71,BARRAGE
	MOVEM.L	D0-A6,-(SP)
PLAY_ZIK3	EQU	*+2
	;JSR	PLAY_ZIK3
	MOVEM.L	(SP)+,D0-A6
	RTE
VBL_INIT
	MOVEM.L	D0-A6,-(SP)
PLAY_ZIK2	EQU	*+2
	;JSR	PLAY_ZIK2
	TST	TIME
	BNE.S	.GO
	JSR	ROUTY
.GO	ADDQ	#1,TIME
	LEA	FAD_END,A6
MODFDEND	EQU	*+2
	LEA	0(A6),A6
	CMPI	#-1,(A6)
	BEQ.S	.OUTE
	SUBQ	#1,BAZOUNGA
	BNE.S	.OUT
	MOVE	#4,BAZOUNGA
	ADDQ	#2,MODFDEND
	MOVE	(A6),$FFFF8242.W
.OUT	BRA	FINVBLINIT
.OUTE	CLR	TIME
	CLR	MODFDEND
	ADD.L	#8000,MODDEP
	BRA	FINVBLINIT
BAZOUNGA	DC	4
FAD_END	DC	$101,$111,$222,$333,$444,$555,$666
	DCB	12,$777
	DC	$777,$667,$556,$445,$334,$223,$112,$101,-1

FINVBLINIT	MOVEM.L	(SP)+,D0-A6
	RTE
VBLR	MOVE.B	#0,$FFFFFA19.W
	MOVE.B	#99,$FFFFFA1F.W       ; BORDER HAUT
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W
	;ICI 33 LIGNES DE LIBRES
	MOVE.W	#$777,$FFFF8242.W
	CLR.L	$FFFF8244.W
	MOVE.L	#$4440444,$FFFF8248.W
	MOVE.L	#$4440444,$FFFF824C.W
	CLR.L	$FFFF8250.W
	CLR.L	$FFFF8254.W
	CLR.L	$FFFF8258.W
	CLR.L	$FFFF825C.W

CODE_1 = *+2
	LEA	$12345678,A0
CODE_2 = *+2
	LEA	$12345678,A2
CODE_3 = *+2
	LEA	$12345678,A3
	LEA	CORES_Y+200*2+135*2,A4
CODE_5 = *+2
	MOVE.W	#$1234,D0	ECART_X
	EXT.L	D0
CODE_6 = *+2
	MOVE.W	#$1234,D1	ECART_Y
	EXT.L	D1
	MOVE.L	SCREEN2,A6
	LEA	160+230+230*135(A6),A6
CODE_4 = *+2
	MOVE.W	#$1234,D5
	LEA	CORES_X+28*2*16,A1
	MOVEQ	#56-1-7-(NB_CERCLES-56),D7
	************
.RECOM_CALC	MOVE.L	D0,D2
	ADD.W	(A2),D2	X(BN+I*3)+ECART_X
	MOVE.L	D1,D3
	ADD.W	2(A2),D3	Y(BN+I*3)+ECART_Y
	;D2 ET D3 SONT LES COORDONNêES DU CENTRE DU CERCLE A AFFICHER
	SUBI.W	#135,D3
	MOVE.L	D2,D4
	ANDI.W	#15,D2
	ADD.W	D2,D2
	ADD.W	D2,D2
	MOVE.L	(A3,D2.W),(A0)+
	MULS.W	#230,D3
	MOVE.L	A6,A5
	ADDA.W	D5,A5
	EORI.W	#4,D5
	ADD.W	D4,D4
	MOVE.W	(A1,D4.W),D4
	EXT.L	D4
	ADD.L	D3,D4
	ADDA.L	D4,A5
	LEA	137*230(A5),A5
	MOVE.L	A5,(A0)+
	SUBA.L	#135*230*2,A5
	MOVE.L	A5,(A0)+
	LEA	4*16*3(A3),A3
	LEA	12(A2),A2
	DBRA	D7,.RECOM_CALC
	**********
	MOVE.L	#SUSPEND_AFF,(A0)+
	CLR.L	(A0)+
	CLR.L	(A0)+
	MOVEQ	#2*(NB_CERCLES-56)-1,D7
	************
.RECOM_CALC2	MOVE.L	D0,D2
	ADD.W	(A2),D2	X(BN+I*3)+ECART_X
	MOVE.L	D1,D3
	ADD.W	2(A2),D3	Y(BN+I*3)+ECART_Y
	;D2 ET D3 SONT LES COORDONNêES DU CENTRE DU CERCLE A AFFICHER
	SUBI.W	#135,D3
	MOVE.L	D2,D4
	ANDI.W	#15,D2
	ADD.W	D2,D2
	ADD.W	D2,D2
	MOVE.L	(A3,D2.W),(A0)+
	MULS.W	#230,D3
	MOVE.L	A6,A5
	ADDA.W	D5,A5
	EORI.W	#4,D5
	ADD.W	D4,D4
	MOVE.W	(A1,D4.W),D4
	EXT.L	D4
	ADD.L	D3,D4
	ADDA.L	D4,A5
	LEA	137*230(A5),A5
	MOVE.L	A5,(A0)+
	SUBA.L	#135*230*2,A5
	MOVE.L	A5,(A0)+
	LEA	4*16*3(A3),A3
	LEA	12(A2),A2
	DBRA	D7,.RECOM_CALC2
	**********
	MOVE.L	#ENDIT_AFF,(A0)+
	TST.W	Z_DEP
	BNE.S	.FUCK
	ADDI.W	#3,BN
.FUCK	SUBQ.W	#1,Z_DEP
	BGE.S	.FUCK_2
	EORI.W	#4,COLOR
	ADDI.W	#3,Z_DEP
.FUCK_2	ADDQ.W	#1,VAL_DEB
INTER_RTE	RTE

INTER_TMA:	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE	#$2100,SR
full
	MOVEQ	#16,D0
	LEA	$FFFF8209.W,A0
	LEA	$FFFF820A.W,A6
	LEA	$FFFF8260.W,A3

	MOVEQ	#2,D3
	MOVEQ	#0,D4

	STOP	#$2100

	MOVE	#$2700,SR
	MOVE	#$2300,(SP)

	MOVEQ	#29,D2
SYNCHROA:	DBF	D2,SYNCHROA
	NOP

	MOVE.B	D4,(A6)	;820A
	REPT	6
	NOP
	ENDR
	MOVE.B	D3,(A6)	;820A

	;SYNCHRO ICI...
.SYNCHRO	MOVE.B	(A0),D2
	BEQ.S	.SYNCHRO
	SUB.B	D2,D0
	LSL.W	D0,D0

	DCB.W	97-25-3,$4E71
	MOVEQ	#1,D0	1
	MOVEQ	#2,D1	1
	MOVEQ	#4,D2	1
	MOVEQ	#8,D3	1
	MOVEQ	#$10,D4	1
	MOVEQ	#$20,D5	1	
	MOVEQ	#$40,D6	1
	MOVEQ	#$80,D7	1
	MOVE.L	PT_ZONE_EFF,A0	5
	MOVE.L	(A0)+,A1	3
	MOVE.L	(A0)+,A2	3
	MOVE.L	(A0)+,A3	3
	MOVE.W	#$8260,A5	2
	MOVE.W	#$820A,A6	2
	JMP	(A1)	2
SUSPIT	BORD_GAUCHE
	NP	88
	BORD_DROIT
	NP	13
	STAB
	NP	12-11
	MOVE.L	(A0)+,A1	3
	MOVE.L	(A0)+,A2	3
	MOVE.L	(A0)+,A3	3
	JMP	(A1)	2

ENDIT	BORD_GAUCHE
	NP	88
	BORD_DROIT
	NP	13-5
	MOVE.L	PT_ZONE_AFF,A0	5
	STAB
	NP	12-11
	MOVE.L	(A0)+,A1	3
	MOVE.L	(A0)+,A2	3
	MOVE.L	(A0)+,A3	3
	JMP	(A1)	2
SUSPEND_AFF
	BORD_GAUCHE
	NP	88
	BORD_DROIT
	NP	13
	STAB
	NP	12
	BORD_GAUCHE
	NP	88
	BORD_DROIT
	NP	13-3
	MOVE.B	#0,(A6)	;$820A
	STAB
	NP	12
	BORD_GAUCHE
	MOVE.W	A5,(A6)
	NP	88-2
	BORD_DROIT
	NP	13
	STAB
	NP	12-11
	MOVE.L	(A0)+,A1	3
	MOVE.L	(A0)+,A2	3
	MOVE.L	(A0)+,A3	3
	JMP	(A1)	2
ENDIT_AFF	

	CLR.W	$FFFF8242.W
	CLR.L	$FFFF8248.W
	CLR.L	$FFFF824C.W

PLAY_ZIK = *+2
	;JSR	$12345678
	BSR	SWAPEC
	MOVE.L	PT_ZONE_EFF,A0
	CMPA.L	#PUT_ADRS_TUNNEL,A0
	BNE.S	.GO_AND_GO
	LEA	PUT_ADRS_TUNNEL+((3*61+1)*4)*3,A0
.GO_AND_GO	LEA	-(3*61+1)*4(A0),A0
	MOVE.L	A0,PT_ZONE_EFF
	***
	MOVE.L	PT_ZONE_AFF,A0
	MOVE.L	#SUSPIT,((56-(NB_CERCLES-56))*3*4)(A0)
	MOVE.L	#ENDIT,((NB_CERCLES+1)*3)*4(A0)
	CMPA.L	#PUT_ADRS_TUNNEL,A0
	BNE.S	.GO_AND_GO2
	LEA	PUT_ADRS_TUNNEL+((3*61+1)*4)*3,A0
.GO_AND_GO2	LEA	-(3*61+1)*4(A0),A0
	MOVE.L	A0,PT_ZONE_AFF
	CLR.W	$FFFF8240.W

	MOVE.L	PT_ZONE_AFF,A0
	MOVE.W	VAL_DEB,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	COURBE_TUNNEL,A1
	ADDA.W	D0,A1
	LEA	COURBE_TUNNEL,A2
	MOVE.W	BN,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	ADDA.W	D0,A2
	LEA	TABLE_ADR_COD,A3
	MOVE.W	Z_DEP,D0
	LSL.W	#6,D0
	ADDA.W	D0,A3
	LEA	CORES_Y+200*2+135*2,A4

	MOVE.L	#224,D0
	SUB.W	(A1)+,D0	ECART_X
	MOVE.L	#135,D1
	SUB.W	(A1)+,D1	ECART_Y
	MOVE.L	SCREEN2,A6
	LEA	160+230+230*135(A6),A6
	MOVE.W	COLOR,D5
	LEA	CORES_X+28*2*16,A1
	MOVEQ	#6,D7
	************
.RECOM_CALC	MOVE.L	D0,D2
	ADD.W	(A2),D2	X(BN+I*3)+ECART_X
	MOVE.L	D1,D3
	ADD.W	2(A2),D3	Y(BN+I*3)+ECART_Y
	;D2 ET D3 SONT LES COORDONNêES DU CENTRE DU CERCLE A AFFICHER
	SUBI.W	#135,D3
	MOVE.L	D2,D4
	ANDI.W	#15,D2
	ADD.W	D2,D2
	ADD.W	D2,D2
	MOVE.L	(A3,D2.W),(A0)+
	MULS.W	#230,D3
	MOVE.L	A6,A5
	ADDA.W	D5,A5
	EORI.W	#4,D5
	ADD.W	D4,D4
	MOVE.W	(A1,D4.W),D4
	EXT.L	D4
	ADD.L	D3,D4
	ADDA.L	D4,A5
	LEA	137*230(A5),A5
	MOVE.L	A5,(A0)+
	SUBA.L	#135*230*2,A5
	MOVE.L	A5,(A0)+
	LEA	4*16*3(A3),A3
	LEA	12(A2),A2
	DBRA	D7,.RECOM_CALC
	MOVE.L	A0,CODE_1
	MOVE.L	A2,CODE_2
	MOVE.L	A3,CODE_3
	MOVE.W	D5,CODE_4
	MOVE.W	D0,CODE_5
	MOVE.W	D1,CODE_6
	CLR.W	$FFFF8240.W
	CMPI.B	#$39,$FFFFFC02.W
	BNE.S	.RTE
	MOVE.W	#$4E71,BOUCLE
.RTE	RTE

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREP_ECR	MOVE.L	#BUFFER1,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	MOVE.L	#BUFFER2,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREPARE_ALL	;ON FABRIQUE CORES_X
	LEA	CORES_X,A0
;	MOVE.W	#56*8*2-1,D0
;.EFFAC1	CLR.W	(A0)+
;	DBRA	D0,.EFFAC1
	MOVE.W	#-224,D0	OFFSET DE DEPART
	MOVEQ	#7,D1	MOTIF DE DEPART
	MOVEQ	#1,D3	PAS D'INCREMENTATION
	MOVE.W	#28*16*2-1,D7
.CONT_REMP	MOVE.W	D1,(A0)+
	MOVE.W	D0,(A0)+
	SUBQ.W	#1,D1
	BGE.S	.PAS_NUL
	MOVEQ	#7,D1
	ADD.W	D3,D0
	EORI.W	#6,D3
.PAS_NUL	DBRA	D7,.CONT_REMP
	MOVE.W	#56*8*2-1,D0
.EFFAC2	CLR.W	(A0)+
	DBRA	D0,.EFFAC2
	;ON FABRIQUE CORES_Y
	LEA	CORES_Y,A0
	MOVE.W	#-230*137,D0
	MOVE.W	#199,D7
.REMP1	MOVE.W	D0,(A0)+
	DBRA	D7,.REMP1
	MOVE.W	#-230*135,D0
	MOVE.W	#273,D7
.REMP2	MOVE.W	D0,(A0)+
	ADDI.W	#230,D0
	DBRA	D7,.REMP2
	MOVE.W	#-230*137,D0
	MOVE.W	#199,D7
.REMP3	MOVE.W	D0,(A0)+
	DBRA	D7,.REMP3
	;ON CODE LES CERCLES
	LEA	COEFF_3D,A0
	LEA	CORES_X+56*8*2*2,A2
	LEA	BUF_COD_TUNNEL,A5
	LEA	TABLE_ADR_COD,A6
	MOVE.W	#182,D5
.CODE_ALL_Z	MOVE.W	(A0),D0
	LEA	12(A0),A0	ON SAUTE 6 Z
	MOVEQ	#0,D4	DECALAGE
.MAK_ALL_DEC	LEA	CERCLE,A1
	MOVE.L	A5,(A6)+
	BSR	COPY_BORD_GAUCHE
	MOVEQ	#21,D7	22 ORS=88 NOPS
	BSR	PUT_ORS
	BSR	COPY_BORD_DROIT
	MOVEQ	#2,D7
	BSR	PUT_ORS
	MOVE.W	#$4E71,(A5)+
	BSR	COPY_STAB
	MOVEQ	#2,D7
	BSR	PUT_ORS
	BSR	COPY_BORD_GAUCHE
	MOVEQ	#21,D7	22 ORS=88 NOPS
	BSR	PUT_ORS
	BSR	COPY_BORD_DROIT
	MOVEQ	#2,D7
	BSR	PUT_ORS
	MOVE.W	#$4E71,(A5)+
	BSR	COPY_STAB
	MOVE.L	#$22582458,(A5)+  MOVE.L (A0)+,A1; MOVE.L (A0)+,A2 : 6 NOPS
	MOVE.L	#$26584E71,(A5)+  MOVE.L (A0)+,A3
	MOVE.W	#$4ED1,(A5)+	;JMP	(A1)
	ADDQ.W	#1,D4
	CMPI.W	#16,D4
	BLT.S	.MAK_ALL_DEC
	DBRA	D5,.CODE_ALL_Z

	MOVE.L	#COURBE_TUNNEL,PT_COURBE
	CLR.W	Z_DEP
	CLR.W	VAL_DEB
	CLR.W	BN
	CLR.W	COLOR
	MOVE.L	#PUT_ADRS_TUNNEL+12*61+4,PT_ZONE_AFF
	MOVE.L	#PUT_ADRS_TUNNEL,PT_ZONE_EFF
	MOVE.L	#ENDIT,PUT_ADRS_TUNNEL
	MOVE.L	#ENDIT,PUT_ADRS_TUNNEL+((3*61+1)*4)*2

	LEA	CORES_X+28*2*16,A0
	MOVEQ	#-8,D0
	MOVEQ	#27,D7
.COPY_1	MOVEQ	#15,D6
.COPY_1_BIS	MOVE.W	D0,-(A0)
	DBRA	D6,.COPY_1_BIS
	SUBQ.W	#8,D0
	DBRA	D7,.COPY_1
	LEA	CORES_X+28*2*16,A0
	MOVEQ	#0,D0
	MOVEQ	#28*2-1,D7
.COPY_2	MOVEQ	#15,D6
.COPY_2_BIS	MOVE.W	D0,(A0)+
	DBRA	D6,.COPY_2_BIS
	ADDQ.W	#8,D0
	DBRA	D7,.COPY_2
	RTS

COPY_BORD_GAUCHE
	MOVE.L	A6,-(SP)
	LEA	.ADR,A6
	MOVE.L	(A6)+,(A5)+
	MOVE.W	(A6)+,(A5)+
	MOVE.L	(SP)+,A6
	RTS
.ADR	BORD_GAUCHE

COPY_BORD_DROIT	MOVE.L	A6,-(SP)
	LEA	.ADR,A6
	MOVE.L	(A6)+,(A5)+
	MOVE.W	(A6)+,(A5)+
	MOVE.L	(SP)+,A6
	RTS
.ADR	BORD_DROIT

COPY_STAB	MOVE.L	A6,-(SP)
	LEA	.ADR,A6
	MOVE.L	(A6)+,(A5)+
	MOVE.W	(A6)+,(A5)+
	MOVE.L	(SP)+,A6
	RTS
.ADR	STAB

PUT_ORS
.CODE_UN_CERCLE	LEA	DIFFERENTS_ORS,A4
	MOVE.W	(A1)+,D1	X
;	ADDI.W	#80,D1
	MOVE.W	(A1)+,D2	Y
;	ADDI.W	#80,D2
	MULS.W	D0,D1
	MULS.W	D0,D2
	ADD.L	D1,D1
	ADD.L	D2,D2
	SWAP	D1	X PROJETê
	SWAP	D2	Y PROJETê
	ADD.W	D4,D1	ON ADDITIONNE LE DECALAGE
	ADD.W	D1,D1
	ADD.W	D1,D1	X*4
	TST.W	D2
	BGE.S	.CONTINUE
	LEA	DIFFERENTS_ORS2,A4
	ADDI.W	#135,D2
	LEA	CORES_Y+200*2+135*2,A3
	BRA.S	.START
.CONTINUE	SUBI.W	#135,D2
	LEA	CORES_Y+200*2+135*2,A3
.START	ADD.W	D2,D2	Y*2
	MOVE.W	(A2,D1.W),D3	0-->7
	MOVE.W	2(A2,D1.W),D1
	ADD.W	(A3,D2.W),D1	OFFSET ECRAN PEUT-ETRE IMPAIR
	ADD.W	D3,D3
	ADD.W	D3,D3	D3*4
	MOVE.W	(A4,D3.W),(A5)+	INSTRUCTION
	MOVE.W	D1,(A5)+	OFFSET
	DBRA	D7,.CODE_UN_CERCLE
	RTS

PROCEDURE_CON	MOVE.L	PT_ZONE_AFF,A0
	MOVE.W	VAL_DEB,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	COURBE_TUNNEL,A1
	ADDA.W	D0,A1
	LEA	COURBE_TUNNEL,A2
	MOVE.W	BN,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	ADDA.W	D0,A2
	LEA	TABLE_ADR_COD,A3
	MOVE.W	Z_DEP,D0
	LSL.W	#6,D0
	ADDA.W	D0,A3
	LEA	CORES_Y+200*2+135*2,A4

	MOVE.L	#224,D0
	SUB.W	(A1)+,D0	ECART_X
	MOVE.L	#135,D1
	SUB.W	(A1)+,D1	ECART_Y
	MOVE.L	SCREEN2,A6
	LEA	160+230+230*135(A6),A6
	MOVE.W	COLOR,D5
	LEA	CORES_X+28*2*16,A1
	MOVEQ	#6,D7
	************
.RECOM_CALC	MOVE.L	D0,D2
	ADD.W	(A2),D2	X(BN+I*3)+ECART_X
	MOVE.L	D1,D3
	ADD.W	2(A2),D3	Y(BN+I*3)+ECART_Y
	;D2 ET D3 SONT LES COORDONNêES DU CENTRE DU CERCLE A AFFICHER
	SUBI.W	#135,D3
	MOVE.L	D2,D4
	ANDI.W	#15,D2
	ADD.W	D2,D2
	ADD.W	D2,D2
	MOVE.L	(A3,D2.W),(A0)+
	MULS.W	#230,D3
	MOVE.L	A6,A5
	ADDA.W	D5,A5
	EORI.W	#4,D5
	ADD.W	D4,D4
	MOVE.W	(A1,D4.W),D4
	EXT.L	D4
	ADD.L	D3,D4
	ADDA.L	D4,A5
	LEA	137*230(A5),A5
	MOVE.L	A5,(A0)+
	SUBA.L	#135*230*2,A5
	MOVE.L	A5,(A0)+
	LEA	4*16*3(A3),A3
	LEA	12(A2),A2
	DBRA	D7,.RECOM_CALC
	MOVE.L	A0,CODE_1
	MOVE.L	A2,CODE_2
	MOVE.L	A3,CODE_3
	MOVE.W	D5,CODE_4
	MOVE.W	D0,CODE_5
	MOVE.W	D1,CODE_6
	RTS


DIFFERENTS_ORS	EOR.B	D0,0(A2)
	EOR.B	D1,0(A2)
	EOR.B	D2,0(A2)
	EOR.B	D3,0(A2)
	EOR.B	D4,0(A2)
	EOR.B	D5,0(A2)
	EOR.B	D6,0(A2)
	EOR.B	D7,0(A2)
DIFFERENTS_ORS2	EOR.B	D0,0(A3)
	EOR.B	D1,0(A3)
	EOR.B	D2,0(A3)
	EOR.B	D3,0(A3)
	EOR.B	D4,0(A3)
	EOR.B	D5,0(A3)
	EOR.B	D6,0(A3)
	EOR.B	D7,0(A3)

ZIK	;INCBIN	BUGGIN6.MUS

CERCLE	INCBIN	CERCLE5.DAT

COEFF_3D	INCBIN	COEFF2.TUN

COURBE_TUNNEL	INCBIN	COURBE2.TUN
FIN_CRB
BUF_COD_TUNNEL
MSG_SPE	;INCBIN	B:\MSG2.DAT
	DCB.B	8000,1
_MSG2	;INCBIN	B:\MSG3.DAT
	DCB.B	8000,3
_MSG3	;INCBIN	B:\MSG6.DAT
	DCB.B	8000,7
_MSG4	DC	$9999

	SECTION	BSS
DEB_BSS
	;POINTEURS DE COURBE
	DS.B	768000-24002
VAL_DEB	DS.W	1
BN	DS.W	1
Z_DEP	DS.W	1
PT_COURBE	DS.L	1
COLOR	DS.W	1
	;
PT_ZONE_AFF	DS.L	1
PT_ZONE_EFF	DS.L	1
PUT_ADRS_TUNNEL	DS.L	(3*61+1)*3
TABLE_ADR_COD	DS.L	16*183
CORES_X	DS.W	56*8*2*3
NB_VBL	DS.W	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
CORES_Y	DS.W	200+274+200
	DS.B	256+25000-674*2+100
BUFFER1	;DS.B	128000+25000*2
	DS.B	64000+25000
BUFFER2	DS.B	64000+25000
FIN_BSS
MSG	EQU	BUFFER2
MSG2	EQU	BUFFER2+8000
MSG3	EQU	BUFFER2+16000
MSG4	EQU	BUFFER2+24000
