	LEA	NEW_PILE,A7

	PEA	0.W
	move.w	#$20,-(sp)
	trap	#1
	addq.l	#6,sp

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	move.w	#5,-(sp)
	trap	#14
	lea	12(sp),sp

	move.l	#BUFFERSCR,d0
	CLR.B	D0
	move.l	d0,SCREEN1
	add.l	#32000,d0
	move.l	d0,SCREEN2

TST	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.B	#2,$FFFF820A.W
	MOVE.B	#$1E,$FFFFFA07.W
	MOVE.B	#$64,$FFFFFA09.W
	MOVE.B	#$00,$FFFFFA0F.W
	MOVE.B	#$1E,$FFFFFA13.W
	MOVE.B	#$00,$FFFFFA1B.W
	MOVE.B	#$47,$FFFFFA21.W
	MOVE.B	#$64,$FFFFFA15.W
	MOVE.B	#$48,$FFFFFA17.W
	MOVE.B	#$00,$FFFFFA19.W
	MOVE.B	#$FF,$FFFFFA1F.W

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.B	#$12,$FFFFFC02.W

	LEA	FIN,A0
	MOVE.L	A0,$008.W
	MOVE.L	A0,$00C.W
	MOVE.L	A0,$010.W
	MOVE.L	A0,$014.W
	MOVE.L	A0,$018.W
	MOVE.L	A0,$01C.W
	MOVE.L	A0,$020.W
	
	LEA	RTE,A0
	MOVE.L	A0,$68.W
	MOVE.L	A0,$134.W
	MOVE.L	A0,$120.W
	MOVE.L	A0,$70.W
	BRA.S	CONTINUE
RTE	RTE
CONTINUE
	move.l	SCREEN1,d0
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)

	MOVE.L	SCREEN1,A0
	JSR	AF_CARRê

	MOVE.L	SCREEN1,A0
	LEA	160*16+8+2(A0),A0
	JSR	AF_CARRê

	MOVE.L	#INTER_RTE,$68.W
	MOVE.L	#INTER_RTE,$120.W

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W

	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	MOVE.L	#VBL,$70.W
	MOVE	#$2300,SR

J	BRA.S	J
AF_CARRê	MOVEQ	#128-1,D0
	MOVEQ	#-1,D1
DO_CARRê
N	SET	0
	REPT	8
	MOVE	D1,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D0,DO_CARRê
	RTS
VBL
	CLR.B	$FFFFFA1B.W
;	MOVE.B	#1,$FFFFFA21.W
;	MOVE.B	#8,$FFFFFA1B.W

	MOVE.B	#0,$FFFFFA19.W		; PREPARATION DU MFP POUR LE
	MOVE.B	#99,$FFFFFA1F.W		; BORDER HAUT
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W

	MOVE	#$101,$FFFF8240.W
	MOVE	#$212,$FFFF8242.W
	MOVE	#$000,$FFFF8244.W
	MOVE	#$212,$FFFF8246.W
INTER_RTE	RTE

INTER_TMA
	movem.l	d0-a6,-(sp)

	CLR.B	$FFFFFA07.W		; ARRET DU MFP POUR NE PAS
	CLR.B	$FFFFFA09.W		; ETRE GENE
	MOVE	#$2100,SR		; ON AUTORISE LA HBL

	MOVE.L	#$FFFF8209,A6
	MOVE.L	#$FFFF8260,A4
	MOVE.L	#$FFFF820A,A3

	MOVEQ	#0,D0
	MOVEQ	#2,D1

	STOP	#$2100		; ATTENTE DE LA PROCHAINE HBL
				; (FIXE A 16 CYCLES PRES ENVIRONS)
	MOVE	#$2700,SR		; ON COUPE TOUTE LES ITs
	MOVE	#$2300,(SP)		; AU RETOUR LE VBL SERA AUTORISEE

	MOVEQ	#29,D2		; ON ATTEND LE BON MOMENT
SYNCHROA:	DBF	D2,SYNCHROA
	NOP

	MOVE.B	D0,(A3)		; ET HOP! PLUS DE BORDER HAUT
	REPT	6
	NOP
	ENDR
	MOVE.B	D1,(A3)

	movem.l	(sp)+,d0-a6
	RTE

FIN	MOVE.L	4.W,A0
	JMP	(A0)

	DATA

	BSS
	DS.L	256
NEW_PILE	DS.L	1
	DS.B	256
BUFFERSCR	DS.B	64000
SCREEN1	DS.L	1
SCREEN2	DS.L	1
