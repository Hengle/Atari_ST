NB_PTS_SMILE = 8
X_FIXE_KEOPS = 224
X_FIXE_SEVERINE = 224

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W

	LEA	DêCAL_YEUX,A1
	MOVEQ	#0,D6
	MOVEQ	#16-1,D5
DêCALE_X	MOVEQ	#8-1,D7
	LEA	OEIL,A0
DêCALE_Y	MOVEQ	#0,D0
	MOVE.L	(A0)+,D0
	LSR.L	D6,D0
	MOVE.L	D0,(A1)+
	DBF	D7,DêCALE_Y
	ADDQ	#1,D6
	DBF	D5,DêCALE_X

	JSR	LINE
	JSR	PREP_ECR

	BSR	PREPARE_KEOPS

	MOVE.L	#SEQUENCE_KEOPS,PT_SEQ
	MOVE.L	#SEQUENCE_KEOPS,D0
	ADD.L	#(6+6+8+4*NB_PTS_SMILE)*20,D0
	MOVE.L	D0,PT_SEQ2

	LEA	TB_EFF_KEOPS,A0
	MOVE.L	SCREEN2,(A0)+
	CLR.L	(A0)+
	MOVE.L	SCREEN2,(A0)+
	CLR.L	(A0)+

	LEA	TB_EFF_SEVE,A0
	MOVE.L	SCREEN2,(A0)+
	CLR.L	(A0)+
	MOVE.L	SCREEN2,(A0)+
	CLR.L	(A0)+

	MOVEQ	#0,D0
	MOVE.L	D0,$FFFF8240.W
	MOVE.L	D0,$FFFF8244.W
	MOVE.L	D0,$FFFF8248.W
	MOVE.L	D0,$FFFF824C.W
	MOVE.L	D0,$FFFF8250.W
	MOVE.L	D0,$FFFF8254.W
	MOVE.L	D0,$FFFF8258.W
	MOVE.L	D0,$FFFF825C.W

 MOVE	#$756,$FFFF8242.W  0001 PEAU
 MOVE	#$777,$FFFF8244.W  0010 YEUX
 MOVE	#$777,$FFFF8246.W  0011 YEUX SUR PEAU
 MOVE	#$000,$FFFF8248.W  0100 CHEVEUX
 MOVE	#$000,$FFFF824A.W  0101 PUPILLE SUR PEAU, CA ARRIVE
 MOVE	#$000,$FFFF824C.W  0110 EXISTE PAS.
 MOVE	#$000,$FFFF824E.W  0111 PUPILLE SUR BLANC DE L'OEIL, LUI MEME SUR LA PEAU
 MOVE	#$101,$FFFF8250.W  1000 JOUES SEULES INVISIBLES
 MOVE	#$745,$FFFF8252.W  1001 JOUES+PEAU=VISIBLES 
 MOVE	#$600,$FFFF8254.W  1010 JOUES+BLANC EXISTE PAS
 MOVE	#$700,$FFFF8256.W  1011 IDEM
 MOVE	#$300,$FFFF8258.W  1100 IDEM
 MOVE	#$700,$FFFF825A.W  1101 IDEM
 MOVE	#$700,$FFFF825C.W  1110 IDEM
 MOVE	#$700,$FFFF825E.W  1111 IDEM

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TIMER_B,$120.W
	MOVE.W	#$2300,SR

IT_VBL	BSR	VSYNC
	CMPI.B	#$1,$FFFFFC02.W
	BEQ.S	IT_VBL
	ADDQ	#1,TIME
	CMPI	#301,TIME
	BNE.S	.PAS_ENCORE
	MOVE	#$700,$FFFF8240.W
	;301,381,521
.PAS_ENCORE
	MOVE	TIME,$FFFFC
	BSR	AFF_KEOPS
	BSR	AFF_SEVE
	BSR	SWAPEC
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIME
	ST	$FFFF8240.W
.NO_TIME
	BRA	IT_VBL
TIME	DC	0
VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

FIN	MOVE.L	4.W,A0
	JMP	(A0)

;EFECR	MOVE.L	SCREEN2,A0
;	MOVEQ	#0,D0
;	MOVEQ	#0,D1
;	MOVEQ	#0,D2
;	MOVEQ	#0,D3
;	MOVEQ	#0,D4
;	MOVEQ	#0,D5
;	MOVEQ	#0,D6
;	MOVEQ	#0,D7
;	SUBA.L	A1,A1
;	SUBA.L	A2,A2
;	SUBA.L	A3,A3
;	SUBA.L	A4,A4
;	SUBA.L	A5,A5
;	SUBA.L	A6,A6
;N	SET	0
;	REPT	200
;	MOVEM.L	D0-D7/A1-A6,N(A0)
;	MOVEM.L	D0-D7/A1-A6,N+4*14(A0)
;	MOVEM.L	D0-D7/A1-A4,N+4*14*2(A0)
;N	SET	N+160
;	ENDR
;	RTS

AFF_KEOPS	BSR	EFF_KEOPS
	MOVE.L	PT_SEQ,A6
	MOVE.W	(A6)+,D0	;No DE L'ETAPE
	LSL.W	#3,D0
	MOVE.W	#X_FIXE_KEOPS,D7
	ADD.W	(A6)+,D7	;X DU CORPS
	MOVE.W	(A6)+,D5	;Y DU CORPS=Y_AFF
	MOVE.W	D5,D4
	;ON AFFICHE LE CORPS DE KEOPS
	MOVE.L	SCREEN2,A1
	ADDQ	#8,A1
	MULS.W	#160,D4
	MOVE	D4,MOD_Y_SMILE
	ANDI.W	#-16,D7
	LSR.W	#1,D7
	ADD.W	D7,D4
	ADDA.W	D4,A1
	MOVE.L	A1,TB_EFF_KEOPS
	LEA	KEOPS_RENSEIGN,A0
	ADDA.W	D0,A0
	MOVE.L	(A0)+,A2	;ADRESSE DU CORPS DE KEOPS
	MOVE.W	(A0)+,D1	;LONGUEUR EN BLOCS DE 16
	MOVE.W	(A0)+,D2	;HAUTEUR-1
	LEA	ROUTS_AFF,A0
	ADD.W	D1,D1
	ADD.W	D1,D1
	MOVE.W	D1,TB_EFF_KEOPS+4
	MOVE.W	D2,TB_EFF_KEOPS+6
	MOVE.L	(A0,D1.W),A0
	JSR	(A0)

	;ON AFFICHE LES 2 YEUX DE KEOPS
	MOVE.W	#X_FIXE_KEOPS,D7
	ADD.W	(A6)+,D7	;X DE L'OEIL GAUCHE
	MOVE.W	#X_FIXE_KEOPS,D5
	ADD.W	(A6)+,D5	;X DE L'OEIL DROIT
	MOVE.W	(A6)+,D4	;Y DE L'OEIL
	;ON AFFICHE LE CORPS DE KEOPS
	MOVE.L	SCREEN2,A1
	ADDQ	#8,A1
	MULS.W	#160,D4
	ANDI.W	#-16,D7
	LSR.W	#1,D7
	ADD.W	D4,D7
	ADDA.W	D7,A1
	ADDQ.W	#2,A1
	LEA	KEOPS_YEUX_RENSEIGN,A0
	ADD.W	D0,D0
	ADDA.W	D0,A0
	MOVE.L	(A0)+,A2	;ADRESSE DE L'OEIL GAUCHE
	MOVE.W	(A0)+,D1	;LONGUEUR EN BLOCS DE 16
	MOVE.W	(A0)+,D2	;HAUTEUR-1
	LEA	ROUTS_AFF,A3
	ADD.W	D1,D1
	ADD.W	D1,D1
	MOVE.L	(A3,D1.W),A3
	JSR	(A3)

	MOVE.L	SCREEN2,A1
	ADDQ	#8,A1
	ANDI.W	#-16,D5
	LSR.W	#1,D5
	ADD.W	D5,D4
	ADDA.W	D4,A1
	ADDQ.W	#2,A1
	MOVE.L	(A0)+,A2	;ADRESSE DE L'OEIL DROIT
	MOVE.W	(A0)+,D1	;LONGUEUR EN BLOCS DE 16
	MOVE.W	(A0)+,D2	;HAUTEUR-1
	LEA	ROUTS_AFF,A0
	ADD.W	D1,D1
	ADD.W	D1,D1
	MOVE.L	(A0,D1.W),A0
	JSR	(A0)

;	BRA	TY
** AFFICHE SMILE
	ADDQ	#8,A6
	MOVEQ	#7-1,D7
.RUN
	MOVEM.L	D7/A6,-(SP)
	MOVE.L	SCREEN2,A0
	ADDQ	#8,A0
	LEA	-160*95+4(A0),A0
MOD_Y_SMILE	EQU	*+2
	LEA	0(A0),A0
	MOVE	(A6),D0
	MOVE	2(A6),D1
	MOVE	4(A6),D2
	MOVE	6(A6),D3
	ADD	#224,D0
	ADD	#224,D2
	ADD	#100,D1
	ADD	#100,D3
	jsr	LINE+78
	MOVEM.L	(SP)+,D7/A6
	ADDQ	#4,A6
	DBF	D7,.RUN
	ADDQ	#4,A6
;	LEA	4*NB_PTS_SMILE(A6),A6
**
** AFFICHE PUPILLE
	LEA	ANX,A0
MOD_ANX	EQU	*+2
	LEA	0(A0),A0
	CMPI	#$9999,(A0)
	BNE.S	.ROOL
	CLR	MOD_ANX
	LEA	ANX,A0
.ROOL	MOVE	(A0),D0
	ADDQ	#2,MOD_ANX

	MOVE.L	SCREEN2,A2
	LEA	160*157(A2),A2
	MOVE.L	SCREEN2,A0
	ADDQ	#8,A0
	LEA	160*25+96+8+4(A0),A0
	ADD	MOD_Y_SMILE,A0
	ADDA	D0,A0
	CMP.L	A2,A0
	BLE.S	.OK1
	MOVE.L	A2,A0
	LEA	96+8+4+8(A0),A0
.OK1	;LEA	OEIL,A1
	LEA	DêCAL_YEUX+8*4*9,A1
	REPT	8
	MOVE	(A1)+,(A0)
	MOVE	(A1)+,8(A0)
	LEA	160(A0),A0
	ENDR

	LEA	ANX,A0
MOD_ANX2	EQU	*+2
	LEA	2*20(A0),A0
	CMPI	#$9999,(A0)
	BNE.S	.ROOL2
	CLR	MOD_ANX2
	LEA	ANX,A0
.ROOL2	MOVE	(A0),D0
	ADDQ	#2,MOD_ANX2

	MOVE.L	SCREEN2,A0
	ADDQ	#8,A0
	LEA	160*25+96+8+4+8(A0),A0
	ADD	MOD_Y_SMILE,A0
	ADDA	D0,A0
	CMP.L	A2,A0
	BLE.S	.OK2
	MOVE.L	A2,A0
	LEA	96+16+4+8(A0),A0
.OK2	;LEA	OEIL2,A1
	LEA	DêCAL_YEUX+8*4*3,A1
	REPT	8
	MOVE	(A1)+,(A0)
	MOVE	(A1)+,8(A0)
	LEA	160(A0),A0
	ENDR

**
*****LES CHEVEUX, MAINTENANT
	MOVE.L	SCREEN2,A0
	LEA	-160*106+4(A0),A0
	ADDA	MOD_Y_SMILE,A0
	MOVE	D0,D7
	EXT.L	D7
	DIVS	#160,D7
	MOVE	D7,D6
	ADD	D7,D7
	MOVE	#215,D0	X1 FIXE
	MOVE	#70+50-5,D1	Y1 FIXE
	MOVE	#190,D2	X2 FIXE
	MOVE	#30+50+5,D3	Y2 FIXE
	ADD	D7,D2
	SUB	D6,D3
	MOVEM.L	D6/D7,-(SP)
	jsr	LINE+78
	MOVEM.L	(SP)+,D6/D7

	MOVE.L	SCREEN2,A0
	LEA	-160*106+4(A0),A0
	ADDA	MOD_Y_SMILE,A0
	MOVE	#240,D0	X1 FIXE
	MOVE	#70+50-5,D1	Y1 FIXE
	MOVE	#250,D2	X2 FIXE
	MOVE	#30+50,D3	Y2 FIXE
	SUB	D7,D2
	ADD	D6,D3
	MOVEM.L	D6/D7,-(SP)
	jsr	LINE+78
	MOVEM.L	(SP)+,D6/D7

	MOVE.L	SCREEN2,A0
	LEA	-160*106+4(A0),A0
	ADDA	MOD_Y_SMILE,A0
	MOVE	#260,D0	X1 FIXE
	MOVE	#70+50-10,D1	Y1 FIXE
	MOVE	#270,D2	X2 FIXE
	MOVE	#30+50+7,D3	Y2 FIXE
	SUB	D7,D2
	SUB	D6,D3
	MOVEM.L	D6/D7,-(SP)
	jsr	LINE+78
	MOVEM.L	(SP)+,D6/D7

*****
TY
;	LEA	8+4*NB_PTS_SMILE(A6),A6
	CMPI.W	#$1234,(A6)
	BNE.S	.PAS_FIN_SEQ
	LEA	SEQUENCE_KEOPS,A6
.PAS_FIN_SEQ	MOVE.L	A6,PT_SEQ

	LEA	TB_EFF_KEOPS,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0)+,D1
	MOVE.L	(A0)+,D2
	MOVE.L	(A0)+,D3
	MOVE.L	D1,-(A0)
	MOVE.L	D0,-(A0)
	MOVE.L	D3,-(A0)
	MOVE.L	D2,-(A0)
	RTS

AFF_SEVE	BSR	EFF_SEVE
	MOVE.L	PT_SEQ2,A6
	MOVE.W	(A6)+,D0	;No DE L'ETAPE
	LSL.W	#3,D0
	MOVE.W	#X_FIXE_SEVERINE,D7
	ADD.W	(A6)+,D7	;X DU CORPS
	MOVE.W	(A6)+,D5	;Y DU CORPS=Y_AFF
	MOVE.B	D5,MOD_TIM
	ADD.B	#20,MOD_TIM
	MOVE.W	D5,D4
	;ON AFFICHE LE CORPS DE SEVERINE
	MOVE.L	SCREEN2,A1
	LEA	-80+8(A1),A1
	MULS.W	#160,D4
	MOVE	D4,MOD_Y_SMILE2
	ANDI.W	#-16,D7
	LSR.W	#1,D7
	ADD.W	D7,D4
	ADDA.W	D4,A1
	MOVE.L	A1,TB_EFF_SEVE
	LEA	KEOPS_RENSEIGN,A0
	ADDA.W	D0,A0
	MOVE.L	(A0)+,A2	;ADRESSE DU CORPS DE KEOPS
	MOVE.W	(A0)+,D1	;LONGUEUR EN BLOCS DE 16
	MOVE.W	(A0)+,D2	;HAUTEUR-1
	LEA	ROUTS_AFF,A0
	ADD.W	D1,D1
	ADD.W	D1,D1
	MOVE.W	D1,TB_EFF_SEVE+4
	MOVE.W	D2,TB_EFF_SEVE+6
	MOVE.L	(A0,D1.W),A0
	JSR	(A0)

*****	ON AFFICHE LES 2 YEUX DE SEVERINE
	MOVE	D0,MOD_D0
	MOVE.W	#X_FIXE_KEOPS,D7
	ADD.W	(A6)+,D7	;X DE L'OEIL GAUCHE
	MOVE.W	#X_FIXE_KEOPS,D5
	ADD.W	(A6)+,D5	;X DE L'OEIL DROIT
	MOVE.W	(A6)+,D4	;Y DE L'OEIL
	;ON AFFICHE LE CORPS DE KEOPS
	MOVE.L	SCREEN2,A1
	LEA	-80+8(A1),A1
	MULS.W	#160,D4
	ANDI.W	#-16,D7
	LSR.W	#1,D7
	ADD.W	D4,D7
	ADDA.W	D7,A1
	ADDQ.W	#2,A1
	LEA	SEVE_YEUX_RENSEIGN,A0
	ADD.W	D0,D0
	ADDA.W	D0,A0
	MOVE.L	(A0)+,A2	;ADRESSE DE L'OEIL GAUCHE
	MOVE.W	(A0)+,D1	;LONGUEUR EN BLOCS DE 16
	MOVE.W	(A0)+,D2	;HAUTEUR-1
	LEA	ROUTS_AFF,A3
	ADD.W	D1,D1
	ADD.W	D1,D1
	MOVE.L	(A3,D1.W),A3
	JSR	(A3)

	MOVE.L	SCREEN2,A1
	LEA	-80+8(A1),A1
	ANDI.W	#-16,D5
	LSR.W	#1,D5
	ADD.W	D5,D4
	ADDA.W	D4,A1
	ADDQ.W	#2,A1
	MOVE.L	(A0)+,A2	;ADRESSE DE L'OEIL DROIT
	MOVE.W	(A0)+,D1	;LONGUEUR EN BLOCS DE 16
	MOVE.W	(A0)+,D2	;HAUTEUR-1
	LEA	ROUTS_AFF,A0
	ADD.W	D1,D1
	ADD.W	D1,D1
	MOVE.L	(A0,D1.W),A0
	JSR	(A0)

*****	ON AFFICHE LES 2 JOUES DE SEVERINE
	SUBQ	#6,A6
MOD_D0	EQU	*+2
	MOVE	#0,D0
	MOVE.W	#X_FIXE_KEOPS,D7
	ADD.W	(A6)+,D7	;X DE L'OEIL GAUCHE
	MOVE.W	#X_FIXE_KEOPS,D5
	ADD.W	(A6)+,D5	;X DE L'OEIL DROIT
	MOVE.W	(A6)+,D4	;Y DE L'OEIL

	MOVE.L	SCREEN2,A1
	LEA	-80+8-8+160*35(A1),A1
	MULS.W	#160,D4
	ANDI.W	#-16,D7
	LSR.W	#1,D7
	ADD.W	D4,D7
	ADDA.W	D7,A1
	ADDQ.W	#6,A1
	LEA	KEOPS_YEUX_RENSEIGN,A0
	ADD.W	D0,D0
	ADDA.W	D0,A0
	MOVE.L	(A0)+,A2	;ADRESSE DE L'OEIL GAUCHE
	MOVE.W	(A0)+,D1	;LONGUEUR EN BLOCS DE 16
	MOVE.W	(A0)+,D2	;HAUTEUR-1
	LEA	ROUTS_AFF,A3
	ADD.W	D1,D1
	ADD.W	D1,D1
	MOVE.L	(A3,D1.W),A3
	JSR	(A3)

	MOVE.L	SCREEN2,A1
	LEA	-80+8+8+160*35(A1),A1
	ANDI.W	#-16,D5
	LSR.W	#1,D5
	ADD.W	D5,D4
	ADDA.W	D4,A1
	ADDQ.W	#6,A1
	MOVE.L	(A0)+,A2	;ADRESSE DE L'OEIL DROIT
	MOVE.W	(A0)+,D1	;LONGUEUR EN BLOCS DE 16
	MOVE.W	(A0)+,D2	;HAUTEUR-1
	LEA	ROUTS_AFF,A0
	ADD.W	D1,D1
	ADD.W	D1,D1
	MOVE.L	(A0,D1.W),A0
	JSR	(A0)
***
	LEA	8+4*NB_PTS_SMILE(A6),A6

** AFFICHE PUPILLE
	MOVE.L	SCREEN2,A2
	LEA	160*151(A2),A2
	MOVE.L	SCREEN2,A0
	LEA	160*25+96+4-72(A0),A0
MOD_Y_SMILE2	EQU	*+2
	LEA	0(A0),A0
	CMP.L	A2,A0
	BLE.S	.OK1
	MOVE.L	A2,A0
	LEA	96+4-72(A0),A0
.OK1	;LEA	OEIL,A1
	LEA	DêCAL_YEUX+8*4*6,A1
	REPT	8
	MOVE	(A1)+,(A0)
	MOVE	(A1)+,8(A0)
	LEA	160(A0),A0
	ENDR

	MOVE.L	SCREEN2,A0
	LEA	160*25+96+4+8+16-72(A0),A0
	ADD	MOD_Y_SMILE2,A0
	CMP.L	A2,A0
	BLE.S	.OK2
	MOVE.L	A2,A0
	LEA	96+16+4+8-72(A0),A0
.OK2	;LEA	OEIL2,A1
	LEA	DêCAL_YEUX,A1
	REPT	8
	MOVE	(A1)+,(A0)
	MOVE	(A1)+,8(A0)
	LEA	160(A0),A0
	ENDR
**AFF SOURIRE
	MOVE.L	SCREEN2,A2
	LEA	160*175(A2),A2

	MOVE.L	SCREEN2,A0
	LEA	160*60+96+4+8-72(A0),A0
	ADD	MOD_Y_SMILE2,A0
	CMP.L	A2,A0
	BLE.S	.OK3
	MOVE.L	A2,A0
	LEA	96+4+8-72(A0),A0
.OK3	LEA	SEVE_SMILE,A1
	REPT	5
	MOVE	(A1)+,(A0)
	MOVE	(A1)+,8(A0)
	LEA	160(A0),A0
	ENDR

***** SOURCILS
	MOVE.L	SCREEN2,A2
	LEA	160*147(A2),A2

	MOVE.L	SCREEN2,A0
	LEA	-160*5+96-16+4+8-72(A0),A0
	ADD	MOD_Y_SMILE2,A0
	CMP.L	A2,A0
	BLE.S	.OK4
	MOVE.L	A2,A0
	LEA	-160*5+96-16+4+8-72(A0),A0
.OK4	LEA	SOURCILS,A1
	MOVE	#18,D6
.E	MOVE	(A1)+,D4
	MOVE	(A1)+,D5
	OR	D4,(A0)
	OR	D5,8(A0)
	LEA	160(A0),A0
	DBF	D6,.E
	LEA	-160*19+32(A0),A0
	MOVE	#18,D6
.F	MOVE	(A1)+,D4
	MOVE	(A1)+,D5
	OR	D4,(A0)
	OR	D5,8(A0)
	LEA	160(A0),A0
	DBF	D6,.F

***** RUBAN
;	MOVE.L	SCREEN2,A2
;	LEA	160*147(A2),A2

	MOVE.L	SCREEN2,A0
	LEA	-160*70(A0),A0
	ADD	MOD_Y_SMILE2,A0
;	CMP.L	A2,A0
;	BLE.S	.OK4
;	MOVE.L	A2,A0
;	LEA	-160*5+96-16+4+8-72(A0),A0
;.OK4
	MOVEQ	#0,D0
	MOVEQ	#25-1,D1
.J
N	SET	0
	REPT	11
	MOVE	D0,6+N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D1,.J

	LEA	RUBAN+4,A1
	MOVE	#55-1,D6
.L2
N	SET	0
	REPT	11
	MOVE	(A1)+,6+N(A0)
	MOVE	(A1)+,D1
	;OR	D1,4+N(A0)
	OR	D1,6+N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	ADDQ	#4,A1
	DBF	D6,.L2
	MOVEQ	#0,D0
	MOVEQ	#20-1,D1
.JR
N	SET	0
	REPT	11
	MOVE	D0,6+N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D1,.JR

***
	CMPI.W	#$1234,(A6)
	BNE.S	.PAS_FIN_SEQ
	LEA	SEQUENCE_KEOPS,A6
.PAS_FIN_SEQ	MOVE.L	A6,PT_SEQ2

	LEA	TB_EFF_SEVE,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0)+,D1
	MOVE.L	(A0)+,D2
	MOVE.L	(A0)+,D3
	MOVE.L	D1,-(A0)
	MOVE.L	D0,-(A0)
	MOVE.L	D3,-(A0)
	MOVE.L	D2,-(A0)
	RTS

ROUTS_AFF	DC.L	ROUT_AFF_0,ROUT_AFF_1,ROUT_AFF_2
	DC.L	ROUT_AFF_3,ROUT_AFF_4,ROUT_AFF_5
	DC.L	ROUT_AFF_6,ROUT_AFF_7,ROUT_AFF_8
	DC.L	ROUT_AFF_9,ROUT_AFF_10,ROUT_AFF_11
	DC.L	ROUT_AFF_12
	;SOURCE=A2, DESTINATION=A1, D2=HAUTEUR-1
	INCLUDE	ROUTSAFF.S

EFF_KEOPS	MOVE.L	TB_EFF_KEOPS,A1	;ADRESSE DE KEOPS SUR ECRAN
	LEA	-(15+20)*160+4(A1),A1
	MOVEQ	#0,D0
	MOVE	#20+20-1,D1
EFCHVEU	MOVE	D0,(A1)
	MOVE	D0,8(A1)
	MOVE	D0,16(A1)
	MOVE	D0,24(A1)
	MOVE	D0,32(A1)
	MOVE	D0,40(A1)
	MOVE	D0,48(A1)
	MOVE	D0,56(A1)
	LEA	160(A1),A1
	DBF	D1,EFCHVEU

	MOVE.L	TB_EFF_KEOPS,A1	;ADRESSE DE KEOPS SUR ECRAN
	MOVE.W	TB_EFF_KEOPS+4,D0 ;SA LONGUEUR EN BLOCS DE 16
	LEA	ROUTS_EFF,A0
	MOVE.L	(A0,D0.W),A0
	MOVE.W	TB_EFF_KEOPS+6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVE.L	D1,A2
	JMP	(A0)

EFF_SEVE	
	MOVE.L	TB_EFF_SEVE,A1
	LEA	-(15)*160+4(A1),A1
	MOVEQ	#0,D0
	MOVE	#20-1,D1
.EFCHVEU	MOVE	D0,(A1)
	MOVE	D0,8(A1)
	MOVE	D0,16(A1)
	MOVE	D0,24(A1)
	MOVE	D0,32(A1)
	MOVE	D0,40(A1)
	MOVE	D0,48(A1)
	MOVE	D0,56(A1)
	LEA	160(A1),A1
	DBF	D1,.EFCHVEU


	MOVE.L	TB_EFF_SEVE,A1
	;LEA	-80+8(A1),A1
	MOVE.W	TB_EFF_SEVE+4,D0
	LEA	ROUTS_EFF,A0
	MOVE.L	(A0,D0.W),A0
	MOVE.W	TB_EFF_SEVE+6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVE.L	D1,A2
	JMP	(A0)

ROUTS_EFF	DC.L	ROUT_EFF_0,ROUT_EFF_1,ROUT_EFF_2
	DC.L	ROUT_EFF_3,ROUT_EFF_4,ROUT_EFF_5
	DC.L	ROUT_EFF_6,ROUT_EFF_7,ROUT_EFF_8
	DC.L	ROUT_EFF_9,ROUT_EFF_10,ROUT_EFF_11
	DC.L	ROUT_EFF_12

VBLR	ADDQ.W	#1,NB_VBL
	CLR.B	$FFFFFA1B.W
MOD_TIM	EQU	*+3
	MOVE.B	#164,$FFFFFA21.W
	MOVE	#165,D0
	MOVEQ	#0,D1
	MOVE.B	MOD_TIM,D1
	SUB	D1,D0
	MOVE.B	D0,MOD_TIM2
	MOVE.B	#8,$FFFFFA1B.W
	MOVE	#$101,$FFFF8240.W
 MOVE	#$300,$FFFF8250.W  1000 RUBAN SEUL
 MOVE	#$300,$FFFF8252.W  1001 RUBAN+PEAU
 MOVE	#$300,$FFFF8254.W  1010 RUBAN+BLANC
 MOVE	#$300,$FFFF8256.W  1011 IDEM
 MOVE	#$000,$FFFF8258.W  1100 IDEM
 MOVE	#$300,$FFFF825A.W  1101 IDEM
 MOVE	#$300,$FFFF825C.W  1110 IDEM
 MOVE	#$300,$FFFF825E.W  1111 IDEM

	RTE

TIMER_B
	CLR.B	$FFFFFA1B.W
MOD_TIM2	EQU	*+3
	MOVE.B	#1,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TB1,$120.W
 MOVE	#$101,$FFFF8250.W  1000 JOUES SEULES INVISIBLES
 MOVE	#$745,$FFFF8252.W  1001 JOUES+PEAU=VISIBLES 
 MOVE	#$600,$FFFF8254.W  1010 JOUES+BLANC EXISTE PAS
 MOVE	#$700,$FFFF8256.W  1011 IDEM
 MOVE	#$300,$FFFF8258.W  1100 IDEM
 MOVE	#$700,$FFFF825A.W  1101 IDEM
 MOVE	#$700,$FFFF825C.W  1110 IDEM
 MOVE	#$700,$FFFF825E.W  1111 IDEM

	BCLR	#0,$FFFFFA0F.W
	RTE			
TB1
	CLR.B	$FFFFFA1B.W
;	MOVE.B	#1,$FFFFFA21.W
;	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TIMER_B,$120
	MOVE	#$112,$FFFF8240.W
	MOVE	#$112,$FFFF8250.W
	BCLR	#0,$FFFFFA0F.W
	RTE			

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	MOVE.L	#BUFFER2,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREPARE_KEOPS	;ON PASSE EN REVUE LA SEQUENCE POUR DETERMINER LES
	;DECALAGES DU CORPS ET DES YEUX SUIVANT L'ETAPE
	LEA	SEQUENCE_KEOPS,A0
	LEA	POZ_RESULT,A1
.CONT_SCANNING	CMPI.W	#$1234,(A0)
	BEQ.S	.FIN_SCANNING
	MOVE.W	(A0)+,D0	;No DE L'ETAPE D'ANIM
	MOVE.W	#X_FIXE_KEOPS,D1
	ADD.W	(A0)+,D1	;X DU CORPS
	ANDI.W	#15,D1	;DECALAGE DE L'ETAPE
	ADDQ.W	#2,A0	;ON SAUTE LE Y
	MOVE.W	#X_FIXE_KEOPS,D2
	MOVE.W	D2,D3
	ADD.W	(A0)+,D2	;X DE L'OEIL GAUCHE
	ADD.W	(A0)+,D3	;X DE L'OEIL DROIT
	ANDI.W	#15,D2
	ANDI.W	#15,D3
	LEA	10+4*NB_PTS_SMILE(A0),A0
	MULU.W	#6,D0
	MOVE.W	D1,(A1,D0.W)	;DECALAGE DU CORPS
	MOVE.W	D2,2(A1,D0.W)	;DECALAGE DE L'OEIL GAUCHE
	MOVE.W	D3,4(A1,D0.W)	;DECALAGE DE L'OEIL DROIT
	BRA.S	.CONT_SCANNING
.FIN_SCANNING	;ON CALCULE LES ANIMS DU CORPS DE KEOPS AU Z=0
	LEA	GFX_CORPSE,A0
	LEA	BUF_CORPS_KEOPS,A2
	LEA	KEOPS_RENSEIGN,A4
	LEA	POZ_RESULT,A5
	MOVEQ	#18,D7
.PROUT	MOVE.L	A2,(A4)+
	MOVE.L	A0,A1
	MOVE.W	#-1000,D0
	MOVE.W	#1000,D6
	MOVEQ	#0,D2	HAUTEUR
.NEXT_LINE	CMPI.W	#-1,(A0)
	BEQ.S	.FIN_SPR
	CMP.W	(A0),D6
	BLE.S	.NAN_NAN
	MOVE.W	(A0),D6
.NAN_NAN	ADDQ.W	#2,A0
	ADDQ.W	#1,D2	+1
	MOVE.W	(A0)+,D1	X2
	CMP.W	D1,D0
	BGE.S	.NEXT_LINE
	MOVE.W	D1,D0
	BRA.S	.NEXT_LINE
.FIN_SPR	MOVE.L	A1,A0
	;D0=LONGUEUR MAXIMUM DE KEOPS
	SUB.W	D6,D0
	ADD.W	(A5),D0
	ADDQ.W	#1,D0
	SUBQ.W	#1,D0
	LSR.W	#4,D0
	ADDQ.W	#1,D0
	MOVE.W	D0,(A4)+
	ADD.W	D0,D0	;D0=LONGUEUR MAX EN OCTETS
	LEA	MOTIFS(PC),A3
	SUBQ.W	#1,D2
	MOVE.W	D2,(A4)+
.ALL_THE_SPRITE	MOVE.W	(A0)+,D3	X1
	SUB.W	D6,D3
	ADD.W	(A5),D3
	MOVE.W	(A0)+,D4	X2
	SUB.W	D6,D4
	ADD.W	(A5),D4
	SUB.W	D3,D4	D4=LONGUEUR-1
	MOVE.W	D3,D5
	ANDI.W	#15,D3
	ADD.W	D3,D3
	MOVE.W	(A3,D3.W),D3	MOTIF DE DEPART
	ANDI.W	#-16,D5
	LSR.W	#3,D5
.TRAC_LIG	OR.W	D3,(A2,D5.W)
	LSR.W	#1,D3
	BNE.S	.FUCK
	MOVE.W	#$8000,D3
	ADDQ.W	#2,D5
.FUCK	DBRA	D4,.TRAC_LIG
	ADDA.W	D0,A2
	DBRA	D2,.ALL_THE_SPRITE
	ADDQ.W	#2,A0
	ADDQ.W	#6,A5
	DBRA	D7,.PROUT

	;ON CALCULE LES YEUX, DONC Z=400
CALCUL_2	LEA	GFX_CORPSE,A0
	LEA	BUF_YEUX_KEOPS,A2
	LEA	KEOPS_YEUX_RENSEIGN,A4
	LEA	COEFF_3D+400*2,A5
	LEA	POZ_RESULT,A6
	MOVEQ	#18,D7
PROUT	BSR	MAKE_ZOOM
	MOVE.L	A0,-(SP)
	BSR	PUT_YEUX
	ADDQ.W	#2,A6
	BSR	PUT_YEUX
	ADDQ.W	#4,A6
	MOVE.L	(SP)+,A0
	DBRA	D7,PROUT	

	;ON CALCULE LES YEUX DE SEVERINE, DONC Z=500
CALCUL_3	LEA	GFX_CORPSE,A0
	LEA	BUF_YEUX_SEVE,A2
	LEA	SEVE_YEUX_RENSEIGN,A4
	LEA	COEFF_3D+500*2,A5
	LEA	POZ_RESULT,A6
	MOVEQ	#18,D7
PROUTY	BSR	MAKE_ZOOM
	MOVE.L	A0,-(SP)
	BSR	PUT_YEUX
	ADDQ.W	#2,A6
	BSR	PUT_YEUX
	ADDQ.W	#4,A6
	MOVE.L	(SP)+,A0
	DBRA	D7,PROUTY

	RTS

MAKE_ZOOM	MOVE.L	A2,-(SP)
	LEA	BUF_ANNEX,A2
	MOVEQ	#0,D5	;Y DE DEPART
.CONT_ZOOM	CMPI.W	#-1,(A0)
	BEQ.S	.FIN_ZOOM
	MOVE.W	D5,D0
	ADD.W	D0,D0
	MULS.W	(A5),D0
	SWAP	D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.W	(A0)+,D1	;X1
	ADD.W	D1,D1
	MOVE.W	(A0)+,D2	;X2
	ADD.W	D2,D2
	MULS.W	(A5),D1
	MULS.W	(A5),D2
	SWAP	D1
	SWAP	D2
	MOVE.W	D1,(A2,D0.W)
	MOVE.W	D2,2(A2,D0.W)
	ADDQ.W	#1,D5	;LIGNE SUIVANTE
	BRA.S	.CONT_ZOOM
.FIN_ZOOM	MOVE.W	#-1,4(A2,D0.W)
	MOVE.L	(SP)+,A2
	ADDQ.W	#2,A0
	RTS
	************

PUT_YEUX	MOVE.L	A2,(A4)+
	LEA	BUF_ANNEX,A0
	MOVE.W	#-1000,D0
	MOVE.W	#1000,D6
	MOVEQ	#0,D2	HAUTEUR
.NEXT_LINE	CMPI.W	#-1,(A0)
	BEQ.S	.FIN_SPR
	CMP.W	(A0),D6
	BLE.S	.NAN_NAN
	MOVE.W	(A0),D6
.NAN_NAN	ADDQ.W	#2,A0
	ADDQ.W	#1,D2	+1
	MOVE.W	(A0)+,D1	X2
	CMP.W	D1,D0
	BGE.S	.NEXT_LINE
	MOVE.W	D1,D0
	BRA.S	.NEXT_LINE
.FIN_SPR	LEA	BUF_ANNEX,A0
	;D0-D1=LONGUEUR MAXIMUM DE KEOPS
	SUB.W	D6,D0
	ADD.W	2(A6),D0
	LSR.W	#4,D0
	ADDQ.W	#1,D0
	MOVE.W	D0,(A4)+
	ADD.W	D0,D0	;D0=LONGUEUR MAX EN OCTETS
	LEA	MOTIFS(PC),A3
	SUBQ.W	#1,D2
	MOVE.W	D2,(A4)+
ALL_THE_SPRITE	MOVE.W	(A0)+,D3	X1
	SUB.W	D6,D3
	MOVE.W	(A0)+,D4	X2
	SUB.W	D6,D4
	ADD.W	2(A6),D3
	ADD.W	2(A6),D4
	SUB.W	D3,D4	D4=LONGUEUR-1
	MOVE.W	D3,D5
	ANDI.W	#15,D3
	ADD.W	D3,D3
	MOVE.W	(A3,D3.W),D3	MOTIF DE DEPART
	ANDI.W	#-16,D5
	LSR.W	#3,D5
.TRAC_LIG	OR.W	D3,(A2,D5.W)
	LSR.W	#1,D3
	BNE.S	.FUCK
	MOVE.W	#$8000,D3
	ADDQ.W	#2,D5
.FUCK	DBRA	D4,.TRAC_LIG
	ADDA.W	D0,A2
	DBRA	D2,ALL_THE_SPRITE
	RTS

	DATA
RUBAN	INCBIN	RUBAN.GFX
SOURCILS	INCBIN	SOURCILS

;OEIL	DC.B	%00111000
;	DC.B	%01111100
;	DC.B	%11111110
;	DC.B	%11111110
;	DC.B	%11111110
;	DC.B	%11111110
;	DC.B	%01111100
;	DC.B	%00111000
OEIL	DC.L	%00111000000000000000000000000000
	DC.L	%01111100000000000000000000000000
	DC.L	%11111110000000000000000000000000
	DC.L	%11111110000000000000000000000000
	DC.L	%11111110000000000000000000000000
	DC.L	%11111110000000000000000000000000
	DC.L	%01111100000000000000000000000000
	DC.L	%00111000000000000000000000000000

;OEIL2	DC	%0000011100000000
;	DC	%0000111110000000
;	DC	%0001111111000000
;	DC	%0001111111000000
;	DC	%0001111111000000
;	DC	%0001111111000000
;	DC	%0000111110000000
;	DC	%0000011100000000

SEVE_SMILE
	DC.L	%00001000000000000000000000010000
	DC.L	%00000110000000000000000001100000
	DC.L	%00000011100000000000000111000000
	DC.L	%00000001111100000000111110000000
	DC.L	%00000000000111111111100000000000

ANX
 DC 0*160
 DC 0*160
 DC 1*160
 DC 2*160
 DC 2*160
 DC 3*160
 DC 4*160
 DC 4*160
 DC 4*160
 DC 4*160
 DC 5*160
 DC 4*160
 DC 4*160
 DC 4*160
 DC 4*160
 DC 3*160
 DC 2*160
 DC 2*160
 DC 1*160
 DC 0*160
 DC 0*160
 DC -1*160
 DC -2*160
 DC -3*160
 DC -3*160
 DC -4*160
 DC -5*160
 DC -5*160
 DC -5*160
 DC -5*160
 DC -5*160
 DC -5*160
 DC -5*160
 DC -5*160
 DC -5*160
 DC -4*160
 DC -3*160
 DC -3*160
 DC -2*160
 DC -1*160
 DC -1*160
 DC $9999

LINE	INCBIN	LINE.BIN

MOTIFS	DC.W	$8000,$4000,$2000,$1000,$800,$400,$200,$100,$80,$40,$20,$10,8,4,2,1

GFX_CORPSE	INCBIN	SPR_KEOP.COR

SEQUENCE_KEOPS	INCBIN	COORKEOP.SPR
	DC.W	$1234

COEFF_3D	INCBIN	KEOP_CF.3D

	SECTION	BSS
PT_SEQ	DS.L	1
PT_SEQ2	DS.L	1
NB_VBL	DS.W	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
DêCAL_YEUX	DS.L	8*16
BUF_ANNEX	DS.B	500
TB_EFF_KEOPS	DS.L	4
TB_EFF_SEVE	DS.L	4
	;ADRESSE+LONGUEUR+HAUTEUR
POZ_RESULT	DS.W	3*19
KEOPS_RENSEIGN	DS.W	(2+1+1)*19
KEOPS_YEUX_RENSEIGN	DS.W	(2+1+1)*2*19
SEVE_YEUX_RENSEIGN	DS.W	(2+1+1)*2*19
BUF_CORPS_KEOPS	DS.B	25630
BUF_YEUX_KEOPS	DS.B	9000
BUF_YEUX_SEVE	DS.B	6486
	DS.B	256+160*80
BUFFER	DS.B	32000
	DS.B	160*80
BUFFER2	DS.B	32000
