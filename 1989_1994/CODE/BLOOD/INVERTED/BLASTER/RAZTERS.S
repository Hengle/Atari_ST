	LEA	NEW_PILE,A7

	PEA	0.W
	move.w	#$20,-(sp)
	trap	#1
	addq.l	#6,sp

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	move.w	#5,-(sp)
	trap	#14
	lea	12(sp),sp

	move.l	#BUFFERSCR,d0
	CLR.B	D0
	move.l	d0,SCREEN1

TST	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W
	MOVE.B	#2,$FFFF820A.W
	MOVE.B	#$1E,$FFFFFA07.W
	MOVE.B	#$64,$FFFFFA09.W
	MOVE.B	#$00,$FFFFFA0F.W
	MOVE.B	#$1E,$FFFFFA13.W
	MOVE.B	#$00,$FFFFFA1B.W
	MOVE.B	#$47,$FFFFFA21.W
	MOVE.B	#$64,$FFFFFA15.W
	MOVE.B	#$48,$FFFFFA17.W
	MOVE.B	#$00,$FFFFFA19.W
	MOVE.B	#$FF,$FFFFFA1F.W

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.B	#$12,$FFFFFC02.W

	LEA	FIN,A0
	MOVE.L	A0,$008.W
	MOVE.L	A0,$00C.W
	MOVE.L	A0,$010.W
	MOVE.L	A0,$014.W
	MOVE.L	A0,$018.W
	MOVE.L	A0,$01C.W
	MOVE.L	A0,$020.W
	
	LEA	RTE,A0
	MOVE.L	A0,$68.W
	MOVE.L	A0,$134.W
	MOVE.L	A0,$120.W
	MOVE.L	A0,$70.W
	BRA.S	CONTINUE
RTE	RTE
CONTINUE
	move.l	SCREEN1,d0
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)

	LEA	PALETTE,A0
	LEA	PALETTE2,A1
	MOVE	#199,D0
ICI	MOVE	(A0)+,D1
	MOVE	D1,(A1)+
	MOVE	D1,(A1)+
	MOVE	D1,(A1)+
	MOVE	D1,(A1)+
	MOVE	D1,(A1)+
	MOVE	D1,(A1)+
	MOVE	D1,(A1)+
	MOVE	D1,(A1)+
	MOVE	D1,(A1)+
	MOVE	D1,(A1)+
	MOVE	D1,(A1)+
	MOVE	D1,(A1)+
	DBF	D0,ICI

	MOVE.L	#INTER_RTE,$68.W
;	MOVE.L	#TIMER_B,$120.W

;	CLR.B	$FFFFFA07.W
;	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W

	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	MOVE.L	#VBL,$70.W
	MOVE	#$2300,SR

J	BRA.S	J
VBL
	CLR.B	$FFFFFA1B.W

	MOVE.B	#0,$FFFFFA19.W		; PREPARATION DU MFP POUR LE
	MOVE.B	#99,$FFFFFA1F.W		; BORDER HAUT
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W

INTER_RTE	RTE
SAVEA1	DC.L	0
SAVEA1B	DC.L	0
INTER_TMA
	movem.l	d0-a6,-(sp)

	CLR.B	$FFFFFA07.W		; ARRET DU MFP POUR NE PAS
	CLR.B	$FFFFFA09.W		; ETRE GENE
	MOVE	#$2100,SR		; ON AUTORISE LA HBL

	MOVE.L	#$FFFF8209,A6
	MOVE.L	#$FFFF8260,A4
	MOVE.L	#$FFFF820A,A3

	MOVEQ	#0,D0
	MOVEQ	#2,D1

	STOP	#$2100		; ATTENTE DE LA PROCHAINE HBL
				; (FIXE A 16 CYCLES PRES ENVIRONS)
	MOVE	#$2700,SR		; ON COUPE TOUTE LES ITs
	MOVE	#$2300,(SP)		; AU RETOUR LE VBL SERA AUTORISEE

	MOVEQ	#29,D2		; ON ATTEND LE BON MOMENT
SYNCHROA:	DBF	D2,SYNCHROA
	NOP

	MOVE.B	D0,(A3)		; ET HOP! PLUS DE BORDER HAUT
	REPT	6
	NOP
	ENDR
	MOVE.B	D1,(A3)
full	
	LEA	$FFFF8209.W,A3	
	LEA	$FFFF8260.W,A1	
	LEA	$FFFF820A.W,A0	
	MOVE	#227,D0		* NB DE LIGNES
	MOVEQ	#0,D1		
	MOVEQ	#0,D4
	MOVEQ	#2,D3
	MOVEQ	#$10,D6
	MOVE	#$2700,SR

SYNCHRO	MOVE.B	(A3),D7     * SYNCHRO
	BEQ.S	SYNCHRO
	SUB.W	D7,D6		
	LSL.W	D6,D1		

	REPT	97-5-3
	NOP	
	ENDR

	LEA	PALETTE2,A4
FULLSCREEN	
	REPT	227
	JSR	ROUTY
	ENDR
	DCB	5,$4E71

	MOVE	a1,(A1)
	MOVE.B	#0,(A1)
	MOVE	(A4)+,$FFFF8240.W
	DCB	88-4,$4E71
	MOVE.b	#0,(A0)
	MOVE	a1,(A0)
	DCB	13,$4E71
	MOVE	a1,(A1)
	MOVE.B	#0,(A1)
	CLR.B	$FFFF820A.W
	DCB	12-4,$4E71

	MOVE	a1,(A1)
	MOVE.B	#0,(A1)
	MOVE	A1,(A0)
	MOVE	(A4)+,$FFFF8240.W
	DCB	88-2-4,$4E71
	MOVE.b	#0,(A0)
	MOVE	a1,(A0)
	DCB	13,$4E71
	MOVE	a1,(A1)
	MOVE.B	#0,(A1)
	DCB	12-5,$4E71

	REPT	40
	JSR	ROUTY
	ENDR

ENDE	movem.l	(sp)+,d0-a6
	MOVE	#$2300,SR
	CMPI.B	#$39,$FFFFFC02.W
	BEQ.S	FIN
	RTE
FIN	MOVE.L	4.W,A0
	JMP	(A0)

ROUTY
	MOVE	a1,(A1)
	MOVE.B	#0,(A1)
	MOVE	(A4)+,$FFFF8240.W
	DCB	88-4,$4E71
	MOVE.b	#0,(A0)
	MOVE	a1,(A0)
	DCB	13,$4E71
	MOVE	a1,(A1)
	MOVE.B	#0,(A1)
	DCB	12-5-4,$4E71
	RTS

	DATA

PALETTE
	;DC	$000,$000,$000
	DC	$100,$210,$321,$432,$543,$654,$765
	DC	$776,$777
	DC	$776,$775,$774,$773,$772,$771,$761
	DC	$751,$741,$731,$721,$711
	DC	$611,$511,$411,$311,$211,$111
	

	BSS
	DS.L	256
NEW_PILE	DS.L	1
	DS.B	256
BUFFERSCR	DS.B	32000
SCREEN1	DS.L	1
PALETTE2	DS	10000