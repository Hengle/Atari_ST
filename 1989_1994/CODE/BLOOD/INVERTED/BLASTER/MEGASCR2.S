;0000	Fond	|
;0001	Damier	|
;0010	Vaisseau	
;0011	Vaisseau	
;0100	Scroller	
;0101	Scroller	
;0110	Scroller	
;0111	Scroller	
;1000	Damier	|
;1001	Damier	
;1010	Vaisseau	
;1011	Vaisseau	
;1100	Scroller	
;1101	Scroller	
;1110	Scroller	
;1111	Scroller	


DEBUT:
	LEA.L 	D_PILE,A7

	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP	

	MOVE.L	#-8,VALUE2
	MOVE.L	#-8,VALUE

	MOVE.L	#DATA,DATAPNT
	MOVE.L	#DATA,DATAPNT2

	MOVE	#$2700,SR

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W

	MOVE.L	#BUFFERSCR,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	MOVE.L	D0,SCREEN
	ADD.L	#128000,D0
	MOVE.L	D0,SCREEN2
	MOVE.L	D0,SCREENA

	LEA	PALET,A0
	LEA	PALET2,A2
	LEA	DêGRADê_SCR,A1
.DEG	MOVE	(A0)+,D0
	MOVE	(A2)+,D2
	CMPI	#-1,D0
	BEQ.S	.FINI
	MOVE	(A0),D1
	MOVE	(A2),D3
	MOVE	D0,(A1)+
	MOVE	D0,(A1)+
	MOVE	D1,(A1)+
	MOVE	D0,(A1)+
	MOVE	D2,(A1)+
	MOVE	D2,(A1)+
	MOVE	D3,(A1)+
	MOVE	D2,(A1)+
	BRA.S	.DEG
.FINI

	LEA	DêGRADê_SCR,A0
	LEA	DêGRADê,A6
	MOVE	#274-1,D0
.MAK
	MOVE	#0,(A6)+	FOND
	MOVE	(A0)+,(A6)+	SCROL
	MOVE	#0,(A6)+	PLAN2...BREF
	DBF	D0,.MAK

	MOVE.L	#INTER_RTE,$68.W
	MOVE.L	#INTER_RTE,$134.W

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W
	CLR.B	$FFFFFA1B.W

	MOVE.B	#$12,$FFFFFC02.W

	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W

	LEA	TEXTE,A0
	MOVE.L	A0,MEM_LET

	MOVE.L	#RTE,$120.W

	MOVE.L	#VBLR,$70.W
	MOVE	#$2300,SR

BOUCLED	BRA.S	BOUCLED

VBLR
	CLR.B	$FFFFFA1B.W
	MOVE.B	#0,$FFFFFA19.W
	MOVE.B	#99,$FFFFFA1F.W		; BORDER HAUT
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W

	move.l	SCREEN,a1
	ADDA.L	VALUE,A1
	LEA	208+160*7(A1),A1
	MOVE.L	A1,MOD_ADRF

	LEA	MINI_TABLE,A0
MODT	EQU	*+2
	LEA	0(A0),A0

	TST.L	(A0)
	BNE.S	.ROULE
	CLR	MODT
	LEA	MINI_TABLE,A0
.ROULE
	MOVE.L	(A0)+,A2
	MOVE.L	(A0)+,MOD_DEP
	MOVE	(A0)+,MOD_NB
	ADD	#8+2,MODT
	JSR	(A2)

	CLR.L	PLUS3		7*
	ADDQ.L	#8,VALUE		9*
	MOVE.L	VALUE,D0		5*
	CMPI.L	#29440,D0		4*
	BNE.S	GLIBO		2
	CLR.L	VALUE		7*
	MOVE.L	#-29440,PLUS3	7*
GLIBO
	;44
	MOVE.L	DATAPNT,A6		5
	CMPI.L	#-1,(A6)		5
	BNE.S	OK2		2
	MOVE.L	#DATA,DATAPNT		7
	MOVE.L	DATAPNT,A6		5
	BRA.S	GF		3
OK2	DCB	15,$4E71
GF	MOVE.L	(A6)+,PLUS		7
	MOVE.L	(A6)+,A5		3
;37
	MOVE.L	A6,MODA6
	MOVE.L	A5,MODA5
INTER_RTE	RTE

INTER_TMA:	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE	#$2100,SR
full
	LEA	$FFFF8209.W,A3
	LEA	$FFFF8260.W,A6
	LEA	$FFFF820A.W,A5

	MOVEQ	#0,D0
	MOVEQ	#2,D1

	STOP	#$2100
	MOVE	#$2700,SR
	MOVE	#$2300,(SP)

	MOVEQ	#29,D2
SYNCHROA:	DBF	D2,SYNCHROA
	NOP

	MOVE.B	D0,(A5)
	REPT	6
	NOP
	ENDR
	MOVE.B	D1,(A5)

******************************************
KJ	MOVE.L	#$FFFF8209,A0
	MOVE.L	#$FFFF8260,A4
	MOVE.L	#$FFFF820A,A3
	MOVE	#199+28,D0
	MOVEQ	#0,D1		
	MOVEQ	#0,D4
	MOVEQ	#2,D3
	MOVEQ	#$10,D6

SYNCHR	MOVE.B	(A0),D7     * SYNCHRO
	BEQ.S	SYNCHR
	SUB.W	D7,D6		
	LSL.W	D6,D1		

	REPT	97-4-4-6-5-3
	NOP	
	ENDR
MODA6	EQU	*+2
	MOVE.L	#0,A6
MODA5	EQU	*+2
	MOVE.L	#0,A5
MODA0	EQU	*+2
	MOVE.L	#0,A0
	MOVE.L	MEM_LET,A2	5

	MOVE	#$000,$FFFF8240.W	4

	JSR	(A5)	*4	T=128
	MOVE.L	(A6)+,A5	*3
	JSR	(A5)
	MOVE.L	(A6)+,A5
	JSR	(A5)
	MOVE.L	(A6)+,A5
	JSR	(A5)
	MOVE.L	(A6)+,A5
	JSR	(A5)
	MOVE.L	(A6)+,A5
	JSR	(A5)

	DCB	7-3-1,$4E71
	MOVE.L	A0,A6	1
	LEA	DêGRADê,A5
VB
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W
	DCB	88-10,$4E71
	MOVE.B	#0,(A3)
	MOVE	A3,(A3)
	DCB	13,$4E71
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-5,$4E71
MOD_DEP	EQU	*+2
	JSR	$12345678
;INT1 = 24 LIGNES
;INT2 = 16+16+1 LIGNES

*********************************************************
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W
	DCB	88-10,$4E71
	MOVE.B	#0,(A3)
	MOVE	A3,(A3)
	DCB	13,$4E71
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-1-2,$4E71
MOD_NB	EQU	*+2
	MOVE	#218,D7

FULLSCREEN	NOP
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W

	DCB	88-10,$4E71
	MOVE.B	#0,(A3)
	MOVE	A3,(A3)
	DCB	13,$4E71
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71

	DBF	D7,FULLSCREEN

	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W

	DCB	88-10,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	2,$2A95
	DCB	3,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	CLR.B	$FFFF820A.W
	DCB	1,$2A95
	DCB	3,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	MOVE	a4,(A3)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W

	DCB	86-10,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	2,$2A95
	DCB	3,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	dcb	12-1-1,$4e71
	MOVEQ	#29,D7
	
FULL_END	NOP
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W

	DCB	88-10,$4E71
	MOVE.B	#0,(A3)
	MOVE	A3,(A3)
	DCB	2,$2A95
	DCB	3,$4E71
 	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71
	DBF	D7,FULL_END

	MOVE.L	MODA6,A6
	LEA	4*5(A6),A6
	MOVE.L	A6,DATAPNT	*5

MOD_ADRF	EQU	*+2
	LEA	$12345678,A1
	ADDQ	#8,A1
	JSR	RT_EFF1

	MOVE.L	PLUS,D0
	ADD.L	D0,SCREEN1
	MOVE.L	PLUS3,D0
	ADD.L	D0,SCREEN1
	MOVE.B  SCREEN2+1,$FFFF8201.W
	MOVE.B  SCREEN2+2,$FFFF8203.W
	JSR	SWAP
	MOVE	#8,COMPTEUR
RTE	RTE

FIN	MOVE.L	4.W,A0
	JMP	(A0)

MINI_TABLE
	DC.L	ANNEX1,INT2
	DC	218-33
	DC.L	ANNEX1B,INT1
	DC	218-24
	DC.L	ANNEX2,INT1
	DC	218-24
	DC.L	ANNEX2B,INT1
	DC	218-24
	DC.L	0,0,0
***************************************
ANNEX2	
	;LEA	IMAGE+34+32*160+8,A0
	MOVE.L	#IMAGE+34+32*160+8,MODA0
	RTS
ANNEX1B
	;LEA	IMAGE+34,A0
	MOVE.L	#IMAGE+34,MODA0
	RTS
ANNEX2B
	;LEA	IMAGE+34+8,A0
	MOVE.L	#IMAGE+34+8,MODA0
	RTS
ANNEX1	MOVE.L	#IMAGE+34,MODA0
	RTS
INT1
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W
	DCB	88-10-9-7*9-5,$4E71
	MOVE.L	A6,A0	1
	ADDA	(A2)+,A0	3
	MOVE.L	(A0),(A1)	5
	MOVE.L	160*1(A0),230*1(A1)	7
	MOVE.L	160*2(A0),230*2(A1)
	MOVE.L	160*3(A0),230*3(A1)
	MOVE.L	160*4(A0),230*4(A1)
	MOVE.L	160*5(A0),230*5(A1)
	MOVE.L	160*6(A0),230*6(A1)
	MOVE.L	160*7(A0),230*7(A1)
	MOVE.L	160*8(A0),230*8(A1)
	MOVE.L	160*9(A0),230*9(A1)
	MOVE	160*10(A0),230*10(A1)

	MOVE.B	#0,(A3)
	MOVE	A3,(A3)
	MOVE	160*10+2(A0),230*10+2(A1)
	MOVE.L	160*11(A0),230*11(A1)
	DCB	13-5-7,$4E71
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	160*12(A0),230*12(A1)
	MOVE	160*13(A0),230*13(A1)

	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W
	MOVE	160*13+2(A0),230*13+2(A1)
	DCB	88-10-5-7*10,$4E71
	MOVE.L	160*14(A0),230*14(A1)
	MOVE.L	160*15(A0),230*15(A1)
	MOVE.L	160*16(A0),230*16(A1)
	MOVE.L	160*17(A0),230*17(A1)
	MOVE.L	160*18(A0),230*18(A1)
	MOVE.L	160*19(A0),230*19(A1)
	MOVE.L	160*20(A0),230*20(A1)
	MOVE.L	160*21(A0),230*21(A1)
	MOVE.L	160*22(A0),230*22(A1)
	MOVE.L	160*23(A0),230*23(A1)
	MOVE.B	#0,(A3)
	MOVE	A3,(A3)
	MOVE.L	160*24(A0),230*24(A1)
	MOVE	160*25(A0),230*25(A1)
	DCB	13-7-5,$4E71
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE	160*25+2(A0),230*25+2(A1)
	MOVE.L	160*26(A0),230*26(A1)

	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W
	DCB	88-10-7*5-2,$4E71
	MOVE.L	160*27(A0),230*27(A1)
	MOVE.L	160*28(A0),230*28(A1)
	MOVE.L	160*29(A0),230*29(A1)
	MOVE.L	160*30(A0),230*30(A1)
	MOVE.L	160*31(A0),230*31(A1)
	LEA	230*32(A1),A1
	MOVE.B	#0,(A3)
	MOVE	A3,(A3)
	DCB	13,$4E71
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	SUBQ	#1,COMPTEUR	5
	BEQ.S	.DONE	3 si saut, 2 sinon
	NOP
	NOP
	JMP	INT1	3, Tot=12!!
.DONE	RTS	4,Total=12,ok...
COMPTEUR	DC	8

INT2
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W
	DCB	88-10-4-3-14*5-1,$4E71
	MOVE.L	A6,A0	1
	ADDA	(A2)+,A0	3
	MOVE.B	9(A0),(A1)	4
	MOVE.B	160+9(A0),230(A1)	5
	MOVE.B	160*2+9(A0),230*2(A1)
	MOVE.B	160*3+9(A0),230*3(A1)
	MOVE.B	160*4+9(A0),230*4(A1)
	MOVE.B	160*5+9(A0),230*5(A1)
	MOVE.B	160*6+9(A0),230*6(A1)
	MOVE.B	160*7+9(A0),230*7(A1)
	MOVE.B	160*8+9(A0),230*8(A1)
	MOVE.B	160*9+9(A0),230*9(A1)
	MOVE.B	160*10+9(A0),230*10(A1)
	MOVE.B	160*11+9(A0),230*11(A1)
	MOVE.B	160*12+9(A0),230*12(A1)
	MOVE.B	160*13+9(A0),230*13(A1)
	MOVE.B	160*14+9(A0),230*14(A1)
	MOVE.B	#0,(A3)
	MOVE	A3,(A3)
	DCB	13-10,$4E71
	MOVE.B	160*15+9(A0),230*15(A1)
	MOVE.B	160*16+9(A0),230*16(A1)
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-10,$4E71
	MOVE.B	160*17+9(A0),230*17(A1)
	MOVE.B	160*18+9(A0),230*18(A1)

	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W
	MOVE.B	160*19+9(A0),230*19(A1)
	MOVE.B	160*20+9(A0),230*20(A1)
	MOVE.B	160*21+9(A0),230*21(A1)
	MOVE.B	160*22+9(A0),230*22(A1)
	MOVE.B	160*23+9(A0),230*23(A1)
	MOVE.B	160*24+9(A0),230*24(A1)
	MOVE.B	160*25+9(A0),230*25(A1)
	MOVE.B	160*26+9(A0),230*26(A1)
	MOVE.B	160*27+9(A0),230*27(A1)
	MOVE.B	160*28+9(A0),230*28(A1)
	MOVE.B	160*29+9(A0),230*29(A1)
	MOVE.B	160*30+9(A0),230*30(A1)
	MOVE.B	160*31+9(A0),230*31(A1)
	LEA	230*32(A1),A1	2

	DCB	88-10-13*5-2,$4E71
	MOVE.B	#0,(A3)
	MOVE	A3,(A3)
	DCB	13,$4E71
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	SUBQ	#1,COMPTEUR	5
	BEQ.S	.DONE	3 si saut, 2 sinon
	NOP
	NOP
	JMP	INT2	3, Tot=12!!
.DONE	NOP
	NOP
	NOP
	NOP

	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W

	MOVE.L	MEM_LET,A2	5
	LEA	2*8(A2),A2	2
	CMPI	#$FFFF,16(A2)	4
	BEQ.S	.THE_END	3/2
	NOP
	BRA.S	.NOT_THE_END	3
.THE_END	LEA	TEXTE,A2	3
.NOT_THE_END
	MOVE.L	A2,MEM_LET	5

	LEA	-29440(A1),A1	2
	LEA	-29440+1(A1),A1	2
	LEA	IMAGE+34,A0	3
	MOVE	#8,COMPTEUR

	DCB	88-10-5-2-4-6-5-4-3-5,$4E71
	MOVE.B	#0,(A3)
	MOVE	A3,(A3)
	DCB	13,$4E71
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	12,$4E71

INT3	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W
	DCB	88-10-4-3-14*5,$4E71
	MOVE.L	A6,A0	1
	ADDA	(A2)+,A0	3
	MOVE.B	(A0),(A1)	3
	MOVE.B	160(A0),230(A1)	5
	MOVE.B	160*2(A0),230*2(A1)
	MOVE.B	160*3(A0),230*3(A1)
	MOVE.B	160*4(A0),230*4(A1)
	MOVE.B	160*5(A0),230*5(A1)
	MOVE.B	160*6(A0),230*6(A1)
	MOVE.B	160*7(A0),230*7(A1)
	MOVE.B	160*8(A0),230*8(A1)
	MOVE.B	160*9(A0),230*9(A1)
	MOVE.B	160*10(A0),230*10(A1)
	MOVE.B	160*11(A0),230*11(A1)
	MOVE.B	160*12(A0),230*12(A1)
	MOVE.B	160*13(A0),230*13(A1)
	MOVE.B	160*14(A0),230*14(A1)
	MOVE.B	#0,(A3)
	MOVE	A3,(A3)
	DCB	13-10,$4E71
	MOVE.B	160*15(A0),230*15(A1)
	MOVE.B	160*16(A0),230*16(A1)
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-10,$4E71
	MOVE.B	160*17(A0),230*17(A1)
	MOVE.B	160*18(A0),230*18(A1)

	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A5)+,$FFFF8240.W
	MOVE	(A5)+,$FFFF8244.W
	MOVE.B	160*19(A0),230*19(A1)
	MOVE.B	160*20(A0),230*20(A1)
	MOVE.B	160*21(A0),230*21(A1)
	MOVE.B	160*22(A0),230*22(A1)
	MOVE.B	160*23(A0),230*23(A1)
	MOVE.B	160*24(A0),230*24(A1)
	MOVE.B	160*25(A0),230*25(A1)
	MOVE.B	160*26(A0),230*26(A1)
	MOVE.B	160*27(A0),230*27(A1)
	MOVE.B	160*28(A0),230*28(A1)
	MOVE.B	160*29(A0),230*29(A1)
	MOVE.B	160*30(A0),230*30(A1)
	MOVE.B	160*31(A0),230*31(A1)
	LEA	230*32(A1),A1	2

	DCB	88-10-13*5-2,$4E71
	MOVE.B	#0,(A3)
	MOVE	A3,(A3)
	DCB	13,$4E71
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	SUBQ	#1,COMPTEUR	5
	BEQ.S	.DONE2	3 si saut, 2 sinon
	NOP
	NOP
	JMP	INT3	3, Tot=12!!
.DONE2	RTS

RT_EFF1
	MOVEQ	#0,D0
	REPT	256
	MOVE.L	D0,4(A1)
	LEA	230(A1),A1
	ENDR
	RTS
;RT_EFF2
;	MOVEQ	#0,D0
;	REPT	256
;	MOVE.L	D0,4(A1)
;	LEA	230(A1),A1
;	ENDR
;	RTS

SWAP
	move.l	SCREEN1,d0
	move.l	SCREEN2,SCREEN1
	move.l	d0,SCREEN2

	move.l	SCREEN,d0
	move.l	SCREENA,SCREEN
	move.l	d0,SCREENA

	move.l	VALUE,d0
	move.l	VALUE2,VALUE
	move.l	d0,VALUE2

	move.l	DATAPNT,d0
	move.l	DATAPNT2,DATAPNT
	move.l	d0,DATAPNT2


	rts
		

NOTHING	
	REPT	117
	NOP
	ENDR
	RTS

H70
	MOVE.B	D3,(A4)
	NOP
	MOVE.B	D4,(A4)
	REPT	89
	NOP				
	ENDR
	MOVE.B	D4,(A3)
	MOVE.B	D3,(A3)
	REPT	13
	NOP
	ENDR
	MOVE.B	D3,(A4)
	NOP
	MOVE.B	D4,(A4)
	REPT	1
	NOP			 
	ENDR
	RTS	=4 NOPS
H44
;	MOVE.B	D3,(A4)
	NOP
;	MOVE.B	D4,(A4)
	REPT	89+4
	NOP				
	ENDR
	MOVE.B	D4,(A3)
	MOVE.B	D3,(A3)
	REPT	13
	NOP
	ENDR
	MOVE.B	D3,(A4)
	NOP
	MOVE.B	D4,(A4)
	REPT	1
	NOP			 
	ENDR
	RTS	=4 NOPS
H24
	MOVE.B	D3,(A4)
	NOP
	MOVE.B	D4,(A4)
	REPT	89-2
	NOP				
	ENDR
	MOVE.B	D4,(A3)
	MOVE.B	D3,(A3)
	REPT	13
	NOP
	ENDR
	MOVE.B	D3,(A4)
	NOP
	MOVE.B	D4,(A4)
	REPT	1+2
	NOP			 
	ENDR
	RTS	=4 NOPS
H2
;	MOVE.B	D3,(A4)
	NOP
;	MOVE.B	D4,(A4)
	REPT	89-2+4
	NOP				
	ENDR
	MOVE.B	D4,(A3)
	MOVE.B	D3,(A3)
	REPT	13
	NOP
	ENDR
	MOVE.B	D3,(A4)
	NOP
	MOVE.B	D4,(A4)
	REPT	1+2
	NOP			 
	ENDR
	RTS	=4 NOPS
H80
	MOVE.B	D3,(A4)
	NOP
	MOVE.B	D4,(A4)
	REPT	35
	NOP
	ENDR
	MOVE.B	D3,(A4)
	NOP
	MOVE.B	D4,(A4)
	REPT	49
	NOP				
	ENDR
	MOVE.B	D4,(A3)
	MOVE.B	D3,(A3)
	REPT	13		
	NOP
	ENDR
	MOVE.B	D3,(A4)
	NOP
	MOVE.B	D4,(A4)
	REPT	1
	NOP			 
	ENDR
	RTS

	SECTION	DATA
IMAGE	INCBIN	BLOCS.PI1
TEXTE	INCBIN	DATA2.TXT

DATA	
	DC.L	-256,NOTHING,NOTHING,NOTHING,NOTHING,NOTHING,NOTHING
	DC.L	256,H44,H44,H44,H44,H44,H44
	DC.L	0,H24,H2,H2,H2,H2,NOTHING
	DC.L	0,H24,NOTHING,NOTHING,NOTHING,NOTHING,NOTHING
	DC.L	0,H80,H44,H2,H70,NOTHING,NOTHING
	DC.L	0,H44,H2,H2,NOTHING,NOTHING,NOTHING
	DC.L	0,H24,H24,NOTHING,NOTHING,NOTHING,NOTHING
	DC.L	0,H80,H70,H2,H2,H70,NOTHING
	DC.L	0,H70,H2,H2,H2,NOTHING,NOTHING
	DC.L	0,H24,H24,H24,NOTHING,NOTHING,NOTHING
	DC.L	0,H44,H44,H2,H2,H2,H2
	DC.L	0,H44,H44,NOTHING,NOTHING,NOTHING,NOTHING
	DC.L	256,H24,H24,H24,H24,NOTHING,NOTHING
	DC.L	-256,H80,H80,H80,H44,H44,NOTHING
	DC.L	0,H70,H44,H2,NOTHING,NOTHING,NOTHING
	DC.L	0,H24,H24,H24,H24,H24,NOTHING
	DC.L	0,H44,H44,H44,H2,H2,NOTHING
	DC.L	0,H70,H24,H44,H2,NOTHING,NOTHING
	DC.L	0,H24,H24,H24,H24,H24,H24
	DC.L	0,H80,H70,H70,H24,H2,H70

	DC.L	256,H70,H70,H24,H2,H2,NOTHING
	DC.L	0,H80,H2,H2,H2,H2,NOTHING	
	DC.L	-256,H80,NOTHING,NOTHING,NOTHING,NOTHING,NOTHING
	DC.L	512,H70,H70,H44,NOTHING,NOTHING,NOTHING
	DC.L	-512,H80,H80,H80,H80,NOTHING,NOTHING
	DC.L	0,H44,H44,H44,H44,H24,NOTHING
	DC.L	512,H70,H70,H70,H2,NOTHING,NOTHING
	DC.L	-512,H80,H80,H80,H80,H24,NOTHING
	DC.L	0,H44,H44,H44,H44,H24,H24
	
	DC.L	512,H70,H70,H24,H24,H44,NOTHING
	DC.L	-256,H80,H80,H80,H80,H24,H24
	DC.L	0,H2,H2,H2,H2,NOTHING,NOTHING

	DC.L	-1,-1

PALET
	DC	$000,$100,$200,$300,$400,$500,$600,$700
	DC	$710,$720,$730,$740,$750,$760,$770,$771
	DC	$772,$773,$774,$775,$776,$777,$677
	dc	$566,$455,$344,$233,$122,$011
	DCb	10,$011
	DC	-1

PALET2	dc	$001,$001,$112,$112,$223,$223,$334,$334,$445,$445,$556,$556,$667,$667,$777,$777
	DC	$767,$656,$545,$434,$323,$212,$101,$001
	DCb	10,$001
	DC	-1

	SECTION	BSS
 	
F_PILE	DS.L 	128	
D_PILE	DS.L 	1	
GLOUBI	DS.L	1
SCREENA	DS.L	1
SCREEN	DS.L	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
DATAPNT2	DS.L	1
DATAPNT	DS.L	1
VALUE2	DS.L	1
VALUE	DS.L	1
ASCREEN2	DS.L	1
ASCREEN1	DS.L	1
PLUS	DS.L	1
PLUS3	DS.L	1
MEM_LET	DS.L	1
DêGRADê	DS.W	274*3
DêGRADê_SCR	DS.W	256
	DS.B	10000
	DS.B	256
BUFFERSCR	DS.B	64000*4
