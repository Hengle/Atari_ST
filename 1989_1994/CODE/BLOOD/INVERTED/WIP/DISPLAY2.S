
	CLR.L	-(SP)
	MOVE	#$20,-(SP)
	TRAP	#1
	ADDQ	#6,SP
	
	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP
	
	LEA	ADR_PAL,A0
	LEA	BUF_PAL,A1
	MOVEQ	#128-1,D0
COP1	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBF	D0,COP1

	LEA	BUF_PAL,A0
	LEA	NEW_PAL,A1
	MOVE	#256-1,D4
TRANSFO_PAL	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVE.B	#%00000111,D0
	MOVE.B	(A0)+,D3	B |
	MOVE.B	(A0)+,D2	V |
	MOVE.B	(A0)+,D1	R |--> ??
	AND	D0,D1 
	AND	D0,D2
	AND	D0,D3
	MOVE.B	D1,(A1)+
	MOVE.B	D3,(A1)
	LSL	#4,D2
	OR.B	D2,(A1)+
	DBF	D4,TRANSFO_PAL

	LEA	ADR_IMG,A0
	LEA	NEW_PAL,A1
	LEA	DATAS_PAL,A2
	ADDA.L	#320*200*2,A2
	LEA	-320*2(A2),A2

	MOVE	#200-1,D6
CREE2	MOVE	#320-1,D7
CREE	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0
	ADD	D0,D0
	MOVE	(A1,D0.W),(A2)+
	DBF	D7,CREE
	LEA	-320*2*2(A2),A2
	DBF	D6,CREE2

	move.l	#BUFFER,d0
	CLR.B	D0
	move.l	d0,SCREEN1
	add.l	#143*256,d0
;	move.l	d0,SCREEN2

	move.l	SCREEN1,d0
;	MOVE.L	SCREEN2,SCREEN1
;	MOVE.L	D0,SCREEN2
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
			
	MOVE.L	#INTER_RTE,$68.W

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W
	
	MOVE.L	#VBL_IR,$70.W
	MOVE.L	#TB0_IR,$120.W
	
	MOVE.B	#$12,$FFFFFC02.W
	
	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W

BOUCLE	BRA.S	BOUCLE
FIN	MOVE.L	4.W,A0
	JMP	(A0)
	
	
VBL_IR	CLR	$FFFF8240.W
	CLR.B	$FFFFFA1B.W
	MOVE.B	#1,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W

	MOVE.B	#0,$FFFFFA19.W		; PREPARATION DU MFP POUR LE
	MOVE.B	#99,$FFFFFA1F.W		; BORDER HAUT
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W

	CMPI.B	#$50,$FFFFFC02.W
	BNE.S	PASF1
	ADD.L	#320*2,PNT_SYNC
PASF1
	CMPI.B	#$48,$FFFFFC02.W
	BNE.S	PASF2
	SUB.L	#320*2,PNT_SYNC
PASF2
	CMPI.B	#$4D,$FFFFFC02.W
	BNE.S	PASF3
	ADD.L	#2,PNT_SYNC
PASF3
	CMPI.B	#$4B,$FFFFFC02.W
	BNE.S	PASF4
	SUB.L	#2,PNT_SYNC
PASF4

	LEA	DATAS_PAL,A0
	ADDA.L	PNT_SYNC,A0
	LEA	$FFFF8240.W,A1
	RTE
PNT_SYNC	DC.L	0
INTER_TMA:	CLR.B	$FFFFFA07.W		; ARRET DU MFP POUR NE PAS
	CLR.B	$FFFFFA09.W		; ETRE GENE
	MOVE	#$2100,SR		; ON AUTORISE LA HBL

	MOVE.L	#$FFFF8209,A6
	MOVE.L	#$FFFF8260,A4
	MOVE.L	#$FFFF820A,A3

	MOVEQ	#0,D0
	MOVEQ	#2,D1

	STOP	#$2100		; ATTENTE DE LA PROCHAINE HBL
				; (FIXE A 16 CYCLES PRES ENVIRONS)
	MOVE	#$2700,SR		; ON COUPE TOUTE LES ITs
	MOVE	#$2300,(SP)		; AU RETOUR LE VBL SERA AUTORISEE

	MOVEQ	#29,D2		; ON ATTEND LE BON MOMENT
SYNCHROA:	DBF	D2,SYNCHROA
	NOP

	MOVE.B	D0,(A3)		; ET HOP! PLUS DE BORDER HAUT
	REPT	6
	NOP
	ENDR
	MOVE.B	D1,(A3)

		
	MOVE.L	#$FFFF8209,A6
	MOVE.L	#$FFFF8260,A4
	MOVE.L	#$FFFF820A,A3
	MOVEQ	#0,D1		
	MOVEQ	#$10,D6
	MOVE	#$2700,SR

	MOVE	#128-1,D0
	
SYNCHRO	MOVE.B	(A6),D7     * SYNCHRO
	BEQ.S	SYNCHRO
	SUB.W	D7,D6		
	LSL.W	D6,D1		

	DCB	97,$4E71
	
LINE	
	REPT	1
	DCB	41,$3298	MOVE (A0)+,(A1)/41*3 NOPS
	DCB	3,$4E71
	LEA	-41*2(A0),A0
	ENDR
	DCB	41,$3298
	LEA	(320-41)*2(A0),A0
	DBF	D0,LINE

	CLR	$FFFF8240.W
	move.l	SCREEN1,d0
	;MOVE.L	SCREEN2,SCREEN1
	;MOVE.L	D0,SCREEN2
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)

	MOVE	#$2300,SR
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
INTER_RTE	RTE

TB0_IR	
	CLR.B	$FFFFFA1B.W
	MOVE.B	#1,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
;	MOVE.L	#TB0_IR,$120
	BCLR	#0,$FFFFFA0F.W
	RTE	

	
IMAGE	INCBIN	MANSION.TGA
ADR_PAL = IMAGE+18
ADR_IMG = ADR_PAL+768

	BSS
SCREEN1	DS.L	1
	DS.B	256
BUFFER	DS.B	143*256*2

BUF_PAL	DS.B	768
NEW_PAL	DS.B	256*2
DATAS_PAL	DS.B	320*200*2