	;JUSTE POUR RIRE.
	;138 OCTETS DE CODE POUR DECOMPACTER UNE PC1,
	;PAS SUPER RAPIDE MAIS ELLE OCCUPE QUE CES 138 OCTETS.
NB_VBL_MAX = 3		;NOMBRE DE VBL POUR LE TEMPO DU FADE.
TIME_MAX = 150/NB_VBL_MAX       ;TEMPS DE VISU DE CHAQUE IMAGE EN MODE AUTO.
		;LE TEMPS EST EXPRIMê EN VBL...

	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.B	$FFFF8260.W

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVEQ	#1,D0
	JSR	ZIK
	MOVE.W	#$2300,SR

	MOVE.L	#LIST_IMG,PT_IMG
	MOVE.W	#0,MODE_FADE
	CLR.W	TIME

NEXT_IMG	BSR	BLACK_PALETTE
	MOVE.L	PT_IMG,A4
	MOVE.L	(A4),A0
	BSR	DECOMP_PC1
	MOVE.L	(A4),A0
	LEA	2(A0),A0
	BSR	CALC_FADE

	BSR	FADE_UP

	BSR	WAIT

	BSR	FADE_DOWN
	CLR.W	TIME
	BRA.S	NEXT_IMG

VBLR	ADDQ.W	#1,NB_VBL
	MOVEM.L	D0-A6,-(SP)
	JSR	ZIK+8
	MOVEM.L	(SP)+,D0-A6
.KB1	CMPI.B	#$3B,$FFFFFC02.W	; F1 ?
	BNE.S	.KB2
	CLR.W	MODE_FADE
	BRA.S	.FINITO
.KB2	CMPI.B	#$3C,$FFFFFC02.W	; F2 ?
	BNE.S	.FINITO
	MOVE.W	#1,MODE_FADE
	MOVE.W	#TIME_MAX,TIME
.FINITO	RTE

WAIT	TST.W	MODE_FADE
	BEQ.S	KEYBOARD
.NOT_NOW	BSR	VSYNC
	ADDQ.W	#1,TIME
	CMPI.W	#TIME_MAX,TIME
	BLT.S	WAIT
	CLR.W	TIME
	MOVE.L	PT_IMG,A0
	ADDQ.W	#4,A0
	CMPI.L	#$12345678,(A0)
	BNE.S	.OK
	LEA	LIST_IMG,A0
.OK	MOVE.L	A0,PT_IMG
	RTS

KEYBOARD	CMPI.B	#$39,$FFFFFC02.W
	BNE.S	WAIT
	MOVE.L	PT_IMG,A0
	ADDQ.W	#4,A0
	CMPI.L	#$12345678,(A0)
	BNE.S	.OK
	LEA	LIST_IMG,A0
.OK	MOVE.L	A0,PT_IMG
	MOVE.B	#$1C,$FFFFFC02.W
	RTS

FADE_UP	MOVE.L	#ADR_FADE,PT_FADE
	CLR.W	NB_VBL
.CONT_FADE	BSR	VSYNC
	MOVE.L	PT_FADE,A0
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	CMPA.L	#ADR_FADE+16*32,A0
	BEQ.S	.FINI
	MOVE.L	A0,PT_FADE
	BRA.S	.CONT_FADE
.FINI	RTS

FADE_DOWN	MOVE.L	#ADR_FADE+16*32,PT_FADE
	CLR.W	NB_VBL
.CONT_FADE	BSR	VSYNC
	MOVE.L	PT_FADE,A0
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	CMPA.L	#ADR_FADE+32*32,A0
	BEQ.S	.FINI
	MOVE.L	A0,PT_FADE
	BRA.S	.CONT_FADE
.FINI	RTS

VSYNC	CMPI.W	#NB_VBL_MAX,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS	

	;A0=ADRESSE DE LA PALETTE
CALC_FADE	MOVEM.L	(A0),D0-D7
	LEA	ADR_FADE+16*32,A0
	MOVEM.L	D0-D7,(A0)
	LEA	OFFSETS(PC),A5
	LEA	COLORS-1(PC),A4
	LEA	32(A0),A1
	MOVE.W	#16*16-1,D7 ;POUR LES 16 COULEURS ET 8 FADES
NEXT_COLOR	MOVEQ	#0,D3
	MOVE.W	(A0)+,D0
	MOVE.W	D0,D1	;COMPOSANTE BLEUE
	ANDI.W	#$F,D1
	MOVEQ	#0,D2
	MOVE.B	(A5,D1.W),D2	;OFFSET
	MOVE.B	(A4,D2.W),D3
	MOVE.W	D0,D1	;COMPOSANTE VERTE
	ANDI.W	#$F0,D1
	LSR.W	#4,D1
	MOVEQ	#0,D2
	MOVE.B	(A5,D1.W),D2
	MOVE.B	(A4,D2.W),D2
	LSL.W	#4,D2
	OR.W	D2,D3
	MOVE.W	D0,D1	;COMPOSANTE ROUGE
	ANDI.W	#$F00,D1
	LSR.W	#4,D1
	LSR.W	#4,D1
	MOVEQ	#0,D2
	MOVE.B	(A5,D1.W),D2
	MOVE.B	(A4,D2.W),D2
	LSL.W	#4,D2
	LSL.W	#4,D2
	OR.W	D2,D3
	MOVE.W	D3,(A1)+
	DBRA	D7,NEXT_COLOR
	LEA	ADR_FADE+32*16,A0
	MOVE.L	A0,A1
	REPT	16
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,-(A1)
	ENDR
	RTS

OFFSETS	DC.B	0,2,4,6,8,10,12,14,1,3,5,7,9,11,13,15
	;DC.B	0,1,2,3,4,5,6,7

	DCB.W	8,0
COLORS	DC.B	0,8,1,9,2,$A,3,$B,4,$C,5,$D,6,$E,7,$F
	;DC.B	0,1,2,3,4,5,6,7

	;A0=ADRESSE DE L'IMAGE
DECOMP_PC1	MOVEM.L	D0-A6,-(SP)

	LEA	34(A0),A0
	MOVEA.L	$44E.W,A2
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEQ	#0,D5
	MOVE.L	A2,A1
RETOUR	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0	;OCTET DE COMMANDE
	BGE.S	RECOP_INTEGRALE
	NEG.B	D0
	MOVE.B	D0,PUT_INC
	MOVE.B	(A0)+,D1
	BTST	#0,D7
	BNE.S	.COPY2	
.COPY	MOVE.B	D1,(A1)+
	SUBQ.W	#1,D0
	BLT.S	.END
.COPY2	MOVE.B	D1,(A1)+
	ADDQ.W	#6,A1
	DBRA	D0,.COPY
.END	BRA.S	REFLEXION

RECOP_INTEGRALE	MOVE.B	D0,PUT_INC
	BTST	#0,D7
	BNE.S	.COPY2	
.COPY	MOVE.B	(A0)+,(A1)+
	SUBQ.W	#1,D0
	BLT.S	.END
.COPY2	MOVE.B	(A0)+,(A1)+
	ADDQ.W	#6,A1
	DBRA	D0,.COPY
.END
REFLEXION
PUT_INC = *+3
	ADDI.B	#$12,D7
	ADDQ.W	#1,D7
	CMPI.B	#40,D7
	BNE.S	PAS_PLAN_SUIV	
	ADDQ.W	#2,A2
	ADDQ.W	#2,D6
	CMPI.W	#8,D6
	BLT.S	PAS_RETOUR_PLAN_1
	LEA	160-8(A2),A2
	ADDQ.W	#1,D5	;UNE LIGNE DE PLUS
	MOVEQ	#0,D6
PAS_RETOUR_PLAN_1
	MOVE.L	A2,A1
	MOVEQ	#0,D7
PAS_PLAN_SUIV	CMPI.W	#200,D5	
	BLT	RETOUR
	MOVEM.L	(SP)+,D0-A6
	RTS

BLACK_PALETTE	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W
	RTS

ZIK	INCBIN	ZIK.ZIK

PIC1	INCBIN	DRAGONBZ.PC1
PIC2	INCBIN	HOLOLOG.PC1
PIC3	INCBIN	SHADOW.PC1
PIC4	INCBIN	TANDEM.PC1

LIST_IMG	DC.L	PIC1
	DC.L	PIC2
	DC.L	PIC3
	DC.L	PIC4
	DC.L	$12345678

	SECTION	BSS
NB_VBL	DS.W	1
MODE_FADE	DS.W	1
TIME	DS.W	1
PT_IMG	DS.L	1
PT_FADE	DS.L	1
ADR_FADE	DS.W	16*32