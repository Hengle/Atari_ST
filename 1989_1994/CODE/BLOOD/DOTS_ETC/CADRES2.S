NB_PASSES	=	25

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	LEA	DEB_BSS,A0
	LEA	END_BSS,A1
.KILL_IT	CLR.L	(A0)+
	CMP.L	A1,A0
	BLE.S	.KILL_IT

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	JSR	PREP_COORD
*
	MOVE.L	SCREEN1,A1
	LEA	160*3+48(A1),A1
	MOVE.L	SCREEN2,A2
	LEA	160*3+48(A2),A2
	MOVEQ	#-1,D1
	MOVE	#12-1,D7
.A1
	MOVE.L	D1,(A1)
	MOVE.L	D1,(A2)
	MOVE.L	D1,4(A1)
	MOVE.L	D1,4(A2)
	MOVE.L	D1,160*193(A1)
	MOVE.L	D1,160*193+4(A1)
	MOVE.L	D1,160*193(A2)
	MOVE.L	D1,160*193+4(A2)
	ADDQ	#8,A1
	ADDQ	#8,A2
	DBF	D7,.A1
	MOVE.L	SCREEN1,A1
	LEA	160*3+48-8(A1),A1
	MOVE.L	SCREEN2,A2
	LEA	160*3+48-8(A2),A2
	MOVE.L	#$00010001,D1
	MOVE.L	#$80008000,D2
	MOVE	#194-1,D7
.A2
	MOVE.L	D1,(A1)
	MOVE.L	D1,(A2)
	MOVE.L	D1,4(A1)
	MOVE.L	D1,4(A2)

	MOVE.L	D2,13*8(A1)
	MOVE.L	D2,13*8(A2)
	MOVE.L	D2,13*8+4(A1)
	MOVE.L	D2,13*8+4(A2)
	
	LEA	160(A1),A1
	LEA	160(A2),A2
	DBF	D7,.A2

	JSR	LINE

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	CLR.W	NB_VBL
	MOVE.W	#$2300,SR

MAIN_LOOP	BSR	VSYNC
	MOVE	#$011,$FFFF8240.W

	BSR	EFFACE1

	LEA	DATAS,A1
MOD_DATAS_	EQU	*+2
	ADDA.L	#0,A1
	CMPI	#$1234,(A1)
	BNE.S	.ROUL
	LEA	-4*4*2(A1),A1
	BRA.S	.ROUL2
.ROUL	
	ADD.L	#4*2*4,MOD_DATAS_
.ROUL2	MOVEQ	#4-1,D7
.AF	MOVEM.W	(A1),D0-D3
	MOVEM.L	D7/A1,-(SP)
	MOVE.L	SCREEN2,A0
	JSR	LINE+78
	MOVEM.L	(SP)+,D7/A1
	ADDQ	#4*2,A1
	DBF	D7,.AF

	BSR	SWAPEC
	
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	BRA	MAIN_LOOP

FIN	MOVE.L	4.W,A0
	JMP	(A0)

VSYNC	CMPI	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	RTE

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

EFFACE1
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVE.W	#200-1,D7
.ALL
N	SET	0
	REPT	20
	MOVE	D0,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBRA	D7,.ALL
	RTS

CADRE1	
	DC	273,7
	DC	315,7

	DC	315,7
	DC	315,197

	DC	315,197
	DC	273,197

	DC	273,197
	DC	273,7
	
CADRE2	
	DC	95,199
	DC	95,5

	DC	95,5
	DC	288,5

	DC	288,5
	DC	288,199

	DC	288,199
	DC	95,199

PREP_COORD
	LEA	DATAS,A0
	LEA	CADRE1,A1
	LEA	CADRE2,A2

	REPT	8
	MOVE.L	(A1)+,(A0)+
	ENDR
	LEA	CADRE1,A1

	MOVEQ	#16-1,D7
.TR	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE	(A1)+,D0
	MOVE	(A2)+,D1
	MOVEM.L	D7/A0-A2,-(SP)
	JSR	DO_TRANS
	MOVEM.L	(SP)+,D7/A0-A2
	ADDQ	#2,A0
	DBF	D7,.TR

	LEA	CADRE2,A2
	LEA	FINDAT,A0
	REPT	8
	MOVE.L	(A2)+,(A0)+
	ENDR
	MOVE	#$1234,(A0)
	RTS
DO_TRANS
	MOVE.L	#NB_PASSES,D2	EN 'D2' PASSES
	MOVE.L	D2,D6
	MOVE	D2,MODIF
	MOVE	D2,MODIF_

	LSL.L	#8,D0
	LSL.L	#8,D1
	CMP.L	D0,D1
	BGT.S	ROUTY1
ROUTY2	;D1 PLUS PETIT
	MOVE.L	D0,D2
	SUB.L	D1,D0	D0=AMPLITUDE
	DIVU	D6,D0
	SWAP	D0
	CLR	D0
	SWAP	D0
MODIF_	EQU	*+2
	MOVE	#0,D7
	SUBQ	#1,D7
DO_IT_AGAIN_	SUB.L	D0,D2
	MOVE.L	D2,D3
	LSR.L	#8,D3	CA EN FAIT UN
	MOVE	D3,(A0)
	LEA	32(A0),A0
	DBF	D7,DO_IT_AGAIN_
FINI2	RTS
****
	
ROUTY1	;D0 PLUS PETIT
	MOVE.L	D0,D2
	SUB.L	D0,D1	D1=AMPLITUDE
	DIVU	D6,D1
	SWAP	D1
	CLR	D1
	SWAP	D1
MODIF	EQU	*+2
	MOVE	#0,D7
	SUBQ	#1,D7
DO_IT_AGAIN	ADD.L	D1,D2
	MOVE.L	D2,D3
	LSR.L	#8,D3	CA EN FAIT UN
	MOVE	D3,(A0)
	LEA	32(A0),A0
	DBF	D7,DO_IT_AGAIN
FINITED	RTS


	DATA
LINE	INCBIN	LINE.BIN

	SECTION	BSS
;BSS g‚n‚rale
DEB_BSS
NB_VBL	DS.W	1
	DS.B	256
BUFFER	DS.B	32000*2
SCREEN1	DS.L	1
SCREEN2	DS.L	1
DATAS	DS.L	8*(NB_PASSES+1)
FINDAT	DS.W	8*2+1
END_BSS