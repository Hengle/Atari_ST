	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	BSR	INIT_SYSTEME

	MOVE.W	#$700,$FFFF8242.W
	BSR	AFFICHE_POLY
J	JMP	J

AFFICHE_POLY	BSR	EFFACE_FLAG
	LEA	POLY_BASE,A0
	MOVE.W	(A0)+,D7	;D7=NB DE POINTS
NEXT	SUBQ.W	#1,D7
	BEQ	FINI
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE.W	(A0)+,D0	;X
	MOVE.W	(A0)+,D1	;Y
	CMP.W	2(A0),D1
	BLT.S	A_DROITE
A_GAUCHE	MOVEQ	#0,D2
	MOVE.W	(A0),D2
	MOVE.W	2(A0),D3
	LEA	BUF_TEMP,A1
	MOVE.W	D1,D4
	SUB.W	D3,D1	;HAUTEUR-1 DU SEGMENT
	BEQ.S	NEXT

	LEA	FLAG_COTE_GAUCHE,A3
	LEA	COTE_GAUCHE,A4
	ADDA.W	D4,A3
	ADD.W	D4,D4
	ADDA.W	D4,A1
	ADDA.W	D4,A4

	ADDQ.W	#1,D1
	SUB.W	D0,D2
	EXT.L	D2
	MOVEQ	#8,D4
	ASL.L	D4,D2
	DIVS.W	D1,D2
	EXT.L	D2
	SWAP	D2
	ROL.L	#8,D2
	SUBQ.W	#2,D1
	MOVE.L	A1,A2
	MOVE.W	D0,(A1)
;	TST.W	D1
;	BEQ.S	.CONT
;	SUBQ.W	#1,D1
.MK_X	ADDX.L	D2,D0
	MOVE.W	D0,-(A1)
	DBRA	D1,.MK_X
	;ON PLACE LES X DE GAUCHE DANS UN TABLEAU GAUCHE LIBRE
.CONT	MOVE.B	(A3),D6
	MOVE.L	A4,A5
.HOPE	TST.W	D6
	BGT.S	.NOT_THIS_ONE
	ADDQ.B	#1,(A3)
	MOVE.W	(A2),(A5)
	CLR.W	(A2)
	SUBQ.W	#2,A2
	SUBQ.W	#2,A4	
	SUBQ.W	#1,A3
	CMPA.L	A1,A2
	BGE.S	.CONT
	BRA	NEXT
.NOT_THIS_ONE	LEA	400(A5),A5	;TABLEAU SUIVANT
	SUBQ.W	#1,D6
	BRA.S	.HOPE

A_DROITE	MOVEQ	#0,D2
	MOVE.W	(A0),D2
	MOVE.W	2(A0),D3
	LEA	BUF_TEMP,A1
	SUB.W	D1,D3	;HAUTEUR-1 DU SEGMENT
	BEQ	NEXT

	LEA	FLAG_COTE_DROIT,A3
	LEA	COTE_DROIT,A4
	ADDA.W	D1,A3
	ADD.W	D1,D1
	ADDA.W	D1,A1
	ADDA.W	D1,A4

	ADDQ.W	#1,D3
	SUB.W	D0,D2
	EXT.L	D2
	MOVEQ	#8,D4
	ASL.L	D4,D2
	DIVS.W	D3,D2
	EXT.L	D2
	SWAP	D2
	ROL.L	#8,D2
	SUBQ.W	#2,D3
	MOVE.L	A1,A2
	MOVE.W	D0,(A1)+
;	TST.W	D3
;	BEQ.S	.CONT
;	SUBQ.W	#1,D3
.MK_X	ADDX.L	D2,D0
	MOVE.W	D0,(A1)+
	DBRA	D3,.MK_X
	;ON PLACE LES X DE DROITE DANS UN TABLEAU DROITE LIBRE
.CONT	MOVE.B	(A3),D6
	MOVE.L	A4,A5
.HOPE	TST.W	D6
	BGT.S	.NOT_THIS_ONE
	ADDQ.B	#1,(A3)
	MOVE.W	(A2),(A5)
	CLR.W	(A2)+
	ADDQ.W	#2,A4
	ADDQ.W	#1,A3
	CMPA.L	A1,A2
	BLT.S	.CONT
	BRA	NEXT
.NOT_THIS_ONE	LEA	400(A5),A5	;TABLEAU SUIVANT
	SUBQ.W	#1,D6
	BRA.S	.HOPE
	BRA	NEXT

EFFACE_FLAG	LEA	FLAG_COTE_GAUCHE,A0
	LEA	FLAG_COTE_DROIT,A1
	MOVE.W	#200-1,D7
.EFF	CLR.B	(A0)+
	CLR.B	(A1)+
	DBRA	D7,.EFF
	RTS

FINI	LEA	COTE_GAUCHE,A1
	LEA	COTE_DROIT,A2
	LEA	FLAG_COTE_GAUCHE,A0
	MOVE.L	SCREEN1,A5
	MOVE.W	#200-1,D6
.ALL_Y	TST.B	(A0)
	BEQ.S	.NEXT_BIPOINT
	CMPI.B	#1,(A0)
	BGT.S	.PLUSIEURS
	MOVE.W	(A1),D0
	CMP.W	(A2),D0
	BLT.S	.UN_BIPOINT
	MOVE.W	(A2),D1
	MOVE.W	D1,(A1)
	MOVE.W	D0,(A2)
	BRA.S	.UN_BIPOINT

.PLUSIEURS	MOVEQ	#0,D7
	MOVE.B	(A0),D7	;NB DE BIPOINTS( >=2 )
	SUBQ.W	#1,D7	;NB DE BIPOINTS A TESTER

.RETOUR_ZERO	MOVE.W	D7,D3
	MOVE.L	A1,A3
.NEXT_TEST	MOVE.W	(A3),D4
	MOVE.W	400(A3),D5
	CMP.W	D4,D5
	BGT.S	.NORMAL
	MOVE.W	D5,(A3)
	MOVE.W	D4,400(A3)
	BRA.S	.RETOUR_ZERO
.NORMAL	LEA	400(A3),A3
	SUBQ.W	#1,D3
	BGT.S	.NEXT_TEST

.RETOUR_ZERO2	MOVE.W	D7,D3
	MOVE.L	A2,A3
.NEXT_TEST2	MOVE.W	(A3),D4
	MOVE.W	400(A3),D5
	CMP.W	D4,D5
	BGT.S	.NORMAL2
	MOVE.W	D5,(A3)
	MOVE.W	D4,400(A3)
	BRA.S	.RETOUR_ZERO2
.NORMAL2	LEA	400(A3),A3
	SUBQ.W	#1,D3
	BGT.S	.NEXT_TEST2

	MOVE.L	A1,A3
	MOVE.L	A2,A4
.AFF_ALL	MOVE.W	(A3),D0
	MOVE.W	(A4),D1
	JSR	REMP
	LEA	400(A3),A3
	LEA	400(A4),A4
	DBRA	D7,.AFF_ALL
	BRA.S	.NEXT_BIPOINT

.UN_BIPOINT	MOVE.W	(A1),D0	;X1
	MOVE.W	(A2),D1	;X2
	JSR	REMP
.NEXT_BIPOINT	ADDQ.W	#1,A0
	ADDQ.W	#2,A1
	ADDQ.W	#2,A2
	LEA	160(A5),A5
	DBRA	D6,.ALL_Y
	RTS

REMP	MOVE.W	D0,D2
	MOVE.W	D1,D3
	LEA	MOTIF_GAUCHE(PC),A6
	ANDI.W	#15,D0
	ADD.W	D0,D0
	MOVE.W	(A6,D0.W),D0
	LEA	MOTIF_DROIT(PC),A6
	ANDI.W	#15,D1
	ADD.W	D1,D1
	MOVE.W	(A6,D1.W),D1
	ANDI.W	#$FFF0,D2
	LSR.W	#1,D2
	ANDI.W	#$FFF0,D3
	LSR.W	#1,D3
	CMP.W	D2,D3
	BGT.S	.PLUS_GRAND
	EOR.W	D0,D1
	NOT.W	D1
	OR.W	D1,0(A5,D2.W)
	RTS
.PLUS_GRAND	OR.W	D0,0(A5,D2.W)
	OR.W	D1,0(A5,D3.W)
	SUB.W	D2,D3
	LSR.W	#3,D3
	SUBQ.W	#2,D3
	BLT.S	.NO_MORE
.COPY	MOVE.W	#-1,8(A5,D2.W)
	ADDQ.W	#8,D2
	DBRA	D3,.COPY
.NO_MORE	RTS

MOTIF_GAUCHE	DC.W	$FFFF,$7FFF,$3FFF,$1FFF,$FFF,$7FF,$3FF,$1FF
	DC.W	$FF,$7F,$3F,$1F,$F,7,3,1

MOTIF_DROIT	DC.W	$8000,$C000,$E000,$F000,$F800,$FC00,$FE00,$FF00
	DC.W	$FF80,$FFC0,$FFE0,$FFF0,$FFF8,$FFFC,$FFFE,$FFFF

INIT_SYSTEME	CLR.B	$FFFF8260.W

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

POLY_BASE	INCBIN	POLY_1.DAT

	SECTION	BSS
	DS.B	256
BUFFER	DS.B	64000
SCREEN1	DS.L	1
SCREEN2	DS.L	1
BUF_TEMP	DS.W	200
COTE_GAUCHE	DS.W	200*8
COTE_DROIT	DS.W	200*8
FLAG_COTE_GAUCHE
	DS.B	200
FLAG_COTE_DROIT	DS.B	200
