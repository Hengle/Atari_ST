;cette source comporte la routine de remplissage convexe...

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	BSR	INIT_SYSTEME
	MOVE.W	POLY,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	MULU.W	#0,D0
	LEA	POLY+2(PC),A0
	ADDA.W	D0,A0
	MOVE.L	A0,PT_POLY
	MOVE.L	#BUF_SOV_GFX,PT_SOV
	CLR.W	$FFFF8240.W
	MOVE.W	#$333,$FFFF8242.W
	MOVE.W	#90-1,D7
.MK_ALL	MOVE.W	D7,SOV
	BSR	EFFAC
	BSR	EFFACE_FLAG
	BSR	AFFICHE_POLY
	BSR	SOV_GFX
	BSR	SWAPEC
SOV = *+2
	MOVE.W	#$1234,D7
	DBRA	D7,.MK_ALL
	CLR.W	FLAG_PUT_IMG
	CLR.W	PT_SOV
	LEA	PIC,A0
	BSR	DECOMP_PC1

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.W	NB_VBL
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

IT_VBL	BSR	VSYNC
	MOVE.W	#$300,$FFFF8240.W
	BSR	PUT_GFX
	BSR	SWAPEC
	CLR.W	$FFFF8240.W

	BRA	IT_VBL

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	RTE

PUT_GFX	MOVE.W	PT_SOV,D0
	MULU.W	#14*147,D0
	MOVE.L	SCREEN2,A1
	LEA	160*11(A1),A1
	LEA	BUF_SOV_GFX,A0
	ADDA.L	D0,A0
	MOVE.W	#200-11-42-1,D7
N	SET	56
.AFF	REPT	7
	MOVE.W	(A0)+,N(A1)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	DBRA	D7,.AFF
	MOVE.W	PT_SOV,D0
	ADDQ.W	#1,D0
	CMPI.W	#90,D0
	BLT	.OK
	MOVE.W	#-1,FLAG_PUT_IMG
	MOVEM.L	PIC+2,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	LEA	BUF_PIC,A5
	MOVE.L	SCREEN2,A6
N	SET	0
	REPT	200
	MOVEM.L	N+48(A5),D0-D7/A0-A4
	MOVEM.L	D0-D7/A0-A4,N+48(A6)
	MOVEM.L	N+48+52(A5),D0-D4
	MOVEM.L	D0-D4,N+48+52(A6)
N	SET	N+160
	ENDR
	MOVEQ	#89,D0
.OK	MOVE.W	D0,PT_SOV
	RTS

SOV_GFX	MOVE.L	SCREEN2,A0
	LEA	160*11(A0),A0
	MOVE.L	PT_SOV,A1
	MOVE.W	#200-11-42-1,D7
N	SET	56
.SOV	REPT	7
	MOVE.W	N(A0),(A1)+
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBRA	D7,.SOV
	MOVE.L	A1,PT_SOV
	RTS

EFFAC	MOVE.L	SCREEN2,A0
	LEA	160*11(A0),A0
	MOVEQ	#0,D0
	MOVE.W	#200-11-42-1,D7
N	SET	56
.EFF	REPT	7
	MOVE.W	D0,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBRA	D7,.EFF
	RTS

AFFICHE_POLY	MOVE.L	PT_POLY,A0
	MOVE.W	POLY(PC),D7	;NB DE POINTS
	SUBQ.W	#2,D7

ALL_POINTS	BSR	EFFAC_TEMP
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVE.W	(A0),D2	;X2
	MOVE.W	2(A0),D3	;Y2
	CMP.W	D1,D3
	BGT	Y2_SUP_Y1
	BEQ	FINITO	;LIGNE HORIZONTALE
Y2_INF_Y1	MOVEQ	#12,D4
	SUB.W	D2,D0
	SUB.W	D3,D1
	EXT.L	D0
	ASL.L	D4,D0
	ADDQ.W	#1,D1
	DIVS.W	D1,D0
	SUBQ.W	#1,D1
	EXT.L	D0

	LEA	BUF_TEMP,A1
	;D3=Y DE DEPART DU TRACE DE LIGNE
	MOVE.W	D1,D6	;ON SAUVE LA HAUTEUR
	MOVE.W	D2,(A1)+
	TST.W	D1
	BEQ	FINITO
	SUBQ.W	#1,D1
	ASL.L	D4,D2
CALC_ALL_THE_LINE_MONTE
	ADD.L	D0,D2
	MOVE.L	D2,D5
	ASR.L	D4,D5
	MOVE.W	D5,(A1)+
	DBRA	D1,CALC_ALL_THE_LINE_MONTE

	LEA	FLAG_COTE_GAUCHE,A3
	ADDA.W	D3,A3
	LEA	BUF_TEMP,A1
	LEA	COTE_GAUCHE,A2
	ADD.W	D3,D3
	ADDA.W	D3,A2
POZ_ALL	MOVE.L	A2,A5
	MOVE.B	(A3),D4
RETURN	MOVE.W	(A5),D5
	CMP.W	(A1),D5
	BEQ.S	LES_MEME
	TST.B	D4
	BNE.S	BUFFER_GAUCHE_SUIVANT
	ADDQ.B	#1,(A3)
LES_MEME	MOVE.W	(A1)+,(A5)
	ADDQ.W	#2,A2
	ADDQ.W	#1,A3
	DBRA	D6,POZ_ALL
	BRA	FINITO

BUFFER_GAUCHE_SUIVANT
	LEA	400(A5),A5
	SUBQ.B	#1,D4
	BRA.S	RETURN

Y2_SUP_Y1	MOVEQ	#12,D4
	SUB.W	D0,D2
	SUB.W	D1,D3
	EXT.L	D2
	ASL.L	D4,D2
	ADDQ.W	#1,D3
	DIVS.W	D3,D2	;D2=PAS_X (.L)
	SUBQ.W	#1,D3
	EXT.L	D2

	LEA	BUF_TEMP,A1
	;D1=Y DE DEPART DU TRACE DE LIGNE
	MOVE.W	D0,(A1)+
	MOVE.W	D3,D6	;ON SAUVE LA HAUTEUR
	TST.W	D3
	BEQ.S	FINITO
	SUBQ.W	#1,D3
	ASL.L	D4,D0
CALC_ALL_THE_LINE_DESC
	ADD.L	D2,D0
	MOVE.L	D0,D5
	ASR.L	D4,D5
	MOVE.W	D5,(A1)+
	DBRA	D3,CALC_ALL_THE_LINE_DESC

	LEA	FLAG_COTE_DROIT,A3
	ADDA.W	D1,A3
	LEA	BUF_TEMP,A1
	LEA	COTE_DROIT,A2
	ADD.W	D1,D1
	ADDA.W	D1,A2
POZ_ALL2	MOVE.L	A2,A5
	MOVE.B	(A3),D4
RETURN2	MOVE.W	(A5),D5
	CMP.W	(A1),D5
	BEQ.S	LES_MEME2
	TST.B	D4
	BNE.S	BUFFER_DROIT_SUIVANT
	ADDQ.B	#1,(A3)
LES_MEME2	MOVE.W	(A1)+,(A5)
	ADDQ.W	#2,A2
	ADDQ.W	#1,A3
	DBRA	D6,POZ_ALL2
	BRA	FINITO

BUFFER_DROIT_SUIVANT
	LEA	400(A5),A5
	SUBQ.B	#1,D4
	BRA.S	RETURN2

FINITO	DBRA	D7,ALL_POINTS

	CMPI.W	#$1234,4(A0)
	BEQ.S	FINI
	ADDQ.W	#4,A0
	MOVE.L	A0,PT_POLY
FINI
	LEA	COTE_GAUCHE,A1
	LEA	COTE_DROIT,A2
	LEA	FLAG_COTE_GAUCHE,A0
	MOVE.L	SCREEN2,A5
	MOVE.W	#200-1,D6
.ALL_Y	TST.B	(A0)
	BEQ	.NEXT_BIPOINT
	MOVE.B	(A0),D0
	CMP.B	200(A0),D0
	BEQ.S	.TOUT_EST_OK
	BGT.S	.PLUS_GRAND
	MOVE.B	(A0),200(A0)
	BRA.S	.TOUT_EST_OK
.PLUS_GRAND	MOVE.B	200(A0),(A0)
.TOUT_EST_OK	CMPI.B	#1,(A0)
	BGT.S	.PLUSIEURS
	MOVE.W	(A1),D0
	CMP.W	(A2),D0
	BLT.S	.UN_BIPOINT
	MOVE.W	(A2),D1
	MOVE.W	D1,(A1)
	MOVE.W	D0,(A2)
	BRA.S	.UN_BIPOINT

.PLUSIEURS	MOVEQ	#0,D7
	MOVE.B	(A0),D7	;NB DE BIPOINTS( >=2 )
	SUBQ.W	#1,D7	;NB DE BIPOINTS A TESTER

.RETOUR_ZERO	MOVE.W	D7,D3
	MOVE.L	A1,A3
.NEXT_TEST	MOVE.W	(A3),D4
	MOVE.W	400(A3),D5
	CMP.W	D4,D5
	BGT.S	.NORMAL
	MOVE.W	D5,(A3)
	MOVE.W	D4,400(A3)
	BRA.S	.RETOUR_ZERO
.NORMAL	LEA	400(A3),A3
	SUBQ.W	#1,D3
	BGT.S	.NEXT_TEST

.RETOUR_ZERO2	MOVE.W	D7,D3
	MOVE.L	A2,A3
.NEXT_TEST2	MOVE.W	(A3),D4
	MOVE.W	400(A3),D5
	CMP.W	D4,D5
	BGT.S	.NORMAL2
	MOVE.W	D5,(A3)
	MOVE.W	D4,400(A3)
	BRA.S	.RETOUR_ZERO2
.NORMAL2	LEA	400(A3),A3
	SUBQ.W	#1,D3
	BGT.S	.NEXT_TEST2

	MOVE.L	A1,A3
	MOVE.L	A2,A4
.AFF_ALL	MOVE.W	(A3),D0
	MOVE.W	(A4),D1
	JSR	REMP
	LEA	400(A3),A3
	LEA	400(A4),A4
	DBRA	D7,.AFF_ALL
	BRA.S	.NEXT_BIPOINT

.UN_BIPOINT	MOVE.W	(A1),D0	;X1
	MOVE.W	(A2),D1	;X2
	JSR	REMP
.NEXT_BIPOINT	ADDQ.W	#1,A0
	ADDQ.W	#2,A1
	ADDQ.W	#2,A2
	LEA	160(A5),A5
	DBRA	D6,.ALL_Y
	RTS

REMP	MOVE.W	D0,D2
	MOVE.W	D1,D3
	LEA	MOTIF_GAUCHE(PC),A6
	ANDI.W	#15,D0
	ADD.W	D0,D0
	MOVE.W	(A6,D0.W),D0
	LEA	MOTIF_DROIT(PC),A6
	ANDI.W	#15,D1
	ADD.W	D1,D1
	MOVE.W	(A6,D1.W),D1
	ANDI.W	#$FFF0,D2
	LSR.W	#1,D2
	ANDI.W	#$FFF0,D3
	LSR.W	#1,D3
	CMP.W	D2,D3
	BGT.S	.PLUS_GRAND
	EOR.W	D0,D1
	NOT.W	D1
	OR.W	D1,0(A5,D2.W)
	RTS
.PLUS_GRAND	OR.W	D0,0(A5,D2.W)
	OR.W	D1,0(A5,D3.W)
	SUB.W	D2,D3
	LSR.W	#3,D3
	SUBQ.W	#2,D3
	BLT.S	.NO_MORE
.COPY	MOVE.W	#-1,8(A5,D2.W)
	ADDQ.W	#8,D2
	DBRA	D3,.COPY
.NO_MORE	RTS

MOTIF_GAUCHE	DC.W	$FFFF,$7FFF,$3FFF,$1FFF,$FFF,$7FF,$3FF,$1FF
	DC.W	$FF,$7F,$3F,$1F,$F,7,3,1

MOTIF_DROIT	DC.W	$8000,$C000,$E000,$F000,$F800,$FC00,$FE00,$FF00
	DC.W	$FF80,$FFC0,$FFE0,$FFF0,$FFF8,$FFFC,$FFFE,$FFFF

EFFAC_TEMP	LEA	BUF_TEMP,A1
	MOVE.W	#200-1,D5
.EFF	CLR.W	(A1)+
	DBRA	D5,.EFF
	RTS

EFFACE_FLAG	LEA	FLAG_COTE_GAUCHE,A0
	LEA	FLAG_COTE_DROIT,A1
	MOVE.W	#200-1,D5
.EFF	CLR.B	(A0)+
	CLR.B	(A1)+
	DBRA	D5,.EFF

	LEA	COTE_GAUCHE,A0
	LEA	COTE_DROIT,A1
	MOVE.W	#200*8-1,D5
.EFF2	CLR.W	(A0)+
	CLR.W	(A1)+
	DBRA	D5,.EFF2

	RTS

INIT_SYSTEME	CLR.B	$FFFF8260.W

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

	;A0=ADRESSE DE L'IMAGE
DECOMP_PC1	MOVEM.L	D0-A6,-(SP)

	LEA	34(A0),A0
	LEA	BUF_PIC,A2
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEQ	#0,D5
	MOVE.L	A2,A1
RETOUR	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0	;OCTET DE COMMANDE
	BGE.S	RECOP_INTEGRALE
	NEG.B	D0
	MOVE.B	D0,PUT_INC
	MOVE.B	(A0)+,D1
	BTST	#0,D7
	BNE.S	.COPY2	
.COPY	MOVE.B	D1,(A1)+
	SUBQ.W	#1,D0
	BLT.S	.END
.COPY2	MOVE.B	D1,(A1)+
	ADDQ.W	#6,A1
	DBRA	D0,.COPY
.END	BRA.S	REFLEXION

RECOP_INTEGRALE	MOVE.B	D0,PUT_INC
	BTST	#0,D7
	BNE.S	.COPY2	
.COPY	MOVE.B	(A0)+,(A1)+
	SUBQ.W	#1,D0
	BLT.S	.END
.COPY2	MOVE.B	(A0)+,(A1)+
	ADDQ.W	#6,A1
	DBRA	D0,.COPY
.END
REFLEXION
PUT_INC = *+3
	ADDI.B	#$12,D7
	ADDQ.W	#1,D7
	CMPI.B	#40,D7
	BNE.S	PAS_PLAN_SUIV	
	ADDQ.W	#2,A2
	ADDQ.W	#2,D6
	CMPI.W	#8,D6
	BLT.S	PAS_RETOUR_PLAN_1
	LEA	160-8(A2),A2
	ADDQ.W	#1,D5	;UNE LIGNE DE PLUS
	MOVEQ	#0,D6
PAS_RETOUR_PLAN_1
	MOVE.L	A2,A1
	MOVEQ	#0,D7
PAS_PLAN_SUIV	CMPI.W	#200,D5	
	BLT	RETOUR
	MOVEM.L	(SP)+,D0-A6
	RTS

PIC	INCBIN	SHADOW.PC1

POLY	INCBIN	morph_1.COR
	DC.W	$1234

	SECTION	BSS
PT_POLY	DS.L	1
PT_SOV	DS.L	1
FLAG_PUT_IMG	DS.W	1
NB_VBL	DS.W	1
	DS.B	256
BUFFER	DS.B	64000
SCREEN1	DS.L	1
SCREEN2	DS.L	1
BUF_TEMP	DS.W	200
FLAG_COTE_GAUCHE	
	DS.B	200
FLAG_COTE_DROIT	DS.B	200
COTE_GAUCHE	DS.W	200*8
COTE_DROIT	DS.W	200*8
BUF_SOV_GFX	DS.W	147*7*100
BUF_PIC	DS.B	32000