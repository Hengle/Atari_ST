X1	EQU	0
X2	EQU	4
X3	EQU	8
X4	EQU	12
X5	EQU	16
X6	EQU	20
X7	EQU	24
X8	EQU	28

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	MOVE.L	#BUFFER,D0
	CLR.W	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	CLR	CHOICE
	MOVE	#1,CHOICE
	JSR	INIT_ROUT_POLY
	MOVE.L	#RT_COOL,TABLE_ADR_COD+40
	MOVE.L	#RETOUR_POLY,TABLE_ADR_COD+10600
	MOVE	#$777,$FFFF8242.W

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	CLR.W	NB_VBL
	MOVE.W	#$2300,SR

IT_VBL	BSR	VSYNC
	SF	$FFFF8240.W

	BSR	EFFAC

	BSR	AFFICH

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIME
	ST	$FFFF8240.W
.NO_TIME

	BSR	SWAPEC
	BRA	IT_VBL

TIME	DC	0

VSYNC	CMPI.W	#2,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	RTE

FIN	MOVE.L	4.W,A0
	JMP	(A0)

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

EFFAC
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVE.W	#200-1,D7
.ALL
N	SET	0
	REPT	20
	MOVE.W	D0,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBRA	D7,.ALL
	RTS

ADRESSES	
	DC.L	DATAS_LINES
	DC.L	DATAS_LINES+4*40
	DC.L	DATAS_LINES+4*80
	DC.L	DATAS_LINES+4*120
	DC.L	DATAS_LINES+4*160
	DC.L	DATAS_LINES+4*200
	DC.L	DATAS_LINES+4*240
	DC.L	DATAS_LINES+4*280
	DC.L	DATAS_LINES+4*320
	DC.L	DATAS_LINES+4*360

AFFICH
	;MOVE	#$5446,MOD_PLAN
	MOVE	#$4E71,MOD_PLAN

	LEA	ADRESSES,A1
	MOVEQ	#10-1,D7
GERE_ONE	
	MOVE.L	(A1),A0
	CMPI	#$9999,4*6(A0)
	BNE.S	.ROUL
	LEA	DATAS_LINES-4,A0
.ROUL	ADDQ	#4,A0
	MOVE.L	A0,(A1)+
	
	MOVEM.L	D7/A1,-(SP)

	lea	face_en_cours+2,a1
	move.l	(a0),(a1)
	ADD	#160,(A1)+
	ADD	#100,(A1)+

	MOVE	#160,(A1)+
	MOVE	#100,(A1)+

	move.l	4*6(a0),(a1)
	ADD	#160,(A1)+
	ADD	#100,(A1)+

	lea	face_en_cours,a0
	JSR	RT_POLY
	
	MOVEM.L	(SP)+,D7/A1
	DBF	D7,GERE_ONE
	
	RTS
face_en_cours	dC.w	3
	ds.l	3

*** Partie affichage
CHOICE	DC	0	;0=MOVE
			;1=OR
INIT_ROUT_POLY	LEA	BUF_COD_GEN,A0
	LEA	TABLE_ADR_COD,A1

	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D3
.GO	MOVE.L	#RT_COOL,(A1)+
	DBRA	D3,.GO

	MOVE.W	#$FFFF,D0	;1ER MOTIF
	MOVE.W	#$8000,D1	;DERNIER MOTIF
	MOVEQ	#0,D3	;DECALAGE

	MOVEQ	#16-1,D6
.DECAL	
	MOVEQ	#16+1,D5
	ADD.W	#18*16,D5	;D5=LONGUEUR DE LA LIGNE
	ADD.W	D3,D5

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
.COPY_MOVED7	BSR	POZ_ADRESSE
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	SUBI.W	#16,D5
	DBRA	D7,.COPY_MOVED7

	MOVE.W	#16+1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_1
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_1
.PAS_D7_1
	TST	CHOICE
	BNE.S	.OR1
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV1
.OR1	MOVE	#$0054,(A0)+	OR.W #$Imm,(A4)
.MOV1	MOVE.W	D0,(A0)+
.RETOUR_1	MOVE.W	#1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_2
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_2
.PAS_D7_2
	TST	CHOICE
	BNE.S	.OR2
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV2
.OR2	MOVE	#$0055,(A0)+	OR "             "
.MOV2	MOVE.W	D1,(A0)+
.RETOUR_2
	BSR	COPY_RT_COOL

	ASR.W	#1,D1
	ADDQ.W	#1,D3	;LONGUEUR SUIVANT
	DBRA	D6,.DECAL

	LEA	TABLE_ADR_COD+1320,A1

	MOVE.W	#$7FFF,D3	;1ER MOTIF
	MOVEQ	#14,D5

	MOVEQ	#14,D2
ALL_RT	
	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D0
.GO	MOVE.L	#RT_COOL,(A1)+
	DBRA	D0,.GO

	MOVE.W	#1,A3

	MOVEQ	#0,D0
	MOVE.W	D5,D6
LITTLE	BSR	POZ_ADRESSE2
	BSET	D6,D0
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_LITTLE
	MOVE.W	#$3887,(A0)+
	BRA.S	.RETOUR_LITTLE
.PAS_D7_LITTLE
	TST	CHOICE
	BNE.S	.OR3
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV3
.OR3	MOVE	#$0054,(A0)+
.MOV3	MOVE.W	D0,(A0)+

.RETOUR_LITTLE
	BSR	COPY_RT_COOL

	ADDQ.W	#1,A3
	DBRA	D6,LITTLE

	MOVE.W	D3,D0
	MOVE.W	#$8000,D1	;DERNIER MOTIF

	MOVEQ	#16-1,D6
DECAL
	MOVE.W	#18*16,A3
	ADD.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
COPY_MOVED7	BSR	POZ_ADRESSE2
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	LEA	-16(A3),A3
	DBRA	D7,COPY_MOVED7

	MOVE.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3
	BSR	POZ_ADRESSE2
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_3
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_3
.PAS_D7_3
	TST	CHOICE
	BNE.S	.OR4
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV4
.OR4	MOVE	#$0054,(A0)+
.MOV4	MOVE.W	D0,(A0)+
.RETOUR_3	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_4
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_4
.PAS_D7_4
	TST	CHOICE
	BNE.S	.OR5
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV5
.OR5	MOVE	#$0055,(A0)+
.MOV5	MOVE.W	D1,(A0)+
.RETOUR_4
	BSR	COPY_RT_COOL

	ASR.W	#1,D1
	DBRA	D6,DECAL

	SUBQ.W	#1,D5
	LSR.W	#1,D3
	LEA	1280(A1),A1
	DBRA	D2,ALL_RT

*******
TBL4:	LEA	TABLE_A4,A0
	MOVE	#319,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#6-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	D6,(A0)+
	MOVE	(A1),(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVEQ	#11-1,D0
.LAST	MOVE	D6,(A0)+
	MOVE	(A1),(A0)+
	ADDQ	#4,D6
	DBF	D0,.LAST
	;ADDQ	#2,A1

	MOVE	#319+213,D7
.FINT	MOVE	D6,(A0)+
	MOVE	(A1),(A0)+
	DBF	D7,.FINT
TBL2:
	LEA	TABLE_A2,A0
	MOVE	#319,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#6-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	(A1),(A0)+
	MOVE	D6,(A0)
	MOVE	D6,D5
	LSR	#2,D5
	AND	#15,D5
	MULU	#1320,D5
	SUB	D5,(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVEQ	#11-1,D0
.LAST	MOVE	(A1),(A0)+
	MOVE	D6,(A0)
	MOVE	D6,D5
	LSR	#2,D5
	AND	#15,D5
	MULU	#1320,D5
	SUB	D5,(A0)+
	ADDQ	#4,D6
	DBF	D0,.LAST
	;ADDQ	#2,A1

	MOVE	#319+213,D7
.FINT	;CLR	(A0)+
	MOVE	(A1),(A0)+
	MOVE	D6,(A0)
	MOVE	D6,D5
	LSR	#2,D5
	AND	#15,D5
	MULU	#1320,D5
	SUB	D5,(A0)+
	DBF	D7,.FINT
*******************************************
TBL4B:	LEA	TABLE_A4B,A0
	MOVE	#319,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	D6,(A0)+
	MOVE	(A1),(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#319,D7
.FINT	MOVE	#319*4,(A0)+
	MOVE	#152,(A0)+
	;CLR	(A0)+
	DBF	D7,.FINT
TBL2B:
	LEA	TABLE_A2B,A0
	MOVE	#319,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	(A1),(A0)+
	MOVE	D6,(A0)
	MOVE	D6,D5
	LSR	#2,D5
	AND	#15,D5
	MULU	#1320,D5
	SUB	D5,(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#319,D7
.FINT	;CLR	(A0)+
	MOVE	#152,(A0)+
	MOVE	#319*4,(A0)+
	DBF	D7,.FINT
****
	LEA	TABLE_A4B,A0
	MOVE	#319+216,D7
.DEBT2	MOVE	#216*4,(A0)+
	MOVE	#104,(A0)+
	DBF	D7,.DEBT2

	LEA	TABLE_A2B,A0
	MOVE	#319+216,D7
.DEBT3
	MOVE	#104,(A0)+
	MOVE	#216*4,(A0)
	MOVE	#216*4,D5
	LSR	#2,D5
	AND	#15,D5
	MULU	#1320,D5
	SUB	D5,(A0)+
	DBF	D7,.DEBT3

	RTS

MINI_TABLE
N	SET	0
	REPT	20
	DC	N
N	SET	N+8
	ENDR


POZ_ADRESSE2	MOVEM.L	D5/A0,-(SP)
	MOVE.W	A3,D5
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS
	

POZ_ADRESSE	MOVEM.L	D5/A0,-(SP)
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS
	
COPY_RT_COOL	LEA	RT_COOL,A6
	MOVE.W	#LONG_RT_COOL,D7
.COPY	MOVE.W	(A6)+,(A0)+
	DBRA	D7,.COPY
	RTS

RT_COOL	ADD.W	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2

	MOVE.W	(A1)+,D1	2  X2
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1

	MOVE.L	(A3,D1.W),D3	5
	ADD	D3,A5	2
	SWAP	D3	1

	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	ADD	(A2)+,A4	3  OFFSET. A3=>1ER MOT
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE... RUSê, HEIN?
	   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE, ZOU.
	MOVE.L	(A6,D3.W),A2	;5
	JMP	(A2)	;2

;TOTAL:31 NOPS. +58=89 NOPS.
LONG_RT_COOL = ((*-RT_COOL)/2)-1


RT_POLY	MOVE.L	A0,ADR_DEPART

	LEA	2(A0),A1	;A1 POINTE SUR LES POINTS
	MOVE.W	(A1),D3
	SUB.W	4(A1),D3
	MOVE.W	2(A1),D4
	SUB.W	6(A1),D4
	MOVE.W	8(A1),D6
	SUB.W	4(A1),D6
	MOVE.W	$A(A1),D5
	SUB.W	6(A1),D5
	MULS.W	D3,D5
	MULS.W	D6,D4
	SUB.L	D4,D5
	BGT.S	.VISIBLE
	RTS
.VISIBLE
;	LEA	REMP_GAUCHE,A1
;	MOVE	#(500/2)-1,D6
;.EFF_TBX	CLR.L	(A1)+
;	DBF	D6,.EFF_TBX

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7

	MOVE.W	(A0)+,D0	;NB DE POINTS COMPOSANT LA SURFACE
	LEA	BUF_EXAM,A1
	MOVE.L	A0,A2
	SUBQ.W	#1,D0
	MOVE.W	D0,NB_PTS
.COPYING	MOVE.L	(A0)+,(A1)+
	DBRA	D0,.COPYING
	MOVE.L	(A2),(A1)+

	LEA	BUF_EXAM,A0
	LEA	BUF_GAUCHE,A2
	LEA	BUF_DROIT,A3
	ADDQ.W	#4,A0
	MOVE.W	-2(A0),D3	;Y DE REFERENCE
NB_PTS = *+2
	MOVE.W	#$1234,D0
	MOVEQ	#-1,D6
	MOVEQ	#-1,D7
ALL_PTS	MOVE.W	(A0),D1	;X
	MOVE.W	2(A0),D2	;Y
	CMP.W	D2,D3
	BGE.S	ON_MONTE
ON_DESCEND	MOVE.L	-4(A0),(A2)+
	MOVE.L	(A0)+,(A2)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D6
	DBRA	D0,ALL_PTS
	BRA.S	FINITO
ON_MONTE	MOVE.L	-4(A0),(A3)+
	MOVE.L	(A0)+,(A3)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D7
	DBRA	D0,ALL_PTS
FINITO	;ICI LES TABLEAU BUF_GAUCHE ET BUF_DROIT SONT CONSTITUêS
	;D6=NB DE PTS-1 DANS BUF_GAUCHE
	;D7=NB DE PTS-1 DANS BUF_DROIT
	LEA	BUF_GAUCHE,A0
MAKE_REMP_GAUCHE
	LEA	REMP_GAUCHE,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT.S	.NEXT
	SUB.W	D1,D3	;HAUTEUR-1 DU SEGMENT
	BEQ.S	.NEXT

	ADD.W	D1,D1
	ADDA.W	D1,A1

	ADDQ.W	#1,D3
	SUB.W	D0,D2
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#2,D3
	MOVE.W	D0,(A1)+
.PUT	MOVE.W	D0,(A1)+
	DBRA	D3,.PUT
	BRA.S	.NEXT
.PAS_VERTICALE	EXT.L	D2
	MOVEQ	#8,D4
	ASL.L	D4,D2
	DIVS.W	D3,D2
	EXT.L	D2
	SWAP	D2
	ROL.L	#8,D2
	SUBQ.W	#2,D3
	MOVE.W	D0,(A1)+
.MK_X	ADDX.L	D2,D0
	MOVE.W	D0,(A1)+
	DBRA	D3,.MK_X
.NEXT	DBRA	D6,MAKE_REMP_GAUCHE

	LEA	BUF_DROIT,A0
MAKE_REMP_DROIT	LEA	REMP_DROIT,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT.S	.NEXT
	MOVE.W	D1,D4
	SUB.W	D3,D1	;HAUTEUR-1 DU SEGMENT
	BEQ.S	.NEXT

	ADD.W	D4,D4
	ADDA.W	D4,A1

	ADDQ.W	#1,D1
	SUB.W	D0,D2
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#2,D1
	MOVE.W	D0,(A1)
.PUT	MOVE.W	D0,-(A1)
	DBRA	D1,.PUT
	BRA.S	.NEXT
.PAS_VERTICALE	EXT.L	D2
	MOVEQ	#8,D4
	ASL.L	D4,D2
	DIVS.W	D1,D2
	EXT.L	D2
	SWAP	D2
	ROL.L	#8,D2
	SUBQ.W	#2,D1
	MOVE.W	D0,(A1)
.MK_X	ADDX.L	D2,D0
	MOVE.W	D0,-(A1)
	DBRA	D1,.MK_X
.NEXT	DBRA	D7,MAKE_REMP_DROIT

ADR_DEPART= *+2
	LEA	$12345678,A0
	MOVE.W	(A0)+,D7
	SUBQ.W	#1,D7
	MOVE.W	#-1000,D0	;MAXIMUM
	MOVE.W	#20000,D1	;MINIMUM
.TEST_MAX	CMP.W	2(A0),D0
	BGE.S	.FUCK
	MOVE.W	2(A0),D0
.FUCK	CMP.W	2(A0),D1
	BLE.S	.FUCK_2
	MOVE.W	2(A0),D1
.FUCK_2	ADDQ.W	#4,A0
	DBRA	D7,.TEST_MAX
	;D0 = Y MAXIMUM
	;D1 = Y MINIMUM
	CMPI.W	#200,D0
	BLT.S	.OK_Y_MAX
	MOVE.W	#199,D0
.OK_Y_MAX	TST.W	D1
	BGE.S	.OK_Y_MIN
	MOVEQ	#0,D1
.OK_Y_MIN

INIT	LEA	REMP_GAUCHE,A0
	LEA	REMP_DROIT,A1
	ADDQ.W	#1,D0
	ADD.W	D0,D0
	MOVE.W	#15,(A1,D0.W)
	MOVE.W	#25,(A0,D0.W)
	ADD.W	D1,D1
	ADDA.W	D1,A0
	ADDA.W	D1,A1
	MULU.W	#80,D1       ;D1=Y_MIN*160 (POUR ADD ECRAN)

	LEA	TABLE_ADR_COD+40,A6
	MOVE	#-1,D7
	MOVE.L	SCREEN2,D6
MOD_PLAN	NOP
	ADD.W	D1,D6	;...+Y_MIN*160
	SUBI.W	#160,D6
	MOVE.L	#TABLE_A2B+320*4,D4
	MOVE.W	#160,D5
	LEA	TABLE_A4B+320*4,A3
	MOVEQ	#0,D2

	ADD.W	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2
	MOVE.W	(A1)+,D1	2  X2
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1
	MOVE.L	(A3,D1.W),D3	5
	ADD	D3,A5	2
	SWAP	D3	1
	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	ADD	(A2)+,A4	3  OFFSET. A3=>1ER MOT
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE*1320... RUSê, HEIN?   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE*1320, ZOU.
	MOVE.L	(A6,D3.W),A2
	JMP	(A2)

RETOUR_POLY	RTS

CLIPPING_Y	MOVEM.L	D4/D6/A6,-(SP)
	MOVEQ	#0,D5
	LEA	TABLE_Y,A6
	ADD.W	D1,D1
	ADD.W	D3,D3
	MOVEQ	#0,D4
	ADD.W	(A6,D1.W),D4
	ADD.W	(A6,D3.W),D4
	ASR.W	#1,D1
	ASR.W	#1,D3
	TST.W	D4
	BEQ	FIN_NORMALE
	CMPI.W	#-2,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#4,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#-1,D4
	BEQ	CLIP_HAUT
	CMPI.W	#2,D4
	BEQ	CLIP_BAS

CLIP_HAUT_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	BRA.S	SUIT
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1

SUIT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_HAUT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

RIEN_DU_TOUT	MOVE.W	#-1,D5
FIN_NORMALE	MOVEM.L	(SP)+,D4/D6/A6
	RTS

	DCB.W	640,-1
TABLE_Y	DCB.W	200,0
	DCB.W	640,2

DATAS_LINES
 DC 200,0
 DC 199,3
 DC 199,6
 DC 199,9
 DC 199,12
 DC 199,15

 DC 199,18
 DC 198,21
 DC 198,25
 DC 198,28
 DC 197,31
 DC 197,34
 DC 196,37
 DC 195,40
 DC 195,43
 DC 194,46
 DC 193,49
 DC 192,52
 DC 192,55
 DC 191,58
 DC 190,61
 DC 189,64
 DC 188,67
 DC 187,70
 DC 185,73
 DC 184,76
 DC 183,79
 DC 182,82
 DC 180,85
 DC 179,87
 DC 178,90
 DC 176,93
 DC 175,96
 DC 173,99
 DC 172,101
 DC 170,104
 DC 168,107
 DC 167,109
 DC 165,112
 DC 163,115
 DC 161,117
 DC 159,120
 DC 158,122
 DC 156,125
 DC 154,127
 DC 152,129
 DC 150,132
 DC 147,134
 DC 145,136
 DC 143,139
 DC 141,141
 DC 139,143
 DC 136,145
 DC 134,147
 DC 132,150
 DC 129,152
 DC 127,154
 DC 125,156
 DC 122,158
 DC 120,159
 DC 117,161
 DC 115,163
 DC 112,165
 DC 109,167
 DC 107,168
 DC 104,170
 DC 101,172
 DC 99,173
 DC 96,175
 DC 93,176
 DC 90,178
 DC 87,179
 DC 85,180
 DC 82,182
 DC 79,183
 DC 76,184
 DC 73,185
 DC 70,187
 DC 67,188
 DC 64,189
 DC 61,190
 DC 58,191
 DC 55,192
 DC 52,192
 DC 49,193
 DC 46,194
 DC 43,195
 DC 40,195
 DC 37,196
 DC 34,197
 DC 31,197
 DC 28,198
 DC 25,198
 DC 21,198
 DC 18,199
 DC 15,199
 DC 12,199
 DC 9,199
 DC 6,199
 DC 3,199
 DC -1,200
 DC -4,199
 DC -7,199
 DC -10,199
 DC -13,199
 DC -16,199
 DC -19,199
 DC -22,198
 DC -26,198
 DC -29,198
 DC -32,197
 DC -35,197
 DC -38,196
 DC -41,195
 DC -44,195
 DC -47,194
 DC -50,193
 DC -53,192
 DC -56,192
 DC -59,191
 DC -62,190
 DC -65,189
 DC -68,188
 DC -71,187
 DC -74,185
 DC -77,184
 DC -80,183
 DC -83,182
 DC -86,180
 DC -88,179
 DC -91,178
 DC -94,176
 DC -97,175
 DC -100,173
 DC -102,172
 DC -105,170
 DC -108,168
 DC -110,167
 DC -113,165
 DC -116,163
 DC -118,161
 DC -121,159
 DC -123,158
 DC -126,156
 DC -128,154
 DC -130,152
 DC -133,150
 DC -135,147
 DC -137,145
 DC -140,143
 DC -142,141
 DC -144,139
 DC -146,136
 DC -148,134
 DC -151,132
 DC -153,129
 DC -155,127
 DC -157,125
 DC -159,122
 DC -160,120
 DC -162,117
 DC -164,115
 DC -166,112
 DC -168,109
 DC -169,107
 DC -171,104
 DC -173,101
 DC -174,99
 DC -176,96
 DC -177,93
 DC -179,90
 DC -180,87
 DC -181,85
 DC -183,82
 DC -184,79
 DC -185,76
 DC -186,73
 DC -188,70
 DC -189,67
 DC -190,64
 DC -191,61
 DC -192,58
 DC -193,55
 DC -193,52
 DC -194,49
 DC -195,46
 DC -196,43
 DC -196,40
 DC -197,37
 DC -198,34
 DC -198,31
 DC -199,28
 DC -199,25
 DC -199,21
 DC -200,18
 DC -200,15
 DC -200,12
 DC -200,9
 DC -200,6
 DC -200,3
 DC -200,0
 DC -200,-4
 DC -200,-7
 DC -200,-10
 DC -200,-13
 DC -200,-16
 DC -200,-19
 DC -199,-22
 DC -199,-26
 DC -199,-29
 DC -198,-32
 DC -198,-35
 DC -197,-38
 DC -196,-41
 DC -196,-44
 DC -195,-47
 DC -194,-50
 DC -193,-53
 DC -193,-56
 DC -192,-59
 DC -191,-62
 DC -190,-65
 DC -189,-68
 DC -188,-71
 DC -186,-74
 DC -185,-77
 DC -184,-80
 DC -183,-83
 DC -181,-86
 DC -180,-88
 DC -179,-91
 DC -177,-94
 DC -176,-97
 DC -174,-100
 DC -173,-102
 DC -171,-105
 DC -169,-108
 DC -168,-110
 DC -166,-113
 DC -164,-116
 DC -162,-118
 DC -160,-121
 DC -159,-123
 DC -157,-126
 DC -155,-128
 DC -153,-130
 DC -151,-133
 DC -148,-135
 DC -146,-137
 DC -144,-140
 DC -142,-142
 DC -140,-144
 DC -137,-146
 DC -135,-148
 DC -133,-151
 DC -130,-153
 DC -128,-155
 DC -126,-157
 DC -123,-159
 DC -121,-160
 DC -118,-162
 DC -116,-164
 DC -113,-166
 DC -110,-168
 DC -108,-169
 DC -105,-171
 DC -102,-173
 DC -100,-174
 DC -97,-176
 DC -94,-177
 DC -91,-179
 DC -88,-180
 DC -86,-181
 DC -83,-183
 DC -80,-184
 DC -77,-185
 DC -74,-186
 DC -71,-188
 DC -68,-189
 DC -65,-190
 DC -62,-191
 DC -59,-192
 DC -56,-193
 DC -53,-193
 DC -50,-194
 DC -47,-195
 DC -44,-196
 DC -41,-196
 DC -38,-197
 DC -35,-198
 DC -32,-198
 DC -29,-199
 DC -26,-199
 DC -22,-199
 DC -19,-200
 DC -16,-200
 DC -13,-200
 DC -10,-200
 DC -7,-200
 DC -4,-200
 DC -1,-200
 DC 3,-200
 DC 6,-200
 DC 9,-200
 DC 12,-200
 DC 15,-200
 DC 18,-200
 DC 21,-199
 DC 25,-199
 DC 28,-199
 DC 31,-198
 DC 34,-198
 DC 37,-197
 DC 40,-196
 DC 43,-196
 DC 46,-195
 DC 49,-194
 DC 52,-193
 DC 55,-193
 DC 58,-192
 DC 61,-191
 DC 64,-190
 DC 67,-189
 DC 70,-188
 DC 73,-186
 DC 76,-185
 DC 79,-184
 DC 82,-183
 DC 85,-181
 DC 87,-180
 DC 90,-179
 DC 93,-177
 DC 96,-176
 DC 99,-174
 DC 101,-173
 DC 104,-171
 DC 107,-169
 DC 109,-168
 DC 112,-166
 DC 115,-164
 DC 117,-162
 DC 120,-160
 DC 122,-159
 DC 125,-157
 DC 127,-155
 DC 129,-153
 DC 132,-151
 DC 134,-148
 DC 136,-146
 DC 139,-144
 DC 141,-142
 DC 143,-140
 DC 145,-137
 DC 147,-135
 DC 150,-133
 DC 152,-130
 DC 154,-128
 DC 156,-126
 DC 158,-123
 DC 159,-121
 DC 161,-118
 DC 163,-116
 DC 165,-113
 DC 167,-110
 DC 168,-108
 DC 170,-105
 DC 172,-102
 DC 173,-100
 DC 175,-97
 DC 176,-94
 DC 178,-91
 DC 179,-88
 DC 180,-86
 DC 182,-83
 DC 183,-80
 DC 184,-77
 DC 185,-74
 DC 187,-71
 DC 188,-68
 DC 189,-65
 DC 190,-62
 DC 191,-59
 DC 192,-56
 DC 192,-53
 DC 193,-50
 DC 194,-47
 DC 195,-44
 DC 195,-41
 DC 196,-38
 DC 197,-35
 DC 197,-32
 DC 198,-29
 DC 198,-26
 DC 198,-22
 DC 199,-19
 DC 199,-16
 DC 199,-13
 DC 199,-10
 DC 199,-7
 DC 199,-4
 DC 200,-1

 DC 200,0
 DC 199,3
 DC 199,6
 DC 199,9
 DC 199,12
 DC 199,15
 DC $9999,$9999
	SECTION	BSS
;BSS gÇnÇrale
	DS.B	65536
BUFFER	DS.B	32000*2
	DS.B	100
SCREEN1	DS.L	1
SCREEN2	DS.L	1
NB_VBL	DS.W	1
;Routine d'affichage
TABLE_A4	DS.L	320*3
TABLE_A2	DS.L	320*3
TABLE_A4B	DS.L	320*3
TABLE_A2B	DS.L	320*3
BUF_EXAM	DS.W	2*50
BUF_GAUCHE	DS.W	4*50
BUF_DROIT	DS.W	4*50
REMP_GAUCHE	DS.W	250
REMP_DROIT	DS.W	250
TABLE_ADR_COD	DS.B	(1280+40)*16
BUF_COD_GEN	DS.B	39000
