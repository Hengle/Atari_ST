X1	EQU	0
X2	EQU	4
X3	EQU	8
X4	EQU	12
X5	EQU	16
X6	EQU	20
X7	EQU	24
X8	EQU	28

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP
	JMP	DêBUT
********
unpack:	moveq	#0,d0
	movem.l	d0-a6,-(sp)
	lea	sp3_53(pc),a6
	movea.l	a0,a1
	cmpi.l	#'SPv3',(a1)+
	bne.s	sp3_02
	tst.w	(a1)
	bne.s	sp3_02
	move.l	(a1)+,d5
	move.l	(a1)+,d0
	move.l	(a1)+,(sp)
	movea.l	a0,a2
	adda.l	d0,a0
	move.l	-(a0),-(a1)
	move.l	-(a0),-(a1)
	move.l	-(a0),-(a1)
	move.l	-(a0),-(a1)
	adda.l	(sp),a1
	lea	sp3_58-sp3_53(a6),a3
	moveq	#128-1,d0
sp3_01:	move.l	(a2)+,(a3)+
	dbf	d0,sp3_01
	suba.l	a2,a3
	move.l	a3,-(sp)
	bsr.s	sp3_03
	bsr	sp3_21
	move.b	-(a0),d0
	adda.l	(sp)+,a0
	move.b	d0,(a0)+
	lea	sp3_58-sp3_53(a6),a2
	bsr	sp3_22
	bsr	sp3_15
sp3_02:	movem.l	(sp)+,d0-a6
	rts
sp3_03:	move.w	SR,d1
	andi.w	#$2000,d1
	beq.s	sp3_04
	move.w	$FFFF8240.W,2(a6)
	btst	#1,$FFFF8260.W
	bne.s	sp3_04
	swap	d5
sp3_04:	clr.w	d5
	move.w	-(a0),d6
	lea	sp3_54-sp3_53(a6),a3
	move.b	d6,(a3)+
	moveq	#1,d3
	moveq	#6,d4
sp3_05:	cmp.b	d6,d3
	bne.s	sp3_06
	addq.w	#2,d3
sp3_06:	move.b	d3,(a3)+
	addq.w	#2,d3
	dbf	d4,sp3_05
	moveq	#$10,d4
	move.b	-(a0),(a3)+
	move.b	d4,(a3)+
	move.b	-(a0),(a3)+
	move.b	d4,(a3)+
	move.b	-(a0),d4
	move.w	d4,(a6)
	lea	sp3_57-sp3_53(a6),a5
	move.b	-(a0),d4
	lea	1(a5,d4.w),a3
sp3_07:	move.b	-(a0),-(a3)
	dbf	d4,sp3_07
	move.b	-(a0),-(a3)
	beq.s	sp3_08
	suba.w	d4,a0
sp3_08:	moveq	#0,d2
	move.b	-(a0),d2
	move.w	d2,d3
	move.b	-(a0),d7
sp3_09:	bsr.s	sp3_10
	bsr.s	sp3_10
	dbf	d2,sp3_09
	rts
sp3_10:	not.w	d4
	add.b	d7,d7
	bne.s	sp3_11
	move.b	-(a0),d7
	addx.b	d7,d7
sp3_11:	bcs.s	sp3_12
	move.w	d2,d0
	subq.w	#1,d3
	sub.w	d3,d0
	add.w	d0,d0
	add.w	d4,d0
	add.w	d0,d0
	neg.w	d0
	move.w	d0,-(a3)
	rts
sp3_12:	moveq	#2,d1
	bsr	sp3_44
	add.w	d0,d0
	beq.s	sp3_13
	move.b	d0,-(a3)
	moveq	#2,d1
	bsr	sp3_44
	add.w	d0,d0
	move.b	d0,-(a3)
	rts
sp3_13:	moveq	#2,d1
	bsr	sp3_44
	move.w	sp3_55-sp3_53(a6),d1
	add.w	d0,d0
	beq.s	sp3_14
	move.w	sp3_55+2-sp3_53(a6),d1
sp3_14:	or.w	d1,d0
	move.w	d0,-(a3)
	rts
sp3_15:	move.w	SR,d1
	andi.w	#$2000,d1
	beq.s	sp3_16
	move.w	2(a6),$FFFF8240.W
sp3_16:	tst.w	d6
	bpl.s	sp3_20
	movea.l	a1,a2
	movea.l	a1,a3
	adda.l	4(sp),a3
sp3_17:	moveq	#3,d6
sp3_18:	move.w	(a2)+,d0
	moveq	#3,d5
sp3_19:	add.w	d0,d0
	addx.w	d1,d1
	add.w	d0,d0
	addx.w	d2,d2
	add.w	d0,d0
	addx.w	d3,d3
	add.w	d0,d0
	addx.w	d4,d4
	dbf	d5,sp3_19
	dbf	d6,sp3_18
	cmpa.l	a2,a3
	blt.s	sp3_20
	movem.w	d1-d4,-8(a2)
	cmpa.l	a2,a3
	bne.s	sp3_17
sp3_20:	rts
sp3_21:	move.b	-(a0),-(a1)
sp3_22:	swap	d5
	beq.s	sp3_23
	move.w	d5,$FFFF8240.W
sp3_23:	lea	sp3_56+2-sp3_53(a6),a3
	cmpa.l	a0,a2
	blt.s	sp3_25
	rts
sp3_24:	adda.w	d3,a3
sp3_25:	add.b	d7,d7
	bcc.s	sp3_28
	beq.s	sp3_27
sp3_26:	move.w	(a3),d3
	bmi.s	sp3_24
	bra.s	sp3_29
sp3_27:	move.b	-(a0),d7
	addx.b	d7,d7
	bcs.s	sp3_26
sp3_28:	move.w	-(a3),d3
	bmi.s	sp3_24
sp3_29:	ext.w	d3
	jmp	sp3_30(pc,d3.w)
sp3_30:	bra.s	sp3_30
	bra.s	sp3_41
	bra.s	sp3_41
	bra.s	sp3_41
	bra.s	sp3_41
	bra.s	sp3_41
	bra.s	sp3_37
	bra.s	sp3_36
	bra.s	sp3_32
	bra.s	sp3_33
	bra.s	sp3_31
	bra.s	sp3_34
	bra.s	sp3_21
sp3_31:	move.b	(a5),-(a1)
	bra.s	sp3_22
sp3_32:	bsr.s	sp3_43
	move.b	1(a5,d0.w),-(a1)
	bra.s	sp3_22
sp3_33:	bsr.s	sp3_43
	add.w	(a6),d0
	move.b	1(a5,d0.w),-(a1)
	bra.s	sp3_22
sp3_34:	moveq	#3,d1
	bsr.s	sp3_44
	lsr.w	#1,d0
	bcc.s	sp3_35
	not.w	d0
sp3_35:	move.b	(a1),d1
	add.w	d0,d1
	move.b	d1,-(a1)
	bra.s	sp3_22
sp3_36:	lea	sp3_52-2-sp3_53(a6),a4
	bsr.s	sp3_48
	addi.w	#16,d0
	lea	1(a1,d0.w),a3
	move.b	-(a3),-(a1)
	move.b	-(a3),-(a1)
	bra	sp3_22
sp3_37:	moveq	#3,d1
	bsr.s	sp3_44
	tst.w	d0
	beq.s	sp3_38
	addq.w	#5,d0
	bra.s	sp3_40
sp3_38:	move.b	-(a0),d0
	beq.s	sp3_39
	addi.w	#20,d0
	bra.s	sp3_40
sp3_39:	moveq	#13,d1
	bsr.s	sp3_44
	addi.w	#276,d0
sp3_40:	move.w	d0,d3
	add.w	d3,d3
sp3_41:	lea	sp3_52-sp3_53(a6),a4
	bsr.s	sp3_48
	lsr.w	#1,d3
	lea	1(a1,d0.w),a3
	move.b	-(a3),-(a1)
sp3_42:	move.b	-(a3),-(a1)
	dbf	d3,sp3_42
	bra	sp3_22
sp3_43:	moveq	#0,d1
	move.b	(a3),d1
sp3_44:	moveq	#0,d0
	cmpi.w	#7,d1
	bpl.s	sp3_47
sp3_45:	add.b	d7,d7
	beq.s	sp3_46
	addx.w	d0,d0
	dbf	d1,sp3_45
	rts
sp3_46:	move.b	-(a0),d7
	addx.b	d7,d7
	addx.w	d0,d0
	dbf	d1,sp3_45
	rts
sp3_47:	move.b	-(a0),d0
	subq.w	#8,d1
	bpl.s	sp3_45
	rts
sp3_48:	moveq	#0,d1
	move.b	(a3),d1
	adda.w	d1,a4
	move.w	(a4),d1
	bsr.s	sp3_44
	tst.b	d6
	beq.s	sp3_51
	move.w	d0,d4
	andi.w	#$FFF0,d4
	andi.w	#$000F,d0
	beq.s	sp3_50
	lsr.w	#1,d0
	beq.s	sp3_49
	roxr.b	#1,d7
	bcc.s	sp3_50
	move.b	d7,(a0)+
	moveq	#-128,d7
	bra.s	sp3_50
sp3_49:	moveq	#2,d1
	bsr.s	sp3_44
	add.w	d0,d0
	or.w	d4,d0
	bra.s	sp3_51
sp3_50:	lea	sp3_54-sp3_53(a6),a3
	or.b	(a3,d0.w),d4
	move.w	d4,d0
sp3_51:	add.w	18(a4),d0
	rts

	DC.W	3
sp3_52:	DC.W	4,5,7,8,9,10,11,12
	DC.W	-16
	DC.W	0,32,96,352,864,1888,3936,8032

sp3_53:	DS.L	1
sp3_54:	DS.B	8
sp3_55:	DS.W	2*64
sp3_56:	DS.W	2
	DS.B	1
sp3_57:	DS.B	1
	DS.B	2*64
sp3_58:	DS.B	512

****************************************************************
DêBUT	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	LEA	DEB_TY,A0
	MOVE	#2*640-1,D7
.R1	MOVE	#-1,(A0)+
	DBF	D7,.R1
	MOVE	#200-1,D7
.R2	CLR	(A0)+
	DBF	D7,.R2
	MOVE	#2*640-1,D7
.R3	MOVE	#2,(A0)+
	DBF	D7,.R3
**
	MOVE	#$777,$FFFF8242.W
	MOVE	#$333,$FFFF8244.W
	MOVE	#$333,$FFFF8246.W
	MOVE	#$000,$FFFF8248.W
	MOVE	#$444,$FFFF824A.W
	MOVE	#$333,$FFFF824C.W
	MOVE	#$333,$FFFF824E.W
	MOVE.L	#$07000700,D0
	MOVE.L	D0,$FFFF8250.W
	MOVE.L	D0,$FFFF8254.W
	MOVE.L	D0,$FFFF8258.W
	MOVE.L	D0,$FFFF825C.W

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	CLR.W	NB_VBL
	MOVE.W	#$2300,SR

	JSR	PREP_ALL
	JSR	DêZOOM
	CLR	TIME
PLAQUE	CLR	CHOICE
	JSR	INIT_ROUT_POLY
	MOVE.L	#RT_COOL,TABLE_ADR_COD+40
	MOVE.L	#VBL_PLAQUE,$70.W
BOUCLE	CMPI	#50,TIME
	BNE.S	BOUCLE

	JSR	COPY_LOGO

	LEA	ETAP1,A0
	LEA	BUF,A1
	MOVE	(A0)+,D7
.COP_E1	MOVE.L	(A0)+,(A1)+
	DBF	D7,.COP_E1
	LEA	BUF,A0
	JSR	unpack
BOUCLE2	BRA.S	BOUCLE2

	MOVE.L	#$07770777,D0
	MOVE.L	D0,$FFFF8240.W
	MOVE.L	D0,$FFFF8244.W
	MOVE.L	D0,$FFFF8248.W
	MOVE.L	D0,$FFFF824C.W
	MOVE.L	D0,$FFFF8250.W
	MOVE.L	D0,$FFFF8254.W
	MOVE.L	D0,$FFFF8258.W
	MOVE.L	D0,$FFFF825C.W

	MOVE.L	#VBL_ZIK,$70.W

	LEA	BUF,A0
	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
	MOVE	#7999,D7
.COPE1	MOVE.L	(A0),(A1)+
	MOVE.L	(A0)+,(A2)+
	DBF	D7,.COPE1

	MOVE.L	#VBL_FAD1,$70.W
BOUCLE5	BRA.S	BOUCLE5
	MOVEQ	#0,D0
	MOVE.L	D0,$FFFF8250.W
	MOVE.L	D0,$FFFF8254.W
	MOVE.L	D0,$FFFF8258.W
	MOVE.L	D0,$FFFF825C.W
	MOVE.L	#VBL_MORPH,$70.W
	
	LEA	ETAP2,A0
	LEA	BUF,A1
	MOVE	(A0)+,D7
.COP_E2	MOVE	(A0)+,(A1)+
	DBF	D7,.COP_E2
	LEA	BUF,A0
	JSR	unpack

BOUCLE3	BRA.S	BOUCLE3
	
	MOVE.L	#$07770777,D0
	MOVE.L	D0,$FFFF8240.W
	MOVE.L	D0,$FFFF8244.W
	MOVE.L	D0,$FFFF8248.W
	MOVE.L	D0,$FFFF824C.W
	MOVE.L	D0,$FFFF8250.W
	MOVE.L	D0,$FFFF8254.W
	MOVE.L	D0,$FFFF8258.W
	MOVE.L	D0,$FFFF825C.W
	MOVE.L	#VBL_ZIK,$70.W
	LEA	BUF,A0
	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
	MOVE	#7999,D7
.COPE2	MOVE.L	(A0),(A1)+
	MOVE.L	(A0)+,(A2)+
	DBF	D7,.COPE2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	CLR	MOD_FAD1+2
	MOVE.L	#VBL_FAD1,$70.W
	JSR	PREP_RAY
	MOVE.L	#VBL_RAY,$70.W
BOUCLE4	BRA.S	BOUCLE4
VBL_RAY	MOVEM.L	D0-A6,-(SP)
	MOVE.L	SCREEN2,A1
	LEA	14*160+64(A1),A1
	LEA	BUF,A0
MOD_BUF2	EQU	*+2
	ADDA.L	#0,A0
	MOVE	#146-1,D7
.PREC	MOVEM.L	(A0)+,D0-D6/A2
	MOVEM.L	D0-D6/A2,(A1)
	LEA	160(A1),A1
	DBF	D7,.PREC
	ADD.L	#146*32,MOD_BUF2
	CMPI.L	#146*32*79,MOD_BUF2
	BNE.S	.ROOL
	CLR.L	MOD_BUF2
.ROOL	
	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN2+1,$FFFF8201.W
	MOVE.B	SCREEN2+2,$FFFF8203.W
	MOVEM.L	(SP)+,D0-A6
	RTE

PREP_RAY
LOOP_RAY
	MOVE.L	SCREEN2,A0
	MOVE.L	SCREEN1,A1
	;LEA	ETAP2+34,A1
	MOVE	#7999,D7
COPIMG	MOVE.L	(A1)+,(A0)+
	DBF	D7,COPIMG
	
	MOVE.L	MODMOT,SAVE

	LEA	CRB,A4
	MOVE.L	SCREEN2,A1
	LEA	14*160+64(A1),A1
	MOVE.L	A1,A2
	MOVE	#146-1,D7
	MOVEQ	#0,D6
AFF_IMG
	LEA	MOTIFS,A6
MODMOT	EQU	*+2
	LEA	4*14(A6),A6
	LEA	TABLE_IMG,A5	13 PIX A ECRIRE/LIGNE
	REPT	13+3
	MOVE.L	(A5)+,A0
	MOVE	(A6)+,D0
	MOVE	(A6)+,D2	OFFSET
	ADD.L	D6,A0
	MOVE	(A0,D2.W),D1
	AND	D0,D1
	OR	D1,(A1,D2.W)
	MOVE	2(A0,D2.W),D1
	AND	D0,D1
	OR	D1,2(A1,D2.W)
	MOVE	4(A0,D2.W),D1
	AND	D0,D1
	OR	D1,4(A1,D2.W)
	MOVE	6(A0,D2.W),D1
	AND	D0,D1
	OR	D1,6(A1,D2.W)
	ADDQ	#8,A0
	ENDR
	ADD.L	#160,D6
	LEA	160(A2),A2
	MOVE.L	A2,A1
	MOVE	(A4)+,D0
	ADD	D0,MODMOT
	DBF	D7,AFF_IMG
	
SAVE	EQU	*+2
	MOVE.L	#0,MODMOT
	
	ADDQ	#4,MODMOT

	MOVE.L	SCREEN2,A1
	LEA	14*160+64(A1),A1
	LEA	BUF,A0
MOD_BUF_RAY	EQU	*+2
	ADDA.L	#0,A0
	MOVE	#146-1,D7
PREC	MOVEM.L	(A1),D0-D6/A2
	MOVEM.L	D0-D6/A2,(A0)
	LEA	160(A1),A1
	LEA	32(A0),A0
	DBF	D7,PREC
	ADD.L	#146*32,MOD_BUF_RAY
	CMPI.L	#146*32*79,MOD_BUF_RAY
	BEQ.S	SUITE_RAY
	JMP	LOOP_RAY
SUITE_RAY

	MOVE.L	SCREEN2,A0
	MOVE.L	SCREEN1,A1
	MOVE	#7999,D7
COPIMGU	MOVE.L	(A1)+,(A0)+
	DBF	D7,COPIMGU

	RTS


VBL_ZIK
	MOVEM.L	D0-A6,-(SP)
	MOVEM.L	(SP)+,D0-A6
	RTE
COPY_LOGO	LEA	LOGO_1P,A0

	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
	LEA	160*(166-40)(A1),A1
	LEA	160*(166-40)(A2),A2
	MOVEQ	#0,D0
	MOVE	#39-1,D7
.EFFLOGO
N	SET	0
	REPT	20
	MOVE	D0,N(A1)
	MOVE	D0,N(A2)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	LEA	160(A2),A2
	DBF	D7,.EFFLOGO

	MOVE	#29-1,D7
.COPLOGO	
N	SET	0
	REPT	20
	MOVE	(A0),N(A1)
	MOVE	(A0)+,N(A2)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	LEA	160(A2),A2
	DBF	D7,.COPLOGO
	RTS

FAD1	
	DC	$777,$777,$777,$777,$777,$777,$777,$777
	DC	$666,$777,$666,$666,$666,$666,$666,$666
	DC	$555,$777,$666,$555,$555,$555,$555,$555
	DC	$444,$777,$666,$555,$444,$444,$444,$444
	DC	$333,$777,$666,$555,$444,$333,$333,$333
	DC	$222,$777,$666,$555,$444,$333,$222,$222
	DC	$111,$777,$666,$555,$444,$333,$222,$111
	DC	$000,$777,$666,$555,$444,$333,$222,$111
	DC	$1234
VBL_FAD1	MOVEM.L	D0-A6,-(SP)
	LEA	FAD1,A0
MOD_FAD1	LEA	0(A0),A0
	CMPI	#$1234,(A0)
	BEQ.S	.RIEN
	MOVEM.L	(A0),D0-D3
	MOVEM.L	D0-D3,$FFFF8240.W
	ADD	#4*4,MOD_FAD1+2
	MOVEM.L	(SP)+,D0-A6
	RTE
.RIEN	MOVE	#$4E71,BOUCLE5
	MOVEM.L	(SP)+,D0-A6
	RTE

VBL_PLAQUE	MOVEM.L	D0-A6,-(SP)
	SF	$FFFF8240.W

	MOVE.L	SCREEN2,A0
	ADDQ	#2,A0
	MOVEQ	#0,D0
	MOVE.W	#200-1,D7
.ALL
N	SET	0
	REPT	20
	MOVE.W	D0,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBRA	D7,.ALL

	MOVE	#$5446,MOD_PLAN
	LEA	END_BUFBUF-32,A0
MODBUFPL	LEA	0(A0),A0
	CMPI	#$9999,30(A0)
	BNE.S	.OK
	MOVE	#$4E71,BOUCLE2
	ADD	#32,MODBUFPL+2
	LEA	32(A0),A0
.OK
	MOVE.L	A0,MODA0

	lea	face_en_cours+2,a1
	move.l	X1(a0),(a1)+
	move.l	X2(a0),(a1)+
	move.l	X3(a0),(a1)+
	move.l	X4(a0),(a1)+
	lea	face_en_cours,a0
	JSR	RT_POLY

MODA0	EQU	*+2
	lea	BUFBUF,a0
	lea	face_en_cours+2,a1
	move.l	X5(a0),(a1)+
	move.l	X6(a0),(a1)+
	move.l	X7(a0),(a1)+
	move.l	X8(a0),(a1)+
	lea	face_en_cours,a0
	JSR	RT_POLY
	SUB	#32,MODBUFPL+2

	ADDQ	#1,TIME
	CMPI	#220,TIME
	BGE	NO_S
	MOVE.L	SCREEN2,A0
	LEA	160*166(A0),A0
	LEA	8-160*16(A0),A1
	MOVE	#28,D7
DO_SHADE
N	SET	0
	REPT	19
	MOVE	2+N(A1),4+N(A0)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	LEA	160(A0),A0
	DBF	D7,DO_SHADE
NO_S

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIME
	ST	$FFFF8240.W
.NO_TIME

	BSR	SWAPEC
	CMPI	#220,TIME
	BNE.S	NO_CHG
	MOVE	#$777,$FFFF8246.W
	MOVE.L	SCREEN2,A0
	LEA	160*160(A0),A0
	MOVE	#28,D7
	MOVEQ	#0,D6
EFF_SHADE
N	SET	0
	REPT	19
	MOVE	D6,4+N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D7,EFF_SHADE

NO_CHG
	MOVEM.L	(SP)+,D0-A6
	RTE

*****


PREP_ALL
	MOVE	#1,CHOICE
	JSR	INIT_ROUT_POLY
	MOVE.L	#RT_COOL,TABLE_ADR_COD+40

	LEA	DONNEES,A0
	MOVE	DONNEE,D0
	REPT	9
	MOVE	D0,(A0)+
	ENDR

IT_VBL	
	SF	$FFFF8240.W

	LEA	TEMPOS,A0
	LEA	FLAGS,A1
	MOVEQ	#8,D7
.GERE_FLAGS	MOVE	(A0),D0
	BEQ.S	.OK
	CMPI	#1,D0
	BNE.S	.OK2
	CLR	(A1)
.OK2	SUBQ	#1,D0
.OK	MOVE	D0,(A0)+
	ADDQ	#2,A1
	DBF	D7,.GERE_FLAGS

	BSR	EFFAC

	BSR	AFFICH

	LEA	BUF,A0
MODBUF	EQU	*+2
	ADDA.L	#0,A0
	MOVE.L	SCREEN2,A1
	MOVE	#(20*200)-1,D7
.COP	MOVE	(A1),(A0)+
	ADDQ	#8,A1
	DBF	D7,.COP
	ADD.L	#8000,MODBUF
	CMPI.L	#8000*66,MODBUF
	BNE.S	.PREC
	BRA.S	SUITE
.PREC	BSR	SWAPEC
	ADDQ	#1,TIME
	;MOVE	TIME,$FFFFC
	BRA	IT_VBL
SUITE	
	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	MOVE	#7999,D7
.KILL_EM	CLR.L	(A0)+
	CLR.L	(A1)+
	DBF	D7,.KILL_EM

INIT_MORPH	MOVE.W	POLY,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	MULU.W	#0,D0
	LEA	POLY+2(PC),A0
	ADDA.W	D0,A0
	MOVE.L	A0,PT_POLY

	MOVE.L	#BUF_SOV_GFX,PT_SOV

	MOVE.W	#90-1,D7
.MK_ALL	MOVE.W	D7,SOV
	BSR	EFFAC_MORPH
	BSR	EFFACE_FLAG
	BSR	AFFICHE_POLY
	BSR	SOV_GFX
	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
SOV = *+2
	MOVE.W	#$1234,D7
	DBRA	D7,.MK_ALL
	CLR.W	FLAG_PUT_IMG
	CLR.W	PT_SOV
	RTS
VBL_MORPH	MOVEM.L	D0-A6,-(SP)
	CLR.W	$FFFF8240.W
	BSR	PUT_GFX
	BSR	SWAPEC
	MOVEM.L	(SP)+,D0-A6
	RTE

PUT_GFX	MOVE.W	PT_SOV,D0
	MULU.W	#14*147,D0
	MOVE.L	SCREEN2,A1
	LEA	160*11+6(A1),A1
	LEA	BUF_SOV_GFX,A0
	ADDA.L	D0,A0
	MOVE.W	#200-11-42-1,D7
N	SET	56
.AFF	REPT	7
	MOVE.W	(A0)+,N(A1)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	DBRA	D7,.AFF
	MOVE.W	PT_SOV,D0
	ADDQ.W	#1,D0
	CMPI.W	#90,D0
	BLT	.OK
	MOVE	#$4E71,BOUCLE3
	MOVE.W	#-1,FLAG_PUT_IMG
	MOVEQ	#89,D0
.OK	MOVE.W	D0,PT_SOV
	RTS

SOV_GFX	MOVE.L	SCREEN2,A0
	LEA	160*11(A0),A0
	MOVE.L	PT_SOV,A1
	MOVE.W	#200-11-42-1,D7
N	SET	56
.SOV	REPT	7
	MOVE.W	N(A0),(A1)+
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBRA	D7,.SOV
	MOVE.L	A1,PT_SOV
	RTS

EFFAC_MORPH	MOVE.L	SCREEN2,A0
	LEA	160*11(A0),A0
	MOVEQ	#0,D0
	MOVE.W	#200-11-42-1,D7
N	SET	56
.EFF	REPT	7
	MOVE.W	D0,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBRA	D7,.EFF
	RTS

AFFICHE_POLY	MOVE.L	PT_POLY,A0
	MOVE.W	POLY(PC),D7	;NB DE POINTS
	SUBQ.W	#2,D7

ALL_POINTS	BSR	EFFAC_TEMP
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVE.W	(A0),D2	;X2
	MOVE.W	2(A0),D3	;Y2
	CMP.W	D1,D3
	BGT	Y2_SUP_Y1
	BEQ	FINITO	;LIGNE HORIZONTALE
Y2_INF_Y1	MOVEQ	#12,D4
	SUB.W	D2,D0
	SUB.W	D3,D1
	EXT.L	D0
	ASL.L	D4,D0
	ADDQ.W	#1,D1
	DIVS.W	D1,D0
	SUBQ.W	#1,D1
	EXT.L	D0

	LEA	BUF_TEMP,A1
	;D3=Y DE DEPART DU TRACE DE LIGNE
	MOVE.W	D1,D6	;ON SAUVE LA HAUTEUR
	MOVE.W	D2,(A1)+
	TST.W	D1
	BEQ	FINITO
	SUBQ.W	#1,D1
	ASL.L	D4,D2
CALC_ALL_THE_LINE_MONTE
	ADD.L	D0,D2
	MOVE.L	D2,D5
	ASR.L	D4,D5
	MOVE.W	D5,(A1)+
	DBRA	D1,CALC_ALL_THE_LINE_MONTE

	LEA	FLAG_COTE_GAUCHE,A3
	ADDA.W	D3,A3
	LEA	BUF_TEMP,A1
	LEA	COTE_GAUCHE,A2
	ADD.W	D3,D3
	ADDA.W	D3,A2
POZ_ALL	MOVE.L	A2,A5
	MOVE.B	(A3),D4
RETURN	MOVE.W	(A5),D5
	CMP.W	(A1),D5
	BEQ.S	LES_MEME
	TST.B	D4
	BNE.S	BUFFER_GAUCHE_SUIVANT
	ADDQ.B	#1,(A3)
LES_MEME	MOVE.W	(A1)+,(A5)
	ADDQ.W	#2,A2
	ADDQ.W	#1,A3
	DBRA	D6,POZ_ALL
	BRA	FINITO

BUFFER_GAUCHE_SUIVANT
	LEA	400(A5),A5
	SUBQ.B	#1,D4
	BRA.S	RETURN

Y2_SUP_Y1	MOVEQ	#12,D4
	SUB.W	D0,D2
	SUB.W	D1,D3
	EXT.L	D2
	ASL.L	D4,D2
	ADDQ.W	#1,D3
	DIVS.W	D3,D2	;D2=PAS_X (.L)
	SUBQ.W	#1,D3
	EXT.L	D2

	LEA	BUF_TEMP,A1
	;D1=Y DE DEPART DU TRACE DE LIGNE
	MOVE.W	D0,(A1)+
	MOVE.W	D3,D6	;ON SAUVE LA HAUTEUR
	TST.W	D3
	BEQ.S	FINITO
	SUBQ.W	#1,D3
	ASL.L	D4,D0
CALC_ALL_THE_LINE_DESC
	ADD.L	D2,D0
	MOVE.L	D0,D5
	ASR.L	D4,D5
	MOVE.W	D5,(A1)+
	DBRA	D3,CALC_ALL_THE_LINE_DESC

	LEA	FLAG_COTE_DROIT,A3
	ADDA.W	D1,A3
	LEA	BUF_TEMP,A1
	LEA	COTE_DROIT,A2
	ADD.W	D1,D1
	ADDA.W	D1,A2
POZ_ALL2	MOVE.L	A2,A5
	MOVE.B	(A3),D4
RETURN2	MOVE.W	(A5),D5
	CMP.W	(A1),D5
	BEQ.S	LES_MEME2
	TST.B	D4
	BNE.S	BUFFER_DROIT_SUIVANT
	ADDQ.B	#1,(A3)
LES_MEME2	MOVE.W	(A1)+,(A5)
	ADDQ.W	#2,A2
	ADDQ.W	#1,A3
	DBRA	D6,POZ_ALL2
	BRA	FINITO

BUFFER_DROIT_SUIVANT
	LEA	400(A5),A5
	SUBQ.B	#1,D4
	BRA.S	RETURN2

FINITO	DBRA	D7,ALL_POINTS

	CMPI.W	#$1234,4(A0)
	BEQ.S	FINI
	ADDQ.W	#4,A0
	MOVE.L	A0,PT_POLY
FINI
	LEA	COTE_GAUCHE,A1
	LEA	COTE_DROIT,A2
	LEA	FLAG_COTE_GAUCHE,A0
	MOVE.L	SCREEN2,A5
	MOVE.W	#200-1,D6
.ALL_Y	TST.B	(A0)
	BEQ	.NEXT_BIPOINT
	MOVE.B	(A0),D0
	CMP.B	200(A0),D0
	BEQ.S	.TOUT_EST_OK
	BGT.S	.PLUS_GRAND
	MOVE.B	(A0),200(A0)
	BRA.S	.TOUT_EST_OK
.PLUS_GRAND	MOVE.B	200(A0),(A0)
.TOUT_EST_OK	CMPI.B	#1,(A0)
	BGT.S	.PLUSIEURS
	MOVE.W	(A1),D0
	CMP.W	(A2),D0
	BLT.S	.UN_BIPOINT
	MOVE.W	(A2),D1
	MOVE.W	D1,(A1)
	MOVE.W	D0,(A2)
	BRA.S	.UN_BIPOINT

.PLUSIEURS	MOVEQ	#0,D7
	MOVE.B	(A0),D7	;NB DE BIPOINTS( >=2 )
	SUBQ.W	#1,D7	;NB DE BIPOINTS A TESTER

.RETOUR_ZERO	MOVE.W	D7,D3
	MOVE.L	A1,A3
.NEXT_TEST	MOVE.W	(A3),D4
	MOVE.W	400(A3),D5
	CMP.W	D4,D5
	BGT.S	.NORMAL
	MOVE.W	D5,(A3)
	MOVE.W	D4,400(A3)
	BRA.S	.RETOUR_ZERO
.NORMAL	LEA	400(A3),A3
	SUBQ.W	#1,D3
	BGT.S	.NEXT_TEST

.RETOUR_ZERO2	MOVE.W	D7,D3
	MOVE.L	A2,A3
.NEXT_TEST2	MOVE.W	(A3),D4
	MOVE.W	400(A3),D5
	CMP.W	D4,D5
	BGT.S	.NORMAL2
	MOVE.W	D5,(A3)
	MOVE.W	D4,400(A3)
	BRA.S	.RETOUR_ZERO2
.NORMAL2	LEA	400(A3),A3
	SUBQ.W	#1,D3
	BGT.S	.NEXT_TEST2

	MOVE.L	A1,A3
	MOVE.L	A2,A4
.AFF_ALL	MOVE.W	(A3),D0
	MOVE.W	(A4),D1
	JSR	REMP
	LEA	400(A3),A3
	LEA	400(A4),A4
	DBRA	D7,.AFF_ALL
	BRA.S	.NEXT_BIPOINT

.UN_BIPOINT	MOVE.W	(A1),D0	;X1
	MOVE.W	(A2),D1	;X2
	JSR	REMP
.NEXT_BIPOINT	ADDQ.W	#1,A0
	ADDQ.W	#2,A1
	ADDQ.W	#2,A2
	LEA	160(A5),A5
	DBRA	D6,.ALL_Y
	RTS

REMP	MOVE.W	D0,D2
	MOVE.W	D1,D3
	LEA	MOTIF_GAUCHE(PC),A6
	ANDI.W	#15,D0
	ADD.W	D0,D0
	MOVE.W	(A6,D0.W),D0
	LEA	MOTIF_DROIT(PC),A6
	ANDI.W	#15,D1
	ADD.W	D1,D1
	MOVE.W	(A6,D1.W),D1
	ANDI.W	#$FFF0,D2
	LSR.W	#1,D2
	ANDI.W	#$FFF0,D3
	LSR.W	#1,D3
	CMP.W	D2,D3
	BGT.S	.PLUS_GRAND
	EOR.W	D0,D1
	NOT.W	D1
	OR.W	D1,0(A5,D2.W)
	RTS
.PLUS_GRAND	OR.W	D0,0(A5,D2.W)
	OR.W	D1,0(A5,D3.W)
	SUB.W	D2,D3
	LSR.W	#3,D3
	SUBQ.W	#2,D3
	BLT.S	.NO_MORE
.COPY	MOVE.W	#-1,8(A5,D2.W)
	ADDQ.W	#8,D2
	DBRA	D3,.COPY
.NO_MORE	RTS

MOTIF_GAUCHE	DC.W	$FFFF,$7FFF,$3FFF,$1FFF,$FFF,$7FF,$3FF,$1FF
	DC.W	$FF,$7F,$3F,$1F,$F,7,3,1

MOTIF_DROIT	DC.W	$8000,$C000,$E000,$F000,$F800,$FC00,$FE00,$FF00
	DC.W	$FF80,$FFC0,$FFE0,$FFF0,$FFF8,$FFFC,$FFFE,$FFFF

EFFAC_TEMP	LEA	BUF_TEMP,A1
	MOVE.W	#200-1,D5
.EFF	CLR.W	(A1)+
	DBRA	D5,.EFF
	RTS

EFFACE_FLAG	LEA	FLAG_COTE_GAUCHE,A0
	LEA	FLAG_COTE_DROIT,A1
	MOVE.W	#200-1,D5
.EFF	CLR.B	(A0)+
	CLR.B	(A1)+
	DBRA	D5,.EFF

	LEA	COTE_GAUCHE,A0
	LEA	COTE_DROIT,A1
	MOVE.W	#200*8-1,D5
.EFF2	CLR.W	(A0)+
	CLR.W	(A1)+
	DBRA	D5,.EFF2

	RTS

DêZOOM
IT_VBL2	BSR	VSYNC
	SF	$FFFF8240.W

	LEA	BUF,A0
MODBUF2	EQU	*+2
	ADDA.L	#0,A0
	MOVE.L	SCREEN2,A1

	MOVE	#200-1,D7
.COP	
N	SET	0
	REPT	20
	MOVE	(A0)+,N(A1)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	DBF	D7,.COP

	ADD.L	#8000,MODBUF2
	CMPI.L	#8000*66,MODBUF2
	BNE.S	.AFF_P
	RTS
	CLR.L	MODBUF2
.AFF_P	BSR	SWAPEC

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIME
	ST	$FFFF8240.W
.NO_TIME	
	BRA	IT_VBL2

TIME	DC	0
DONNEE	DC	1956
VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	RTE
FIN	MOVE.L	4.W,A0
	JMP	(A0)
SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

EFFAC
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVE.W	#200-1,D7
.ALL
N	SET	0
	REPT	20
	MOVE.W	D0,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBRA	D7,.ALL
	RTS

AFFICH
	MOVE	#$4E71,MOD_PLAN

	lea	H_PTS,a0
	LEA	NB_DE_POLYS,A6
	MOVEQ	#9-1,D6

	LEA	DONNEES,A2
	LEA	FLAGS,A3
AFF_A_POLY	MOVE	(A6)+,D7
	SUBQ	#1,D7
AFF_A_LETTER	lea	face_en_cours+2,a1

	MOVE	(A2),D1

	;MOVE	DONNEE,D1
	REPT	4

	MOVE	(A0)+,D0
	SUB	#160,D0
	MULS	D1,D0
	LSR.L	#8,D0
	ADD	#160,D0
	MOVE	D0,(A1)+

	MOVE	(A0)+,D0
	SUB	#100-9,D0
	MULS	D1,D0
	LSR.L	#8,D0
	ADD	#100,D0
	ADD	18(A2),D0
	MOVE	D0,(A1)+

	ENDR
	
	;move.l	(a0)+,(a1)+
	;move.l	(a0)+,(a1)+
	;move.l	(a0)+,(a1)+
	;move.l	(a0)+,(a1)+
	MOVEM.L	D6/D7/A0/A2/A3/A6,-(SP)
	lea	face_en_cours,a0
	JSR	RT_POLY
	MOVEM.L	(SP)+,D6/D7/A0/A2/A3/A6
	DBF	D7,AFF_A_LETTER
	TST	(A3)+
	BNE.S	.NO_DEZOOM
	CMPI	#66,18(A2)
	BEQ.S	.NO_ADD
	ADD	#3,18(A2)
.NO_ADD	SUB	#34*2,(A2)+
	
.NO_DEZOOM	DBF	D6,AFF_A_POLY

	LEA	DONNEES,A2
	MOVEQ	#8,D7
.GERE_DONNEES
	CMPI	#256,(A2)
	BNE.S	.F
	MOVE	#256+34*2,(A2)
.F
	ADDQ	#2,A2
	DBF	D7,.GERE_DONNEES

	;MOVE	#$5446,MOD_PLAN
	;MOVE	DONNEE,$FFFFC
	RTS
face_en_cours	dC.w	4
	ds.l	4


*** Partie affichage
CHOICE	DC	0	;0=MOVE
			;1=OR
INIT_ROUT_POLY	LEA	BUF_COD_GEN,A0
	LEA	TABLE_ADR_COD,A1

	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D3
.GO	MOVE.L	#RT_COOL,(A1)+
	DBRA	D3,.GO

	MOVE.W	#$FFFF,D0	;1ER MOTIF
	MOVE.W	#$8000,D1	;DERNIER MOTIF
	MOVEQ	#0,D3	;DECALAGE

	MOVEQ	#16-1,D6
.DECAL
	MOVEQ	#16+1,D5
	ADD.W	#18*16,D5	;D5=LONGUEUR DE LA LIGNE
	ADD.W	D3,D5

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
.COPY_MOVED7	BSR	POZ_ADRESSE
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	SUBI.W	#16,D5
	DBRA	D7,.COPY_MOVED7

	MOVE.W	#16+1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_1
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_1
.PAS_D7_1
	TST	CHOICE
	BNE.S	.OR1
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV1
.OR1	MOVE	#$0054,(A0)+	OR.W #$Imm,(A4)
.MOV1	MOVE.W	D0,(A0)+
.RETOUR_1	MOVE.W	#1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_2
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_2
.PAS_D7_2
	TST	CHOICE
	BNE.S	.OR2
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV2
.OR2	MOVE	#$0055,(A0)+	OR "             "
.MOV2	MOVE.W	D1,(A0)+
.RETOUR_2
	BSR	COPY_RT_COOL

	ASR.W	#1,D1
	ADDQ.W	#1,D3	;LONGUEUR SUIVANT
	DBRA	D6,.DECAL

	LEA	TABLE_ADR_COD+1320,A1

	MOVE.W	#$7FFF,D3	;1ER MOTIF
	MOVEQ	#14,D5

	MOVEQ	#14,D2
ALL_RT
	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D0
.GO	MOVE.L	#RT_COOL,(A1)+
	DBRA	D0,.GO

	MOVE.W	#1,A3

	MOVEQ	#0,D0
	MOVE.W	D5,D6
LITTLE	BSR	POZ_ADRESSE2
	BSET	D6,D0
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_LITTLE
	MOVE.W	#$3887,(A0)+
	BRA.S	.RETOUR_LITTLE
.PAS_D7_LITTLE
	TST	CHOICE
	BNE.S	.OR3
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV3
.OR3	MOVE	#$0054,(A0)+
.MOV3	MOVE.W	D0,(A0)+

.RETOUR_LITTLE
	BSR	COPY_RT_COOL

	ADDQ.W	#1,A3
	DBRA	D6,LITTLE

	MOVE.W	D3,D0
	MOVE.W	#$8000,D1	;DERNIER MOTIF

	MOVEQ	#16-1,D6
DECAL
	MOVE.W	#18*16,A3
	ADD.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
COPY_MOVED7	BSR	POZ_ADRESSE2
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	LEA	-16(A3),A3
	DBRA	D7,COPY_MOVED7

	MOVE.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3
	BSR	POZ_ADRESSE2
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_3
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_3
.PAS_D7_3
	TST	CHOICE
	BNE.S	.OR4
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV4
.OR4	MOVE	#$0054,(A0)+
.MOV4	MOVE.W	D0,(A0)+
.RETOUR_3	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_4
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_4
.PAS_D7_4
	TST	CHOICE
	BNE.S	.OR5
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV5
.OR5	MOVE	#$0055,(A0)+
.MOV5	MOVE.W	D1,(A0)+
.RETOUR_4
	BSR	COPY_RT_COOL

	ASR.W	#1,D1
	DBRA	D6,DECAL

	SUBQ.W	#1,D5
	LSR.W	#1,D3
	LEA	1280(A1),A1
	DBRA	D2,ALL_RT

*******
TBL4:	LEA	TABLE_A4,A0
	MOVE	#639,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	D6,(A0)+
	MOVE	(A1),(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#639,D7
.FINT	MOVE	#319*4,(A0)+
	MOVE	#152,(A0)+
	;CLR	(A0)+
	DBF	D7,.FINT
TBL2:
	LEA	TABLE_A2,A0
	MOVE	#639,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	(A1),(A0)+
	MOVE	D6,(A0)
	MOVE	D6,D5
	LSR	#2,D5
	AND	#15,D5
	MULU	#1320,D5
	SUB	D5,(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#639,D7
.FINT	;CLR	(A0)+
	MOVE	#152,(A0)+
	MOVE	#319*4,(A0)+
	DBF	D7,.FINT
	RTS

MINI_TABLE
N	SET	0
	REPT	20
	DC	N
N	SET	N+8
	ENDR


POZ_ADRESSE2	MOVEM.L	D5/A0,-(SP)
	MOVE.W	A3,D5
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS
	

POZ_ADRESSE	MOVEM.L	D5/A0,-(SP)
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS
	
COPY_RT_COOL	LEA	RT_COOL,A6
	MOVE.W	#LONG_RT_COOL,D7
.COPY	MOVE.W	(A6)+,(A0)+
	DBRA	D7,.COPY
	RTS

RT_COOL	ADD.L	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2

	MOVE.W	(A1)+,D1	2  X2
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1

	MOVE.L	(A3,D1.W),D3	5
	ADD	D3,A5	2
	SWAP	D3	1

	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	ADD	(A2)+,A4	3  OFFSET. A3=>1ER MOT
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE... RUSê, HEIN?
	   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE, ZOU.
	MOVE.L	(A6,D3.W),A2	;5
	JMP	(A2)	;2

;TOTAL:31 NOPS. +58=89 NOPS.
LONG_RT_COOL = ((*-RT_COOL)/2)-1

RT_POLY
	MOVE.L	A0,ADR_DEPART
	MOVE	#$4E75,MODIFIê
	LEA	2(A0),A1	;A1 POINTE SUR LES POINTS
	MOVE.W	(A1),D3
	SUB.W	4(A1),D3
	MOVE.W	2(A1),D4
	SUB.W	6(A1),D4
	MOVE.W	8(A1),D6
	SUB.W	4(A1),D6
	MOVE.W	$A(A1),D5
	SUB.W	6(A1),D5
	MULS.W	D3,D5
	MULS.W	D6,D4
	SUB.L	D4,D5
	BGT.S	.VISIBLE
	RTS
.VISIBLE
	MOVE	#25,REMP_GAUCHE+201*2
	MOVE	#15,REMP_DROIT+201*2

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7

	MOVE.W	(A0)+,D0	;NB DE POINTS COMPOSANT LA SURFACE
	LEA	BUF_EXAM,A1
	MOVE.L	A0,A2
	SUBQ.W	#1,D0
	MOVE.W	D0,NB_PTS
.COPYING	MOVE.L	(A0)+,(A1)+
	DBRA	D0,.COPYING
	MOVE.L	(A2),(A1)+

	LEA	BUF_EXAM,A0
	LEA	BUF_GAUCHE,A2
	LEA	BUF_DROIT,A3
	ADDQ.W	#4,A0
	MOVE.W	-2(A0),D3	;Y DE REFERENCE
NB_PTS = *+2
	MOVE.W	#$1234,D0
	MOVEQ	#-1,D6
	MOVEQ	#-1,D7
ALL_PTS	MOVE.W	(A0),D1	;X
	MOVE.W	2(A0),D2	;Y
	CMP.W	D2,D3
	BGE.S	ON_MONTE
ON_DESCEND	MOVE.L	-4(A0),(A2)+
	MOVE.L	(A0)+,(A2)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D6
	DBRA	D0,ALL_PTS
	BRA.S	FINITOUTI
ON_MONTE	MOVE.L	-4(A0),(A3)+
	MOVE.L	(A0)+,(A3)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D7
	DBRA	D0,ALL_PTS
FINITOUTI	;ICI LES TABLEAU BUF_GAUCHE ET BUF_DROIT SONT CONSTITUêS
	;D6=NB DE PTS-1 DANS BUF_GAUCHE
	;D7=NB DE PTS-1 DANS BUF_DROIT

	LEA	BUF_GAUCHE,A0
MAKE_REMP_GAUCHE
	LEA	REMP_GAUCHE,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT.S	.NEXT
	MOVE	#$4E71,MODIFIê
	SUB.W	D1,D3	;HAUTEUR-1 DU SEGMENT
	BEQ.S	.NEXT

	ADD.W	D1,D1
	ADDA.W	D1,A1

	ADDQ.W	#1,D3
	SUB.W	D0,D2
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#1,D3
.PUT	MOVE.W	D0,(A1)+
	DBRA	D3,.PUT
	BRA.S	.NEXT
.PAS_VERTICALE	EXT.L	D2
	MOVEQ	#8,D4
	ASL.L	D4,D2
	DIVS.W	D3,D2
	EXT.L	D2
	ASL.L	#8,D2
	SUBQ.W	#2,D3
	EXT.L	D0
	MOVEQ	#16,D4
	MOVE.W	D0,(A1)+
	ASL.L	D4,D0
.MK_X	ADD.L	D2,D0
	MOVE.L	D0,D1
	SWAP	D1
	MOVE.W	D1,(A1)+
	DBRA	D3,.MK_X
.NEXT	DBRA	D6,MAKE_REMP_GAUCHE

MODIFIê	RTS

	LEA	BUF_DROIT,A0
MAKE_REMP_DROIT	LEA	REMP_DROIT,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT.S	.NEXT
	SUB.W	D3,D1	;HAUTEUR-1 DU SEGMENT
	BEQ.S	.NEXT

	ADD.W	D3,D3
	ADDA.W	D3,A1

	ADDQ.W	#1,D1
	SUB.W	D2,D0
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#1,D1
.PUT	MOVE.W	D2,(A1)+
	DBRA	D1,.PUT
	BRA.S	.NEXT
.PAS_VERTICALE	EXT.L	D0
	MOVEQ	#8,D4
	ASL.L	D4,D0
	DIVS.W	D1,D0
	EXT.L	D0
	ASL.L	#8,D0
	SUBQ.W	#2,D1
	MOVE.W	D2,(A1)+
	EXT.L	D2
	MOVEQ	#16,D4
	ASL.L	D4,D2
.MK_X	ADD.L	D0,D2
	MOVE.L	D2,D3
	SWAP	D3
	MOVE.W	D3,(A1)+
	DBRA	D1,.MK_X
.NEXT	DBRA	D7,MAKE_REMP_DROIT

*****
ADR_DEPART= *+2
	LEA	$12345678,A0
	MOVE.W	(A0)+,D7
	SUBQ.W	#1,D7
	MOVE.W	#-1000,D0	;MAXIMUM
	MOVE.W	#20000,D1	;MINIMUM
.TEST_MAX	CMP.W	2(A0),D0
	BGE.S	.FUCK
	MOVE.W	2(A0),D0
.FUCK	CMP.W	2(A0),D1
	BLE.S	.FUCK_2
	MOVE.W	2(A0),D1
.FUCK_2	ADDQ.W	#4,A0
	DBRA	D7,.TEST_MAX
	;D0 = Y MAXIMUM
	;D1 = Y MINIMUM

	CMPI.W	#200,D0
	BLT.S	.OK_Y_MAX
	MOVE.W	#199,D0
.OK_Y_MAX	TST.W	D1
	BGE.S	.OK_Y_MIN
	MOVEQ	#0,D1
.OK_Y_MIN

INIT	LEA	REMP_GAUCHE,A0
	LEA	REMP_DROIT,A1
	ADDQ.W	#1,D0
	ADD.W	D0,D0
	MOVE.W	#15,(A1,D0.W)
	MOVE.W	#$9999,2(A1,D0.W)
	MOVE.W	#25,(A0,D0.W)

.F	ADD.W	D1,D1
	ADDA.W	D1,A0
	ADDA.W	D1,A1
	MULU.W	#80,D1       ;D1=Y_MIN*160 (POUR ADD ECRAN)

	SWAP	D1
	CLR	D1
	SWAP	D1
MODIF	NOP
	NOP
	NOP
	LEA	TABLE_ADR_COD+40,A6
	MOVEQ	#-1,D7
	MOVE.L	SCREEN2,D6
MOD_PLAN	NOP
	ADD.L	D1,D6	;...+Y_MIN*160
	SUBI.L	#160,D6
	MOVE.L	#TABLE_A2+640*4,D4
	MOVE.L	#160,D5
	LEA	TABLE_A4+640*4,A3
	MOVEQ	#0,D2

	ADD.L	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2
	MOVE.W	(A1)+,D1	2  X2
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1
	MOVE.L	(A3,D1.W),D3	5
	ADD	D3,A5	2
	SWAP	D3	1
	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	ADD	(A2)+,A4	3  OFFSET. A3=>1ER MOT
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE*1320... RUSê, HEIN?   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE*1320, ZOU.
	MOVE.L	(A6,D3.W),A2
	JMP	(A2)

RETOUR_POLY	RTS

CLIPPING_Y	MOVEM.L	D4/D6/A6,-(SP)
	MOVEQ	#0,D5
	LEA	TABLE_Y,A6
	ADD.W	D1,D1
	ADD.W	D3,D3
	MOVEQ	#0,D4
	ADD.W	(A6,D1.W),D4
	ADD.W	(A6,D3.W),D4
	ASR.W	#1,D1
	ASR.W	#1,D3
	TST.W	D4
	BEQ	FIN_NORMALE
	CMPI.W	#-2,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#4,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#-1,D4
	BEQ	CLIP_HAUT
	CMPI.W	#2,D4
	BEQ	CLIP_BAS

CLIP_HAUT_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	BRA.S	SUIT
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1

SUIT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_HAUT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

RIEN_DU_TOUT	MOVE.W	#-1,D5
FIN_NORMALE	MOVEM.L	(SP)+,D4/D6/A6
	RTS

	DATA

	DC	$9999
BUFBUF	INCBIN	DATA.PLK
END_BUFBUF
LOGO_1P	INCBIN	LOGO.1P

POLY	INCBIN	morph.COR
	DC.W	$1234

ETAP1	DC	93-1
	INCBIN	ETAP1	372
ETAP2	DC	901-1
	INCBIN	ETAP2	1802

H_PTS	;5 polys
 DC 107,77
 DC 107,105
 DC 108,105
 DC 108,77
 DC 108,77
 DC 108,80
 DC 111,77
 DC 108,77
 DC 115,77
 DC 115,105
 DC 116,105
 DC 116,77
 DC 108,82
 DC 108,83
 DC 115,83
 DC 115,82
 DC 112,93
 DC 112,105
 DC 115,105
 DC 115,91

O_PTS	;6 polys
 DC 119,77
 DC 119,105
 DC 120,105
 DC 120,77
 DC 120,77
 DC 120,80
 DC 123,78
 DC 124,77
 DC 122,77
 DC 122,78
 DC 128,78
 DC 128,77
 DC 127,77
 DC 127,105
 DC 128,105
 DC 128,78
 DC 120,104
 DC 120,105
 DC 127,105
 DC 127,104
 DC 124,94
 DC 124,104
 DC 127,104
 DC 127,91

L_PTS	;4 polys
 DC 131,77
 DC 131,105
 DC 132,105
 DC 132,77
 DC 132,77
 DC 132,80
 DC 135,77
 DC 133,77
 DC 132,105
 DC 140,105
 DC 140,104
 DC 132,104
 DC 140,105
 DC 140,101
 DC 138,104
 DC 140,104

XD	SET	23
O_PTS2	;6 polys
 DC 119+XD,77
 DC 119+XD,105
 DC 120+XD,105
 DC 120+XD,77
 DC 120+XD,77
 DC 120+XD,80
 DC 123+XD,78
 DC 124+XD,77
 DC 122+XD,77
 DC 122+XD,78
 DC 128+XD,78
 DC 128+XD,77
 DC 127+XD,77
 DC 127+XD,105
 DC 128+XD,105
 DC 128+XD,78
 DC 120+XD,104
 DC 120+XD,105
 DC 127+XD,105
 DC 127+XD,104
 DC 124+XD,94
 DC 124+XD,104
 DC 127+XD,104
 DC 127+XD,91

C_PTS	;4 polys
 DC 154,77
 DC 154,105
 DC 155,105
 DC 155,77
 DC 155,77
 DC 155,78
 DC 163,78
 DC 163,77
 DC 155,105
 DC 163,105
 DC 163,104
 DC 155,104
 DC 163,100
 DC 159,104
 DC 163,105
 DC 163,103

A_PTS	;6 polys
 DC 166,77
 DC 166,105
 DC 167,105
 DC 167,77
 DC 167,77
 DC 167,80
 DC 170,77
 DC 168,77
 DC 167,77
 DC 167,78
 DC 175,78
 DC 175,77
 DC 174,78
 DC 174,105
 DC 175,105
 DC 175,78
 DC 167,83
 DC 167,84
 DC 175,84
 DC 175,83
 DC 171,94
 DC 171,105
 DC 174,105
 DC 174,91

U_PTS	;5 polys
 DC 178,77
 DC 178,105
 DC 179,105
 DC 179,77
 DC 179,77
 DC 179,80
 DC 182,77
 DC 180,77
 DC 179,104
 DC 179,105
 DC 187,105
 DC 187,104
 DC 186,77
 DC 186,105
 DC 187,105
 DC 187,77
 DC 183,94
 DC 183,104
 DC 187,104
 DC 187,91

S_PTS	;5 polys
 DC 190,99
 DC 190,105
 DC 199,105
 DC 199,91
 DC 198,83
 DC 198,96
 DC 199,96
 DC 199,83
 DC 191,83
 DC 191,84
 DC 199,84
 DC 199,83
 DC 190,77
 DC 190,84
 DC 191,84
 DC 191,77
 DC 191,77
 DC 191,78
 DC 199,78
 DC 199,77

T_PTS	;5 polys
 DC 202,77
 DC 202,80
 DC 205,77
 DC 203,77
 DC 210,77
 DC 213,80
 DC 213,77
 DC 212,77
 DC 204,77
 DC 204,78
 DC 212,78
 DC 212,77
 DC 207,78
 DC 207,105
 DC 208,105
 DC 208,78
 DC 204,94
 DC 204,105
 DC 208,105
 DC 208,92

NB_DE_POLYS	DC	5,6,4,6,4,6,5,5,5
DONNEES	DC	0,0,0,0,0,0,0,0,0
DONNEES2	DC	0,0,0,0,0,0,0,0,0
FLAGS	DC	1,1,1,1,1,1,1,1,1
TEMPOS	DC	1,5,10,15,20,25,30,35,40

PERSOS1	INCBIN	PERSO1.PI1
PERSOS2	INCBIN	PERSO2.PI1

TABLE_IMG	
	DC.L	PERSOS2+34+32
	DC.L	PERSOS2+34
	DC.L	PERSOS1+34+32*4
	DC.L	PERSOS1+34+32*3
	DC.L	PERSOS1+34+32*2
	DC.L	PERSOS1+34+32

	DC.L	PERSOS1+34
	DC.L	PERSOS1+34
	DC.L	PERSOS1+34
	DC.L	PERSOS1+34

	DC.L	PERSOS1+34+32
	DC.L	PERSOS1+34+32*2
	DC.L	PERSOS1+34+32*3
	DC.L	PERSOS1+34+32*4
	DC.L	PERSOS2+34
	DC.L	PERSOS2+34+32

MOTIFS	
N	SET	-3*8
	REPT	23
	DC	$8000,N
	DC	$4000,N
	DC	$2000,N
	DC	$1000,N

	DC	$800,N
	DC	$400,N
	DC	$200,N
	DC	$100,N

	DC	$80,N
	DC	$40,N
	DC	$20,N
	DC	$10,N

	DC	$8,N
	DC	$4,N
	DC	$2,N
	DC	$1,N
N	SET	N+8
	ENDR

CRB	
	REPT	37
	DC	0
	DC	0
	DC	0
	DC	4
	ENDR

	SECTION	BSS
;BSS gÇnÇrale
DEB_TY	DS.W	2*640
TABLE_Y	DS.W	200
	DS.W	2*640

	DS.B	256
BUFFER	DS.B	32000*2
	DS.B	100
SCREEN1	DS.L	1
SCREEN2	DS.L	1
NB_VBL	DS.W	1
;Routine d'affichage
TABLE_A4	DS.L	320+640*2
TABLE_A2	DS.L	320+640*2
BUF_EXAM	DS.W	2*50
BUF_GAUCHE	DS.W	4*50
BUF_DROIT	DS.W	4*50
REMP_GAUCHE	DS.W	250
REMP_DROIT	DS.W	250
TABLE_ADR_COD	DS.B	(1280+40)*16
BUF_COD_GEN	DS.B	39000
*
BUF	DS.B	66*8000
;Bss morph
PT_POLY	DS.L	1
PT_SOV	DS.L	1
FLAG_PUT_IMG	DS.W	1
BUF_TEMP	DS.W	200
FLAG_COTE_GAUCHE	
	DS.B	200
FLAG_COTE_DROIT	DS.B	200
COTE_GAUCHE	DS.W	200*8
COTE_DROIT	DS.W	200*8
BUF_SOV_GFX	DS.W	147*7*100
