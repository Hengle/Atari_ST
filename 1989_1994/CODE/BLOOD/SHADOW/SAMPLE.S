	CLR.L	-(SP)
	MOVE 	#$20,-(SP)
	TRAP 	#1
	ADDQ.L 	#6,SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	
	MOVE.W	#$2700,SR

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W

	LEA	TABLE_SAMPLES,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0)+,D1
	MOVE.L	D0,MODA1+2
	MOVE.L	D0,MODA2+2
	MOVE.L	D0,MODA3+2
	MOVE.L	D0,MODA4+2
	MOVE.L	D1,MODB1+2
	MOVE.L	D1,MODB2+2
	MOVE.L	D1,MODB3+2

	JSR	GO

MODA1	MOVE.L	#SAMPLE1,BEAT1

MODA2	LEA	SAMPLE1,A0
MODB1	MOVE.L	#TEST_FIN1,D7
MODA3	MOVE.L	#SAMPLE1,D6
	SUB.L	D6,D7
	LSR.L	#1,D7
	SUBQ.L	#1,D7
	;MOVE.W	#((TEST_FIN-BEAT)/2)-1,D7
.CONT	MOVE.B	(A0),D0
	ADDI.B	#$80,D0
	MOVE.B	D0,(A0)+
	MOVE.B	(A0),D0
	ADDI.B	#$80,D0
	MOVE.B	D0,(A0)+
	DBRA	D7,.CONT
MODA4	LEA	SAMPLE1,A0
MODB2	LEA	TEST_FIN1,A1
	MOVE.W	#255,D7
.A	MOVE.B	(A0)+,(A1)+
	DBRA	D7,.A
	MOVE.L	BEAT1,A0

	MOVE.L	#INTER_RTE,$68.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W

	MOVE.W	#$2300,SR

	MOVE.L	#VBLR_IR,$70.W
	MOVE.L	#TB0_IR,$120.W
	
	MOVE.B	#$12,$FFFFFC02.W
	
	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W

BOUCLE	BRA.S	BOUCLE
GO	
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE.W	#$8800,A6
	MOVEQ	#$A,D0
SET_YM1	MOVE.B	D1,(A6)	;ON REMET LES 10 REGISTRES SONORES A ZERO
	CLR.B	2(A6)
	ADDQ.W	#1,D1
	DBRA	D0,SET_YM1

	MOVE.B	#7,(A6)
	MOVE.B	#$FF,2(A6)
	;MOVE	#1,FLAG_ZIK
	RTS
GO2
	MOVE.B	#$20,$FFFFFA07.W
	MOVE.B	$FFFFFA07.W,$FFFFFA13.W
	MOVE.B	$FFFFFA09.W,$FFFFFA15.W
	MOVE.L	#PLAY_B,$134.W
	MOVE.B	#38*2,$FFFFFA1F.W
	MOVE.B	#1,$FFFFFA19.W
	MOVE.B	#$40,$FFFFFA17.W
	RTS


VBLR_IR
	CLR.B	$FFFFFA1B.W

	JSR	GO2

MODB3	CMPA.L	#TEST_FIN1,A0
	BLE.S	.NO
	MOVE.L	#INTER_RTE,$134.W
.NO
	MOVE.W	#$8800,A1
	LEA	CODE,A2

INTER_RTE	RTE
TB0_IR	RTE

PLAY_B	;ST	$FFFF8240.W
	MOVEQ	#0,D0	;1
	MOVE.B	(A0)+,D0	;2
	LSL.W	#3,D0	;3
	MOVE.L	0(A2,D0.W),D1	;5
	MOVE.W	4(A2,D0.W),D2	;5
	MOVEP.L	D1,(A1)	;6
	MOVEP.W	D2,(A1)	;4     ;=26 NOPS
	;SF	$FFFF8240.W
	RTE	;5    ;=31 NOPS

BEAT1	DS.L	1

FIN:	MOVE.L	4.W,A0
	JMP	(A0)

	DATA
CODE	INCBIN	TABLEREP.BIN
SAMPLE1	INCBIN	BOOM.SPL
	EVEN
TEST_FIN1	DCB.B	256,0

TABLE_SAMPLES
 DC.L SAMPLE1,TEST_FIN1
 
 	SECTION BSS
DEB_BSS
END_BSS