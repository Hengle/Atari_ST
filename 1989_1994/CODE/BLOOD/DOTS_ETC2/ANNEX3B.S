MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$01C.W

	MOVE.L	#BUF,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADD.L	#32000,D0
	MOVE.L	D0,SCREEN2

	MOVE.L	#RAM_SAVER,D0
	MOVE.L	D0,ADR_SV1
	ADD.L	#4*6*200,D0
	MOVE.L	D0,ADR_SV2

	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
	MOVE.L	ADR_SV1,A3
	MOVE.L	ADR_SV2,A4
	MOVE	#199,D7
.SA	REPT	6
	MOVE.L	(A1)+,(A3)+
	MOVE.L	(A2)+,(A4)+
	ENDR
	LEA	160-6*4(A1),A1
	LEA	160-6*4(A2),A2
	DBF	D7,.SA

	MOVE.L	#VBL,$70.W
J	BRA.S	J
FIN	MOVE.L	4.W,A0
	JMP	(A0)
TIME_MSG	DC	0
VBL
	MOVE.L	SCREEN1,A1
	MOVE.L	ADR_SV1,A3
	MOVE	#199,D7
.SA	REPT	6
	MOVE.L	(A3)+,(A1)+
	ENDR
	LEA	160-6*4(A1),A1
	DBF	D7,.SA

	MOVE.L	SCREEN1,A0
MODMDK	LEA	160*199(A0),A0
MODMDK2	MOVE	#1-1,D7
	BLT.S	.OOPS
	LEA	MSG1,A1
.PIPO	REPT	6
	MOVE.L	(A1)+,(A0)+
	ENDR
	LEA	160-6*4(A0),A0
	DBF	D7,.PIPO

	CMPI	#150,TIME_MSG
	BLE.S	.RIEN_A_FOUTRE

	ADD	#160,MODMDK+2
	SUBQ	#1,MODMDK2+2
	BRA.S	.OK
.RIEN_A_FOUTRE	ADDQ	#1,TIME_MSG
	CMPI	#88,MODMDK2+2
	BEQ.S	.OK
	SUB	#160,MODMDK+2
	ADDQ	#1,MODMDK2+2
.OK

.OOPS	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.L	ADR_SV1,D0
	MOVE.L	ADR_SV2,ADR_SV1
	MOVE.L	D0,ADR_SV2
	RTE

	DATA
MSG1	INCBIN	MSG3.GFX	1680

	BSS
	DS.B	256
BUF	DS.B	64000
SCREEN1	DS.L	1
SCREEN2	DS.L	1
ADR_SV1	DS.L	1
ADR_SV2	DS.L	1
RAM_SAVER	DS.L	6*200*2
