PROGRAM:
	CLR.L	-(SP)
	MOVE 	#$20,-(SP)
	TRAP 	#1
	ADDQ.L 	#6,SP

	MOVEQ	#1,D0
	JSR	ZIK
	
	LEA	ZIK+8,A0
	LEA	RET,A1
	JMP	DEBUT
ZIK	INCBIN	ZIKO
RET	MOVE	#$2700,SR
	MOVE	#$700,$FFFF8240.W
	BRA.S	RET

DEBUT
	MOVE.L	A0,MODZIK
	MOVE.L	A1,MODRET
	LEA	BSS_DEB,A0
	LEA	BSS_END,A1
.KILL	CLR.L	(A0)+
	CLR.L	(A0)+
	CLR.L	(A0)+
	CLR.L	(A0)+
	CMP.L	A1,A0
	BLE.S	.KILL

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	MOVE.L	#INTER_RTE,$68.W

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W

	MOVE.L	#DATA_HS,PT_HARD
		
	MOVE.L	#BUFFERSCR,D0
	MOVE.L	#180*256,D1
	LEA	SCREEN1,A0
	CLR.B	D0
	MOVE.L	D0,(A0)+
	REPT	15
	ADD.L	D1,D0
	MOVE.L	D0,(A0)+
	ENDR

	MOVE.L	#VBL,$70.W
	MOVE.L	#INTER_RTE,$120.W

	MOVE.B	#$12,$FFFFFC02.W

	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W

BOUCLE	BRA.S	BOUCLE
MODRET	EQU	*+2
	JMP	$12345678

VBL
	CLR.B	$FFFFFA1B.W

	MOVE.B	#0,$FFFFFA19.W
	MOVE.B	#99,$FFFFFA1F.W
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W


	TST	TIME
	BNE	NON
	LEA	BUF_TRAVAIL,A5
	LEA	DATAS_SCROL,A6
MOD_SCROL	EQU	*+2
	ADDA.L	#0,A6
	CMPI.L	#$12345678,(A6)
	BNE.S	.ROOL
	MOVE	#$4E71,BOUCLE
	LEA	DATAS_SCROL,A6
	CLR.L	MOD_SCROL
.ROOL	MOVE.L	(A6),A6


N	SET	0
N2	SET	0
	REPT	200
	MOVE.L	N2(A6),8+N(A5)
	MOVE.L	N2+4(A6),12+N(A5)
N	SET	N+16
N2	SET	N2+160
	ENDR
NON	ADDQ	#1,TIME
	CMPI	#16,TIME
	BNE	INTER_RTE
	CLR	TIME
	ADDQ.L	#4,MOD_SCROL
	ADDQ.L	#8,Y_VALUE

INTER_RTE	RTE
TIME	DC	0
INTER_TMA:	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE	#$2100,SR

	LEA	$FFFF8209.W,A3
	LEA	$FFFF8260.W,A6
	LEA	$FFFF820A.W,A5

	MOVEQ	#0,D0
	MOVEQ	#2,D1

	STOP	#$2100
	MOVE	#$2700,SR
	MOVE	#$2300,(SP)

	MOVEQ	#29,D2
SYNCHROA:	DBF	D2,SYNCHROA
	NOP
	MOVE.B	D0,(A5)
	REPT	6
	NOP
	ENDR
	MOVE.B	D1,(A5)
**
	MOVEA.L	PT_HARD,A2

	LEA	$FFFF8209.W,A6
	LEA	$FFFF8260.W,A4
	LEA	$FFFF820A.W,A3
	MOVEQ	#0,D1		
	MOVEQ	#$10,D6
	MOVE.L	#228-12+2-1-1-1,D0

SYNCHRO	MOVE.B	(A6),D7
	BEQ.S	SYNCHRO
	SUB.W	D7,D6		
	LSL.W	D6,D1		

	DCB	97-3-4,$4E71

	REPT	9
	MOVE.L	(A2)+,A1	*3
	JSR	(A1)	*4
	ENDR

	DCB	7-5,$4E71

	JSR	ROUTI
	JSR	ROUTI
	JSR	ROUTI
	JSR	ROUTI
	JSR	ROUTI
	;JSR	ROUTI
	DCB	5,$4E71

	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	88-21,$4E71
	MOVEM.L	IMG1+2,D0-D7
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12,$4E71

	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	MOVEM.L	D0-D7,$FFFF8240.W
	MOVE.L	SCREEN1,A1	5
	LEA	160*10+230*6+224-8(A1),A1	2
	ADD.L	Y_VALUE,A1	6
	LEA	BUF_TRAVAIL,A0
	DCB	88-5-2-3-6-19,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-5,$4E71


	REPT	200-10
	JSR	SHIFT
	ENDR

	MOVE.L	#$00010001,D0
	MOVE.L	D0,$FFFF8240.W
	MOVE.L	D0,$FFFF8244.W
	MOVE.L	D0,$FFFF8248.W
	MOVE.L	D0,$FFFF824C.W
	MOVE.L	D0,$FFFF8250.W
	MOVE.L	D0,$FFFF8254.W
	MOVE.L	D0,$FFFF8258.W
	MOVE.L	D0,$FFFF825C.W


;	CMPI.B	#$50,$FFFFFC02.W
;	BNE.S	PAS_HAUT
;	ADD.L	#230,Y_VALUE
;PAS_HAUT	CMPI.B	#$48,$FFFFFC02.W
;	BNE.S	PAS_BAS
;	SUB.L	#230,Y_VALUE
;PAS_BAS
;	CMPI.B	#$4B,$FFFFFC02.W
;	BNE.S	PAS_GA
;	ADDQ.L	#8,Y_VALUE
;PAS_GA	CMPI.B	#$4D,$FFFFFC02.W
;	BNE.S	PAS_DR
;	SUBQ.L	#8,Y_VALUE
;PAS_DR

	MOVEQ	#0,D0	
	MOVEQ	#0,D1
	MOVE.L	Y_VALUE,D0
	
	LSR.L	#8,D0
	MOVE.L	D0,D1
	LSL.L	#8,D1
	
	MOVE.L	Y_VALUE,D0
	SUB.L	D1,D0

	LEA	DATA_HS,A0

	TST.L	D0
	BNE.S	DEJ
	LEA	RIEN,A0
	MOVE.L	A0,PT_HARD
	BRA.S	OKAY
DEJ	SUBQ.L	#2,D0
	MULU	#18,D0
	ADDA.L	D0,A0
	MOVE.L	A0,PT_HARD
		
OKAY
	MOVE.L	SCREEN1,D0
	ADD.L	D1,D0
	LSR.L	#8,D0
	LEA	$FFFF8201.W,A0
	MOVEP	D0,(A0)

	MOVE.L	SCREEN16,D0
	MOVE.L	SCREEN15,SCREEN16
	MOVE.L	SCREEN14,SCREEN15
	MOVE.L	SCREEN13,SCREEN14
	MOVE.L	SCREEN12,SCREEN13
	MOVE.L	SCREEN11,SCREEN12
	MOVE.L	SCREEN10,SCREEN11
	MOVE.L	SCREEN9,SCREEN10
	MOVE.L	SCREEN8,SCREEN9
	MOVE.L	SCREEN7,SCREEN8
	MOVE.L	SCREEN6,SCREEN7
	MOVE.L	SCREEN5,SCREEN6
	MOVE.L	SCREEN4,SCREEN5
	MOVE.L	SCREEN3,SCREEN4
	MOVE.L	SCREEN2,SCREEN3
	MOVE.L	SCREEN1,SCREEN2
	MOVE.L	D0,SCREEN1
MODZIK	EQU	*+2
	JSR	$12345678

	RTE

ROUTI	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	88,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4-5,$4E71
	RTS

Y_VALUE	DC.L	0

TB0_IR	RTE

SHIFT	;A0=BUF_TRAVAIL
	;A1=ECRAN
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	88-32-5-6-4,$4E71
PLAN	SET	0
	REPT	4
	ROXL	PLAN+8(A0)
	ROXL	PLAN(A0)
PLAN	SET	PLAN+2
	ENDR		4*2*4=32 NOPS
	MOVE.L	(A0)+,(A1)	5
	MOVE.L	(A0)+,4(A1)	6
	ADDQ	#8,A0	2
	LEA	230(A1),A1	2
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4-5,$4E71

	RTS

FIN:	MOVE.L	4.W,A0
	JMP	(A0)

H0	
	DCB	128-4-3-4,$4E71
	RTS

H70
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	3+17*5,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	NOP
	RTS	=4 NOPS
H44
	DCB	3+18*5,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	NOP
	RTS	=4 NOPS
H24
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	1+17*5,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	REPT	3
	NOP
	ENDR
	RTS	=4 NOPS
H2
	NOP
	DCB	18*5,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	REPT	3
	NOP
	ENDR
	RTS	=4 NOPS
H80
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	7*5,$4E71
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	3+9*5,$4E71

	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	NOP
	RTS
	 
	DATA

DATA_HS
 DC.L H44,H44,H2,H2,H2,H80,H0,H0,H0
 DC.L H44,H44,H2,H2,H80,H0,H0,H0,H0
 DC.L H44,H44,H2,H80,H0,H0,H0,H0,H0
 DC.L H44,H44,H80,H0,H0,H0,H0,H0,H0
 DC.L H44,H24,H24,H2,H80,H0,H0,H0,H0
 DC.L H44,H24,H24,H80,H0,H0,H0,H0,H0
 DC.L H24,H2,H2,H2,H2,H2,H0,H0,H0
 DC.L H24,H2,H2,H2,H2,H0,H0,H0,H0
 DC.L H24,H2,H2,H2,H0,H0,H0,H0,H0
 DC.L H24,H2,H2,H0,H0,H0,H0,H0,H0
 DC.L H24,H2,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H0,H0,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H24,H24,H80,H80,H0,H0
 DC.L H70,H44,H2,H2,H2,H80,H0,H0,H0
 DC.L H70,H44,H2,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H2,H80,H0,H0,H0,H0,H0
 DC.L H44,H2,H2,H2,H2,H2,H0,H0,H0
 DC.L H44,H2,H2,H2,H2,H0,H0,H0,H0
 DC.L H44,H2,H2,H2,H0,H0,H0,H0,H0
 DC.L H44,H2,H2,H0,H0,H0,H0,H0,H0
 DC.L H44,H2,H0,H0,H0,H0,H0,H0,H0
 DC.L H44,H0,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H2,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H0,H0,H0,H0,H0,H0,H0
 DC.L H44,H44,H44,H2,H80,H0,H0,H0,H0
 DC.L H44,H44,H44,H80,H0,H0,H0,H0,H0
 DC.L H70,H70,H2,H2,H2,H80,H0,H0,H0
 DC.L H70,H70,H2,H2,H80,H0,H0,H0,H0
 DC.L H70,H70,H2,H80,H0,H0,H0,H0,H0
 DC.L H44,H24,H2,H2,H2,H2,H0,H0,H0
 DC.L H44,H24,H2,H2,H2,H0,H0,H0,H0
 DC.L H44,H24,H2,H2,H0,H0,H0,H0,H0
 DC.L H44,H24,H2,H0,H0,H0,H0,H0,H0
 DC.L H44,H24,H0,H0,H0,H0,H0,H0,H0
 DC.L H70,H0,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H44,H2,H2,H80,H0,H0,H0
 DC.L H70,H44,H44,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H44,H80,H0,H0,H0,H0,H0
 DC.L H44,H24,H24,H2,H80,H70,H0,H0,H0
 DC.L H70,H44,H24,H24,H80,H0,H0,H0,H0
 DC.L H44,H44,H2,H2,H0,H0,H0,H0,H0
 DC.L H44,H44,H2,H0,H0,H0,H0,H0,H0
 DC.L H44,H44,H0,H0,H0,H0,H0,H0,H0
 DC.L H70,H24,H2,H2,H0,H0,H0,H0,H0
 DC.L H70,H24,H2,H0,H0,H0,H0,H0,H0
 DC.L H70,H24,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H2,H2,H2,H80,H0,H0
 DC.L H70,H70,H44,H2,H2,H80,H0,H0,H0
 DC.L H70,H70,H44,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H2,H2,H2,H2,H2,H0,H0
 DC.L H70,H44,H2,H2,H2,H2,H0,H0,H0
 DC.L H70,H44,H2,H2,H2,H0,H0,H0,H0
 DC.L H70,H44,H2,H2,H0,H0,H0,H0,H0
 DC.L H70,H44,H2,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H24,H2,H2,H0,H0
 DC.L H24,H24,H24,H24,H24,H2,H0,H0,H0
 DC.L H24,H24,H24,H24,H24,H0,H0,H0,H0
 DC.L H70,H44,H44,H44,H80,H0,H0,H0,H0
 DC.L H70,H70,H70,H2,H2,H2,H80,H0,H0
 DC.L H70,H70,H70,H2,H2,H80,H0,H0,H0
 DC.L H70,H70,H70,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H24,H2,H2,H2,H2,H0,H0
 DC.L H44,H44,H44,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H2,H2,H0,H0,H0,H0
 DC.L H70,H44,H24,H2,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H0,H0,H0,H0,H0,H0
 DC.L H70,H70,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H70,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H2,H2,H80,H0,H0
 DC.L H70,H70,H44,H44,H2,H80,H0,H0,H0
 DC.L H70,H70,H44,H44,H80,H0,H0,H0,H0
 DC.L H70,H70,H44,H24,H24,H2,H80,H0,H0
 DC.L H70,H44,H44,H2,H2,H2,H0,H0,H0
 DC.L H70,H44,H44,H2,H2,H0,H0,H0,H0
 DC.L H70,H44,H44,H2,H0,H0,H0,H0,H0
 DC.L H70,H44,H44,H0,H0,H0,H0,H0,H0
 DC.L H70,H70,H24,H2,H2,H0,H0,H0,H0
 DC.L H70,H70,H24,H2,H0,H0,H0,H0,H0
 DC.L H70,H70,H24,H0,H0,H0,H0,H0,H0
 DC.L H70,H24,H24,H24,H24,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H24,H24,H24,H0,H0
 DC.L H70,H70,H70,H44,H2,H2,H80,H0,H0
 DC.L H44,H44,H44,H44,H2,H2,H0,H0,H0
 DC.L H44,H44,H44,H44,H2,H0,H0,H0,H0
 DC.L H44,H44,H44,H44,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H2,H2,H2,H0,H0,H0
 DC.L H70,H70,H44,H2,H2,H0,H0,H0,H0
 DC.L H70,H70,H44,H2,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H24,H24,H0,H0,H0,H0
 DC.L H70,H70,H24,H24,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H44,H80,H2,H0,H0
 DC.L H70,H70,H44,H44,H44,H80,H0,H0,H0
 DC.L H70,H70,H70,H44,H24,H2,H2,H80,H0
 DC.L H44,H44,H44,H44,H24,H2,H2,H0,H0
 DC.L H44,H44,H44,H44,H24,H2,H0,H0,H0
 DC.L H44,H44,H44,H44,H24,H0,H0,H0,H0
 DC.L H70,H44,H44,H44,H0,H0,H0,H0,H0
 DC.L H70,H70,H70,H2,H2,H2,H0,H0,H0
 DC.L H70,H70,H70,H2,H2,H0,H0,H0,H0
 DC.L H70,H70,H70,H2,H0,H0,H0,H0,H0
 DC.L H70,H70,H70,H0,H0,H0,H0,H0,H0
 DC.L H24,H70,H70,H24,H24,H0,H0,H0,H0
 DC.L H70,H24,H24,H24,H24,H24,H24,H0,H0
 DC.L H44,H44,H44,H44,H44,H2,H2,H0,H0
 DC.L H44,H44,H44,H44,H44,H2,H0,H0,H0
 DC.L H44,H44,H44,H44,H44,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H2,H2,H2,H0,H0
 DC.L H70,H70,H44,H44,H2,H2,H0,H0,H0
 DC.L H70,H70,H44,H44,H2,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H0,H0,H0,H0,H0
 DC.L H70,H44,H44,H24,H24,H24,H0,H0,H0
 DC.L H70,H70,H70,H24,H2,H0,H0,H0,H0
 DC.L H70,H70,H70,H24,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H70,H70,H0,H0,H0
 DC.L H44,H44,H44,H44,H44,H24,H2,H2,H2
 DC.L H44,H44,H44,H44,H44,H24,H2,H2,H0
 DC.L H70,H44,H44,H44,H44,H2,H2,H0,H0
 DC.L H70,H44,H44,H44,H44,H2,H0,H0,H0
 DC.L H70,H44,H44,H44,H44,H0,H0,H0,H0
 DC.L H70,H44,H44,H44,H24,H24,H2,H0,H0
 DC.L H70,H70,H70,H44,H2,H2,H0,H0,H0
 DC.L H70,H70,H70,H44,H2,H0,H0,H0,H0
 DC.L H70,H70,H70,H44,H0,H0,H0,H0,H0

RIEN	REPT	9
	DC.L	H0
	ENDR

IMG1	INCBIN	LOGO1.PI1
IMG2	INCBIN	LOGO2.PI1
IMG3	INCBIN	LOGO3.PI1
IMG4	INCBIN	LOGO4.PI1

DATAS_SCROL
N	SET	0
	REPT	20
	DC.L	IMG1+34+N
N	SET	N+8
	ENDR
N	SET	0
	REPT	20
	DC.L	IMG2+34+N
N	SET	N+8
	ENDR

N	SET	0
	REPT	20
	DC.L	IMG3+34+N
N	SET	N+8
	ENDR

N	SET	0
	REPT	20
	DC.L	IMG4+34+N
N	SET	N+8
	ENDR

	REPT	20
	DC.L	IMG4+34+152
	ENDR

	DC.L	$12345678

 	SECTION BSS
BSS_DEB
BUF_TRAVAIL	DS.L	200*2*2
PT_HARD	DS.L	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
SCREEN3	DS.L	1
SCREEN4	DS.L	1
SCREEN5	DS.L	1
SCREEN6	DS.L	1
SCREEN7	DS.L	1
SCREEN8	DS.L	1
SCREEN9	DS.L	1
SCREEN10	DS.L	1
SCREEN11	DS.L	1
SCREEN12	DS.L	1
SCREEN13	DS.L	1
SCREEN14	DS.L	1
SCREEN15	DS.L	1
SCREEN16	DS.L	1
	DS.B	256
BUFFERSCR	DS.B	180*256*16
BSS_END			