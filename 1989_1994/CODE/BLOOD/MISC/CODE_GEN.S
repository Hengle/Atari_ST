	;ON PREPARE LE CODE GENERE DES SPRITES
	LEA	BUF_COD_GEN,A6

	BSR	EFFAC_SCREEN
	LEA	BANK_PLAYER,A0
	MOVE.W	(A0)+,D0	;D0=NB DE SPRITE DE LA BANK
	MOVE.W	(A0)+,D1	;LONGUEUR
	MOVE.W	(A0)+,D2	;HAUTEUR
	SUBQ.W	#1,D2
	MOVE.L	(A0)+,A1
	SUBQ.W	#1,D1
	ANDI.W	#$FFF0,D1
	LSR.W	#4,D1	;D1=NB DE MOTS-1
	MOVE.W	D1,D3
	ADDQ.W	#1,D3
	LSL.W	#3,D3
	MOVE.W	#160,D4
	SUB.W	D3,D4	;D4=OFFSET DE FIN DE LIGNE
	MOVE.W	D1,NB_MOTS_TEMP
	MOVE.W	D2,HIGH_TEMP
	MOVE.L	SCREEN1,A2
.AFF_Y	MOVE.W	D1,D3
.AFF_X	MOVE.L	(A1)+,D5
	MOVE.L	(A1)+,D6
	MOVE.L	D5,(A2)
	MOVE.L	D6,4(A2)
	OR.L	D6,D5
	MOVE.L	D5,D6
	SWAP	D6
	OR.L	D6,D5	;MASK (EN.L)
	NOT.L	D5
	MOVE.L	D5,160*100(A2)
	MOVE.L	D5,4+160*100(A2)
	ADDQ.W	#8,A2
	DBRA	D3,.AFF_X
	ADD.W	D4,A2
	DBRA	D2,.AFF_Y

NB_MOTS_TEMP = *+2
	MOVE.W	#$1234,D2	;NB DE MOTS PAR LIGNES
HIGH_TEMP = *+2
	MOVE.W	#$1234,D5	;HAUTEUR
	BSR	MAKE_COD_GEN

	RTS

	;D4=OFFSET DE FIN DE LIGNE
	;D5=HAUTEUR
	;D2=NB DE MOTS PAR LIGNES
MAKE_COD_GEN	MOVE.L	SCREEN1,A0
	LEA	160*100(A0),A1
	MOVEQ	#0,D3	;OFFSET DE DEPART
COD_Y	MOVE.W	D2,D6
COD_X	MOVEQ	#0,D0
	TST.W	(A0)
	BEQ.S	.VIDE_1
	ADDQ.W	#1,D0
.VIDE_1	TST.W	2(A0)
	BEQ.S	.VIDE_2
	ADDQ.W	#2,D0
.VIDE_2	TST.W	4(A0)
	BEQ.S	.VIDE_3
	ADDQ.W	#4,D0
.VIDE_3	TST.W	6(A0)
	BEQ.S	.VIDE_4
	ADDQ.W	#8,D0
.VIDE_4	;D0 REPRESENTE LE CAS PRESENT (0-->15)
	LEA	TABLE_CAS(PC),A2
	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.L	(A2,D0.W),A2
	JSR	(A2)
	ADDQ.W	#8,A0
	ADDQ.W	#8,A1
	ADDQ.W	#8,D3
	DBRA	D6,COD_X
	ADDA.W	D4,A0
	ADDA.W	D4,A1
	ADD.W	D4,D3
	DBRA	D5,COD_Y
	MOVE.W	#$4E75,(A6)+
	RTS

VIDE_TOTAL	RTS

PLAN_1	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	(A0),(A6)+
	MOVE.W	D3,(A6)+
	RTS

PLANS_12	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	(A0),(A6)+
	MOVE.W	D3,(A6)+
	RTS

PLANS_13	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	(A0),(A6)+
	MOVE.W	D3,(A6)+
	;
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLANS_123	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	(A0),(A6)+
	MOVE.W	D3,(A6)+
	;
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLAN_2	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	2(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	2(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	RTS

PLANS_23	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	2(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	2(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	RTS

PLAN_3	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLAN_4	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	6(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_14	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	(A0),(A6)+
	MOVE.W	D3,(A6)+
	;
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	6(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_24	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	2(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	2(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	;
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	6(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_124	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	(A0),(A6)+
	MOVE.W	D3,(A6)+
	;
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	6(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_34	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLANS_134	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	(A0),(A6)+
	MOVE.W	D3,(A6)+
	;
	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLANS_234	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	2(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	2(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	;
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	6(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_1234	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	(A0),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

TABLE_CAS	DC.L	VIDE_TOTAL
	DC.L	PLAN_1
	DC.L	PLAN_2
	DC.L	PLANS_12
	DC.L	PLAN_3
	DC.L	PLANS_13
	DC.L	PLANS_23
	DC.L	PLANS_123
	DC.L	PLAN_4
	DC.L	PLANS_14
	DC.L	PLANS_24
	DC.L	PLANS_124
	DC.L	PLANS_34
	DC.L	PLANS_134
	DC.L	PLANS_234
	DC.L	PLANS_1234
