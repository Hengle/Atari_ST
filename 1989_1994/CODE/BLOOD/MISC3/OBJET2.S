;Cheater 320*200= 513.6 lignes/VBL!
;	+ le coeff multiplicatif
;	+ la gestion correcte du z
;	+ routine d'affichage plus courte
;	+ une routine 3D VRAIMENT sans bugs!
;	+ 2 algos d'effaáage...
;	+ la gestion des 4 plans!
;	+ LE DOUBLE CLIPPING EN X!!! ON GAGNE 200K (!!!) SUR
;	  LES TABLEAUX DANS CERTAINS OBJETS!!!

DONNEE_INIT	= 150	150
TIME_F	= 256+2	;Nombre de vbls Ö prÇcalculer
NB_PTS_	= 8*2
NB_FACES	= 14
INC_D	= 0	50*2
COEFF_MULTI	= 2
COEFF_INIT	= 380
X_DEP	= 160
Y_DEP	= -150

ALGO	= 0	0=Normal, 1=Par colonne

X1	EQU	0
X2	EQU	4
X3	EQU	8
X4	EQU	12
X5	EQU	16
X6	EQU	20
X7	EQU	24
X8	EQU	28
X9	EQU	32
X10	EQU	36
X11	EQU	40
X12	EQU	44
X13	EQU	48
X14	EQU	52
X15	EQU	56
X16	EQU	60
X17	EQU	64
X18	EQU	68
X19	EQU	72
X20	EQU	76
X21	EQU	80
X22	EQU	84
X23	EQU	88
X24	EQU	92
X25	EQU	96
X26	EQU	100
X27	EQU	104
X28	EQU	108
X29	EQU	112
X30	EQU	116
X31	EQU	120
X32	EQU	124
X33	EQU	128
X34	EQU	132
X35	EQU	136
X36	EQU	140
X37	EQU	144
X38	EQU	148
X39	EQU	152
X40	EQU	156
X41	EQU	160
X42	EQU	164
X43	EQU	168
X44	EQU	172
X45	EQU	176
X46	EQU	180
X47	EQU	184
X48	EQU	188
X49	EQU	192
X50	EQU	196
X51	EQU	200
X52	EQU	204
X53	EQU	208
X54	EQU	212
X55	EQU	216
X56	EQU	220
X57	EQU	224
X58	EQU	228
X59	EQU	232
X60	EQU	236

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP
*************************************************************************

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	LEA	DEB_BSS,A0
	LEA	END_BSS,A1
.KILL_IT	CLR.L	(A0)+
	CMP.L	A1,A0
	BLE.S	.KILL_IT

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	LEA	POINTS+2,A1
	MOVE	#COEFF_MULTI,D0

	MOVE	#(NB_PTS_*3)-1,D7
.OQP	MOVEQ	#0,D1
	MOVE	(A1),D1
	MULS	D0,D1
	MOVE	D1,(A1)+
	DBF	D7,.OQP

	LEA	POINTS,A0
	MOVE	(A0)+,D7
	SUBQ	#1,D7
.X2	REPT	3
	MOVE	(A0),D0
	ADD	D0,D0
	MOVE	D0,(A0)+
	ENDR
	DBF	D7,.X2

	LEA	POINTS,A0
	LEA	OBJ,A1
	MOVE	(A0)+,D7
	SUBQ	#1,D7
.COPIT	MOVE.L	(A0)+,(A1)+
	MOVE	(A0)+,(A1)+
	DBF	D7,.COPIT

	MOVE	#$023,$FFFF8240.W
	MOVE	#$334,$FFFF8242.W
	MOVE	#$434,$FFFF8244.W
	MOVE	#$434,$FFFF8246.W
	MOVE	#$102,$FFFF8248.W
	MOVE	#$102,$FFFF824A.W
	MOVE	#$102,$FFFF824C.W
	MOVE	#$102,$FFFF824E.W

	MOVE	#$112,$FFFF8250.W
	MOVE	#$112,$FFFF8252.W
	MOVE	#$112,$FFFF8254.W
	MOVE	#$112,$FFFF8256.W
	MOVE	#$112,$FFFF8258.W
	MOVE	#$112,$FFFF825A.W
	MOVE	#$112,$FFFF825C.W
	MOVE	#$112,$FFFF825E.W

	LEA	TABLE_ADR_COD,A0
	MOVE	#5279,D7
.C	MOVE.L	#RT_COOL,(A0)+
	DBF	D7,.C

	LEA	TABLE_ADR_COD2,A0
	MOVE	#5279,D7
.C2	MOVE.L	#RT_COOL2,(A0)+
	DBF	D7,.C2

	BSR	PREPA_LOG_EXP
	CLR	CHOICE
	;MOVE	#1,CHOICE
	JSR	INIT_ROUT_POLY
	JSR	INIT_ROUT_POLY2
	MOVE.L	#RT_COOL,TABLE_ADR_COD+40
	MOVE.L	#RT_COOL2,TABLE_ADR_COD2+40

	MOVE	#DONNEE_INIT,DONNEE
	MOVE.L	#BUF_COD,ADR_BUF
	MOVE.L	#TABLE_ADR,ADR_TAB
	MOVE.L	#BUF_INFO,ADR_INFO
	MOVE.L	#BUF_TABLOS,ADR_TABLOS
	MOVE.L	#BUF_VISION,ADR_VISION

	LEA	SAVE_VALUES,A0
	LEA	ANGLES,A1
	MOVE	DONNEE,(A0)+
	MOVE.L	(A1)+,(A0)+
	MOVE	(A1),(A0)

	CLR.W	Z_BASE
	JSR	GERE_Z

	LEA	SINUS,A0
	LEA	SINUS+LONG_SINUS,A1
	MOVE.W	#(LONG_SINUS/8)-1,D7
.RECOPY	MOVE.W	(A0)+,(A1)+
	DBRA	D7,.RECOPY

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	CLR.W	NB_VBL
	MOVE.W	#$2300,SR

	MOVE.L	#BUF_TEMP,D0
	MOVE.L	D0,TEMP1
	ADDI.L	#32000,D0
	MOVE.L	D0,TEMP2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	LEA	SAVE_VALUES,A0
	LEA	ANGLES,A1
	MOVE	(A0)+,DONNEE
	MOVE.L	(A0)+,(A1)+
	MOVE	(A0),(A1)

	CLR.W	Z_BASE
	JSR	GERE_Z

MAIN_LOOP	BSR	VSYNC
	MOVE	#$011,$FFFF8240.W

	LEA	POINTS,A0
	LEA	ANGLES,A1
	LEA	TRANS,A6
	LEA	BUF_PTS,A2
	BSR	ROTATE

	TST	FLAG_TEMPS
	BEQ.S	.NO_TIMEA
	MOVE	#$700,$FFFF8240.W
.NO_TIMEA
	BSR	EFFAC

	TST	FLAG_TEMPS
	BEQ.S	.NO_TIMEB
	MOVE	#$070,$FFFF8240.W
.NO_TIMEB
	BSR	AFFICH
	JSR	CREE_EFF

	BSR	SWAPEC

	CMPI.B	#$3B,$FFFFFC02.W
	BNE.S	.NO_Z1
	ADDQ	#2,DONNEE
.NO_Z1	CMPI.B	#$3C,$FFFFFC02.W
	BNE.S	.NO_Z2
	SUBQ	#2,DONNEE
.NO_Z2
	JSR	GERE_Z

	TST	FLAG_TEMPS
	BEQ.S	.NO_TIME
	ST	$FFFF8240.W
.NO_TIME
	MOVE.B	$FFFFFC02.W,D7
	LEA	ANGLES,A0
	MOVE.W	(A0),D0
	ADD	ADX,D0
	CMPI.B	#$1,D7
	BNE.S	.NO_F1
	ADDQ.W	#2,D0
.NO_F1
	CMPI.B	#$2,D7
	BNE.S	.NO_F2
	SUBQ.W	#2,D0
.NO_F2
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+
	MOVE.W	(A0),D0
	ADD	ADY,D0
	CMPI.B	#$3,D7
	BNE.S	.NO_F3
	ADDQ.W	#2,D0
.NO_F3
	CMPI.B	#$4,D7
	BNE.S	.NO_F4
	SUBQ.W	#2,D0
.NO_F4
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+
	MOVE.W	(A0),D0
	ADD	ADZ,D0
	CMPI.B	#$5,D7
	BNE.S	.NO_F5
	ADDQ.W	#2,D0
.NO_F5
	CMPI.B	#$6,D7
	BNE.S	.NO_F6
	SUBQ.W	#2,D0
.NO_F6
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	CMPI.B	#7,D7
	BNE.S	.NO7
	ADDQ.W	#1,Z_BASE
.NO7
	CMPI.B	#8,D7
	BNE.S	.NO8
	SUBQ.W	#1,Z_BASE
.NO8
	;MOVE	DONNEE,$FFFFC
	;MOVE	TIME,$FFFFC
	JSR	GERE_TIME

	ADD	#INC_D,DONNEE

	CMPI	#TIME_F,TIME
	BEQ.S	TERMINê
	BRA	MAIN_LOOP
TERMINê
	JMP	SUITE_DEMO
FLAG_TEMPS	DC	0
CREE_EFF
	JSR	COD_GEN
	MOVE.L	SCREEN2,A0
	MOVE.L	TEMP2,A1
	MOVE	#200-1,D7
.REMP_B
	REPT	20
	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	ENDR
	DBF	D7,.REMP_B
	RTS

GERE_TIME	ADDQ	#1,TIME
	;ADDQ	#1,MOD_X+2
	ADDQ	#2,MOD_Y+2
	RTS
;	CMPI	#TIME1,TIME
;	BGE.S	.NON
;	SUBQ	#1,FLAG
;	BNE.S	.NON
;	MOVE	#FLAG_INIT,FLAG
;	ADDQ	#2,DONNEE
;.NON
;	CMPI	#TIME2,TIME
;	BLE.S	.NON2
;	SUBQ	#8,MOD_Y+2
;	ADDQ	#6,MOD_X+2
;.NON2
;	RTS

SUITE_DEMO
LETS_GO
	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	MOVEQ	#0,D0
	MOVE	#7999,D7
.EFF_ECRS	MOVE.L	D0,(A0)+
	MOVE.L	D0,(A1)+
	DBF	D7,.EFF_ECRS

	CLR	TIME
	;MOVE	#FLAG_INIT,FLAG
	;MOVE	#3,FLAG_RT
	MOVE.L	#TABLE_ADR,SAVE_A1+2
	MOVE	#Y_DEP,MOD_Y+2
	MOVE	#X_DEP,MOD_X+2
	LEA	SAVE_VALUES,A0
	LEA	ANGLES,A1
	MOVE	(A0)+,DONNEE
	MOVE.L	(A0)+,(A1)+
	MOVE	(A0),(A1)

	MOVE	#DONNEE_INIT,DONNEE
	;SUB	#75*2,DONNEE
	JSR	GERE_Z

LOOP_PREC	BSR	VSYNC
	MOVE	#$011,$FFFF8240.W

	LEA	POINTS,A0	Pts du cube
	LEA	ANGLES,A1
	LEA	TRANS,A6
	LEA	BUF_PTS,A2
	BSR	ROTATE

SAVE_A1	LEA	TABLE_ADR,A1
	TST	(A1)
	BNE.S	.CA_TOURNE
	JMP	FINAL_PART
.CA_TOURNE	MOVE.L	(A1)+,A2
	MOVE.L	A1,SAVE_A1+2
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	JSR	(A2)

	BSR	AFFICH

.FRUIT_LOOP
	BSR	VSYNC
	MOVE	#$011,$FFFF8240.W
	BSR	AFFICH
	BSR	EMUL_EFFAC
	ST	$FFFF8240.W
	MOVE.L	ADR_INFO,A0
	CMPI.B	#$43,$FFFFFC02.W	Tps reel
	BEQ.S	.OK_TAKE_NOTE
	CMPI.B	#$42,$FFFFFC02.W	PrÇcalc
	BEQ.S	.OK_TAKE_NOTE_TOO
	BRA.S	.FRUIT_LOOP
.OK_TAKE_NOTE	CLR.B	(A0)+
	BRA.S	.SAV
.OK_TAKE_NOTE_TOO
	MOVE.B	#1,(A0)+
.SAV	MOVE.L	A0,ADR_INFO
	BSR	SWAPEC

	CMPI.B	#$3B,$FFFFFC02.W
	BNE.S	.NO_Z1
	ADDQ	#2,DONNEE
.NO_Z1	CMPI.B	#$3C,$FFFFFC02.W
	BNE.S	.NO_Z2
	SUBQ	#2,DONNEE
.NO_Z2
	JSR	GERE_Z

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIME
	ST	$FFFF8240.W
.NO_TIME
	MOVE.B	$FFFFFC02.W,D7
	LEA	ANGLES,A0
	MOVE.W	(A0),D0
	ADD	ADX,D0
	CMPI.B	#$1,D7
	BNE.S	.NO_F1
	ADDQ.W	#2,D0
.NO_F1
	CMPI.B	#$2,D7
	BNE.S	.NO_F2
	SUBQ.W	#2,D0
.NO_F2
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+
	MOVE.W	(A0),D0
	ADD	ADY,D0
	CMPI.B	#$3,D7
	BNE.S	.NO_F3
	ADDQ.W	#2,D0
.NO_F3
	CMPI.B	#$4,D7
	BNE.S	.NO_F4
	SUBQ.W	#2,D0
.NO_F4
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+
	MOVE.W	(A0),D0
	ADD	ADZ,D0
	CMPI.B	#$5,D7
	BNE.S	.NO_F5
	ADDQ.W	#2,D0
.NO_F5
	CMPI.B	#$6,D7
	BNE.S	.NO_F6
	SUBQ.W	#2,D0
.NO_F6
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	CMPI.B	#7,D7
	BNE.S	.NO7
	ADDQ.W	#1,Z_BASE
.NO7
	CMPI.B	#8,D7
	BNE.S	.NO8
	SUBQ.W	#1,Z_BASE
.NO8
	;MOVE	DONNEE,$FFFFC
;.WAIT	CMPI.B	#$39,$FFFFFC02.W
;	BNE.S	.WAIT
;.WAIT2	CMPI.B	#$39,$FFFFFC02.W
;	BEQ.S	.WAIT2

	ADD	#INC_D,DONNEE
	;CMPI	#TIME_F,TIME
	;BEQ.S	DEF
	JSR	GERE_TIME
	BRA	LOOP_PREC
*************************************************************************
FINAL_PART
	LEA	TABLE_ADR,A0
.SEEK	TST.L	(A0)+
	BNE.S	.SEEK
	MOVE.L	-8(A0),A0
.AND_DESTROY	CMPI	#$4E75,(A0)+
	BNE.S	.AND_DESTROY
	MOVE.L	A0,D6
	SUBI.L	#BUF_COD,D6	;D6=Taille du code gÇnÇrÇ
			;   en octets.
	LEA	BUF_INFO,A0
	LEA	TABLE_ADR,A1
	MOVEQ	#0,D0
	MOVEQ	#0,D2	;Nb d'octets virÇs
	MOVE	#TIME_F-1,D7
.LOOPY
	MOVE.B	(A0)+,D0
	BEQ.S	.REAL_TIME

.PREKALK	MOVE.L	A1,A2
	MOVE.L	(A2),A2	;A2=Adresse dÇbut code
	SUB.L	D2,A2
	MOVE.L	A2,D4
.LOOK_FOR_RTE	CMPI	#$4E75,(A2)+
	BNE.S	.LOOK_FOR_RTE
	MOVE.L	A2,D5
	SUB.L	D4,D5	;D5=Taille du morceau
			;   en cours
	SUB.L	D5,D6
	SUB.L	D2,(A1)+
	;ADDQ	#4,A1
	BRA.S	.EGGS_IT

.REAL_TIME	MOVE.L	A1,A2
	MOVE.L	(A2),A2	;A2=Adresse dÇbut code
	SUB.L	D2,A2
	MOVE.L	A2,D4
.LOOK_FOR_IT	ADDQ	#1,$FFFF8240.W
	CMPI	#$4E75,(A2)+
	BNE.S	.LOOK_FOR_IT
	MOVE.L	A2,D5
	SUB.L	D4,D5	;D5=Taille du morceau
			;   en cours
	SUB.L	D5,D6
	ADD.L	D5,D2
	MOVE.L	D6,D5
	LSR.L	#2,D5
	MOVE.L	D4,A3
.SHIFT_IT	MOVE.L	(A2)+,(A3)+
	DBF	D5,.SHIFT_IT

	LEA	4(A1),A2
	MOVE.L	A1,A3
	MOVE	#TIME_F,D5
.SHIFT_TABLE	MOVE.L	(A2)+,(A3)+
	DBF	D5,.SHIFT_TABLE
.EGGS_IT
	DBF	D7,.LOOPY

	NOP
	NOP
LETS_TRY_IT
	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	MOVEQ	#0,D0
	MOVE	#7999,D7
.EFF_ECRS	MOVE.L	D0,(A0)+
	MOVE.L	D0,(A1)+
	DBF	D7,.EFF_ECRS

	CLR	TIME
	MOVE.L	#TABLE_ADR,SAVE_A1B+2
	MOVE.L	#BUF_INFO,ADR_INFO
	MOVE.L	#BUF_INFO_TABLO,ADR_TABLO
	MOVE	#Y_DEP,MOD_Y+2
	MOVE	#X_DEP,MOD_X+2
	LEA	SAVE_VALUES,A0
	LEA	ANGLES,A1
	MOVE	(A0)+,DONNEE
	MOVE.L	(A0)+,(A1)+
	MOVE	(A0),(A1)
	JSR	GERE_Z
	
LOOP_FINAL	
	ST	$FFFF8240.W
	BSR	VSYNC
	MOVE	#$011,$FFFF8240.W

	LEA	POINTS,A0
	LEA	ANGLES,A1
	LEA	TRANS,A6
	LEA	BUF_PTS,A2
	BSR	ROTATE

	MOVE.L	ADR_INFO,A0
	MOVE.B	(A0)+,D0
	;MOVE.L	A0,ADR_INFO
	TST.B	D0
	BNE.S	SAVE_A1B
	JSR	EFFAC
	BRA.S	POPO
SAVE_A1B	LEA	TABLE_ADR,A1
	TST	(A1)
	BNE.S	.CA_TOURNE
	JMP	LETS_TRY_IT
.CA_TOURNE	MOVE.L	(A1)+,A2
	;MOVE.L	A1,SAVE_A1B+2
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	JSR	(A2)
POPO
	BSR	AFFICH
	CMPI.B	#$43,$FFFFFC02.W
	BNE.S	.NOF9
	MOVE.L	ADR_TABLO,A0
	CLR.B	(A0)+
	BRA.S	.GO_GO
.NOF9
	CMPI.B	#$42,$FFFFFC02.W
	BNE.S	.NOF8
	MOVE.L	ADR_TABLO,A0
	MOVE.B	#1,(A0)+
	BRA.S	.GO_GO
.NOF8
	;ST	$FFFF8240.W
	JMP	LOOP_FINAL

.GO_GO	MOVE.L	A0,ADR_TABLO
	MOVE.L	ADR_INFO,A0
	MOVE.B	(A0)+,D0
	BEQ.S	.H
	MOVE.L	SAVE_A1B+2,A1
	ADDQ	#4,A1
	MOVE.L	A1,SAVE_A1B+2
.H
	MOVE.L	A0,ADR_INFO

	BSR	SWAPEC

	CMPI.B	#$3B,$FFFFFC02.W
	BNE.S	.NO_Z1
	ADDQ	#2,DONNEE
.NO_Z1	CMPI.B	#$3C,$FFFFFC02.W
	BNE.S	.NO_Z2
	SUBQ	#2,DONNEE
.NO_Z2
	JSR	GERE_Z

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIME
	ST	$FFFF8240.W
.NO_TIME
	MOVE.B	$FFFFFC02.W,D7
	LEA	ANGLES,A0
	MOVE.W	(A0),D0
	ADD	ADX,D0
	CMPI.B	#$1,D7
	BNE.S	.NO_F1
	ADDQ.W	#2,D0
.NO_F1
	CMPI.B	#$2,D7
	BNE.S	.NO_F2
	SUBQ.W	#2,D0
.NO_F2
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+
	MOVE.W	(A0),D0
	ADD	ADY,D0
	CMPI.B	#$3,D7
	BNE.S	.NO_F3
	ADDQ.W	#2,D0
.NO_F3
	CMPI.B	#$4,D7
	BNE.S	.NO_F4
	SUBQ.W	#2,D0
.NO_F4
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+
	MOVE.W	(A0),D0
	ADD	ADZ,D0
	CMPI.B	#$5,D7
	BNE.S	.NO_F5
	ADDQ.W	#2,D0
.NO_F5
	CMPI.B	#$6,D7
	BNE.S	.NO_F6
	SUBQ.W	#2,D0
.NO_F6
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	CMPI.B	#7,D7
	BNE.S	.NO7
	ADDQ.W	#1,Z_BASE
.NO7
	CMPI.B	#8,D7
	BNE.S	.NO8
	SUBQ.W	#1,Z_BASE
.NO8
	;MOVE	DONNEE,$FFFFC
;.WAIT	CMPI.B	#$39,$FFFFFC02.W
;	BNE.S	.WAIT
;.WAIT2	CMPI.B	#$39,$FFFFFC02.W
;	BEQ.S	.WAIT2

	ADD	#INC_D,DONNEE
	CMPI	#TIME_F,TIME
	BEQ.S	KOD_GEN_TWO
	JSR	GERE_TIME
	BRA	LOOP_FINAL
KOD_GEN_TWO
	JSR	CREE_TABLOX

	CLR	TIME
	MOVE.L	#BUF_INFO_TABLO,ADR_TABLO
	MOVE.L	#BUF_TABLOS,ADR_TABLOS
	LEA	SAVE_VALUES,A0
	LEA	ANGLES,A1
	MOVE	(A0)+,DONNEE
	MOVE.L	(A0)+,(A1)+
	MOVE	(A0),(A1)
	JSR	GERE_Z
	MOVE	#Y_DEP,MOD_Y+2
	MOVE	#X_DEP,MOD_X+2

LOOP_PR_T
	LEA	POINTS,A0
	LEA	ANGLES,A1
	LEA	TRANS,A6
	LEA	BUF_PTS,A2
	BSR	ROTATE

	MOVE.L	ADR_TABLO,A0
	MOVE.B	(A0)+,D0
	MOVE.L	A0,ADR_TABLO
	TST.B	D0
	BEQ.S	.REAL_T
	BSR	AFFICH
.REAL_T
	JSR	GERE_Z

	MOVE.B	$FFFFFC02.W,D7
	LEA	ANGLES,A0
	MOVE.W	(A0),D0
	ADD	ADX,D0
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	MOVE.W	(A0),D0
	ADD	ADY,D0
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	MOVE.W	(A0),D0
	ADD	ADZ,D0
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	ADD	#INC_D,DONNEE
	CMPI	#TIME_F,TIME
	BEQ.S	KOD_GEN_OUT
	JSR	GERE_TIME
	BRA	LOOP_PR_T
KOD_GEN_OUT
;OUF! On arrive donc tout doucement Ö la boucle ultime, la plus WARG!, la
;plus OUHLALA!, la plus WIIIIIIIIIIIIIIIIIIZ!..... :

;On efface, dÇjÖ, parce que depuis le temps, ils doivent plus etre träs
;clairs, les Screens...
LAST_HOPE
	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	MOVEQ	#0,D0
	MOVE	#7999,D7
.EFF_ECRS	MOVE.L	D0,(A0)+
	MOVE.L	D0,(A1)+
	DBF	D7,.EFF_ECRS

	CLR	TIME		;Indispensable.
	LEA	MODIF,A1
	MOVE.L	#$4E714E71,(A1)+	;Sinon, grosse
	MOVE	#$4E71,(A1)		;panique...
	MOVE.L	#TABLE_ADR,SAVE_ULTIME_A1+2	;Effaáage, ready.
	MOVE.L	#BUF_INFO,ADR_INFO	;Infos effaáage, ready.
	MOVE.L	#BUF_INFO_TABLO,ADR_TABLO	;Infos affichage, ready.
	MOVE.L	#BUF_TABLOS,ADR_TABLOS	;Affichage, ready, pfoooo.
	MOVE.L	#BUF_VISION,ADR_VISION	;Visible ou non
	CLR	VISION
	MOVE	#Y_DEP,MOD_Y+2	;Au cas oó.
	MOVE	#X_DEP,MOD_X+2	;Idem
	LEA	SAVE_VALUES,A0	;On rÇinitialise tout
	LEA	ANGLES,A1	;le bordel.
	MOVE	(A0)+,DONNEE	;
	MOVE.L	(A0)+,(A1)+	;
	MOVE	(A0),(A1)	;
	JSR	GERE_Z	;

LAST_RUN	
	BSR	VSYNC
	CMPI.B	#$39,$FFFFFC02.W
	BNE.S	.ONE
	BSR	VSYNC
.ONE
	MOVE	#$010,$FFFF8240.W
	MOVE	#$040,$FFFF8242.W
	MOVE	#$030,$FFFF8244.W
	MOVE	#$030,$FFFF8246.W
	MOVE	#$777,$FFFF8248.W
	MOVE	#$777,$FFFF824A.W
	MOVE	#$777,$FFFF824C.W
	MOVE	#$777,$FFFF824E.W

	MOVE	#$444,$FFFF8250.W
	MOVE	#$444,$FFFF8252.W
	MOVE	#$444,$FFFF8254.W
	MOVE	#$444,$FFFF8256.W
	MOVE	#$444,$FFFF8258.W
	MOVE	#$444,$FFFF825A.W
	MOVE	#$444,$FFFF825C.W
	MOVE	#$444,$FFFF825E.W

;Bon. Les choses sÇrieuses commencent. On commence par rÇgler le
;compte de l'effaáage.

	MOVE.L	ADR_INFO,A0
	MOVE.B	(A0)+,D0
	MOVE.L	A0,ADR_INFO

	TST.B	D0	;0=Temps rÇÇl
			;1=Deltapackor le retour
	BNE.S	SAVE_ULTIME_A1
	JSR	EFFAC	;Movems, 4 plans, comme un porc.
	BRA.S	ZAP_ZAP_ZAPIDO
SAVE_ULTIME_A1	LEA	TABLE_ADR,A1
	TST	(A1)
	BNE.S	.CA_TOURNE
	JMP	DEF_FUCK
.CA_TOURNE	MOVE.L	(A1)+,A2
	MOVE.L	A1,SAVE_ULTIME_A1+2
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	JSR	(A2)
ZAP_ZAP_ZAPIDO

;Bon.. au tour de l'affichage. Ca va pas trainer.

	MOVE.L	ADR_TABLO,A0
	MOVE.B	(A0)+,D0
	MOVE.L	A0,ADR_TABLO

	TST.B	D0	;0=Temps rÇÇl
			;1=PrÇcadÇmo IV, the Revenge.
	BNE.S	PREK

	LEA	POINTS,A0	;Ö optimiser. Les calculs
	LEA	ANGLES,A1	;ne sont pas toujours
	LEA	TRANS,A6	;nÇcessaires.
	LEA	BUF_PTS,A2	;
	BSR	ROTATE	;

	JSR	AFFICH	;Normal.
	BRA.S	FINI__
PREK	JSR	AFFICH2	;Anormal.
FINI__	;C'Çtait long, hein?

	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2

	MOVE.L	SCREEN1,D0
	lsr.l	#8,d0
	LEA	$FFFF8201.W,A0
	movep	d0,(a0)

	JSR	GERE_Z

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIME
	ST	$FFFF8240.W
.NO_TIME
	MOVE.B	$FFFFFC02.W,D7
	LEA	ANGLES,A0
	MOVE.W	(A0),D0
	ADD	ADX,D0
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	MOVE.W	(A0),D0
	ADD	ADY,D0
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	MOVE.W	(A0),D0
	ADD	ADZ,D0
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	ADD	#INC_D,DONNEE
	CMPI	#TIME_F-1,TIME
	BEQ.S	DEF_FUCK
	JSR	GERE_TIME

	BRA	LAST_RUN

DEF_FUCK
	MOVE	#2,TIME		;Indispensable.
	LEA	MODIF,A1
	MOVE.L	#$4E714E71,(A1)+	;Sinon, grosse
	MOVE	#$4E71,(A1)		;panique...
	MOVE.L	#TABLE_ADR,SAVE_ULTIME_A1+2	;Effaáage, ready.
	MOVE.L	#BUF_INFO,ADR_INFO	;Infos effaáage, ready.
	MOVE.L	#BUF_INFO_TABLO,ADR_TABLO	;Infos affichage, ready.
	MOVE.L	#BUF_TABLOS,ADR_TABLOS	;Affichage, ready, pfoooo.
	MOVE.L	#BUF_VISION,ADR_VISION	;Visible ou non
	CLR	VISION
	MOVE	#Y_DEP,MOD_Y+2	;Au cas oó.
	MOVE	#X_DEP,MOD_X+2	;Idem
	LEA	SAVE_VALUES,A0	;On rÇinitialise tout
	LEA	ANGLES,A1	;le bordel.
	MOVE	(A0)+,DONNEE	;
	MOVE.L	(A0)+,(A1)+	;
	MOVE	(A0),(A1)	;

	MOVE.L	ADR_INFO,A0
	MOVE.W	(A0)+,D0
	MOVE.L	A0,ADR_INFO

	MOVE.L	SAVE_ULTIME_A1+2,A1
	ADDQ	#8,A1
	MOVE.L	A1,SAVE_ULTIME_A1+2

	MOVE.L	ADR_TABLO,A0
	MOVE	(A0)+,D0
	MOVE.L	A0,ADR_TABLO

	MOVE.L	SAVE_POUR_APRES,ADR_TABLOS

	MOVE.L	ADR_VISION,A0
	LEA	NB_FACES*2(A0),A0
	MOVE.L	A0,ADR_VISION

	;JSR	GERE_Z

	LEA	ANGLES,A0
	MOVE.W	(A0),D0
	ADD	ADX,D0
	ADD	ADX,D0
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	MOVE.W	(A0),D0
	ADD	ADY,D0
	ADD	ADY,D0
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	MOVE.W	(A0),D0
	ADD	ADZ,D0
	ADD	ADZ,D0
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	ADD	#INC_D,DONNEE
	ADD	#INC_D,DONNEE
	JSR	GERE_Z	;

	BRA	LAST_RUN

;TIMER
;	CLR.B	$FFFFFA1B.W
;	MOVE.B	V1,$FFFFFA21.W
;	MOVE.B	#8,$FFFFFA1B.W
;	MOVE.L	#TIMER2,$120.W
;	BCLR	#0,$FFFFFA0F.W
;	RTE

OBJ
	DCB	3*NB_PTS_

GERE_Z	LEA	OBJ,A0
	LEA	POINTS+2,A1
	MOVE	DONNEE,D0

	REPT	NB_PTS_*3
	MOVEQ	#0,D1
	MOVE	(A0)+,D1
	MULS	D0,D1
	LSR.L	#8,D1
	ADD	D1,D1
	MOVE	D1,(A1)+
	ENDR

	;REPT	NB_PTS_
	;MOVE.L	(A0)+,(A1)+
	;MOVE	(A0)+,(A1)
	;ADD	D0,(A1)+
	;ENDR

	RTS

ADR_BUF	DS.L	1
ADR_TAB	DS.L	1
;
;Routine valable si tout plan n l'emporte sur tous les plans <n
;

COD_GEN	MOVE.L	SCREEN2,A0	On vient de l'Çcrire
	MOVE.L	TEMP2,A1	Ecran prÇcÇdent
	MOVE.L	ADR_BUF,A6
	MOVE.L	ADR_TAB,A5
	MOVE.L	A6,(A5)+
	MOVE.L	A5,ADR_TAB
	MOVEQ	#0,D7	Offset Y
	MOVEQ	#0,D6	Offset X
LOOP_GEN
**
	MOVE	6(A0),D0	New Plan4
	BEQ	.NEW_P4_NUL
;New P4 utilisÇ...
	CMPI	#-1,D0
	BEQ	.PLEIN
	MOVE	(A0),D0
	BEQ	.P1V
	MOVE	2(A0),D0
	BEQ.S	.P2VB
	MOVE	4(A0),D0
	BEQ.S	.P3VB
	BRA	RIEN_A_FAIRE
.P3VB	MOVE	4(A1),D0
	BEQ	RIEN_A_FAIRE
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#4,(A6)+
	BRA	RIEN_A_FAIRE
.P2VB	MOVE	4(A0),D0
	BEQ.S	.P3VC
	MOVE	2(A1),D0
	BEQ	RIEN_A_FAIRE
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#2,(A6)+
	BRA	RIEN_A_FAIRE
.P3VC	;NEW 2/3 VIDES
	MOVE	2(A1),D0
	BEQ.S	.OP2V
	MOVE	4(A1),D0
	BEQ.S	.Z
	MOVE	#$2140,(A6)+	MOVE.L D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#2,(A6)+
	BRA	RIEN_A_FAIRE
.Z
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#2,(A6)+
	BRA	RIEN_A_FAIRE
.OP2V	MOVE	4(A1),D0
	BEQ	RIEN_A_FAIRE
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#4,(A6)+
	BRA	RIEN_A_FAIRE

.P1V
	MOVE	2(A0),D0
	BEQ	.P2V
	MOVE	4(A0),D0
	BEQ.S	.P3V_P2PV
	MOVE	(A1),D0
	BEQ.S	.OP1VA
	MOVE	2(A1),D0
	BEQ.S	.E
	MOVE	#$2140,(A6)+	MOVE.L D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA	RIEN_A_FAIRE
.E
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA	RIEN_A_FAIRE

.OP1VA	MOVE	2(A1),D0
	BEQ	RIEN_A_FAIRE
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#2,(A6)+
	BRA	RIEN_A_FAIRE
.P3V_P2PV	MOVE	(A1),D0
	BEQ.S	.OP1VK
	MOVE	4(A1),D0
	BEQ.S	.LOLO
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#4,(A6)+
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA	RIEN_A_FAIRE
.LOLO
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA	RIEN_A_FAIRE

.OP1VK	MOVE	4(A1),D0
	BEQ	RIEN_A_FAIRE
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#4,(A6)+
	BRA	RIEN_A_FAIRE

.P2V	MOVE	4(A0),D0
	BEQ.S	.P3V
	MOVE	(A1),D0
	BEQ.S	.OP1VK2
	MOVE	2(A1),D0
	BEQ.S	.L2
	MOVE	#$2140,(A6)+	MOVE.L D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA	RIEN_A_FAIRE
.L2
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA	RIEN_A_FAIRE

.OP1VK2	MOVE	2(A1),D0
	BEQ	RIEN_A_FAIRE
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#2,(A6)+
	BRA	RIEN_A_FAIRE

.P3V
	MOVE	(A1),D0
	BEQ.S	.OLDP1VID
	MOVE	2(A1),D0
	BEQ.S	.ZZE
	MOVE	4(A1),D0
	BEQ.S	.EDED
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#4,(A6)+
	MOVE	#$2140,(A6)+	MOVE.L D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA	RIEN_A_FAIRE
.EDED
	MOVE	#$2140,(A6)+	MOVE.L D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA	RIEN_A_FAIRE
	
.ZZE	MOVE	4(A1),D0
	BEQ.S	.POPOP
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#4,(A6)+
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA	RIEN_A_FAIRE
.POPOP	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA	RIEN_A_FAIRE
.OLDP1VID
	MOVE	2(A1),D0
	BEQ.S	.OLDP2VID
	MOVE	4(A1),D0
	BEQ.S	.AAA
	MOVE	#$2140,(A6)+	MOVE.L D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#2,(A6)+
	BRA	RIEN_A_FAIRE
.AAA
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#2,(A6)+
	BRA	RIEN_A_FAIRE
.OLDP2VID
	MOVE	4(A1),D0
	BEQ	RIEN_A_FAIRE
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#4,(A6)+
	BRA	RIEN_A_FAIRE	
.PLEIN	TST	(A0)
	BNE.S	.V1
	MOVE	(A1),(A0)
.V1	TST	2(A0)
	BNE.S	.V2
	MOVE	2(A1),2(A0)
.V2	TST	4(A0)
	BNE.S	.V3
	MOVE	4(A1),4(A0)
.V3	BRA	RIEN_A_FAIRE

*
.NEW_P4_NUL	MOVE	6(A1),D0
	BEQ.S	FIRST_VERSION
;Ici: New P4 nul, Old P4 utilisÇ
	MOVE	4(A0),D0
	BEQ.S	.NEW_P3_NUL
;New P3 utilisÇ, New P4 nul: on efface que le old plan4
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#6,(A6)+
	BRA.S	FIRST_VERSION
*
.NEW_P3_NUL	MOVE	4(A1),D0
	BEQ.S	.OLD_P3_NUL
;New P3/P4 nuls. Pas les Olds. Kill!
	MOVE	#$2140,(A6)+	MOVE.L D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#4,(A6)+
	CLR.L	4(A1)	On les dÇgage, on enchaine.
	BRA.S	FIRST_VERSION
*
.OLD_P3_NUL
;New P3/P4 et old P3 nuls: on efface juste le old P4
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#6,(A6)+
;et on enchaine sur la 1äre version....
**
;Ici: plan 4 inutilisÇ (1äre version de ce gÇnÇrateur)
FIRST_VERSION	MOVE	(A0),D0	New Plan1
	BEQ	.NEW_P1_NUL
;Ici: New P1 occupÇ.
	MOVE	2(A0),D0
	BNE.S	.OQP2
;Ici: New P1 occupÇ, New P2 vide.
	MOVE	4(A0),D0
	BEQ.S	.VID3
;Ici: New P1 occupÇ, New P2 vide, New P3 occupÇ.
	CMPI	#-1,D0
	BNE.S	.OK_1
	MOVE	2(A1),2(A0)
	BRA	RIEN_A_FAIRE
.OK_1	MOVE	2(A1),D0
	BEQ	RIEN_A_FAIRE
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#2,(A6)+
	BRA	RIEN_A_FAIRE
.VID3
;Ici: New P1 occupÇ, New P2 vide, New P3 vide.
	MOVE	2(A1),D0
	BEQ.S	.VID2
	MOVE	4(A1),D0
	BEQ.S	.VID3B
	MOVE	#$2140,(A6)+	MOVE.L D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#2,(A6)+
	BRA	RIEN_A_FAIRE
.VID3B
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#2,(A6)+
	BRA	RIEN_A_FAIRE
.VID2	MOVE	4(A1),D0
	BEQ	RIEN_A_FAIRE
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#4,(A6)+
	BRA	RIEN_A_FAIRE
.OQP2	MOVE	4(A0),D0
	BNE	RIEN_A_FAIRE	(3 new plans OQP)
;Ici: New P1 et New P2 occupÇs, New P3 vide.
	MOVE	4(A1),D0
	BEQ	RIEN_A_FAIRE
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#4,(A6)+
	BRA	RIEN_A_FAIRE
*****
.NEW_P1_NUL	MOVE	2(A0),D0	New Plan2
	BEQ.S	.NEW_P2_NUL
;Ici: New P1 nul. New P2 occupÇ.
	MOVE	4(A0),D0
	BEQ.S	.NUL3
;Ici: New P1 nul. New P2 occupÇ. New P3 occupÇ.
	MOVE	(A1),D0
	BEQ	RIEN_A_FAIRE
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA	RIEN_A_FAIRE
.NUL3
;Ici: New P1 et New P3 nuls. New P2 occupÇ.
	MOVE	4(A1),D0
	BEQ.S	.NUL_3
;Ici: Old P3 non nul.
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#4,(A6)+
.NUL_3
;Ici: Old P3 nul.
	CMPI	#-1,2(A0)
	BNE.S	.OK_2
	MOVE	(A1),(A0)
	BRA	RIEN_A_FAIRE
.OK_2	MOVE	(A1),D0
	BEQ	RIEN_A_FAIRE
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA	RIEN_A_FAIRE
*****
.NEW_P2_NUL	MOVE	4(A0),D0	New Plan3
	BEQ.S	.TOUS_NULS
;Ici: New P1 et New P2 nuls, New P3 occupÇ.
;Si P3=-1, c'est bon vu que P3 l'emporte sur tout.
;Sinon on efface les plans 1 et 2.
	CMPI	#-1,D0
	BNE.S	.OK_3
	MOVE.L	(A1),(A0)
	BRA	RIEN_A_FAIRE
.OK_3	MOVE	(A1),D0
	BEQ.S	.NUL1
	MOVE	2(A1),D0
	BEQ.S	.NUL2
	MOVE	#$2140,(A6)+	MOVE.L D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA.S	RIEN_A_FAIRE
.NUL2
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA.S	RIEN_A_FAIRE

.NUL1	MOVE	2(A1),D0
	BEQ.S	RIEN_A_FAIRE
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#2,(A6)+
	BRA.S	RIEN_A_FAIRE
**************************************************************************
.TOUS_NULS	;les 3 nouveaux plans sont nuls.
	MOVE	(A1),D0	Old Plan1
	BEQ.S	.OLD_P1_NUL
*****
;Ici: ancien plan1 non nul. A effacer, en w ou l.
	MOVE	2(A1),D0	Old Plan2
	BNE.S	.MOVE_L_
;Ici: old P1 non nul, old P2 nul => MOVE.W et raccord
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA.S	.OLD_P2_NUL
.MOVE_L_
;Ici: old P1 et old P2 non nuls => MOVE.L et raccord
	MOVE	#$2140,(A6)+	MOVE.L D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)+
	BRA.S	.OLD_P2_NUL
*****
.OLD_P1_NUL	MOVE	2(A1),D0	Old Plan2
	BEQ.S	.OLD_P2_NUL
;Ici: ancien plan1 nul, ancien plan2 non nul.
;A effacer en .W ou .L
	MOVE	4(A1),D0	Old Plan3
	BNE.S	.MOVE_L
;Ici: effacage du plan2 only.
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#2,(A6)+
	BRA.S	RIEN_A_FAIRE
.MOVE_L
;Ici: effacage des plans 2 et 3.
	MOVE	#$2140,(A6)+	MOVE.L D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#2,(A6)+
	BRA.S	RIEN_A_FAIRE
*****
.OLD_P2_NUL	MOVE	4(A1),D0	Old Plan3
	BEQ.S	RIEN_A_FAIRE
;Ici: tout est nul sauf l'ancien plan3. Donc on l'efface.
	MOVE	#$3140,(A6)+	MOVE D0,n(A0)
	MOVE	D7,(A6)
	ADD	D6,(A6)
	ADDQ	#4,(A6)+	Plan3
*****
RIEN_A_FAIRE	;les 3 anciens plans sont nuls Çgalement.
	IFNE	ALGO
	ADD	#160,D7
	LEA	20*8(A0),A0
	LEA	20*8(A1),A1
	CMPI	#160*200,D7
	BNE	.LOOP_GEN
	MOVEQ	#0,D7
	ADDQ	#8,D6

	LEA	-20*8*200+8(A0),A0
	LEA	-20*8*200+8(A1),A1
	
	MOVE.L	SCREEN2,A2
	LEA	160(A2),A2
	CMP.L	A0,A2
	BNE	LOOP_GEN
	MOVE	#$4E75,(A6)+
	MOVE.L	A6,ADR_BUF
	RTS
	ENDC

	IFEQ	ALGO
	ADDQ	#8,D7
	ADDQ	#8,A0	On passe au
	ADDQ	#8,A1	bloc suivant
	MOVE.L	SCREEN2,A2
	LEA	200*160(A2),A2
	CMP.L	A0,A2
	BNE	LOOP_GEN
	MOVE	#$4E75,(A6)+
	MOVE.L	A6,ADR_BUF
	RTS
	ENDC


DONNEE	DC	194
TIME	DC	0

VSYNC	CMPI	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	CMPI.B	#$44,$FFFFFC02.W
	BNE.S	.NO_F10
	TST	FLAG_TEMPS
	BEQ.S	.P
	CLR	FLAG_TEMPS
	BRA.S	.NO_F10
.P	MOVE	#1,FLAG_TEMPS
.NO_F10
	RTE

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2

	MOVE.L	TEMP1,D0
	MOVE.L	TEMP2,TEMP1
	MOVE.L	D0,TEMP2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

FIN	MOVE.L	4.W,A0
	JMP	(A0)

EFFAC
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVE.L	D0,A1
	MOVE.L	D0,A2
	MOVE.L	D0,A3
	MOVE.L	D0,A4
	MOVE.L	D0,A5
	MOVE.L	D0,A6
	MOVE	#200-1,D7
.EFF	MOVEM.L	D0-D6/A1-A6,(A0)
	MOVEM.L	D0-D6/A1-A6,4*13(A0)
	MOVEM.L	D0-D6/A1-A6,4*13*2(A0)
	MOVE.L	D0,4*13*3(A0)
	LEA	160(A0),A0
	DBF	D7,.EFF
	RTS
	;194    (DONNEE)
EMUL_EFFAC
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVE.L	D0,A1
	MOVE.L	D0,A2
	MOVE.L	D0,A3
	MOVE.L	D0,A4
	MOVE.L	D0,A5
	MOVE.L	D0,A6
	MOVE	#200-1,D7
.EFF	MOVEM.L	D0-D6/A1-A6,(A0)
	MOVEM.L	D0-D6/A1-A6,4*13(A0)
	MOVEM.L	D0-D6/A1-A6,4*13*2(A0)
	MOVE.L	D0,4*13*3(A0)
	LEA	0(A0),A0
	DBF	D7,.EFF
	RTS

AFFICH	LEA	BUF_PTS,A0
	JSR	PROJETTE

	LEA	DONNêES_AFFICH,A6
	MOVE	(A6)+,D7
.LOOP_AFF	MOVE.L	D7,-(SP)
	MOVE	(A6)+,MOD_PLAN
	LEA	COORD,A0
	LEA	face_en_cours,A1
	MOVE	(A6),(A1)+
	MOVE	(A6)+,D6
	SUBQ	#1,D6
.FACES	MOVE	(A6)+,D5
	MOVE.L	(A0,D5.W),(A1)+
	DBF	D6,.FACES
	LEA	face_en_cours,A0
	MOVE.L	A6,-(SP)
	JSR	RT_POLY
	MOVE.L	(SP)+,A6
	MOVE.L	(SP)+,D7
	DBF	D7,.LOOP_AFF
	RTS

SPE	DC	0
AFFICH2	;LEA	BUF_PTS,A0
	;JSR	PROJETTE

	MOVE.L	ADR_TABLOS,A3

	LEA	DONNêES_AFFICH,A6
	MOVE	(A6)+,D7
.LOOP_AFF	MOVE.L	D7,-(SP)
	;MOVE	(A6)+,MOD_PLAN
	MOVE	(A6)+,MOD_PLAN2
	LEA	COORD,A0
	LEA	face_en_cours,A1
	MOVE	(A6),(A1)+
	MOVE	(A6)+,D6
	SUBQ	#1,D6
.FACES	MOVE	(A6)+,D5
	MOVE.L	(A0,D5.W),(A1)+
	DBF	D6,.FACES
	LEA	face_en_cours,A0
	MOVE.L	A6,-(SP)
	JSR	RT_POLY2
	MOVE.L	(SP)+,A6
	MOVE.L	(SP)+,D7
	DBF	D7,.LOOP_AFF

	MOVE.L	A3,ADR_TABLOS
	ADDQ	#1,SPE
	CMPI	#2,SPE
	BNE.S	.FOI
	MOVE.L	A3,SAVE_POUR_APRES
.FOI	RTS
SAVE_POUR_APRES	DC.L	0
face_en_cours	dC.w	4
	ds.l	4

PROJETTE	LEA	CORES_X,A2
	LEA	COEFF+COEFF_INIT*2,A1	380*2
	LEA	COORD,A5
	MOVE.W	#NB_PTS_-1,D7
AFF_ALL	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	MOVE.W	(A0)+,D2
	ADD.W	Z_BASE,D2
	ADD.W	D2,D2
	MOVE.W	(A1,D2.W),D2
	MULS.W	D2,D0
	ASR.L	#8,D0
	MULS.W	D2,D1
	ASR.L	#8,D1
MOD_X	ADDI.W	#X_DEP,D0
MOD_Y	ADDI.W	#Y_DEP,D1
	MOVE	D0,(A5)+
	MOVE	D1,(A5)+
	;MULS.W	#160,D1
	;ADD.W	D0,D0
	;ADD.W	D0,D0
	;ADD.W	2(A2,D0.W),D1
	;MOVE.W	(A2,D0.W),D0
	;OR.W	D0,(A3,D1.W)
	DBRA	D7,AFF_ALL
	RTS

COORD	DCB	2*NB_PTS_,0

;ENTRêES : A0=TABLE DE POINTS ( .W:NB DE POINTS, ET LES POINTS)
;          A1=TABLE D'ANGLE ( ANGLE X, ANGLE Y, ANGLE Z)
;          A6=TABLE DE TRANSLATION ( U_X, U_Y, U_Z)
;          A2=TABLE DE DESTINATION POUR LES POINTS

;Formules de rotation 3 axes:
;X'=X[SIN(T)SIN(U)SIN(V)+COS(T)COS(V)]+Y[COS(T)SIN(U)SIN(V)-SIN(T)COS(V)]+Z[COS(U)SIN(V)]
;Y'=X[SIN(T)COS(U)]+Y[COS(T)COS(U)]-ZSIN(U)
;Z'=X[SIN(T)SIN(U)COS(V)-COS(T)SIN(V)]+Y[COS(T)SIN(U)COS(V)+SIN(T)SIN(V)]+Z[COS(U)COS(V)]
ROTATE	LEA	SINUS(PC),A4	;SINUS
	LEA	LONG_SINUS/4(A4),A3	;COSINUS

	MOVE.W	(A1)+,D0	;ANGLE_X
	ADD.W	D0,D0
	MOVE.W	(A3,D0.W),D1	;COSINUS TETA_X
	MOVE.W	(A4,D0.W),D0	;SINUS TETA_X

	MOVE.W	(A1)+,D2	;ANGLE_Y
	ADD.W	D2,D2
	MOVE.W	(A3,D2.W),D3	;COSINUS TETA_Y
	MOVE.W	(A4,D2.W),D2	;SINUS TETA_Y

	MOVE.W	(A1)+,D4	;ANGLE_Z
	ADD.W	D4,D4
	MOVE.W	(A3,D4.W),D5	;COSINUS TETA_Z
	MOVE.W	(A4,D4.W),D4	;SINUS TETA_Z

	LEA	MATRICE,A4
	LEA	LOGARITHMES+512*2(PC),A3
	;ON FABRIQUE SIN(T)*SIN(U)*SIN(V)+COS(T)*COS(V)
	MOVE.W	D0,D6	;D6=SIN(T)
	MULS.W	D2,D6	;D6=SIN(T)*SIN(U)
	ASR.L	#8,D6
	MULS.W	D4,D6	;D6=SIN(T)*SIN(U)*SIN(V)
	MOVE.W	D1,D7	;D7=COS(T)
	MULS.W	D5,D7	;D7=COS(T)*COS(V)
	ADD.L	D7,D6
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK1
	MOVEQ	#1*2,D6
.OK1	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(T)*SIN(U)*SIN(V)-SIN(T)*COS(V)
	MOVE.W	D1,D6	;D6=COS(T)
	MULS.W	D2,D6	;D6=COS(T)*SIN(U)
	ASR.L	#8,D6
	MULS.W	D4,D6	;D6=COS(T)*SIN(U)*SIN(V)
	MOVE.W	D0,D7	;D7=SIN(T)
	MULS.W	D5,D7	;D7=SIN(T)*COS(V)
	SUB.L	D7,D6
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK2
	MOVEQ	#1*2,D6
.OK2	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(U)*SIN(V)
	MOVE.W	D3,D6	;D6=COS(U)
	MULS.W	D4,D6	;D6=COS(U)*SIN(V)
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK3
	MOVEQ	#1*2,D6
.OK3	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE SIN(T)*COS(U)
	MOVE.W	D0,D6	;D6=SIN(T)
	MULS.W	D3,D6	;D6=SIN(T)*COS(U)
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK4
	MOVEQ	#1*2,D6
.OK4	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(T)*COS(U)
	MOVE.W	D1,D6	;D6=COS(T)
	MULS.W	D3,D6	;D6=COS(T)*COS(U)
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK5
	MOVEQ	#1*2,D6
.OK5	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE -SIN(U)
	MOVE.W	D2,D6
	NEG.W	D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK6
	MOVEQ	#1*2,D6
.OK6	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE SIN(T)*SIN(U)*COS(V)-COS(T)*SIN(V)
	MOVE.W	D0,D6	;D6=SIN(T)
	MULS.W	D2,D6	;D6=SIN(T)*SIN(U)
	ASR.L	#8,D6
	MULS.W	D5,D6	;D6=SIN(T)*SIN(U)*COS(V)
	MOVE.W	D1,D7	;D7=COS(T)
	MULS.W	D4,D7	;D7=COS(T)*SIN(V)
	SUB.L	D7,D6
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK7
	MOVEQ	#1*2,D6
.OK7	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(T)*SIN(U)*COS(V)+SIN(T)*SIN(V)
	MOVE.W	D1,D6	;D6=COS(T)
	MULS.W	D2,D6	;D6=COS(T)*SIN(U)
	ASR.L	#8,D6
	MULS.W	D5,D6	;D6=COS(T)*SIN(U)*COS(V)
	MOVE.W	D0,D7	;D7=SIN(T)
	MULS.W	D4,D7	;D7=SIN(T)*SIN(V)
	ADD.L	D7,D6
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK8
	MOVEQ	#1*2,D6
.OK8	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(U)*COS(V)
	MULS.W	D3,D5	;D5=COS(U)*COS(V)
	ASR.L	#8,D5
	ADD.W	D5,D5
	TST.W	D5
	BNE.S	.OK9
	MOVEQ	#1*2,D5
.OK9	MOVE.W	(A3,D5.W),(A4)+

	MOVE.W	(A6)+,D0
	ADD.W	D0,D0	;U_X*2
	MOVE.W	D0,TRANSLATE_X
	MOVE.W	(A6)+,D0
	ADD.W	D0,D0	;U_Y*2
	MOVE.W	D0,TRANSLATE_Y
	MOVE.W	(A6),D0
	ADD.W	D0,D0	;U_Z*2
	MOVE.W	D0,TRANSLATE_Z

	MOVE.W	(A0)+,D7	;D7=NB DE POINTS
	SUBQ.W	#1,D7
	MOVE.L	#MATRICE,D6
	LEA	BUF_EXP,A5

ROTATE_ALL	MOVE.L	D6,A4
	MOVE.W	(A0)+,D0	;X*2
TRANSLATE_X = *+2
	ADDI.W	#$1234,D0
	MOVE.W	(A0)+,D1	;Y*2
TRANSLATE_Y = *+2
	ADDI.W	#$1234,D1
	MOVE.W	(A0)+,D2	;Z*2
TRANSLATE_Z = *+2
	ADDI.W	#$1234,D2

	REPT	3
	MOVE.W	(A3,D0.W),D3	;LOG(X)
	ADD.W	(A4)+,D3	;LOG(X)+LOG(COEFF)
	MOVE.W	(A5,D3.W),D3	;D3=X*COEFF
	MOVE.W	(A3,D1.W),D4	;LOG(Y)
	ADD.W	(A4)+,D4	;LOG(Y)+LOG(COEFF)
	MOVE.W	(A5,D4.W),D4	;D4=Y*COEFF
	MOVE.W	(A3,D2.W),D5	;LOG(Z)
	ADD.W	(A4)+,D5	;LOG(Z)+LOG(COEFF)
	MOVE.W	(A5,D5.W),D5	;D5=Z*COEFF
	ADD.W	D4,D3
	ADD.W	D5,D3
	MOVE.W	D3,(A2)+
	ENDR

	DBRA	D7,ROTATE_ALL
	RTS

PREPA_LOG_EXP	;ON ARRANGE LA PARTIE NEGATIVE DE LA TABLE DES LOG.
	LEA	LOGARITHMES(PC),A0
	MOVE.W	#512-1,D7
LOG_NEGATIFS	MOVE.W	(A0),D0
	ADD.W	D0,D0
	ADDI.W	#LONG_EXP/2,D0
	MOVE.W	D0,(A0)+
	DBRA	D7,LOG_NEGATIFS
	MOVE.W	#LONG_EXP/2,D7
	MULU.W	#3,D7
	MOVE.W	D7,(A0)+	;D7=LONG_EXP*3 (ZERO)
	MOVE.W	#512-1,D7
LOG_POSITIFS	MOVE.W	(A0),D0
	ADD.W	D0,D0
	MOVE.W	D0,(A0)+	;LOG*2
	DBRA	D7,LOG_POSITIFS

;ON CREE LA PARTIE NEGATIVE ET LA 2EME PARTIE POSITIVE DES EXPONENTIELLES.
	LEA	EXPONENTIELLES(PC),A0
	LEA	BUF_EXP,A1
	LEA	LONG_EXP/2(A1),A2
	LEA	LONG_EXP/2(A2),A3
	MOVE.W	#LONG_EXP/4-1,D7
MAKE_EXP	MOVEQ	#0,D0
	MOVE.L	(A0)+,D0
	LSR.L	#8,D0	;VALEUR REELLE DU RESULTAT
	MOVE.W	D0,(A1)+
	MOVE.W	D0,(A3)+
	NEG.W	D0
	MOVE.W	D0,(A2)+
	DBRA	D7,MAKE_EXP

;ON CREE LES 2 TABLES DE ZERO LONGUES CHACUNES DE LONG_EXP.
	LEA	BUF_EXP+(LONG_EXP/2)*3,A0
	MOVE.W	#(LONG_EXP/4)*2-1,D7
MAKE_ZERO_TABLE	CLR.W	(A0)+
	DBRA	D7,MAKE_ZERO_TABLE
	RTS

SINUS	INCBIN	SINUSROT.DAT
LONG_SINUS = *-SINUS
	DCB.W	LONG_SINUS/4,0

LOGARITHMES	INCBIN	LOG512.LOG

EXPONENTIELLES	INCBIN	EXP512.EXP
LONG_EXP = *-EXPONENTIELLES

COEFF	INCBIN	COEFF2.3D

	DCB.W	500,0
N	SET	0
CORES_X	REPT	20
	DC.W	32768,N,16384,N,8192,N,4096,N,2048,N,1024,N,512,N,256,N,128,N,64,N,32,N,16,N,8,N,4,N,2,N,1,N
N	SET	N+8
	ENDR
	DCB.W	500,0

*** Partie affichage
CHOICE	DC	0	;0=MOVE
			;1=OR
INIT_ROUT_POLY	LEA	BUF_COD_GEN,A0
	LEA	TABLE_ADR_COD,A1

	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D3
.GO	MOVE.L	#RT_COOL,(A1)+
	DBRA	D3,.GO

	MOVE.W	#$FFFF,D0	;1ER MOTIF
	MOVE.W	#$8000,D1	;DERNIER MOTIF
	MOVEQ	#0,D3	;DECALAGE

	MOVEQ	#16-1,D6
.DECAL
	MOVEQ	#16+1,D5
	ADD.W	#18*16,D5	;D5=LONGUEUR DE LA LIGNE
	ADD.W	D3,D5

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
.COPY_MOVED7	BSR	POZ_ADRESSE
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	SUBI.W	#16,D5
	DBRA	D7,.COPY_MOVED7

	MOVE.W	#16+1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_1
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_1
.PAS_D7_1
	TST	CHOICE
	BNE.S	.OR1
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV1
.OR1	MOVE	#$0054,(A0)+	OR.W #$Imm,(A4)
.MOV1	MOVE.W	D0,(A0)+
.RETOUR_1	MOVE.W	#1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_2
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_2
.PAS_D7_2
	TST	CHOICE
	BNE.S	.OR2
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV2
.OR2	MOVE	#$0055,(A0)+	OR "             "
.MOV2	MOVE.W	D1,(A0)+
.RETOUR_2
	BSR	COPY_RT_COOL

	ASR.W	#1,D1
	ADDQ.W	#1,D3	;LONGUEUR SUIVANT
	DBRA	D6,.DECAL

	LEA	TABLE_ADR_COD+1320,A1

	MOVE.W	#$7FFF,D3	;1ER MOTIF
	MOVEQ	#14,D5

	MOVEQ	#14,D2
ALL_RT
	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D0
.GO	MOVE.L	#RT_COOL,(A1)+
	DBRA	D0,.GO

	MOVE.W	#1,A3

	MOVEQ	#0,D0
	MOVE.W	D5,D6
LITTLE	BSR	POZ_ADRESSE2
	BSET	D6,D0
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_LITTLE
	MOVE.W	#$3887,(A0)+
	BRA.S	.RETOUR_LITTLE
.PAS_D7_LITTLE
	TST	CHOICE
	BNE.S	.OR3
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV3
.OR3	MOVE	#$0054,(A0)+
.MOV3	MOVE.W	D0,(A0)+

.RETOUR_LITTLE
	BSR	COPY_RT_COOL

	ADDQ.W	#1,A3
	DBRA	D6,LITTLE

	MOVE.W	D3,D0
	MOVE.W	#$8000,D1	;DERNIER MOTIF

	MOVEQ	#16-1,D6
DECAL
	MOVE.W	#18*16,A3
	ADD.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
COPY_MOVED7	BSR	POZ_ADRESSE2
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	LEA	-16(A3),A3
	DBRA	D7,COPY_MOVED7

	MOVE.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3
	BSR	POZ_ADRESSE2
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_3
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_3
.PAS_D7_3
	TST	CHOICE
	BNE.S	.OR4
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV4
.OR4	MOVE	#$0054,(A0)+
.MOV4	MOVE.W	D0,(A0)+
.RETOUR_3	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_4
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_4
.PAS_D7_4
	TST	CHOICE
	BNE.S	.OR5
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV5
.OR5	MOVE	#$0055,(A0)+
.MOV5	MOVE.W	D1,(A0)+
.RETOUR_4
	BSR	COPY_RT_COOL

	ASR.W	#1,D1
	DBRA	D6,DECAL

	SUBQ.W	#1,D5
	LSR.W	#1,D3
	LEA	1280(A1),A1
	DBRA	D2,ALL_RT

*******
TBL4:	LEA	TABLE_A4,A0
	MOVE	#639,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	D6,(A0)+
	MOVE	(A1),(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#639,D7
.FINT	MOVE	#319*4,(A0)+
	MOVE	#152,(A0)+
	;CLR	(A0)+
	DBF	D7,.FINT
TBL2:
	LEA	TABLE_A2,A0
	MOVE	#639,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	(A1),(A0)+
	MOVE	D6,(A0)
	MOVE	D6,D5
	LSR	#2,D5
	AND	#15,D5
	MULU	#1320,D5
	SUB	D5,(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#639,D7
.FINT	;CLR	(A0)+
	MOVE	#152,(A0)+
	MOVE	#319*4,(A0)+
	DBF	D7,.FINT
	RTS

MINI_TABLE
N	SET	0
	REPT	20
	DC	N
N	SET	N+8
	ENDR

POZ_ADRESSE2	MOVEM.L	D5/A0,-(SP)
	MOVE.W	A3,D5
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS

POZ_ADRESSE	MOVEM.L	D5/A0,-(SP)
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS

COPY_RT_COOL	LEA	RT_COOL,A6
	MOVE.W	#LONG_RT_COOL,D7
.COPY	MOVE.W	(A6)+,(A0)+
	DBRA	D7,.COPY
	RTS

RT_COOL	ADD.L	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2

	MOVE.W	(A1)+,D1	2  X2
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1

	MOVE.L	(A3,D1.W),D3	5
	ADD	D3,A5	2
	SWAP	D3	1

	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	ADD	(A2)+,A4	3  OFFSET. A3=>1ER MOT
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE... RUSê, HEIN?
	   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE, ZOU.
	MOVE.L	(A6,D3.W),A2	;5
	JMP	(A2)	;2

;TOTAL:31 NOPS. +58=89 NOPS.
LONG_RT_COOL = ((*-RT_COOL)/2)-1

RT_POLY
	TST	VISION
	BEQ.S	.NO
	MOVE.L	ADR_VISION,A1
	CLR.B	(A1)+
	MOVE.L	A1,ADR_VISION
.NO
	MOVE.L	A0,ADR_DEPART
	MOVE	#$4E75,MODIFIê
	LEA	2(A0),A1	;A1 POINTE SUR LES POINTS
	MOVE.W	(A1),D3
	SUB.W	4(A1),D3
	MOVE.W	2(A1),D4
	SUB.W	6(A1),D4
	MOVE.W	8(A1),D6
	SUB.W	4(A1),D6
	MOVE.W	$A(A1),D5
	SUB.W	6(A1),D5
	MULS.W	D3,D5
	MULS.W	D6,D4
	SUB.L	D4,D5
	BGT.S	.VISIBLE
	RTS
.VISIBLE
	MOVE	#25,REMP_GAUCHE+201*2
	MOVE	#15,REMP_DROIT+201*2

;	LEA	REMP_GAUCHE,A1
;	MOVE	#(500/2)-1,D6
;.EFF_TBX	CLR.L	(A1)+
;	DBF	D6,.EFF_TBX

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7

	TST	FLAG_TEMPS
	BEQ.S	.NO_TIMEB
	MOVE	#$034,$FFFF8240.W
.NO_TIMEB
	MOVE.W	(A0)+,D0	;NB DE POINTS COMPOSANT LA SURFACE
	LEA	BUF_EXAM,A1
	MOVE.L	A0,A2
	SUBQ.W	#1,D0
	MOVE.W	D0,NB_PTS
.COPYING	MOVE.L	(A0)+,(A1)+
	DBRA	D0,.COPYING
	MOVE.L	(A2),(A1)+

	LEA	BUF_EXAM,A0
	LEA	BUF_GAUCHE,A2
	LEA	BUF_DROIT,A3
	ADDQ.W	#4,A0
	MOVE.W	-2(A0),D3	;Y DE REFERENCE
NB_PTS = *+2
	MOVE.W	#$1234,D0
	MOVEQ	#-1,D6
	MOVEQ	#-1,D7
ALL_PTS	MOVE.W	(A0),D1	;X
	MOVE.W	2(A0),D2	;Y
	CMP.W	D2,D3
	BGE.S	ON_MONTE
ON_DESCEND	MOVE.L	-4(A0),(A2)+
	MOVE.L	(A0)+,(A2)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D6
	DBRA	D0,ALL_PTS
	BRA.S	FINITO
ON_MONTE	MOVE.L	-4(A0),(A3)+
	MOVE.L	(A0)+,(A3)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D7
	DBRA	D0,ALL_PTS
FINITO	;ICI LES TABLEAU BUF_GAUCHE ET BUF_DROIT SONT CONSTITUêS
	;D6=NB DE PTS-1 DANS BUF_GAUCHE
	;D7=NB DE PTS-1 DANS BUF_DROIT

	LEA	BUF_GAUCHE,A0
MAKE_REMP_GAUCHE
	LEA	REMP_GAUCHE,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT.S	.NEXT
	MOVE	#$4E71,MODIFIê
	SUB.W	D1,D3	;HAUTEUR-1 DU SEGMENT
	BEQ.S	.NEXT

	ADD.W	D1,D1
	ADDA.W	D1,A1

	ADDQ.W	#1,D3
	SUB.W	D0,D2
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#1,D3
.PUT	MOVE.W	D0,(A1)+
	DBRA	D3,.PUT
	BRA.S	.NEXT
.PAS_VERTICALE	EXT.L	D2
	MOVEQ	#8,D4
	ASL.L	D4,D2
	DIVS.W	D3,D2
	EXT.L	D2
	ASL.L	#8,D2
	SUBQ.W	#2,D3
	EXT.L	D0
	MOVEQ	#16,D4
	MOVE.W	D0,(A1)+
	ASL.L	D4,D0
.MK_X	ADD.L	D2,D0
	MOVE.L	D0,D1
	SWAP	D1
	MOVE.W	D1,(A1)+
	DBRA	D3,.MK_X
.NEXT	DBRA	D6,MAKE_REMP_GAUCHE

MODIFIê	RTS

	TST	VISION
	BEQ.S	.NO2
	MOVE.L	ADR_VISION,A0
	MOVE.B	#1,-1(A0)
.NO2

	LEA	BUF_DROIT,A0
MAKE_REMP_DROIT	LEA	REMP_DROIT,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT.S	.NEXT
	SUB.W	D3,D1	;HAUTEUR-1 DU SEGMENT
	BEQ.S	.NEXT

	ADD.W	D3,D3
	ADDA.W	D3,A1

	ADDQ.W	#1,D1
	SUB.W	D2,D0
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#1,D1
.PUT	MOVE.W	D2,(A1)+
	DBRA	D1,.PUT
	BRA.S	.NEXT
.PAS_VERTICALE	EXT.L	D0
	MOVEQ	#8,D4
	ASL.L	D4,D0
	DIVS.W	D1,D0
	EXT.L	D0
	ASL.L	#8,D0
	SUBQ.W	#2,D1
	MOVE.W	D2,(A1)+
	EXT.L	D2
	MOVEQ	#16,D4
	ASL.L	D4,D2
.MK_X	ADD.L	D0,D2
	MOVE.L	D2,D3
	SWAP	D3
	MOVE.W	D3,(A1)+
	DBRA	D1,.MK_X
.NEXT	DBRA	D7,MAKE_REMP_DROIT

*****
ADR_DEPART= *+2
	LEA	$12345678,A0
	MOVE.W	(A0)+,D7
	SUBQ.W	#1,D7
	MOVE.W	#-1000,D0	;MAXIMUM
	MOVE.W	#20000,D1	;MINIMUM
.TEST_MAX	CMP.W	2(A0),D0
	BGE.S	.FUCK
	MOVE.W	2(A0),D0
.FUCK	CMP.W	2(A0),D1
	BLE.S	.FUCK_2
	MOVE.W	2(A0),D1
.FUCK_2	ADDQ.W	#4,A0
	DBRA	D7,.TEST_MAX
	;D0 = Y MAXIMUM
	;D1 = Y MINIMUM

	CMPI.W	#200,D0
	BLT.S	.OK_Y_MAX
	MOVE.W	#199,D0
.OK_Y_MAX	TST.W	D1
	BGE.S	.OK_Y_MIN
	MOVEQ	#0,D1
.OK_Y_MIN
	TST	FLAG_TEMPS
	BEQ.S	.NO_TIMEA
	MOVE	#$023,$FFFF8240.W
.NO_TIMEA

INIT	LEA	REMP_GAUCHE,A0
	LEA	REMP_DROIT,A1
	ADDQ.W	#1,D0
	ADD.W	D0,D0
	MOVE.W	#15,(A1,D0.W)
	MOVE.W	#$9999,2(A1,D0.W)
	MOVE.W	#25,(A0,D0.W)

.F	ADD.W	D1,D1
	ADDA.W	D1,A0
	ADDA.W	D1,A1
	MULU.W	#80,D1       ;D1=Y_MIN*160 (POUR ADD ECRAN)

	SWAP	D1
	CLR	D1
	SWAP	D1
MODIF	NOP
	NOP
	NOP
	LEA	TABLE_ADR_COD+40,A6
	MOVEQ	#-1,D7
	MOVE.L	SCREEN2,D6
MOD_PLAN	NOP
	ADD.L	D1,D6	;...+Y_MIN*160
	SUBI.L	#160,D6
	MOVE.L	#TABLE_A2+640*4,D4
	MOVE.L	#160,D5
	LEA	TABLE_A4+640*4,A3
	MOVEQ	#0,D2

	ADD.L	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2
	MOVE.W	(A1)+,D1	2  X2
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1
	MOVE.L	(A3,D1.W),D3	5
	ADD	D3,A5	2
	SWAP	D3	1
	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	ADD	(A2)+,A4	3  OFFSET. A3=>1ER MOT
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE*1320... RUSê, HEIN?   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE*1320, ZOU.
	MOVE.L	(A6,D3.W),A2
	JMP	(A2)

RETOUR_POLY	RTS

CLIPPING_Y	MOVEM.L	D4/D6/A6,-(SP)
	MOVEQ	#0,D5
	LEA	TABLE_Y,A6
	ADD.W	D1,D1
	ADD.W	D3,D3
	MOVEQ	#0,D4
	ADD.W	(A6,D1.W),D4
	ADD.W	(A6,D3.W),D4
	ASR.W	#1,D1
	ASR.W	#1,D3
	TST.W	D4
	BEQ	FIN_NORMALE
	CMPI.W	#-2,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#4,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#-1,D4
	BEQ	CLIP_HAUT
	CMPI.W	#2,D4
	BEQ	CLIP_BAS

CLIP_HAUT_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	BRA.S	SUIT
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1

SUIT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_HAUT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

RIEN_DU_TOUT	MOVE.W	#-1,D5
FIN_NORMALE	MOVEM.L	(SP)+,D4/D6/A6
	RTS
********************************************************************
INIT_ROUT_POLY2	LEA	BUF_COD_GEN2,A0
	LEA	TABLE_ADR_COD2,A1

	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D3
.GO	MOVE.L	#RT_COOL2,(A1)+
	DBRA	D3,.GO

	MOVE.W	#$FFFF,D0	;1ER MOTIF
	MOVE.W	#$8000,D1	;DERNIER MOTIF
	MOVEQ	#0,D3	;DECALAGE

	MOVEQ	#16-1,D6
.DECAL
	MOVEQ	#16+1,D5
	ADD.W	#18*16,D5	;D5=LONGUEUR DE LA LIGNE
	ADD.W	D3,D5

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
.COPY_MOVED7	BSR	POZ_ADRESSE
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	SUBI.W	#16,D5
	DBRA	D7,.COPY_MOVED7

	MOVE.W	#16+1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_1
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_1
.PAS_D7_1
	TST	CHOICE
	BNE.S	.OR1
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV1
.OR1	MOVE	#$0054,(A0)+	OR.W #$Imm,(A4)
.MOV1	MOVE.W	D0,(A0)+
.RETOUR_1	MOVE.W	#1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_2
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_2
.PAS_D7_2
	TST	CHOICE
	BNE.S	.OR2
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV2
.OR2	MOVE	#$0055,(A0)+	OR "             "
.MOV2	MOVE.W	D1,(A0)+
.RETOUR_2
	BSR	COPY_RT_COOL2

	ASR.W	#1,D1
	ADDQ.W	#1,D3	;LONGUEUR SUIVANT
	DBRA	D6,.DECAL

	LEA	TABLE_ADR_COD2+1320,A1

	MOVE.W	#$7FFF,D3	;1ER MOTIF
	MOVEQ	#14,D5

	MOVEQ	#14,D2
ALL_RT2
	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D0
.GO	MOVE.L	#RT_COOL2,(A1)+
	DBRA	D0,.GO

	MOVE.W	#1,A3

	MOVEQ	#0,D0
	MOVE.W	D5,D6
LITTLE_TWO	BSR	POZ_ADRESSE2
	BSET	D6,D0
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_LITTLE
	MOVE.W	#$3887,(A0)+
	BRA.S	.RETOUR_LITTLE
.PAS_D7_LITTLE
	TST	CHOICE
	BNE.S	.OR3
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV3
.OR3	MOVE	#$0054,(A0)+
.MOV3	MOVE.W	D0,(A0)+

.RETOUR_LITTLE
	BSR	COPY_RT_COOL2

	ADDQ.W	#1,A3
	DBRA	D6,LITTLE_TWO

	MOVE.W	D3,D0
	MOVE.W	#$8000,D1	;DERNIER MOTIF

	MOVEQ	#16-1,D6
DECAL_TWO
	MOVE.W	#18*16,A3
	ADD.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
COPY_MOVED7_	BSR	POZ_ADRESSE2
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	LEA	-16(A3),A3
	DBRA	D7,COPY_MOVED7_

	MOVE.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3
	BSR	POZ_ADRESSE2
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_3
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_3
.PAS_D7_3
	TST	CHOICE
	BNE.S	.OR4
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV4
.OR4	MOVE	#$0054,(A0)+
.MOV4	MOVE.W	D0,(A0)+
.RETOUR_3	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_4
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_4
.PAS_D7_4
	TST	CHOICE
	BNE.S	.OR5
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV5
.OR5	MOVE	#$0055,(A0)+
.MOV5	MOVE.W	D1,(A0)+
.RETOUR_4
	BSR	COPY_RT_COOL2

	ASR.W	#1,D1
	DBRA	D6,DECAL_TWO

	SUBQ.W	#1,D5
	LSR.W	#1,D3
	LEA	1280(A1),A1
	DBRA	D2,ALL_RT2

	RTS

COPY_RT_COOL2	LEA	RT_COOL2,A6
	MOVE.W	#LONG_RT_COOL2,D7
.COPY	MOVE.W	(A6)+,(A0)+
	DBRA	D7,.COPY
	RTS

RT_COOL2	ADD.L	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)

	MOVE.B	(A3)+,D3	2
	ADD	D3,A5	2

	MOVE.B	(A3)+,D3	2
	ADD	D3,A4	2

	MOVE	(A3)+,D4	2
	MOVE.L	(A6,D4.W),A2	5
	JMP	(A2)	2

LONG_RT_COOL2 = ((*-RT_COOL2)/2)-1

RT_POLY2
	LEA	2(A0),A1	;A1 POINTE SUR LES POINTS
;	MOVE.W	(A1),D3
;	SUB.W	4(A1),D3
;	MOVE.W	2(A1),D4
;	SUB.W	6(A1),D4
;	MOVE.W	8(A1),D6
;	SUB.W	4(A1),D6
;	MOVE.W	$A(A1),D5
;	SUB.W	6(A1),D5
;	MULS.W	D3,D5
;	MULS.W	D6,D4
;	SUB.L	D4,D5
;	BGT.S	.VISIBLE
;	RTS
;.VISIBLE

	MOVE.L	ADR_VISION,A6
	TST.B	(A6)+
	BNE.S	.CONTN
	MOVE.L	A6,ADR_VISION
	RTS
.CONTN	MOVE.L	A6,ADR_VISION

	MOVEQ	#0,D1
	MOVE	(A3)+,D1
APPEL	;MOVE.L	SAVE_ADR_TAB,A3

	LEA	TABLE_ADR_COD2+40,A6
	MOVEQ	#-1,D7
	MOVE.L	SCREEN2,D6
;	MOVE	MOD_PLAN,KODE_DE_KRAD
MOD_PLAN2	NOP
	ADD.L	D1,D6	;...+Y_MIN*160
	SUBI.L	#160,D6
	MOVE.L	#160,D5
	MOVEQ	#0,D2
	MOVEQ	#0,D3

	ADD.L	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)

	;MOVEQ	#0,D3	1
	MOVE.B	(A3)+,D3	2
	ADD	D3,A5	2

	;MOVEQ	#0,D3	1
	MOVE.B	(A3)+,D3	2
	ADD	D3,A4	2

	MOVE	(A3)+,D4	2
	MOVE.L	(A6,D4.W),A2	5
	JMP	(A2)	2

VISION	DC	0
CREE_TABLOX
	LEA	MODIF,A1
	MOVE	#$4EF9,(A1)+
	MOVE.L	#JUMP,(A1)
	MOVE	#1,VISION
	RTS
FLAG_DêB	DC	0
JUMP
PRêCALC	CLR	FLAG_DêB
	MOVE.L	#TABLE_A2+640*4,D4
	LEA	TABLE_A4+640*4,A3

	MOVE.L	ADR_TABLOS,A4
	MOVE	D1,(A4)+
.LOOP_PRê
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2
	MOVE.W	(A1)+,D1	2  X2
	CMPI	#$9999,D1
	BNE.S	.NOT_END
	MOVE.L	A4,ADR_TABLOS
	RTS
.NOT_END	TST	FLAG_DêB
	BNE.S	.NO_TEST
	TST	D1
	BGE.S	.OK1
	TST	(A0)
	BGE.S	.NO_TEST
;Ici, les 2 x sont nÇgatifs.
	ADDQ	#2,A0
	ADD	#160,-2(A4)
	BRA.S	.LOOP_PRê
.OK1	CMPI	#320,(A0)
	BLT.S	.NO_TEST
	ADDQ	#2,A0
	ADD	#160,-2(A4)
	BRA.S	.LOOP_PRê

.NO_TEST	MOVE	#1,FLAG_DêB
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1
	MOVE.L	(A3,D1.W),D3	5
	MOVE.B	D3,(A4)+
	SWAP	D3	1
	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	MOVE	(A2)+,D1
	MOVE.B	D1,(A4)+
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE*1320... RUSê, HEIN?   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE*1320, ZOU.
	MOVE	D3,(A4)+
	BRA.S	.LOOP_PRê

	DCB.W	2*640,-1
TABLE_Y	DCB.W	200,0
	DCB.W	2*640,2

ADX	DC	0	Z
ADY	DC	4	X
ADZ	DC	4	Y

POINTS
	DC	NB_PTS_

N	SET	150

	DC	-N/2,-N/2,-N/2
	DC	-N/2,N/2,-N/2
	DC	N/2,N/2,-N/2
	DC	N/2,-N/2,-N/2

	DC	-N/2,-N/2,N/2
	DC	-N/2,N/2,N/2
	DC	N/2,N/2,N/2
	DC	N/2,-N/2,N/2

T	SET	200
Z	SET	40

	DC	-T/2,-T/2,-Z
	DC	-T/2,T/2,-Z
	DC	T/2,T/2,-Z
	DC	T/2,-T/2,-Z

	DC	-T/2,-T/2,Z
	DC	-T/2,T/2,Z
	DC	T/2,T/2,Z
	DC	T/2,-T/2,Z


DONNêES_AFFICH	
	DC	NB_FACES-1

	DC	$4E71,4,X1,X2,X3,X4
	DC	$4E71,4,X13,X9,X12,X16
	DC	$4E71,4,X8,X7,X6,X5
	DC	$4E71,4,X10,X14,X15,X11
	
	DC	$5446,4,X12,X11,X15,X16
	DC	$5446,4,X13,X14,X10,X9

	DC	$5846,4,X9,X10,X2,X1
	DC	$5846,4,X4,X3,X11,X12
	DC	$5846,4,X13,X16,X8,X5
	DC	$5846,4,X6,X7,X15,X14

	DC	$5C46,4,X9,X1,X4,X12
	DC	$5C46,4,X2,X10,X11,X3
	DC	$5C46,4,X16,X15,X7,X8
	DC	$5C46,4,X13,X5,X6,X14

ANGLES	DC.W	0,0,0
TRANS	DC.W	0,0,0

;REB
;	INCLUDE	REBOND.CRB
;	DC	$1234

	SECTION	BSS
;BSS gÇnÇrale
DEB_BSS
SCREEN1	DS.L	1
SCREEN2	DS.L	1

	DS.B	256+10000
BUFFER	DS.B	160*256
	DS.B	160*256
	DS.B	160*256
	DS.B	160*256
Z_BASE	DS.W	1
;Calcul des points
MATRICE	DS.W	9
BUF_EXP	DS.W	(LONG_EXP/4)*5
NB_VBL	DS.W	1
BUF_PTS	DS.W	NB_PTS_*3
;Routines d'affichage
TABLE_A4	DS.L	320+640*2
TABLE_A2	DS.L	320+640*2
BUF_EXAM	DS.W	2*50
BUF_GAUCHE	DS.W	4*50
BUF_DROIT	DS.W	4*50
REMP_GAUCHE	DS.W	260
REMP_DROIT	DS.W	260
TABLE_ADR_COD	DS.B	(1280+40)*16
TABLE_ADR_COD2	DS.B	(1280+40)*16
BUF_COD_GEN	DS.B	39000
BUF_COD_GEN2	DS.B	39000
;Infos prÇcalc
ADR_INFO	DS.L	1
BUF_INFO	DS.B	TIME_F+100
ADR_TABLO	DS.L	1
BUF_INFO_TABLO	DS.B	TIME_F+100
;Delta-packing
SAVE_VALUES	DS.W	4
BUF_TEMP	DS.B	32000*2
TEMP1	DS.L	1
TEMP2	DS.L	1
ADR_VISION	DS.L	1
BUF_VISION	DS.B	20000
TABLE_ADR	DS.L	1000
BUF_COD	DS.B	400000
;PrÇcalcul des tableaux
ADR_TABLOS	DS.L	1
BUF_TABLOS	DS.B	500000
END_BSS