	;7396 POINTS
MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	BSR	INIT_SYSTEME

	BSR	PREPARE_CODE
	MOVE.L	#INCREMENTS,PT_INC

	LEA	TEXT,A0
	MOVE.L	SCREEN1,A1
	ADDQ.W	#2,A1
	MOVE.L	SCREEN2,A2
	ADDQ.W	#2,A2
	MOVE.W	#20*24-1,D7
.COPY_1	MOVE.W	(A0),(A1)
	MOVE.W	(A0)+,(A2)
	ADDQ.W	#8,A1
	ADDQ.W	#8,A2
	DBRA	D7,.COPY_1
	MOVE.L	SCREEN1,A1
	LEA	2+160*175(A1),A1
	MOVE.L	SCREEN2,A2
	LEA	2+160*175(A2),A2
	MOVE.W	#20*24-1,D7
.COPY_2	MOVE.W	(A0),(A1)
	MOVE.W	(A0)+,(A2)
	ADDQ.W	#8,A1
	ADDQ.W	#8,A2
	DBRA	D7,.COPY_2

	MOVE.W	#$420,$FFFF8242.W
	MOVE.W	#$332,$FFFF8244.W
	MOVE.W	#$332,$FFFF8246.W
	MOVE.W	#$332,$FFFF824E.W
	MOVE.W	#$332,$FFFF8254.W
	MOVE.W	#$332,$FFFF8256.W
	MOVE.W	#$332,$FFFF825E.W
	MOVE.W	#$420,$FFFF8250.W

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

.J	BRA.S	.J

VBLR	MOVE.W	#$112,$FFFF8240.W

	MOVEQ	#0,D0
	MOVE.L	SCREEN2,A2
N	SET	0
	REPT	200
	MOVE.L	D0,N+6(A2)
	MOVE.L	D0,N+22(A2)
	MOVE.L	D0,N+38(A2)
	MOVE.L	D0,N+54(A2)
	MOVE.L	D0,N+70(A2)
	MOVE.L	D0,N+86(A2)
	MOVE.L	D0,N+102(A2)
	MOVE.L	D0,N+118(A2)
	MOVE.L	D0,N+134(A2)
	MOVE.L	D0,N+150(A2)
N	SET	N+160
	ENDR

	MOVEQ	#$80,D0
	MOVEQ	#$40,D1
	MOVEQ	#$20,D2
	MOVEQ	#$10,D3
	MOVEQ	#8,D4
	MOVEQ	#4,D5
	MOVEQ	#2,D6
	MOVEQ	#1,D7
	MOVE.L	SCREEN2,A2
	LEA	160*100(A2),A2
	MOVE.L	PT_INC,A0
	JSR	BUF_COD
	CMPI.W	#$1234,(A0)
	BNE.S	.OK
	LEA	INCREMENTS,A0
.OK	MOVE.L	A0,PT_INC

	BSR	SWAPEC

	CLR.W	$FFFF8240.W

	RTE

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

INIT_SYSTEME	CLR.B	$FFFF8260.W

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000+15360+1280*2,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREPARE_CODE	LEA	BUF_COD,A6
	LEA	COLOR_VAGUE(PC),A1
	MOVE.W	(A1)+,D0
	MOVE.W	D0,D1
	ADD.W	D1,D1
	MULU.W	#362,D0	;362=SQR(2)*256
	LSR.W	#8,D0	;D0=RAYON MAXIMUM
	MOVE.W	D0,D5	;COULEUR DE DEPART

	;CODE PRELIMINAIRE
NEXT_COLOR	MOVE.L	CODE_FIXE(PC),(A6)+
	MOVE.L	A1,A0

	MOVE.W	D1,D7     ;LONGUEUR D'UN DES COTêS DU CARRê
	SUBQ.W	#1,D7
	MOVE.W	D7,PRATIK
	MOVEQ	#0,D4	;Y=0
SCAN_Y	MOVEQ	#0,D3	;X=0
PRATIK = *+2
	MOVE.W	#$1234,D6
SCAN_X	MOVEQ	#0,D2
	MOVE.B	(A0)+,D2	;LA COULEUR
	CMP.W	D5,D2
	BNE.S	.PAS_BONNE
	MOVEM.L	D0-D7,-(SP)
	LEA	GRILLE(PC),A3
	ADD.W	D1,D1
	ADD.W	D1,D1
	MULU.W	D4,D1
	ADD.W	D3,D3
	ADD.W	D3,D3
	ADD.W	D3,D1
	MOVE.W	(A3,D1.W),D3	;X ECRAN
	MOVE.W	2(A3,D1.W),D4	;Y ECRAN
;	CMPI.W	#200,D4
;	BGE.S	.CLIP
	CMPI.W	#320,D3
	BGE.S	.CLIP
	TST.W	D3
	BLT.S	.CLIP
;	TST.W	D4
;	BLT.S	.CLIP
	SUBI.W	#100,D4
	MULU.W	#160,D4
	MOVE.W	D3,D2
	ANDI.W	#$FFE0,D2
	LSR.W	#1,D2
	ADD.W	D2,D4
	LEA	TABLE_X(PC),A3
	ANDI.W	#31,D3
	ADD.W	D3,D3
	ADD.W	D3,D3
	ADD.W	2(A3,D3.W),D4	;D4=OFFSET ECRAN
	MOVE.W	(A3,D3.W),D3
	ADD.W	D3,D3
	ADD.W	D3,D3
	LEA	AFFICH_OR(PC),A3
	MOVE.W	(A3,D3.W),(A6)+
	MOVE.W	D4,(A6)+
.CLIP	MOVEM.L	(SP)+,D0-D7
.PAS_BONNE	ADDQ.W	#1,D3	;X=X+1
	DBRA	D6,SCAN_X
	ADDQ.W	#1,D4	;Y=Y+1
	DBRA	D7,SCAN_Y
	SUBQ.W	#1,D5
	BGT	NEXT_COLOR
	MOVE.W	#$4E75,(A6)+
	RTS

TABLE_X	DC.W	0,6,1,6,2,6,3,6,4,6,5,6,6,6,7,6
	DC.W	0,7,1,7,2,7,3,7,4,7,5,7,6,7,7,7
	DC.W	0,8,1,8,2,8,3,8,4,8,5,8,6,8,7,8
	DC.W	0,9,1,9,2,9,3,9,4,9,5,9,6,9,7,9

CODE_FIXE	MOVE.L	A2,A1	;1
	ADDA.W	(A0)+,A1	;3

	;D0=$80, D1=$40, ... ,D7=1
AFFICH_OR	OR.B	D0,$1234(A1)
	OR.B	D1,$1234(A1)
	OR.B	D2,$1234(A1)
	OR.B	D3,$1234(A1)
	OR.B	D4,$1234(A1)
	OR.B	D5,$1234(A1)
	OR.B	D6,$1234(A1)
	OR.B	D7,$1234(A1)

EFFAC	MOVE.B	D0,$1234(A1)

TEXT	INCBIN	TEXT.DAT

COLOR_VAGUE	INCBIN	VAGUE_4.VAG

GRILLE	INCBIN	COOR3.VAG

INCREMENTS	INCBIN	INC_4.VAG
	INCBIN	INC_5.VAG
	INCBIN	INC_6.VAG
	DC.W	$1234

	SECTION	BSS
PT_INC	DS.L	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256+26000
BUFFER	DS.B	64000+16000+26000
BUF_COD	DS.B	29000