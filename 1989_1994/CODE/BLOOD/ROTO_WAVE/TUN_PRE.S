NB_PTS_PER_CERCLE = 60
NB_CERCLE = 80

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	BSR	INIT_SYSTEME

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

	CLR.W	NO

	CLR.W	OFF
	CLR.W	ACT

	LEA	BUF_PRECALC,A6
	MOVE.W	#50-1,D7
GO_AGAIN	MOVE.W	D7,SPECIAL

	BSR	EFFAC_FLAG

	LEA	COURBE_TUNNEL(PC),A0
	MOVE.W	ACT,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	ADD.W	OFF,D0	;D0=ACT*4+OFF
	MULU.W	#6,D0
;	ADD.W	D0,D0
;	ADD.W	D0,D0
	MOVE.W	2(A0,D0.W),D1	;Y_CRB(ACT*4+OFF)=D_Y
	MOVE.W	(A0,D0.W),D0	;X_CRB(ACT*4+OFF)=D_X
.NEXT	TST.W	4(A0)
	BGE.S	.OK
	LEA	24(A0),A0
	BRA.S	.NEXT
.OK	;A0=ADRESSE DU POINT DE LA COURBE OU FIGURE LE PREMIER Z>=0
	LEA	(NB_CERCLE-1)*4*6(A0),A0
	LEA	BUF_CALC,A5
	LEA	COEFF_3D(PC),A1
	MOVEQ	#NB_CERCLE-1,D6
.AFF2	MOVE.W	4(A0),D2	;Z_CRB
	ADD.W	D2,D2
	MOVE.W	(A1,D2.W),D2	;D2=COEFF
	LEA	CERCLE_DEP(PC),A2
	MOVE.W	#NB_PTS_PER_CERCLE-1,D7
.AFF	MOVE.W	(A0),D3	;D3=X_CRB
	ADD.W	(A2)+,D3	;D3=X_CRB+X(N)
	SUB.W	D0,D3	;D3=X_CRB+X(N)-D_X
	ADD.W	D3,D3
	MULS.W	D2,D3
	SWAP	D3
	ADDI.W	#160,D3
	MOVE.W	2(A0),D4	;D4=Y_CRB
	ADD.W	(A2)+,D4	;D4=Y_CRB+Y(N)
	SUB.W	D1,D4	;D4=Y_CRB+Y(N)-D_Y
	ADD.W	D4,D4
	MULS.W	D2,D4
	SWAP	D4
	ADDI.W	#100,D4
	;D3=X, D4=Y
	MOVE.W	D3,(A5)+	;X_C
	MOVE.W	D4,(A5)+	;Y_X
	DBRA	D7,.AFF
	LEA	-6*4(A0),A0
	DBRA	D6,.AFF2

	MOVE.L	SCREEN1,A1
	BSR	EFFAC_SCREEN

	CLR.W	VALUE

	LEA	BUF_CALC+60*4,A0
	MOVEQ	#NB_CERCLE-2,D7
.GOGO	MOVE.W	D7,SS
	LEA	POLY,A1
	MOVE.W	#NB_PTS_PER_CERCLE,D7
	MOVE.W	D7,(A1)+
	SUBQ.W	#1,D7
.POZ_COOR	MOVE.W	(A0)+,(A1)+
	MOVE.W	(A0)+,(A1)+
	DBRA	D7,.POZ_COOR

	MOVE.L	A0,-(SP)
	LEA	POLY,A0
	MOVE.L	A6,-(SP)
	JSR	RT_POLY
	MOVE.L	(SP)+,A6

	LEA	BUF_CALC,A0
	MOVE.L	SCREEN1,A1
	LEA	CORES_X2,A2
	LEA	CORES_Y-200,A3
	LEA	BUF_FLAG,A4
	MOVE.W	VALUE,D6
.TEST_ALL	MOVEQ	#NB_PTS_PER_CERCLE-1,D7
.TEST_CERCLE	MOVE.W	(A0)+,D0	;X
	MOVE.W	(A0)+,D1	;Y
	ADD.W	D1,D1
	MOVE.W	(A3,D1.W),D1
	ADD.W	D0,D0
	ADD.W	D0,D0
	ADD.W	2(A2,D0.W),D1
	MOVE.W	(A2,D0.W),D0
	BTST	D0,(A1,D1.W)
	BNE.S	.ON_GARDE
	MOVE.B	#-1,(A4)
.ON_GARDE	ADDQ.W	#1,A4
	DBRA	D7,.TEST_CERCLE
	DBRA	D6,.TEST_ALL
	ADDQ.W	#1,VALUE

	MOVE.L	SCREEN1,A1
	BSR	EFFAC_SCREEN

	MOVE.L	(SP)+,A0
SS = *+2
	MOVE.W	#$1234,D7
	DBRA	D7,.GOGO

VERIFY	LEA	BUF_CALC,A0
	LEA	BUF_FLAG,A1
	LEA	CORES_X,A2
	LEA	CORES_Y-200,A3
	MOVE.L	SCREEN1,A4
	MOVEQ	#NB_CERCLE-1,D6
.AFF2	MOVEQ	#NB_PTS_PER_CERCLE-1,D7
.AFF1	TST.B	(A1)+
	BNE.S	.GO_FUCK
	MOVE.W	(A0),D0	;X
	MOVE.W	2(A0),D1	;Y
	ADD.W	D0,D0
	ADD.W	D0,D0
	ADD.W	D1,D1
	MOVE.W	(A3,D1.W),D1	;Y*160
	ADD.W	2(A2,D0.W),D1
	MOVE.W	(A2,D0.W),D0
	OR.W	D0,(A4,D1.W)
;	ADDQ.W	#1,(A6)
.GO_FUCK	ADDQ.W	#4,A0
	DBRA	D7,.AFF1
	DBRA	D6,.AFF2
;	ADDQ.W	#2,A6

	SF	FLAG_FIRST
	LEA	BUF_FLAG,A0
	MOVEQ	#0,D1
	MOVE.W	#NB_CERCLE-1,D6
.ALL_CERCLES	MOVEQ	#0,D0
	MOVE.W	#NB_PTS_PER_CERCLE-1,D7
.EXAM	TST.B	(A0)+
	BNE.S	.RIEN
	ADDQ.W	#1,D0	;UN POINT DE PLUS
.RIEN	DBRA	D7,.EXAM
	MOVE.L	A0,A2
	TST.W	D0
	BNE.S	.PAS_NUL
	ADDQ.W	#1,D1
	BRA.S	.NEXT
;.PAS_FIRST	CLR.B	(A6)+
;	CLR.B	(A6)+
;	BRA.S	.NEXT
.PAS_NUL	TST.B	FLAG_FIRST
	BNE.S	.PAS_FIRST
	ST	FLAG_FIRST
	MOVE.B	D1,(A6)+
.PAS_FIRST	MOVE.B	D0,(A6)+	;NB DE POINTS A AFFICHER
	CMPI.W	#NB_PTS_PER_CERCLE,D0
	BLT.S	.PAS_NB
	MOVE.B	D0,(A6)+
	CLR.B	(A6)+
	BRA.S	.NEXT
.PAS_NB	LEA	-NB_PTS_PER_CERCLE(A0),A0
	MOVE.L	A0,A1
.NEXT_ZERO	TST.B	(A0)+
	BEQ.S	.NEXT_ZERO
.NEXT_1	TST.B	(A0)+
	BNE.S	.NEXT_1
	SUBQ.W	#1,A0
	SUBA.L	A1,A0
	MOVE.L	A0,D0
	MOVE.B	D0,(A6)+	;POINT DE DEPART
.NEXT	MOVE.L	A2,A0
	DBRA	D6,.ALL_CERCLES

	MOVE.L	SCREEN1,A0
	LEA	BUF_GFX,A1
	MOVE.W	NO,D0
	MULU.W	#8000,D0
	ADDA.L	D0,A1
	MOVE.W	#20*200-1,D7
.COPY	MOVE.W	(A0),(A1)+
	ADDQ.W	#8,A0
	DBRA	D7,.COPY
	ADDQ.W	#1,NO

	ADDQ.W	#1,OFF
	CMPI.W	#4,OFF
	BLT.S	.OK
	CLR.W	OFF
	ADDQ.W	#1,ACT
.OK
	LEA	COURBE_TUNNEL,A0
	MOVE.W	#LONG_CRB,D7
.SUB	SUBQ.W	#4,4(A0)
	ADDQ.W	#6,A0
	DBRA	D7,.SUB

SPECIAL = *+2
	MOVE.W	#$1234,D7
	DBRA	D7,GO_AGAIN

	MOVE.W	#$0,$FFFF8240.W
	MOVE.W	#$777,$FFFF8242.W

	CLR.W	NO
	CLR.W	NB_VBL

IT	BSR	VSYNC
	MOVE.L	SCREEN1,A0
	LEA	BUF_GFX,A1
	MOVE.W	NO,D0
	MULU.W	#8000,D0
	ADDA.L	D0,A1
	MOVE.W	#20*200-1,D7
.COPY	MOVE.W	(A1)+,(A0)
	ADDQ.W	#8,A0
	DBRA	D7,.COPY
	ADDQ.W	#1,NO
	CMPI.W	#50,NO
	BLT.S	.OK
	CLR.W	NO
.OK	BRA.S	IT

.J	BRA.S	.J

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	RTE

INIT_SYSTEME	CLR.B	$FFFF8260.W

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	JSR	INIT_ROUT_POLY
	MOVE.L	#RT_COOL,TABLE_ADR_COD+40
	RTS

EFFAC_FLAG	LEA	BUF_FLAG,A0
	MOVE.W	#NB_PTS_PER_CERCLE*(NB_CERCLE+1)-1,D7
.F	CLR.B	(A0)+
	DBRA	D7,.F
	RTS

*** Partie affichage
CHOICE	DC	0	;0=MOVE

FLAG_FIRST	DS.W	1

POLY	DC.W	0
	DCB.W	NB_PTS_PER_CERCLE*2,0

	DCB.W	32*20,0
CORES_X
N	SET	0
	REPT	10
	DC.W	$8000,N,$4000,N,$2000,N,$1000,N,$800,N,$400,N,$200,N,$100,N,$80,N,$40,N,$20,N,$10,N,8,N,4,N,2,N,1,N
N	SET	N+8
	ENDR
	REPT	10
	DC.W	$8000,N,$4000,N,$2000,N,$1000,N,$800,N,$400,N,$200,N,$100,N,$80,N,$40,N,$20,N,$10,N,8,N,4,N,2,N,1,N
N	SET	N+8
	ENDR
	DCB.W	32*20,0

	DCB.W	32*20,0
CORES_X2
N	SET	0
	REPT	10
	DC.W	7,N,6,N,5,N,4,N,3,N,2,N,1,N,0,N,7,N+1,6,N+1,5,N+1,4,N+1,3,N+1,2,N+1,1,N+1,0,N+1
N	SET	N+8
	ENDR
	REPT	10
	DC.W	7,N,6,N,5,N,4,N,3,N,2,N,1,N,0,N,7,N+1,6,N+1,5,N+1,4,N+1,3,N+1,2,N+1,1,N+1,0,N+1
N	SET	N+8
	ENDR
	DCB.W	32*20,0

	DCB.W	200,-160
N	SET	0
	REPT	100
	DC.W	N
N	SET	N+160
	ENDR
CORES_Y
	REPT	100
	DC.W	N
N	SET	N+160
	ENDR
	DCB.W	200,-160

COEFF_3D	INCBIN	COEFF.3DT

CERCLE_DEP	INCBIN	TEST.SIN

COURBE_TUNNEL	INCBIN	CRB.SIN
LONG_CRB = ((*-COURBE_TUNNEL)/6)-1

OFF	DS.W	1
ACT	DS.W	1
VALUE	DS.W	1

EFFAC_SCREEN	MOVEQ	#0,D0
	MOVE.W	#200-1,D7
.F
N	SET	0
	REPT	20
	MOVE.W	D0,N(A1)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	DBRA	D7,.F
	RTS
				;1=OR
INIT_ROUT_POLY	LEA	BUF_COD_GEN,A0
	LEA	TABLE_ADR_COD,A1

	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D3
.GO	MOVE.L	#RT_COOL,(A1)+
	DBRA	D3,.GO

	MOVE.W	#$FFFF,D0	;1ER MOTIF
	MOVE.W	#$8000,D1	;DERNIER MOTIF
	MOVEQ	#0,D3	;DECALAGE

	MOVEQ	#16-1,D6
.DECAL	
	MOVEQ	#16+1,D5
	ADD.W	#18*16,D5	;D5=LONGUEUR DE LA LIGNE
	ADD.W	D3,D5

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
.COPY_MOVED7	BSR	POZ_ADRESSE
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	SUBI.W	#16,D5
	DBRA	D7,.COPY_MOVED7

	MOVE.W	#16+1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_1
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_1
.PAS_D7_1
	TST	CHOICE
	BNE.S	.OR1
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV1
.OR1	MOVE	#$0054,(A0)+	OR.W #$Imm,(A4)
.MOV1	MOVE.W	D0,(A0)+
.RETOUR_1	MOVE.W	#1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_2
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_2
.PAS_D7_2
	TST	CHOICE
	BNE.S	.OR2
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV2
.OR2	MOVE	#$0055,(A0)+	OR "             "
.MOV2	MOVE.W	D1,(A0)+
.RETOUR_2
	BSR	COPY_RT_COOL

	ASR.W	#1,D1
	ADDQ.W	#1,D3	;LONGUEUR SUIVANT
	DBRA	D6,.DECAL

	LEA	TABLE_ADR_COD+1320,A1

	MOVE.W	#$7FFF,D3	;1ER MOTIF
	MOVEQ	#14,D5

	MOVEQ	#14,D2
ALL_RT	
	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D0
.GO	MOVE.L	#RT_COOL,(A1)+
	DBRA	D0,.GO

	MOVE.W	#1,A3

	MOVEQ	#0,D0
	MOVE.W	D5,D6
LITTLE	BSR	POZ_ADRESSE2
	BSET	D6,D0
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_LITTLE
	MOVE.W	#$3887,(A0)+
	BRA.S	.RETOUR_LITTLE
.PAS_D7_LITTLE
	TST	CHOICE
	BNE.S	.OR3
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV3
.OR3	MOVE	#$0054,(A0)+
.MOV3	MOVE.W	D0,(A0)+

.RETOUR_LITTLE
	BSR	COPY_RT_COOL

	ADDQ.W	#1,A3
	DBRA	D6,LITTLE

	MOVE.W	D3,D0
	MOVE.W	#$8000,D1	;DERNIER MOTIF

	MOVEQ	#16-1,D6
DECAL
	MOVE.W	#18*16,A3
	ADD.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
COPY_MOVED7	BSR	POZ_ADRESSE2
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	LEA	-16(A3),A3
	DBRA	D7,COPY_MOVED7

	MOVE.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3
	BSR	POZ_ADRESSE2
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_3
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_3
.PAS_D7_3
	TST	CHOICE
	BNE.S	.OR4
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV4
.OR4	MOVE	#$0054,(A0)+
.MOV4	MOVE.W	D0,(A0)+
.RETOUR_3	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_4
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_4
.PAS_D7_4
	TST	CHOICE
	BNE.S	.OR5
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV5
.OR5	MOVE	#$0055,(A0)+
.MOV5	MOVE.W	D1,(A0)+
.RETOUR_4
	BSR	COPY_RT_COOL

	ASR.W	#1,D1
	DBRA	D6,DECAL

	SUBQ.W	#1,D5
	LSR.W	#1,D3
	LEA	1280(A1),A1
	DBRA	D2,ALL_RT

*******
TBL4:	LEA	TABLE_A4,A0
	MOVE	#319,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	D6,(A0)+
	MOVE	(A1),(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#319,D7
.FINT	MOVE	#319*4,(A0)+
	MOVE	#152,(A0)+
	;CLR	(A0)+
	DBF	D7,.FINT
TBL2:
	LEA	TABLE_A2,A0
	MOVE	#319,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	(A1),(A0)+
	MOVE	D6,(A0)
	MOVE	D6,D5
	LSR	#2,D5
	AND	#15,D5
	MULU	#1320,D5
	SUB	D5,(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#319,D7
.FINT	;CLR	(A0)+
	MOVE	#152,(A0)+
	MOVE	#319*4,(A0)+
	DBF	D7,.FINT

	RTS

MINI_TABLE
N	SET	0
	REPT	20
	DC	N
N	SET	N+8
	ENDR

POZ_ADRESSE2	MOVEM.L	D5/A0,-(SP)
	MOVE.W	A3,D5
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS
	

POZ_ADRESSE	MOVEM.L	D5/A0,-(SP)
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS
	
COPY_RT_COOL	LEA	RT_COOL,A6
	MOVE.W	#LONG_RT_COOL,D7
.COPY	MOVE.W	(A6)+,(A0)+
	DBRA	D7,.COPY
	RTS

RT_COOL	ADD.L	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2

	MOVE.W	(A1)+,D1	2  X2
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1

	MOVE.L	(A3,D1.W),D3	5
	ADD	D3,A5	2
	SWAP	D3	1

	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	ADD	(A2)+,A4	3  OFFSET. A3=>1ER MOT
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE... RUSê, HEIN?
	   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE, ZOU.
	MOVE.L	(A6,D3.W),A2	;5
	JMP	(A2)	;2

;TOTAL:31 NOPS. +58=89 NOPS.
LONG_RT_COOL = ((*-RT_COOL)/2)-1

RT_POLY	MOVE.L	A0,ADR_DEPART

	LEA	2(A0),A1	;A1 POINTE SUR LES POINTS
	MOVE.W	(A1),D3
	SUB.W	4(A1),D3
	MOVE.W	2(A1),D4
	SUB.W	6(A1),D4
	MOVE.W	8(A1),D6
	SUB.W	4(A1),D6
	MOVE.W	$A(A1),D5
	SUB.W	6(A1),D5
	MULS.W	D3,D5
	MULS.W	D6,D4
	SUB.L	D4,D5
;	BGT.S	.VISIBLE
;	RTS
.VISIBLE
	MOVE	#25,REMP_GAUCHE+249*2
	MOVE	#15,REMP_DROIT+249*2

;	LEA	REMP_GAUCHE,A1
;	MOVE	#(500/2)-1,D6
;.EFF_TBX	CLR.L	(A1)+
;	DBF	D6,.EFF_TBX

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIMEB
	MOVE	#$034,$FFFF8240.W
.NO_TIMEB

	MOVE.W	(A0)+,D0	;NB DE POINTS COMPOSANT LA SURFACE
	LEA	BUF_EXAM,A1
	MOVE.L	A0,A2
	SUBQ.W	#1,D0
	MOVE.W	D0,NB_PTS
.COPYING	MOVE.L	(A0)+,(A1)+
	DBRA	D0,.COPYING
	MOVE.L	(A2),(A1)+

	LEA	BUF_EXAM,A0
	LEA	BUF_GAUCHE,A2
	LEA	BUF_DROIT,A3
	ADDQ.W	#4,A0
	MOVE.W	-2(A0),D3	;Y DE REFERENCE
NB_PTS = *+2
	MOVE.W	#$1234,D0
	MOVEQ	#-1,D6
	MOVEQ	#-1,D7
ALL_PTS	MOVE.W	(A0),D1	;X
	MOVE.W	2(A0),D2	;Y
	CMP.W	D2,D3
	BGE.S	ON_MONTE
ON_DESCEND	MOVE.L	-4(A0),(A2)+
	MOVE.L	(A0)+,(A2)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D6
	DBRA	D0,ALL_PTS
	BRA.S	FINITO
ON_MONTE	MOVE.L	-4(A0),(A3)+
	MOVE.L	(A0)+,(A3)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D7
	DBRA	D0,ALL_PTS
FINITO	;ICI LES TABLEAU BUF_GAUCHE ET BUF_DROIT SONT CONSTITUêS
	;D6=NB DE PTS-1 DANS BUF_GAUCHE
	;D7=NB DE PTS-1 DANS BUF_DROIT

	LEA	BUF_GAUCHE,A0
MAKE_REMP_GAUCHE
	LEA	REMP_GAUCHE,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT.S	.NEXT
	SUB.W	D1,D3	;HAUTEUR-1 DU SEGMENT
	BEQ.S	.NEXT

	ADD.W	D1,D1
	ADDA.W	D1,A1

	ADDQ.W	#1,D3
	SUB.W	D0,D2
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#1,D3
.PUT	MOVE.W	D0,(A1)+
	DBRA	D3,.PUT
	BRA.S	.NEXT
.PAS_VERTICALE	EXT.L	D2
	MOVEQ	#8,D4
	ASL.L	D4,D2
	DIVS.W	D3,D2
	EXT.L	D2
	ASL.L	#8,D2
	SUBQ.W	#2,D3
	EXT.L	D0
	MOVEQ	#16,D4
	MOVE.W	D0,(A1)+
	ASL.L	D4,D0
.MK_X	ADD.L	D2,D0
	MOVE.L	D0,D1
	SWAP	D1
	MOVE.W	D1,(A1)+
	DBRA	D3,.MK_X
.NEXT	DBRA	D6,MAKE_REMP_GAUCHE

	LEA	BUF_DROIT,A0
MAKE_REMP_DROIT	LEA	REMP_DROIT,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT.S	.NEXT
	SUB.W	D3,D1	;HAUTEUR-1 DU SEGMENT
	BEQ.S	.NEXT

	ADD.W	D3,D3
	ADDA.W	D3,A1

	ADDQ.W	#1,D1
	SUB.W	D2,D0
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#1,D1
.PUT	MOVE.W	D2,(A1)+
	DBRA	D1,.PUT
	BRA.S	.NEXT
.PAS_VERTICALE	EXT.L	D0
	MOVEQ	#8,D4
	ASL.L	D4,D0
	DIVS.W	D1,D0
	EXT.L	D0
	ASL.L	#8,D0
	SUBQ.W	#2,D1
	MOVE.W	D2,(A1)+
	EXT.L	D2
	MOVEQ	#16,D4
	ASL.L	D4,D2
.MK_X	ADD.L	D0,D2
	MOVE.L	D2,D3
	SWAP	D3
	MOVE.W	D3,(A1)+
	DBRA	D1,.MK_X
.NEXT	DBRA	D7,MAKE_REMP_DROIT

*****

ADR_DEPART= *+2
	LEA	$12345678,A0
	MOVE.W	(A0)+,D7
	SUBQ.W	#1,D7
	MOVE.W	#-1000,D0	;MAXIMUM
	MOVE.W	#20000,D1	;MINIMUM
.TEST_MAX	CMP.W	2(A0),D0
	BGE.S	.FUCK
	MOVE.W	2(A0),D0
.FUCK	CMP.W	2(A0),D1
	BLE.S	.FUCK_2
	MOVE.W	2(A0),D1
.FUCK_2	ADDQ.W	#4,A0
	DBRA	D7,.TEST_MAX
	;D0 = Y MAXIMUM
	;D1 = Y MINIMUM
	CMPI.W	#200,D0
	BLT.S	.OK_Y_MAX
	MOVE.W	#199,D0
.OK_Y_MAX	TST.W	D1
	BGE.S	.OK_Y_MIN
	MOVEQ	#0,D1
.OK_Y_MIN

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIMEA
	MOVE	#$023,$FFFF8240.W
.NO_TIMEA


INIT	LEA	REMP_GAUCHE,A0
	LEA	REMP_DROIT,A1
	ADDQ.W	#1,D0
	ADD.W	D0,D0
	MOVE.W	#15,(A1,D0.W)
	MOVE.W	#25,(A0,D0.W)
	ADD.W	D1,D1
	ADDA.W	D1,A0
	ADDA.W	D1,A1
	MULU.W	#80,D1       ;D1=Y_MIN*160 (POUR ADD ECRAN)

	LEA	TABLE_ADR_COD+40,A6
	MOVE	#-1,D7
	MOVE.L	SCREEN1,D6
MOD_PLAN	NOP
	ADD.L	D1,D6	;...+Y_MIN*160
	SUBI.L	#160,D6
	MOVE.L	#TABLE_A2+320*4,D4
	MOVE.L	#160,D5
	LEA	TABLE_A4+320*4,A3
	MOVEQ	#0,D2

	ADD.L	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2
	MOVE.W	(A1)+,D1	2  X2
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1
	MOVE.L	(A3,D1.W),D3	5
	ADD	D3,A5	2
	SWAP	D3	1
	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	ADD	(A2)+,A4	3  OFFSET. A3=>1ER MOT
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE*1320... RUSê, HEIN?   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE*1320, ZOU.
	MOVE.L	(A6,D3.W),A2
	JMP	(A2)

RETOUR_POLY	RTS

CLIPPING_Y	MOVEM.L	D4/D6/A6,-(SP)
	MOVEQ	#0,D5
	LEA	TABLE_Y,A6
	ADD.W	D1,D1
	ADD.W	D3,D3
	MOVEQ	#0,D4
	ADD.W	(A6,D1.W),D4
	ADD.W	(A6,D3.W),D4
	ASR.W	#1,D1
	ASR.W	#1,D3
	TST.W	D4
	BEQ	FIN_NORMALE
	CMPI.W	#-2,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#4,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#-1,D4
	BEQ	CLIP_HAUT
	CMPI.W	#2,D4
	BEQ	CLIP_BAS

CLIP_HAUT_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	BRA.S	SUIT
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1

SUIT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_HAUT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

RIEN_DU_TOUT	MOVE.W	#-1,D5
FIN_NORMALE	MOVEM.L	(SP)+,D4/D6/A6
	RTS

	DCB.W	640,-1
TABLE_Y	DCB.W	200,0
	DCB.W	640,2

	SECTION	BSS
	DS.B	256+160
BUFFER	DS.B	32000
SCREEN1	DS.L	1
NB_VBL	DS.W	1
NO	DS.W	1
;BSS
BUF_CALC	DS.W	2*NB_PTS_PER_CERCLE*(NB_CERCLE+1)
BUF_FLAG	DS.B	NB_PTS_PER_CERCLE*(NB_CERCLE+1)
;Routine d'affichage
TABLE_A4	DS.L	320*3
TABLE_A2	DS.L	320*3
BUF_EXAM	DS.W	2*100
BUF_GAUCHE	DS.W	4*50
BUF_DROIT	DS.W	4*50
REMP_GAUCHE	DS.W	250
REMP_DROIT	DS.W	250
TABLE_ADR_COD	DS.B	(1280+40)*16
BUF_COD_GEN	DS.B	39000
;
TOTAUX	DS.W	50
BUF_PRECALC	DS.B	2*80*50
BUF_GFX	DS.B	8000*50