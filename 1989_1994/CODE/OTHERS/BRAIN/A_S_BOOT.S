***************************************************
* ROUTINE BOOTSECTEUR DU LOGICIEL AUDIO SCULPTURE *
***************************************************

START:
	LEA	MESSAGE(PC),A0	Please do NOT crack...
	
	ORI.B	#0,D0
	
	BSR.S	DCODE_TEXT
	
	PEA	MESSAGE(PC)
	MOVE.W	#9,-(A7)
	NOP
	ADDQ.W	#6,A7

	MOVEA.W	#$8604,A6

	MOVE.L	#$70000,D7

	ORI.B	#0,D0

	BSR	AVANTI

	JMP	$70000

DCODE_TEXT:
	NEG.B	(A0)+
	DBF	D7,DCODE_TEXT
	RTS

	MOVE.L	#$4E714AFC,D0
TESTING:
	EOR.W	D0,(A0)+
	ADDQ.W	#1,D0
	DBF	D7,TESTING

AVANTI:	BSR	SETCOLORS
	BSR	WAIT
	BSR	NEXT_TRK

	MOVE.L	POINTEUR(PC),$50000
	MOVE.L	POINTEUR(PC),$1FC(A6)

	BSR	TEST_TRK
	BSR	WAIT
	BSR	SET_DMA
	BSR	WAIT
	BSR	READ_SECTOR
	BSR	SEEK_TRK

	SF	$43E.W
	
	RTS

SEEK_TRK:
	MOVE.W	#$86,2(A6)	FDC DATA REGISTER
	MOVE.W	#1,(A6)		PISTE 1

	MOVE.W	#$80,2(A6)	FDC COMMAND REGISTER
	MOVE.W	#$10,(A6)	SEEK TRACK COMMAND
	BSR	ATTEND

	RTS

SETCOLORS:
	MOVEA.W	#$8240,A0		TOUS LES REGISTRES
	MOVEQ	#$F,D1			AVEC LA COULEUR BLEUE.
DO_COLOR:
	MOVE.W	#7,(A0)+
	DBF	D1,DO_COLOR

	MOVE.B	#2,$FFFF8260.W
	MOVE.B	#1,$FFFF820A.W

	ST	$43E.W

	MOVEQ	#0,D1

	TST.W	$446.W		TESTER BOOTDEV_
	BEQ.S	SUITE		= LECTEUR A, ALORS ON CONTINUE

	LEA	FLAG(PC),A0
	LEA	POINTEUR(PC),A1
	MOVE.W	#3,(A0)
	MOVE.L	#$E0E0303,(A1)
	RTS

SUITE:	LEA	FLAG(PC),A0
	LEA	POINTEUR(PC),A1
	MOVE.W	#5,(A0)
	MOVE.L	#$E0E0505,(A1)
	RTS

WAIT:	MOVEQ	#$28,D6
WAIT1:	DBF	D6,WAIT1
	RTS

NEXT_TRK:
	MOVE.L	$1FC(A6),D2	MOVE.L	508(A6),D6

	MOVE.W	#$86,2(A6)	FDC DATA REGISTER
	MOVE.W	(A6),D0		SAUVER DMADAT
	MOVE.W	#1,(A6)		PISTE 1

	MOVE.W	#$84,2(A6)	FDC SECTOR REGISTER
	MOVE.W	#2,(A6)		SECTEUR 2

	MOVE.W	FLAG(PC),D1

	RTS

TEST_TRK:
	BSR.S	TRUC1

	MOVE.W	#$80,2(A6)	FDC COMMAND REGISTER
	MOVE.W	#$10,(A6)	SEEK TRACK COMMAND
	BSR	ATTEND
	BNE.S	ARMOIRE		ERREUR, C'EST LA DAUBE.
	RTS

ARMOIRE:
	MOVE.W	#7,$FFFF8240.W	FOND EN BLEU
	BRA.S	ARMOIRE		BOUCLE INFINIE...

ATTEND:	MOVE.L	#$80000,D6	DELAI D'ATTENTE
ATTEND1:
	BTST	#5,$FFFFFA01.W	INTERRUPTION TERMINEE ?
	BEQ.S	CCR_4		OUI, LA SUITE
	SUBQ.L	#1,D6		SINON, DIMINUER COMPTEUR
	BNE.S	ATTEND1		JUSQU'A ZERO.

	CMPA.W	#0,A7		TESTER LA PILE ???

	RTS

CCR_4:	MOVE.B	#4,CCR		>>> A VOIR <<<
	RTS

SET_DMA:
	MOVE.B	D7,9(A6)
	LSR.L	#8,D7
	MOVE.B	D7,7(A6)
	LSR.W	#8,D7
	MOVE.B	D7,5(A6)
DMA_IN:
	MOVE.W	#$90,2(A6)
	MOVE.W	#$190,2(A6)
	MOVE.W	#$90,2(A6)
	MOVE.W	#1,(A6)		1 SECTEUR
	RTS

READ_SECTOR:
	MOVE.W	#$80,2(A6)	FDC COMMAND REGISTER
	MOVE.W	#$80,(A6)	READ SECTOR COMMAND
	BSR	WAIT
	BSR.S	ATTEND
	BNE.S	ARMOIRE		PAS BON, ON BLOQUE TOUT.
	RTS

TRUC1:	ROL.L	#8,D2
	ANDI.W	#7,D2
	CMP.B	D1,D2
	BEQ.S	TRUC2
	MOVEQ	#1,D0

TRUC2:	ANDI.W	#$F,D0
	SUBQ.W	#1,D0
	ADD.W	D0,(A6)
	MOVEQ	#5,D1
	RTS

POINTEUR:	DC.B	0,0,0,0
FLAG:		DC.B	0,0
MESSAGE:
	DC.B	$B0,$94
	DC.B	$9B,$9F,$8D,$9B,$E0,$9C,$91,$E0
	DC.B	$B2,$B1,$AC,$E0,$9D,$8E,$9F,$9D
	DC.B	$95,$D2,$D2,$D2,2,0,0,0
	DC.B	0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0

	END