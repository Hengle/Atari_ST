****************************************************************************
*		ROUTINE BOOTSECTEUR DU JEU JUPITER PROBE
****************************************************************************

START:	LEA	$6FFF0,A7		PILE SUR $6FFF0
	MOVE.W	#$2700,SR		IPL 7

	CLR.W	$FF8240			COULEUR 0 = NOIR

	LEA	$FF8606,A6		DMAMODE DANS A6

	LEA	0.W,A4

	MOVEQ	#5,D0

	MOVE	SR,-(A7)		SAUVER LE SR
	ORI.W	#$700,SR

	MOVE.B	#$E,$FFFF8800.W		SELECTION DU PORT PARALLELE
	MOVE.B	$FFFF8800.W,D1		LECTURE
	MOVE.B	D1,D2			SAUVE D1 DANS D2

	ANDI.B	#$F8,D1			CLEAR BIT 0,1,2 (LECTEUR DE DISK)
	OR.B	D1,D0			SELECTION DRIVE 0 / FACE 0

	MOVE.B	D0,$FFFF8802.W		ECRITURE

	MOVE.W	(A7)+,SR		RESTAURER LE SR

	MOVE.W	#$80,(A6)		FDC COMMAND REGISTER
	BSR	READ_STATUS

	MOVE.L	#$60000,D7		DELAI D'ATTENTE
	
	MOVE.W	#$86,(A6)		FDC DATA REGISTER
	MOVE.W	#1,D0			ENVOIE PISTE 1
	BSR	WD_WRITE

	MOVE.W	#$80,(A6)		FDC COMMAND REGISTER
	MOVE.W	#$13,D0			SEEK TRACK
	BSR	WD_WRITE
	BSR	WAIT_DONE

	CMPI.W	#$FFFF,D0		SI ERREUR OU -1
	BEQ.S	START			ON RECOMMENCE.

	MOVE.W	#$82,(A6)		FDC TRACK REGISTER
	MOVE.W	#$FD,D0			PISTE 253 ??? (BIZARRE !)
	BSR	WD_WRITE

	MOVE.W	#1,D6			SECTEUR SUR 1

SUITE:	MOVE.W	#$84,(A6)		FDC SECTOR REGISTER
	MOVE.W	D6,D0			SECTEUR DANS D0
	ADDI.W	#$91,D0			PLUS 191 DANS D0
	BSR	WD_WRITE		DONC SECTEUR=SECTEUR+191 ?!?

	MOVE.L	A4,D0			ADRESSE DU CHARGEMENT
	MOVE.B	D0,$FFFF860D.W		LOW-DMA
	LSR.L	#8,D0
	MOVE.B	D0,$FFFF860B.W		MID-DMA
	LSR.L	#8,D0
	MOVE.B	D0,$FFFF8609.W		HIGH-DMA

	MOVE.W	#$80,(A6)		FDC COMMAND REGISTER	
	BSR	READ_STATUS

	MOVE.L	#$60000,D7		DELAI D'ATTENTE

	MOVE.W	#$90,(A6)		DMA_IN
	MOVE.W	#$190,(A6)
	MOVE.W	#$90,(A6)
	MOVE.W	#1,D0			1 SECTEUR A LIRE
	BSR.S	WD_WRITE

	MOVE.W	#$80,(A6)		FDC COMMAND REGISTER
	MOVE.W	#$80,D0			FDC READ SECTOR
	BSR.S	WD_WRITE
	BSR	WAIT_DONE

	ANDI.B	#$1C,D0			MASQUER SUR 28
	BNE.S	ENCORE			PAS ENCORE ATTEIND
					ALORS ON CONTINUE...

	MOVE.W	#$90,(A6)		FDC SECTOR COUNT REGISTER
	MOVE.W	(A6),D0
	BTST	#0,D0
	BNE.S	JUMPING

ENCORE:	BRA	START

JUMPING:
	MOVE.W	#$222,$FF8240
	
	ADDA.L	#$200,A4		+ 512 OCTETS : 1 SECTEUR LU
	
	ADDQ.W	#1,D6
	CMPI.W	#$B,D6			11 SECTEUR ?
	BNE.S	SUITE
	
	MOVE.W	#$660,$FF8240
	
	MOVE.W	#$82,(A6)		FDC TRACK REGISTER
	
	MOVE.W	#1,D0			TRACK 1
	BSR	WD_WRITE

	JMP	$14.W			MAIS OU VA T'ON ?

WD_WRITE:
	BSR.S	ATTEND
	MOVE.W	D0,$FFFF8604.W
	BRA.S	ATTEND

READ_STATUS:
	BSR.S	ATTEND
	MOVE.W	$FFFF8604.W,D0		FDC STATUS DANS D0

ATTEND:	MOVE	SR,-(A7)
	MOVE.W	D7,-(A7)
	MOVEQ	#$20,D7			UNE PETITE PAUSE...
ATTEND1:
	DBF	D7,ATTEND1
	MOVE.W	(A7)+,D7
	MOVE.W	(A7)+,SR
	RTS

FDC_RESET:
	MOVE.W	#$80,(A6)
	MOVE.W	#$D0,D0
	BSR.S	WD_WRITE

	BSR.S	ATTEND
	BSR.S	ATTEND
	BSR.S	ATTEND
	BSR.S	ATTEND

	BSR.S	READ_STATUS
	RTS

	MOVE.W	#$80,(A6)
	BSR.S	READ_STATUS

	MOVE.W	#3,D0
	BSR.S	WD_WRITE
	RTS

WAIT_DONE:
	SUBQ.L	#1,D7
	BEQ.S	ERREUR

	BTST	#5,$FFFFFA01.W
	BNE.S	WAIT_DONE

	BRA.S	READ_STATUS

ERREUR:
	BSR.S	FDC_RESET
	MOVEQ	#-1,D0
	RTS