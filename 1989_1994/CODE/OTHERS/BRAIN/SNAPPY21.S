***************************************************************************
* SNAPPY VERSION 2.0 (C) DENIS NARDEAU - DECEMBRE 1991.
***************************************************************************
REGS	REG	D0-D7/A0-A6

	MOVE.L	SP,A0			RECUP ADR DE LA PILE
	MOVE.L	4(A0),A0		ADR DE LA BASE PAGE
	MOVE.L	$C(A0),D0		TAILLE ZONE TEXT
	ADD.L	$14(A0),D0		TAILLE ZONE DATA
	ADD.L	$1C(A0),D0		TAILLE ZONE BSS
	ADD.L	#256,D0			TAILLE DE LA BASE PAGE.
	MOVE.L	D0,D7			TAILLE DU PRG DANS D7

	MOVE.L	#PROMPT,PRINT+2
	BSR.S	PRINT			AFFICHAGE DU PROMPT

	BSR.S	GETKEY			ATTENTE CLAVIER
	CMPI.B	#$1C,D0			SI <RETURN> CONTINUER...
	BNE.S	QUIT			SINON QUITTER.

KEY_LOOP:
	MOVE.L	#CHOOSE,PRINT+2		CHOIX DU DRIVE DE SAUVEGARDE SNAP
	BSR.S	PRINT
KEY_LOOP2:
	BSR.S	GETKEY			ATTENTE CLAVIER

	CMPI.B	#$1C,D0			SI <RETURN>
	BEQ.S	INSTALL			INSTALLER LA ROUTINE

	CMPI.B	#$3B,D0
	BNE.S	KEY_LOOP2
	ADDI.B	#1,CHOOSE+34
	ADDI.B	#1,FNAME

	CMPI.B	#'Q',CHOOSE+34
	BNE.S	KEY_LOOP
	MOVE.B	#'A',CHOOSE+34

	BRA.S	KEY_LOOP		SINON BOUCLER...

QUIT:	CLR.W	-(A7)
	MOVE.W	#$4C,-(A7)
	TRAP	#1			SINON ON QUITTE.

PRINT:	MOVE.L	#0,-(A7)		ADRESSE MODIFIABLE
	MOVE.W	#9,-(A7)
	TRAP	#1			CCONWS
	ADDQ.L	#6,A7
	RTS

GETKEY:	MOVE.W	#7,-(A7)
	TRAP	#1			ATTENTE CLAVIER
	ADDQ.L	#2,A7
	SWAP	D0
	RTS

INSTALL:
	CLR.L	-(A7)
	MOVE.W	#$20,-(A7)
	TRAP	#1			MODE SUPERVISEUR
	ADDQ.L	#6,A7
	MOVE.L	D0,ETV_CRITIC		SAUVER LE STACK PROVISOIREMENT

	MOVE.L	#SNAPPY,$502		INSTALLER SNAPPY
	
	MOVE.L	ETV_CRITIC,-(A7)
	MOVE.W	#$20,-(A7)
	TRAP	#1			MODE UTILISATEUR
	ADDQ.L	#6,A7

	CLR.W	-(A7)
	MOVE.L	D7,-(A7)		TAILLE DU PRG
	MOVE.W	#$31,-(A7)
	TRAP	#1			PTERMRES

****************************************************************************
* ROUTINE DE SNAPSHOT
****************************************************************************
SNAPPY:
	MOVEM.L	REGS,-(A7)		SAUVER LES REGISTRES
 	MOVE.W	SR,-(A7)		SAUVER LE STATUS REGISTER
	MOVE.W	#$2500,SR		IPL 5

	MOVE.B	#$13,$FFFC02		COUPER LE TRANSFERT DE DONNEES VERS L'UNITE CENTRALE

	MOVE.L	#$404,ETV_CRITIC	SAUVER VECTEUR D'ERREURS DISQUETTE
	MOVE.L	#NEW_CRITIC,$404	REMPLACER PAR LE VECTEUR D'ERREUR SNAPPY

	ADDI.B	#1,FNAME+10		INCREMENTER LE NUMERO DE SNAP
	
	MOVE.B	#'1',FNAME+14		METTRE '1' DANS L'EXTENSION APRES 'PI'
	MOVE.B	$44C,D0			RESOLUTION DANS D0
	ADD.B	D0,FNAME+14		AJOUTER REZ A '1'
	MOVE.B	D0,OFFSET+1		METTRE LE RESULTAT DANS L'OFFSET

	CLR.W	-(A7)
	PEA	FNAME
	MOVE.W	#$3C,-(A7)
	TRAP	#1		FCREATE
	ADDQ.L	#8,A7
	TST.W	D0		ERREUR ?
	BMI.S	ARMOIRE		OUI, ON SE CASSE !
	MOVE.W	D0,_HANDLE	SINON TRANSFERT DU HANDLE

	MOVE.L	#OFFSET,_W_ADR+2
	MOVE.L	#2,_W_OCT+2
	BSR	FWRITE		SAUVER L'OFFSET (2 OCTETS)
	BMI.S	ARMOIRE		SI ERREUR, ON SORT.

	MOVE.L	#$FF8240,_W_ADR+2
	MOVE.L	#32,_W_OCT+2
	BSR	FWRITE		SAUVER LA PALETTE (32 OCTETS)
	BMI.S	ARMOIRE		SI ERREUR, ON SORT.

	MOVEQ	#0,D0
	MOVE.B	$FF8201,D0
	LSL.W	#8,D0
	MOVE.B	$FF8203,D0
	LSL.L	#8,D0		ADRESSE PHYSBASE DANS D0

	MOVE.L	D0,_W_ADR+2
	MOVE.L	#32000,_W_OCT+2
	BSR	FWRITE		SAUVER LE CONTENU DE L'ECRAN (32000 OCTETS)
	BMI.S	ARMOIRE		SI ERREUR, ON SORT.

	MOVE.W	_HANDLE,-(A7)
	MOVE.W	#$3E,-(A7)
	TRAP	#1		FCLOSE
	ADDQ.L	#4,A7

ARMOIRE:
	MOVE.L	ETV_CRITIC,$404	RESTAURER ANCIEN VECTEUR D'ERREURS DISQUETTE

	MOVE.B	#$11,$FFFC02	RETABLIR LE TRANSFERT DE DONNEES VERS L'UNITE CENTRALE

	MOVE.W	(A7)+,SR	RESTAURER L'ANCIEN STATUS REGISTER
	MOVEM.L	(A7)+,REGS	RESTAURER LES REGISTRES
	RTS

NEW_CRITIC:
	MOVEQ	#-2,D0		NUMERO D'ERREUR = -2, CAR -1 (SYSTEME)
	RTS			AFFICHE LA BOITE D'ALERTE...ARMOIRE !
****************************************************************************
* SOUS-PROGRAMME D'ECRITURE DANS LE FICHIER 'SNAP'
****************************************************************************
FWRITE:

_W_ADR:	MOVE.L	#0,-(A7)	ADRESSE DU BUFFER A SAUVER
_W_OCT:	MOVE.L	#0,-(A7)	NOMBRE D'OCTETS A ECRIRE
	MOVE.W	_HANDLE,-(A7)
	MOVE.W	#$40,-(A7)
	TRAP	#1		FWRITE
	LEA	12(A7),A7
	TST.W	D0		TESTER LES ERREURS
	RTS
	
****************************************************************************

	SECTION DATA

PROMPT:	DC.B	13,10,27,'p',' SNAPPY 2.1 ',189,' Brain 1992. ',27,'q'
	DC.B	13,10,10,' [RETURN] TO KEEP RESIDENT',13,10,0
	EVEN

CHOOSE:	DC.B	27,'E[F1] SNAP DRIVE [RETURN] KEEP : A',0

FNAME:	DC.B	'A:\SNAPPY_@.PI1',0
	EVEN

	SECTION BSS

_HANDLE:
	DS.W	1
OFFSET:
	DS.W	1
ETV_CRITIC:
	DS.L	1

	END