
NB_PASSES = 200

	CLR.L	-(SP)
	MOVE	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP	

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W

*DEPART: 2 TABLEAUX : Xs DE LA VBL PRECEDENTE
*	       Xs DE LA VBL EN COURS

	JSR	INIT
	MOVE.L	#TABLO1,TAB1
	MOVE.L	#TABLO2,TAB2
	MOVE.L	#280,D0	DEPART
	MOVE.L	#160,D1	ARRIVEE
	MOVE.L	TAB2,A0
	JSR	DO_TRANS

	MOVE.L	#VBL,$70.W
K	JMP	K
DO_TRANS
	MOVE.L	#NB_PASSES,D2	EN 'D2' PASSES
	MOVE.L	D2,D6
	MOVE	D2,MODIF
	MOVE	D2,MODIF_

	LSL.L	#8,D0
	LSL.L	#8,D1
	CMP.L	D0,D1
	BGT.S	ROUTY1
ROUTY2	;D1 PLUS PETIT
	MOVE.L	D0,D2
	SUB.L	D1,D0	D1=AMPLITUDE
	DIVU	D6,D0
	SWAP	D0
	CLR	D0
	SWAP	D0
MODIF_	EQU	*+2
	MOVE	#0,D7
	SUBQ	#1,D7
DO_IT_AGAIN_	SUB.L	D0,D2
	MOVE.L	D2,D3
	LSR.L	#8,D3	CA EN FAIT UN
	MOVE	D3,(A0)+
;	ADDQ.L	#4,A0
	DBF	D7,DO_IT_AGAIN_
FINI2	RTS
****
	
ROUTY1	;D0 PLUS PETIT
	MOVE.L	D0,D2
	SUB.L	D0,D1	D1=AMPLITUDE
	DIVU	D6,D1
	SWAP	D1
	CLR	D1
	SWAP	D1
MODIF	EQU	*+2
	MOVE	#0,D7
	SUBQ	#1,D7
DO_IT_AGAIN	ADD.L	D1,D2
	MOVE.L	D2,D3
	LSR.L	#8,D3	CA EN FAIT UN
	MOVE	D3,(A0)+
;	ADDQ.L	#4,A0
	DBF	D7,DO_IT_AGAIN
FINITED	RTS

VBL	SF	$FFFF8240.W


	MOVE.L	$44E.W,A6
	LEA	-160(A6),A6
	MOVE.L	TAB1,A5
	MOVE.L	TAB2,A1
	LEA	TABLE1,A2
	LEA	TABLE2,A3

	MOVE	#200-1,D3
BOUCLE	
	LEA	160(A6),A6
	MOVE.L	A6,A0

	MOVE	(A5)+,D6	X1
	MOVE	(A1)+,D4	X3

	MOVE	D4,D7
	SUB	D6,D4
	BGT	EFFACE	X1 < X3
	BLT.S	AFFICHE	X1 > X3
;ICI, X1=X3 => NE RIEN FAIRE
;
;REPETER L'OPERATION POUR X2 ET X4
;	D'OU:
	DBF	D3,BOUCLE
FINI	NOP
;	CMPI.B	#$F,$FFFFFC02.W
;	BNE.S	KS
;	ST	$FFFF8240.W
	MOVE	#$002,$FFFF8240.W	
KS	
;	MOVE.L	TAB1,D0
;	MOVE.L	TAB2,TAB1
;	MOVE.L	D0,TAB2


MODX	EQU	*+2
	MOVE.L	#280,D0	DEPART
MODX2	EQU	*+2
	MOVE.L	#160,D1	ARRIVEE
	MOVE.L	TAB2,A0
	JSR	DO_TRANS
	MOVE	#$004,$FFFF8240.W
	CMPI.B	#$3B,$FFFFFC02.W
	BNE.S	PASTOUCH
	SUB.L	#1,MODX
;	SUB.L	#1,MODX2
PASTOUCH	
	CMPI.B	#$3C,$FFFFFC02.W
	BNE.S	PASTOUCH2
	ADD.L	#1,MODX
;	ADD.L	#1,MODX2
PASTOUCH2

	RTE
TAB1	DC.L	0
TAB2	DC.L	0
FIN	MOVE.L	4.W,A0
	JMP	(A0)
AFFICHE	LEA	TABLE,A4
	MOVE.L	#$FFFFFFFF,D1
;	MOVE	#1,D6
	MOVE	D6,D4
;	MOVE	#319,D7
	MOVE	D7,D5

	SUB	D7,D6	DELTA X
	LSR	#4,D6	D7=>DETERMINE ROUT A APPELER
	LSL	#2,D6

	ADD	D5,D5	x3*4 ( x3<x1 )
	ADD	D5,D5
	MOVE	(A2,D5.W),D0	motif
	ADD	2(A2,D5.W),A0
	ADD	D4,D4	x1*4 ( ancien x )
	ADD	D4,D4
	MOVE	(A3,D4.W),D2	motif

	MOVE.L	(A4,D6.W),A4
	JSR	(A4)
	DBF	D3,BOUCLE
	JMP	FINI
***************
EFFACE	LEA	TABLE_EFF,A4
	moveq	#0,d1
	EXG.L	D6,D7
;	MOVE	#1,D6
	MOVE	D6,D4
;	MOVE	#319,D7
	MOVE	D7,D5

	SUB	D7,D6	DELTA X
	LSR	#4,D6	D7=>DETERMINE ROUT A APPELER
	LSL	#2,D6

	ADD	D5,D5	x3*4 ( x3<x1 )
	ADD	D5,D5
	MOVE	(A2,D5.W),D0	motif
	ADD	2(A2,D5.W),A0
	ADD	D4,D4	x1*4 ( ancien x )
	ADD	D4,D4
	MOVE	(A3,D4.W),D2	motif

	MOVE.L	(A4,D6.W),A4
	JSR	(A4)
	DBF	D3,BOUCLE
	JMP	FINI

**********
TABLO1	DCB	200,319
TABLO2	DCB	200,0

**********************
INIT	
	LEA	CODE,A0
	LEA	TABLE,A1

	MOVE.L	A0,(A1)+
	MOVE.L	#8,D0
	MOVE	#$C440,(A0)+
	MOVE	#$8550,(A0)+
	MOVE	#$4E75,(A0)+
	MOVE.L	A0,(A1)+
	MOVE.L	#8,D0
	MOVE	#$8150,(A0)+
	MOVE	#$8568,(A0)+
	MOVE	D0,(A0)+
	MOVE	#$4E75,(A0)+

	MOVE	#17,D7
	MOVE	#-1,D6
DOCODE	
	MOVE.L	A0,(A1)+

	MOVE.L	#8,D0
	ADDQ	#1,D6
	MOVE	D6,D1
	MOVE	#$8150,(A0)+
DOMOVE	MOVE	#$3141,(A0)+
	MOVE	D0,(A0)+
	ADDQ	#8,D0
	DBF	D1,DOMOVE
	MOVE	#$8568,(A0)+
	MOVE	D0,(A0)+
	MOVE	#$4E75,(A0)+
	DBF	D7,DOCODE
************
	LEA	CODE2,A0
	LEA	TABLE_EFF,A1

	MOVE.L	A0,(A1)+
	MOVE.L	#8,D0
	MOVE	#$C440,(A0)+
	MOVE	#$C550,(A0)+
	MOVE	#$4E75,(A0)+
	MOVE.L	A0,(A1)+
	MOVE.L	#8,D0
	MOVE	#$C150,(A0)+
	MOVE	#$C568,(A0)+
	MOVE	D0,(A0)+
	MOVE	#$4E75,(A0)+

	MOVE	#17,D7
	MOVE	#-1,D6
DOCODE2
	MOVE.L	A0,(A1)+

	MOVE.L	#8,D0
	ADDQ	#1,D6
	MOVE	D6,D1
	MOVE	#$C150,(A0)+
DOMOVE2	MOVE	#$3141,(A0)+
	MOVE	D0,(A0)+
	ADDQ	#8,D0
	DBF	D1,DOMOVE2
	MOVE	#$C568,(A0)+
	MOVE	D0,(A0)+
	MOVE	#$4E75,(A0)+
	DBF	D7,DOCODE2

	RTS

	DATA
TABLE1
N	SET	0
	REPT	20
	DC	%1111111111111111,N
	DC	%0111111111111111,N
	DC	%0011111111111111,N
	DC	%0001111111111111,N
	DC	%0000111111111111,N
	DC	%0000011111111111,N
	DC	%0000001111111111,N
	DC	%0000000111111111,N
	DC	%0000000011111111,N
	DC	%0000000001111111,N
	DC	%0000000000111111,N
	DC	%0000000000011111,N
	DC	%0000000000001111,N
	DC	%0000000000000111,N
	DC	%0000000000000011,N
	DC	%0000000000000001,N
N	SET	N+8
	ENDR

TABLE2
N	SET	0
	REPT	20
;	DC	%0000000000000000,N
	DC	%1000000000000000,N
	DC	%1100000000000000,N
	DC	%1110000000000000,N
	DC	%1111000000000000,N
	DC	%1111100000000000,N
	DC	%1111110000000000,N
	DC	%1111111000000000,N
	DC	%1111111100000000,N
	DC	%1111111110000000,N
	DC	%1111111111000000,N
	DC	%1111111111100000,N
	DC	%1111111111110000,N
	DC	%1111111111111000,N
	DC	%1111111111111100,N
	DC	%1111111111111110,N
	DC	%1111111111111111,N
N	SET	N+8
	ENDR


	BSS
TABLE	DS.L	20
CODE	DS.B	900
TABLE_EFF	DS.L	20
CODE2	DS.B	900
