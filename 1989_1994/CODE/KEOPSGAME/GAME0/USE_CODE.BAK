MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	BSR	INIT_SYSTEME

	BSR	INIT_CODE

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

J	BRA.S	J

VBLR	MOVE.W	#2000,D0
.BCL	DBRA	D0,.BCL

	MOVE.W	#0,D0
	MOVE.W	#0,D1
	BSR	AFF_KEOPS

	BSR	SWAPEC

	RTE

	;D0=X
	;D1=Y
	;D2=0(GAUCHE) OU 1(DROITE)
AFF_KEOPS	LEA	CODE_GEN(PC),A1
	MULU.W	#4*5*16,D2
	ADDA.W	D2,A1
	MOVE.W	D0,D3
	ANDI.W	#$FFF0,D3
	LSR.W	#1,D3
	MULU.W	#160,D1
	ADD.W	D3,D1
	MOVE.L	SCREEN2,A0
	ADDA.W	D1,A0
	ANDI.W	#15,D0
	MULU.W	#4*5,D0
	ADDA.W	D0,A1
	MOVE.L	16(A1),A2
	MOVEM.L	(A2),D0-D6
	MOVEQ	#0,D7
	MOVE.L	(A1),A2
	JSR	(A2)
	RTS

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

INIT_CODE	LEA	CODE_GEN,A0
	MOVE.L	A0,D0
	MOVE.W	#5*16*2-1,D7
.ADDIT	ADD.L	D0,(A0)+
	DBRA	D7,.ADDIT
	RTS

INIT_SYSTEME	CLR.B	$FFFF8260.W

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	MOVEM.L	PALETTE,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	RTS

CODE_GEN	INCBIN	KEOPS.COD

PALETTE	DC.W	$123,$777,$444,$320,$420,$531,$111,$753
	DC.W	$210,$431,$132,$243,$555,$333,$222,$345

	SECTION	BSS
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.B	64000