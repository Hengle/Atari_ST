NB_PASSES_RECHERCHE = 200

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	BSR	INIT_SYSTEME

	BSR	INIT_CODE

	CLR.W	PT_MAP
	;INIT DES MONSTRES ET DE LEUR EFFACAGE
	BSR	INIT_MAP_MONSTRES

	LEA	ADR_EFF_MONSTRE,A0
	LEA	BUF_EFF_MONSTRE,A1
	MOVE.L	A1,(A0)+
	LEA	6*15(A1),A1
	MOVE.L	A1,(A0)+

	MOVE.W	#160,X_PLAYER
	MOVE.W	#100,Y_PLAYER

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

J	BRA.S	J

VBLR	;ST	$FFFF8240.W
	BSR	EFFAC_MONSTRES

;	ADDI.W	#$123,$FFFF8240.W

	BSR	GEST_MONSTRES

;	MOVE.W	PALETTE,$FFFF8240.W

	LEA	ADR_EFF_MONSTRE,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0),-(A0)
	MOVE.L	D0,4(A0)

	BSR	SWAPEC

	RTE

EFFAC_MONSTRES	MOVE.L	ADR_EFF_MONSTRE,A0
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVE.W	NB_MONSTRES_MAX,D7
	SUBQ.W	#1,D7
.NEXT_MONSTRE	MOVE.W	D7,A6
	MOVE.W	(A0)+,D0	;(NB DE MOTS-1)*4
	BLT.S	.NOT_THIS_ONE
	MOVE.W	(A0)+,D1	;HAUTEUR-1
	LEA	TAB_ROUTS_EFF(PC),A1
	MOVE.L	(A1,D0.W),A1
	MOVEQ	#0,D0
	MOVE.L	D0,A4
	MOVEQ	#0,D7
	MOVE.L	SCREEN2,A2
	ADDA.W	(A0)+,A2	;+OFFSET ECRAN
	JSR	(A1)
	BRA.S	.NEXT
.NOT_THIS_ONE	ADDQ.W	#4,A0
.NEXT	MOVE.W	A6,D7
	DBRA	D7,.NEXT_MONSTRE
	RTS

TAB_ROUTS_EFF	DC.L	EFF_1,EFF_2,EFF_3,EFF_4,EFF_5,EFF_6,EFF_7

EFF_1	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	LEA	160-8(A2),A2
	DBRA	D1,EFF_1
	RTS

EFF_2	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	LEA	160-16(A2),A2
	DBRA	D1,EFF_2
	RTS

EFF_3	MOVEM.L	D0/D2-D5/A4,(A2)
	LEA	160(A2),A2
	DBRA	D1,EFF_3
	RTS

EFF_4	MOVEM.L	D0/D2-D7/A4,(A2)
	LEA	160(A2),A2
	DBRA	D1,EFF_4
	RTS

EFF_5	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	LEA	160-40(A2),A2
	DBRA	D1,EFF_5
	RTS

EFF_6	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	LEA	160-48(A2),A2
	DBRA	D1,EFF_6
	RTS

EFF_7	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	LEA	160-56(A2),A2
	DBRA	D1,EFF_7
	RTS

GEST_MONSTRES	LEA	TABLO_MONSTRES,A6
	MOVE.L	ADR_EFF_MONSTRE,A3
	MOVE.W	NB_MONSTRES_MAX,D7
	SUBQ.W	#1,D7
RETURN_OF	CMPI.W	#-2,(A6)
	BEQ.S	MONSTRE_EXPLOSE
	CMPI.W	#-1,(A6)
	BNE.S	MONSTRE_VIVANT
MONSTRE_MORT	MOVE.L	PT_MAP2,A0
	CMPI.W	#$1234,(A0)
	BNE.S	.ON_PEUT
	MOVE.W	#-1,(A3)
	ADDQ.W	#6,A3
	BRA.S	NEXT_ONE
.ON_PEUT	MOVE.L	A6,A5
	MOVE.W	(A0)+,D0
	CMPI.W	#100,D0
	BLT.S	.COURBE
.SEARCH	BSR	INIT_MONSTRE_SEARCH
	BRA.S	.FINI
.COURBE	BSR	INIT_MONSTRE_COURBE
.FINI	MOVE.L	A0,PT_MAP2
	MOVE.L	A5,A6
	MOVE.W	8(A6),D0	;X
	MOVE.W	10(A6),D1	;Y
	ADD.W	12(A6),D1	;Y+Y_DEP
	MOVE.W	14(A6),D2	;SENS
	BSR	AFF_KEOPS
	BRA.S	NEXT_ONE
MONSTRE_EXPLOSE	;GESTION DE L'EXPLO AVANT
	MOVE.W	#-1,(A3)
	ADDQ.W	#6,A3
	BRA.S	NEXT_ONE
MONSTRE_VIVANT	MOVE.W	(A6),D0
	CMPI.W	#100,D0
	BLT.S	.GEST_COURBE
.GEST_SEARCH	BSR	GESTION_RECHERCHE
	BRA.S	.FINI
.GEST_COURBE	BSR	GESTION_COURBE
.FINI	CMPI.W	#-1,(A6)
	BEQ.S	NEXT_ONE
	MOVE.W	8(A6),D0	;X
	MOVE.W	10(A6),D1	;Y
	ADD.W	12(A6),D1	;Y+Y_DEP
	MOVE.W	14(A6),D2	;SENS
	BSR	AFF_KEOPS
NEXT_ONE	LEA	18(A6),A6
	DBRA	D7,RETURN_OF
	RTS

GESTION_RECHERCHE
	MOVE.W	8(A6),D0	;X DU KEOPS
	MOVE.W	X_PLAYER,D1
	CMP.W	D0,D1
	BEQ.S	EGAL_X
	BGT.S	PLAYER_A_DROITE
PLAYER_A_GAUCHE	CLR.W	14(A6)
	SUBQ.W	#1,D0
	BRA.S	EGAL_X
PLAYER_A_DROITE	MOVE.W	#1,14(A6)
	ADDQ.W	#1,D0
EGAL_X	MOVE.W	D0,8(A6)
	MOVE.W	10(A6),D0	;Y DU KEOPS
	MOVE.W	Y_PLAYER,D1
	CMP.W	D0,D1
	BEQ.S	EGAL_Y
	BGT.S	PLAYER_EN_BAS
PLAYER_EN_HAUT	SUBQ.W	#1,D0
	BRA.S	EGAL_Y
PLAYER_EN_BAS	ADDQ.W	#1,D0
EGAL_Y	MOVE.W	D0,10(A6)

	RTS

GESTION_COURBE	MOVE.L	4(A6),A1	;ADRESSE DE LA COURBE
	MOVE.W	(A1),D0	;PAS_X
	TST.W	14(A6)
	BNE.S	.DROITE
.GAUCHE	SUB.W	D0,8(A6)	;X-PAS_X
	BRA.S	.SUIT
.DROITE	ADD.W	D0,8(A6)	;X+PAS_X
.SUIT	MOVE.W	2(A6),D0	;POINTEUR
	ADDQ.W	#2,D0
	ADDA.W	D0,A1
 	CMPI.W	#$1234,(A1)
	BNE.S	.PAS_FIN_COURBE
	MOVE.W	#-1,(A6)	;MORT
	MOVE.W	#-1,(A3)
	ADDQ.W	#6,A3
	RTS
.PAS_FIN_COURBE	MOVE.W	D0,2(A6)	;POINTEUR
	MOVE.W	(A1),D0	;NEW Y
	MOVE.W	D0,10(A6)
	RTS

	;D0=X
	;D1=Y
	;D2=0(GAUCHE) OU 1(DROITE)
AFF_KEOPS	MOVEM.L	D0-D7,-(SP)
	CMPI.W	#-39,D0
	BLE	.NO_AFF
	CMPI.W	#320,D0
	BGE	.NO_AFF
	CMPI.W	#-35,D1
	BLE	.NO_AFF
	CMPI.W	#200,D1
	BGE	.NO_AFF
	LEA	CODE_GEN(PC),A1
	MULU.W	#4*5*16,D2
	ADDA.W	D2,A1
	MOVE.W	D0,D3
	ANDI.W	#$FFF0,D3
	ASR.W	#1,D3
	TST.W	D0
	BGE.S	.X_POSITIF
.X_NEGATIF	ST	FLAG_CLIP_GAUCHE
.X_POSITIF	MULU.W	#160,D1
	ADD.W	D3,D1
	MOVE.L	SCREEN2,A0
	;NB DE MOTS=4 DONC ON MET (4-1)*4=12
	MOVE.W	#12,(A3)+
	;HAUTEUR-1=34
	MOVE.W	#34,(A3)+
	;D1=OFFSET ECRAN (< OU >0)
	TST.W	D1
	BGT.S	.OFF_POSITIF
.OFF_NEGATIF	CLR.W	(A3)+
	BRA.S	.S
.OFF_POSITIF	MOVE.W	D1,(A3)+
.S	ADDA.W	D1,A0
	ANDI.W	#15,D0
	MULU.W	#4*5,D0
	ADDA.W	D0,A1
	MOVE.L	16(A1),A2
	CMPI.W	#136,D3
	BLT.S	.OK_DROITE
	ST	FLAG_CLIP_DROITE
	SUBI.W	#160,D3
	NEG.W	D3
	LSR.W	#1,D3
	MOVE.L	(A1,D3.W),A4
	MOVE.W	(A4),SOV_MOT
	MOVE.W	#$4E75,(A4)
.OK_DROITE	MOVEQ	#0,D0
	TST.B	FLAG_CLIP_GAUCHE
	BEQ.S	.GO
	SF	FLAG_CLIP_GAUCHE
	NEG.W	D3
	LSR.W	#1,D3
	MOVE.W	D3,D0

.GO	MOVE.L	0(A1,D0.W),A1
	MOVEM.L	(A2),D0-D6
	MOVEQ	#0,D7
	JSR	(A1)

	TST.B	FLAG_CLIP_DROITE
	BEQ.S	.NO_AFF
	SF	FLAG_CLIP_DROITE
SOV_MOT = *+2
	MOVE.W	#$1234,(A4)
.NO_AFF	MOVEM.L	(SP)+,D0-D7
	RTS

FLAG_CLIP_GAUCHE	DS.B	1
FLAG_CLIP_DROITE	DS.B	1

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

INIT_CODE	LEA	CODE_GEN,A0
	MOVE.L	A0,D0
	MOVE.W	#5*16*2-1,D7
.ADDIT	ADD.L	D0,(A0)+
	DBRA	D7,.ADDIT
	RTS

INIT_SYSTEME	CLR.B	$FFFF8260.W

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000+40*160,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	MOVEM.L	PALETTE,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	RTS

INIT_MAP_MONSTRES
	LEA	TABLO_MONSTRES,A6
	MOVE.W	PT_MAP,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	ADR_MAP_MONSTRES,A0
	MOVE.L	(A0,D0.W),A0
	MOVE.W	(A0)+,D7
	MOVE.W	D7,NB_MONSTRES_MAX
	SUBQ.W	#1,D7
.NEXT_ONE	MOVE.W	(A0)+,D0
	CMPI.W	#100,D0
	BLT.S	.COURBE
.SEARCH	BSR	INIT_MONSTRE_SEARCH
	BRA.S	.FINI
.COURBE	BSR	INIT_MONSTRE_COURBE
.FINI	DBRA	D7,.NEXT_ONE
	MOVE.L	A0,PT_MAP2
	RTS

	;D0=No DE LA COURBE
	;A6=POINTEUR DANS TABLO_MONSTRES
INIT_MONSTRE_COURBE
	MOVE.W	D0,(A6)+
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	ADR_COURBE_MONSTRE(PC),A1
	MOVE.L	(A1,D0.W),A1
	MOVE.W	#2,(A6)+	;POINTEUR DANS LA COURBE
	MOVE.L	A1,(A6)+	;ADRESSE DE LA COURBE
	MOVE.W	(A0)+,D0	;Y_DEP
	MOVE.W	(A0)+,D1	;SENS
	MOVE.W	(A0)+,D2	;FORCE
	TST.W	D1
	BNE.S	.DROITE
.GAUCHE	MOVE.W	#319,(A6)+
	CLR.W	(A6)+
	MOVE.W	D0,(A6)+	;Y_DEP
	CLR.W	(A6)+	;SENS
	MOVE.W	D2,(A6)+	;FORCE
	BRA.S	.FINI
.DROITE	MOVE.W	#-39,(A6)+
	CLR.W	(A6)+
	MOVE.W	D0,(A6)+
	MOVE.W	#1,(A6)+
	MOVE.W	D2,(A6)+
.FINI	RTS

	;A6=POINTEUR DANS TABLO_MONSTRES
INIT_MONSTRE_SEARCH
	MOVE.W	#100,(A6)+
	MOVE.W	#NB_PASSES_RECHERCHE,(A6)+
	CLR.L	(A6)+
	MOVE.W	(A0)+,D0	;Y_DEP
	MOVE.W	(A0)+,D1	;SENS
	MOVE.W	(A0)+,D2	;FORCE
	TST.W	D1
	BNE.S	.DROITE
.GAUCHE	MOVE.W	#319,(A6)+	;X
	MOVE.W	D0,(A6)+	;Y
	CLR.W	(A6)+
	CLR.W	(A6)+
	BRA.S	.FINI
.DROITE	MOVE.W	#-39,(A6)+
	MOVE.W	D0,(A6)+
	CLR.W	(A6)+
	MOVE.W	#1,(A6)+
.FINI	MOVE.W	D2,(A6)+	;FORCE
	RTS

ADR_COURBE_MONSTRE
	DC.L	COURBE_1,COURBE_2,COURBE_3,COURBE_4

COURBE_1	INCBIN	COURBE_1.CRB
	DC.W	$1234
COURBE_2	INCBIN	COURBE_2.CRB
	DC.W	$1234
COURBE_3	INCBIN	COURBE_3.CRB
	DC.W	$1234
COURBE_4	INCBIN	COURBE_4.CRB
	DC.W	$1234

ADR_MAP_MONSTRES
	DC.L	MONSTRE_MAP_1

MONSTRE_MAP_1	DC.W	5
	DC.W	0,0,0,50
	DC.W	1,0,1,50
	DC.W	100,0,0,50
	DC.W	3,0,1,50
	DC.W	3,0,0,50
	DC.W	2,0,1,50
	DC.W	1,0,0,50
	DC.W	0,0,1,50
	DC.W	3,0,0,50
	DC.W	0,0,0,50
	DC.W	$1234

PALETTE	DC.W	$123,$777,$444,$320,$420,$531,$111,$753
	DC.W	$210,$431,$132,$243,$555,$333,$222,$345

CODE_GEN	INCBIN	KEOPS.COD

	SECTION	BSS
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256+160*40
BUFFER	DS.B	64000+80*160
;
X_PLAYER	DS.W	1
Y_PLAYER	DS.W	1
;
ADR_EFF_MONSTRE	DS.L	2
BUF_EFF_MONSTRE	DS.W	3*15*2
PT_MAP	DS.W	1
PT_MAP2	DS.L	1
NB_MONSTRES_MAX	DS.W	1
NB_MONSTRES	DS.W	1
TABLO_MONSTRES	DS.W	9*15