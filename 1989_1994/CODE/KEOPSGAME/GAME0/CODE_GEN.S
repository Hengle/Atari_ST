MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	BSR 	INIT

	LEA	BANK_PLAYER,A0
	BSR	INIT_BNK_SPR

	BSR	COD_GEN

	BSR	EFFAC_SCREEN
	CLR.W	NO

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

J	BRA.S	J

VBLR	MOVE.W	#2640,D0
BCL	DBRA	D0,BCL
	ST	$FFFF8240.W
	MOVEQ	#-1,D0
	MOVEQ	#-1,D1
	MOVEQ	#-1,D2
	MOVEQ	#-1,D3
	MOVEQ	#-1,D4
	MOVEQ	#-1,D5
	MOVEQ	#-1,D6
	MOVEQ	#-1,D7
	MOVE.L	D7,A1
	MOVE.L	D7,A2
	MOVE.L	D7,A3
	MOVE.L	D7,A4
	MOVE.L	SCREEN1,A0
N	SET	0
	REPT	35
	MOVEM.L	D0-D7,N(A0)
N	SET	N+160
	ENDR
	ADDI.W	#$1234,$FFFF8240.W
	MOVE.L	SCREEN1,A0
	LEA	ADR_COD_GEN,A1
	MOVE.W	NO,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.L	(A1,D0.W),A1
	MOVEQ	#0,D7
	JSR	(A1)
	ADDQ.W	#1,NO
	ANDI.W	#15,NO
	SF	$FFFF8240.W
	RTE

NO	DS.W	1

COD_GEN	;ON PREPARE LE CODE GENERE DES SPRITES
	LEA	BUF_COD_GEN,A6
	LEA	ADR_COD_GEN,A5
	LEA	TIME,A4

	BSR	EFFAC_SCREEN
	LEA	BANK_PLAYER,A0
	MOVE.W	(A0)+,D0	;D0=NB DE SPRITE DE LA BANK
	MOVE.W	(A0)+,D1	;LONGUEUR
	MOVE.W	(A0)+,D2	;HAUTEUR
	SUBQ.W	#1,D2
	MOVE.L	(A0)+,A1
	SUBQ.W	#1,D1
	ANDI.W	#$FFF0,D1
	LSR.W	#4,D1	;D1=NB DE MOTS-1
	MOVE.W	D1,D3
	ADDQ.W	#1,D3
	LSL.W	#3,D3
	MOVE.W	#160,D4
	SUB.W	D3,D4	;D4=OFFSET DE FIN DE LIGNE
	MOVE.W	D1,NB_MOTS_TEMP
	MOVE.W	D2,HIGH_TEMP
	MOVE.L	SCREEN1,A2
.AFF_Y	MOVE.W	D1,D3
.AFF_X	MOVE.L	(A1)+,D5
	MOVE.L	(A1)+,D6
	MOVE.L	D5,(A2)
	MOVE.L	D6,4(A2)
	OR.L	D6,D5
	MOVE.L	D5,D6
	SWAP	D6
	OR.L	D6,D5	;MASK (EN.L)
	NOT.L	D5
	MOVE.L	D5,160*100(A2)
	MOVE.L	D5,4+160*100(A2)
	ADDQ.W	#8,A2
	DBRA	D3,.AFF_X
	ADD.W	D4,A2
	DBRA	D2,.AFF_Y

NB_MOTS_TEMP = *+2
	MOVE.W	#$1234,D2	;NB DE MOTS PAR LIGNES
HIGH_TEMP = *+2
	MOVE.W	#$1234,D5	;HAUTEUR

	BSR	MAKE_COD_GEN

	SUBQ.W	#8,D4
	REPT	15
	BSR	DECAL
	MOVE.W	NB_MOTS_TEMP,D2
	ADDQ.W	#1,D2
	MOVE.W	HIGH_TEMP,D5
	BSR	MAKE_COD_GEN
	ENDR

	RTS

DECAL	MOVE.L	SCREEN1,A0
	LEA	160*100(A0),A1
	MOVE.W	HIGH_TEMP,D5
.DEC_Y	MOVEQ	#4-1,D6
.DEC_PLANES	MOVEQ	#0,D1
	MOVE.L	A0,A2
	MOVE.W	NB_MOTS_TEMP,D7
	ADDQ.W	#1,D7
.DEC_X	MOVEQ	#0,D0
	MOVE.W	(A2),D0
	ROR.L	#1,D0
	OR.W	D1,D0
	MOVE.W	D0,(A2)
	SWAP	D0
	MOVE.W	D0,D1
	ADDQ.W	#8,A2
	DBRA	D7,.DEC_X
	ADDQ.W	#2,A0
	DBRA	D6,.DEC_PLANES
	LEA	152(A0),A0
	DBRA	D5,.DEC_Y

	MOVE.L	SCREEN1,A0
	LEA	160*100(A0),A1
	MOVE.W	HIGH_TEMP,D6
.MASK_Y	MOVE.L	A0,A2
	MOVE.L	A1,A3
	MOVE.W	NB_MOTS_TEMP,D7
	ADDQ.W	#1,D7
.MASK_X	MOVE.L	(A2)+,D0
	MOVE.L	(A2)+,D1
	OR.L	D1,D0
	MOVE.L	D0,D1
	SWAP	D1
	OR.L	D1,D0
	NOT.L	D0
	MOVE.L	D0,(A3)+
	MOVE.L	D0,(A3)+
	DBRA	D7,.MASK_X
	LEA	160(A0),A0
	LEA	160(A1),A1
	DBRA	D6,.MASK_Y
	RTS

	;D4=OFFSET DE FIN DE LIGNE
	;D5=HAUTEUR
	;D2=NB DE MOTS PAR LIGNES
MAKE_COD_GEN	MOVE.L	SCREEN1,A0
	LEA	160*100(A0),A1
	MOVE.L	A6,(A5)+
	MOVEQ	#0,D3	;OFFSET DE DEPART
COD_Y	MOVE.W	D2,D6
COD_X	MOVEQ	#0,D0
	TST.W	(A0)
	BEQ.S	.VIDE_1
	ADDQ.W	#1,D0
.VIDE_1	TST.W	2(A0)
	BEQ.S	.VIDE_2
	ADDQ.W	#2,D0
.VIDE_2	TST.W	4(A0)
	BEQ.S	.VIDE_3
	ADDQ.W	#4,D0
.VIDE_3	TST.W	6(A0)
	BEQ.S	.VIDE_4
	ADDQ.W	#8,D0
.VIDE_4	;D0 REPRESENTE LE CAS PRESENT (0-->15)
	LEA	TABLE_CAS(PC),A2
	CMPI.W	#-1,(A1)
	BEQ.S	.NIB
	TST.W	(A1)
	BNE.S	.MAKE_AND_OR
.MAKE_MOVE	LEA	TABLE_CAS_MOVE(PC),A2
.MAKE_AND_OR	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.L	(A2,D0.W),A2
	JSR	(A2)
.NIB	ADDQ.W	#8,A0
	ADDQ.W	#8,A1
	ADDQ.W	#8,D3
	DBRA	D6,COD_X
	ADDA.W	D4,A0
	ADDA.W	D4,A1
	ADD.W	D4,D3
	DBRA	D5,COD_Y
	MOVE.W	#$4E75,(A6)+
	RTS

VIDE_TOTAL	RTS

PLAN_1	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	(A1),(A6)+
	MOVE.W	D3,(A6)
	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	(A0),(A6)+
	MOVE.W	D3,(A6)+
	RTS

PLANS_12	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	(A0),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLANS_13	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	(A0),(A6)+
	MOVE.W	D3,(A6)+
	;
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_123	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	(A0),(A6)+
	MOVE.W	D3,(A6)+
	;
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+	
	RTS

PLAN_2	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	2(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	2(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	2(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	2(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLANS_23	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	4(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	2(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	2(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+	
	RTS

PLAN_3	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	2(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+	
	RTS

PLAN_4	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	6(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_14	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	(A0),(A6)+
	MOVE.W	D3,(A6)+
	;
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	6(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_24	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	2(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	2(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	;
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	6(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_124	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	(A0),(A6)+
	MOVE.W	D3,(A6)+
	;
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	6(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_34	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLANS_134	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	(A0),(A6)+
	MOVE.W	D3,(A6)+
	;
	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLANS_234	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	2(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	2(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	;
	MOVE.W	#$268,(A6)+	;ANDI.W Imm,d16(A0)
	MOVE.W	6(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+	
	MOVE.W	#$68,(A6)+	;ORI.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_1234	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	(A1),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	(A0),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$2A8,(A6)+	;ANDI.L Imm,d16(A0)
	MOVE.L	4(A1),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$A8,(A6)+	;ORI.L Imm,d16(A0)
	MOVE.L	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

	;DEBUT DU CODE POUR LES MOVES
PLAN_1_MOVE	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	(A0),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	MOVE.W	#$2147,(A6)+	;MOVE.L D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLANS_12_MOVE	MOVE.W	#$217C,(A6)+	;MOVE.L Imm,d16(A0)
	MOVE.L	(A0),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$2147,(A6)+	;MOVE.L D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLANS_13_MOVE	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	(A0),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	;
	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_123_MOVE	MOVE.W	#$217C,(A6)+	;MOVE.L Imm,d16(A0)
	MOVE.L	(A0),(A6)+
	MOVE.W	D3,(A6)+
	;
	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLAN_2_MOVE	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)+
	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	2(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	MOVE.W	#$2147,(A6)+	;MOVE.L D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLANS_23_MOVE	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)+
	MOVE.W	#$217C,(A6)+	;MOVE.L Imm,d16(A0)
	MOVE.L	2(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLAN_3_MOVE	MOVE.W	#$2147,(A6)+	;MOVE.L D7,d16(A0)
	MOVE.W	D3,(A6)+
	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLAN_4_MOVE	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$2147,(A6)+	;MOVE.L D7,d16(A0)
	MOVE.W	D3,(A6)+
	RTS

PLANS_14_MOVE	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	(A0),(A6)+
	MOVE.W	D3,(A6)+
	;
	MOVE.W	#$2147,(A6)+	;MOVE.L D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_24_MOVE	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)+
	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	2(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	;
	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_124_MOVE	MOVE.W	#$217C,(A6)+	;MOVE.L Imm,d16(A0)
	MOVE.L	(A0),(A6)+
	MOVE.W	D3,(A6)+
	;
	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_34_MOVE	MOVE.W	#$2147,(A6)+	;MOVE.L D7,d16(A0)
	MOVE.W	D3,(A6)+
	MOVE.W	#$217C,(A6)+	;MOVE.L Imm,d16(A0)
	MOVE.L	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLANS_134_MOVE	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	(A0),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	;
	MOVE.W	#$217C,(A6)+	;MOVE.L Imm,d16(A0)
	MOVE.L	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

PLANS_234_MOVE	
	MOVE.W	#$3147,(A6)+	;MOVE.W D7,d16(A0)
	MOVE.W	D3,(A6)+
	MOVE.W	#$217C,(A6)+	;MOVE.L Imm,d16(A0)
	MOVE.L	2(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#2,(A6)+
	;
	MOVE.W	#$317C,(A6)+	;MOVE.W Imm,d16(A0)
	MOVE.W	6(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#6,(A6)+
	RTS

PLANS_1234_MOVE	MOVE.W	#$217C,(A6)+	;MOVE.L Imm,d16(A0)
	MOVE.L	(A0),(A6)+
	MOVE.W	D3,(A6)+
	MOVE.W	#$217C,(A6)+	;MOVE.L Imm,d16(A0)
	MOVE.L	4(A0),(A6)+
	MOVE.W	D3,(A6)
	ADDQ.W	#4,(A6)+
	RTS

TIENS	ILLEGAL

TABLE_CAS_MOVE	DC.L	VIDE_TOTAL
	DC.L	PLAN_1_MOVE
	DC.L	PLAN_2_MOVE
	DC.L	PLANS_12_MOVE
	DC.L	PLAN_3_MOVE
	DC.L	PLANS_13_MOVE
	DC.L	PLANS_23_MOVE
	DC.L	PLANS_123_MOVE
	DC.L	PLAN_4_MOVE
	DC.L	PLANS_14_MOVE
	DC.L	PLANS_24_MOVE
	DC.L	PLANS_124_MOVE
	DC.L	PLANS_34_MOVE
	DC.L	PLANS_134_MOVE
	DC.L	PLANS_234_MOVE
	DC.L	PLANS_1234_MOVE

TABLE_CAS	DC.L	VIDE_TOTAL
	DC.L	PLAN_1
	DC.L	PLAN_2
	DC.L	PLANS_12
	DC.L	PLAN_3
	DC.L	PLANS_13
	DC.L	PLANS_23
	DC.L	PLANS_123
	DC.L	TIENS
	DC.L	TIENS
	DC.L	TIENS
	DC.L	TIENS
	DC.L	TIENS
	DC.L	TIENS
	DC.L	TIENS
	DC.L	TIENS
;	DC.L	PLAN_4
;	DC.L	PLANS_14
;	DC.L	PLANS_24
;	DC.L	PLANS_124
;	DC.L	PLANS_34
;	DC.L	PLANS_134
;	DC.L	PLANS_234
;	DC.L	PLANS_1234

EFFAC_SCREEN	MOVE.L	SCREEN1,A0
	MOVE.W	#8000-1,D7
.EFF	CLR.L	(A0)+
	DBRA	D7,.EFF
	RTS

INIT_BNK_SPR	MOVE.L	A0,D1
	MOVE.W	(A0)+,D0	;D0=NB DE SPR
	SUBQ.W	#1,D0
.RETURN	ADDQ.W	#4,A0
	ADD.L	D1,(A0)+
	DBRA	D0,.RETURN
	RTS

INIT	CLR.B	$FFFF8260.W
	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	MOVEM.L	PALETTE,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	RTS

BANK_PLAYER	INCBIN	DA_EVIL.BNK

PALETTE	DC.W	$123,$777,$444,$320,$420,$531,$111,$753
	DC.W	$210,$431,$132,$243,$555,$333,$222,$345
	;INCBIN	KEOPS_2.PAL

	SECTION	BSS
	DS.B	256
BUFFER	DS.B	32000
SCREEN1	DS.L	1
CALC	DS.W	1
TIME	DS.W	16
ADR_COD_GEN	DS.L	16
BUF_COD_GEN	DS.B	50000