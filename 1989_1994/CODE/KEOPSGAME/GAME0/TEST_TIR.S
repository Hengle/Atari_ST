NB_PASSES_TIR = 8

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	BSR	INIT_SYSTEME

	BSR	PREP_ARME_SORT

	LEA	BUF_ADR_TIR_STRETCH+2*NB_PASSES_TIR*5*0,A0
	MOVE.L	SCREEN1,A2
	MOVEQ	#NB_PASSES_TIR*5-1,D7
.AFF_ALL	MOVE.W	(A0)+,D0	;LONGUEUR EN PIXELS
	MOVE.W	(A0)+,D0	;NB DE MOTS-1
	MOVE.W	(A0)+,D1	;D1=HAUTEUR
	SUBQ.W	#1,D1
	MOVE.L	(A0)+,A1
.AFF_Y	MOVE.L	A2,A3
	MOVE.W	D0,D3
.AFF_X	MOVE.W	(A1)+,(A3)
	ADDQ.W	#8,A3
	DBRA	D3,.AFF_X
	LEA	160(A2),A2
	DBRA	D1,.AFF_Y
	DBRA	D7,.AFF_ALL

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
.J	BRA.S	.J

PREP_ARME_SORT	;MOVE.L	ADR_TIR_PLAYER,A0   ;DEJA FAIT!!!
	LEA	BANK_PYTHON,A0
	BSR	INIT_BNK_SPR

;	MOVE.L	ADR_TIR_PLAYER,A0
;	ADDQ.W	#2,A0
	LEA	BANK_PYTHON+2,A0
	LEA	BUF_ADR_TIR_STRETCH,A6
	LEA	BUF_TIR_STRETCH,A2

	;TIRS VERS LA DROITE
	LEA	TABLE_VIRE_GAUCHE(PC),A3
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	MOVE.L	(A0)+,A1
	MOVE.W	D1,HAUTEUR_TEMP
	MOVE.W	D0,D2
	SUBQ.W	#1,D2
	LSR.W	#4,D2
	ADDQ.W	#1,D2
	LSL.W	#3,D2
	MOVE.W	D2,LONGUEUR_TEMP
	MOVE.W	D0,D2
	DIVU.W	#NB_PASSES_TIR,D2 ;D2=PAS DE DECREMENTATION
	ADDQ.W	#1,D2
	MOVE.W	D2,PAS_SCROLL_TIR
	MOVE.W	D0,D3
	SUBQ.W	#1,D3
	SF	FLAG_SORT

NEXT_CUT	SUB.W	D2,D3	;D3=X1
	BGT.S	.POSITIF
	ST	FLAG_SORT
	MOVEQ	#0,D3
.POSITIF	MOVE.W	D3,-(SP)
	MOVE.W	D0,D4
	SUBQ.W	#1,D4	;D4=X2
	MOVE.W	D4,D6
	SUB.W	D3,D4
	ADDQ.W	#1,D4
	MOVE.W	D4,(A6)+	;VRAIE LONGUEUR
	MOVE.W	D6,D4
	MOVE.W	D3,D5
	ANDI.W	#15,D5
	ANDI.W	#$FFF0,D3
	LSR.W	#1,D3
	ANDI.W	#$FFF0,D4
	LSR.W	#1,D4
	SUB.W	D3,D4
	LSR.W	#3,D4
	MOVE.W	D4,(A6)+
	MOVE.W	HAUTEUR_TEMP(PC),(A6)+
	MOVE.L	A2,(A6)+
	MOVE.W	D4,NB_MOTS_TEMP
	MOVE.L	A1,A4
	ADDA.W	D3,A4
HAUTEUR_TEMP = *+2
	MOVE.W	#$1234,D3
	SUBQ.W	#1,D3
	ADD.W	D5,D5
	MOVE.W	(A3,D5.W),D7	;MASQUE DE GAUCHE
	LSR.W	#1,D5
.COPY_Y	MOVE.L	A4,A5
	MOVEQ	#0,D6
	MOVE.W	(A5),D6
	AND.W	D7,D6
	LSL.W	D5,D6
	MOVE.W	D6,(A2)
	ADDQ.W	#8,A5
NB_MOTS_TEMP = *+2
	MOVE.W	#$1234,D4
	SUBQ.W	#1,D4
.COPY_X	MOVEQ	#0,D6
	MOVE.W	(A5),D6
	ROL.L	D5,D6
	SWAP	D6
	OR.W	D6,(A2)+
	SWAP	D6
	MOVE.W	D6,(A2)
	ADDQ.W	#8,A5
	DBRA	D4,.COPY_X
	ADDQ.W	#2,A2
LONGUEUR_TEMP = *+2
	LEA	$1234(A4),A4
	DBRA	D3,.COPY_Y
	MOVE.W	(SP)+,D3	;RECUPäRE LE X1
	TST.B	FLAG_SORT
	BEQ	NEXT_CUT
FIN_CUT_1	;ON DECALE LA PREMIERE DêCOUPE !
	LEA	BUF_ADR_TIR_STRETCH,A5
	MOVE.W	#NB_PASSES_TIR-1,D7
.ALL_DEC	MOVE.W	(A5)+,D6	;LONGUEUR EN PIXELS
	MOVE.W	(A5)+,D0	;LONGUEUR EN MOTS-1
	MOVE.W	(A5)+,D1	;HAUTEUR
	MOVE.L	(A5)+,A4	;ADRESSE
	MOVE.W	D6,(A6)+	;LONGUEUR EN PIXELS
	MOVE.W	D0,(A6)
	ADDQ.W	#1,(A6)+
	MOVE.W	D1,(A6)+
	MOVE.L	A2,(A6)+
	SUBQ.W	#1,D1
.DEC_Y	MOVEQ	#0,D3
	MOVE.W	D0,D4
.DEC_X	MOVEQ	#0,D2
	MOVE.W	(A4)+,D2
	ROR.L	#1,D2
	OR.W	D3,D2
	MOVE.W	D2,(A2)+
	SWAP	D2
	MOVE.W	D2,D3
	DBRA	D4,.DEC_X
	OR.W	D3,(A2)+
	DBRA	D1,.DEC_Y
	DBRA	D7,.ALL_DEC
SECOND_HêHê
	MOVE.W	#NB_PASSES_TIR*14-1,D7
.ALL_DEC	MOVE.W	(A5)+,D6	;LONGUEUR EN PIXELS
	MOVE.W	(A5)+,D0	;LONGUEUR EN MOTS-1
	MOVE.W	(A5)+,D1	;HAUTEUR
	MOVE.L	(A5)+,A4	;ADRESSE
	MOVE.W	D6,(A6)+	;LONGUEUR EN PIXELS
	MOVE.W	D0,(A6)+
	MOVE.W	D1,(A6)+
	MOVE.L	A2,(A6)+
	SUBQ.W	#1,D1
.DEC_Y	MOVEQ	#0,D3
	MOVE.W	D0,D4
.DEC_X	MOVEQ	#0,D2
	MOVE.W	(A4)+,D2
	ROR.L	#1,D2
	OR.W	D3,D2
	MOVE.W	D2,(A2)+
	SWAP	D2
	MOVE.W	D2,D3
	DBRA	D4,.DEC_X
	DBRA	D1,.DEC_Y
	DBRA	D7,.ALL_DEC

	;TIR VERS LA GAUCHE
SECOND_TIR	MOVE.W	(A0)+,D0	;LONGUEUR
	MOVE.W	(A0)+,D1	;HAUTEUR
	MOVE.L	(A0)+,A1	;ADRESSE DU SPRITE
	LEA	TABLE_VIRE_DROITE(PC),A3
	MOVE.W	PAS_SCROLL_TIR,D2
	MOVE.W	D0,D3
	SUBQ.W	#1,D3
	LSR.W	#4,D3
	ADDQ.W	#1,D3
	LSL.W	#3,D3
	MOVE.W	D3,LONGUEUR_TEMP_2

	MOVEQ	#0,D3
	MOVEQ	#NB_PASSES_TIR-1,D7
.ALL_CUTS	ADD.W	D2,D3	;X2
	MOVE.W	D3,-(SP)
	MOVE.W	D3,D4
	ANDI.W	#15,D4
	ADD.W	D4,D4
	MOVE.W	(A3,D4.W),D4	;MASK POUR LA DROITE
	MOVE.W	D3,(A6)+	;LONGUEUR EN PIXELS
	LSR.W	#4,D3	;NB DE MOTS-1
	MOVE.W	D3,(A6)+	;LONGUEUR EN MOTS-1
	MOVE.W	D1,(A6)+	;HAUTEUR
	MOVE.L	A2,(A6)+	;ADRESSE DU SPRITE
	MOVE.L	A1,A4
	MOVE.W	D1,D6
	SUBQ.W	#1,D6
.COPY_Y	MOVE.L	A4,A5
	MOVE.W	D3,D5
	SUBQ.W	#1,D5
	BLT.S	.Hê_HO
.COPY_X	MOVE.W	(A5),(A2)+
	ADDQ.W	#8,A5
	DBRA	D5,.COPY_X
.Hê_HO	MOVE.W	(A5),D5
	ADDQ.W	#8,A5
	AND.W	D4,D5
	MOVE.W	D5,(A2)+
LONGUEUR_TEMP_2 = *+2
	LEA	$1234(A4),A4
	DBRA	D6,.COPY_Y
	MOVE.W	(SP)+,D3
	DBRA	D7,.ALL_CUTS

	;ON DECALE LA DEUXIEME DêCOUPE !
	LEA	BUF_ADR_TIR_STRETCH+2*5*NB_PASSES_TIR*16,A5
	MOVE.W	#NB_PASSES_TIR-1,D7
.ALL_DEC	MOVE.W	(A5)+,D6	;LONGUEUR EN PIXELS
	MOVE.W	(A5)+,D0	;LONGUEUR EN MOTS-1
	MOVE.W	(A5)+,D1	;HAUTEUR
	MOVE.L	(A5)+,A4	;ADRESSE
	MOVE.W	D6,(A6)+	;LONGUEUR EN PIXELS
	MOVE.W	D0,(A6)
	ADDQ.W	#1,(A6)+
	MOVE.W	D1,(A6)+
	MOVE.L	A2,(A6)+
	SUBQ.W	#1,D1
.DEC_Y	MOVEQ	#0,D3
	MOVE.W	D0,D4
.DEC_X	MOVEQ	#0,D2
	MOVE.W	(A4)+,D2
	ROR.L	#1,D2
	OR.W	D3,D2
	MOVE.W	D2,(A2)+
	SWAP	D2
	MOVE.W	D2,D3
	DBRA	D4,.DEC_X
	OR.W	D3,(A2)+
	DBRA	D1,.DEC_Y
	DBRA	D7,.ALL_DEC
TROISIEME_HêHê
	MOVE.W	#NB_PASSES_TIR*14-1,D7
.ALL_DEC	MOVE.W	(A5)+,D6	;LONGUEUR EN PIXELS
	MOVE.W	(A5)+,D0	;LONGUEUR EN MOTS-1
	MOVE.W	(A5)+,D1	;HAUTEUR
	MOVE.L	(A5)+,A4	;ADRESSE
	MOVE.W	D6,(A6)+	;LONGUEUR EN PIXELS
	MOVE.W	D0,(A6)+
	MOVE.W	D1,(A6)+
	MOVE.L	A2,(A6)+
	SUBQ.W	#1,D1
.DEC_Y	MOVEQ	#0,D3
	MOVE.W	D0,D4
.DEC_X	MOVEQ	#0,D2
	MOVE.W	(A4)+,D2
	ROR.L	#1,D2
	OR.W	D3,D2
	MOVE.W	D2,(A2)+
	SWAP	D2
	MOVE.W	D2,D3
	DBRA	D4,.DEC_X
	DBRA	D1,.DEC_Y
	DBRA	D7,.ALL_DEC

;	LEA	BUF_TIRS,A0
;	MOVE.W	#$1234,(A0)
	RTS

FLAG_SORT	DS.B	1
	EVEN

TABLE_VIRE_GAUCHE
	DC.W	$FFFF,$7FFF,$3FFF,$1FFF,$FFF,$7FF,$3FF,$1FF,$FF,$7F,$3F,$1F,$F,7,3,1

TABLE_VIRE_DROITE
	DC.W	$8000,$C000,$E000,$F000,$F800,$FC00,$FE00,$FF00,$FF80,$FFC0,$FFE0,$FFF0,$FFF8,$FFFC,$FFFE,$FFFF

INIT_SYSTEME	CLR.B	$FFFF8260.W

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

INIT_BNK_SPR	MOVE.L	A0,D0
	MOVE.W	(A0)+,D7
	SUBQ.W	#1,D7
.INIT	ADDQ.W	#4,A0
	ADD.L	D0,(A0)+
	DBRA	D7,.INIT
	RTS

BANK_PYTHON	INCBIN	TIR_4.BNK

	SECTION	BSS
SCREEN1	DS.L	1
	DS.B	256
BUFFER	DS.B	32000
;BSS IMPORTANTE, A RECOPIER DANS LE SOURCE PRINCIPAL DU JEU.
PAS_SCROLL_TIR	DS.W	1
BUF_ADR_TIR_STRETCH
	DS.W	5*NB_PASSES_TIR*16*2
BUF_TIR_STRETCH	DS.W	20*16*15*2