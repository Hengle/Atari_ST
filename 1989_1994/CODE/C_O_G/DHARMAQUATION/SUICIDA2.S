SEG=(136+200)/4
NB_PTS=4*SEG
PAS=1
NB_POS=(90/PAS)
LARG_FONTE = 32
HAUT_FONTE = 32

PROG:
	JMP	MAIN
ZIK	INCBIN	LOGICAL.MUS
REG_TXT	DS.L	16
VBL_DEB	
	CLR.B	$FFFFFA1B.W
	MOVE.B	#40,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W

	MOVE.L	#1,$140.W
	MOVEM.L	D0-A6,-(SP)

	MOVEM.L	REG_TXT,D0-A6
MODMOD	EQU	*+2
	JMP	VBL_TXT
VBEND	MOVEM.L	D0-A6,REG_TXT
	JMP	VBSUITEZ
VBL_END	MOVE.L	#1,$140.W
;	MOVE	#$700,$FFFF8240.W
	RTE
FIN	MOVE.L	4.W,A0
	JMP	(A0)
RETOUR	MOVE	#$070,$FFFF8240.W
J	JMP	J

VBL_ZIK	MOVEM.L	D0-A6,-(SP)
VBSUITEZ	JSR	ZIK+8
	move.l	SCREEN1,d0
	CMPI.B	#$1,$FFFFFC02.W
	BNE.S	NO_CHG
	MOVE.L	SCREEN3,D0
NO_CHG	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)
	MOVEM.L	(SP)+,D0-A6
VGAND	ADDQ.L	#1,$140.W
INTER_RTE	RTE
VBL_TXT
LOOP_AFF_MES
MOD_ADR	EQU	*+2
	LEA	MESSAGE,A0
	CMPI.B	#-1,(A0)
	BEQ	TERMINê
	MOVE	#(20*5)-1,D6
ICI	MOVEQ	#0,D0
SCR_CHG	EQU	*+2
	MOVE.L	SCREEN1,A5
	LEA	160*60+2(A5),A5
	LEA	CORRES,A2
MOD	EQU	*+2
	LEA	0(A5),A5
MOD2	EQU	*+2
	LEA	0(A5),A5
	MOVE.B	(A0)+,D0
	SUBI.B	#32,D0
;	TST.B	D0
;	BEQ.S	GO
;	SUBQ.B	#1,D0
GO	LSL.L	#2,D0
	ADDA.L	D0,A2
	MOVE.L	(A2),A2
	MOVEQ	#15,D0
AFF_A_LETTER	MOVE	(A2),(A5)
	LEA	160(A2),A2
	LEA	160(A5),A5
	DBF	D0,AFF_A_LETTER
	ADDQ	#8,MOD
	CMPI	#160,MOD
	BNE.S	OKAY_MAN
	CLR	MOD
	ADD	#160*16,MOD2
OKAY_MAN	DBF	D6,ICI
	MOVE.L	A0,MOD_ADR
	CLR	MOD
	CLR	MOD2
	CLR	PNT_FAD
	CLR	PNT_FADB
FAD1	LEA	FAD_IN,A0
PNT_FAD	EQU	*+2
	LEA	0(A0),A0
	MOVE	(A0),D0
	CMPI	#-1,D0
	BEQ.S	ENDED
ADR_CHG	EQU	*+2
	MOVE	D0,MODCOL
	MOVE.L	#WAIT_A_BIT,MODMOD
	JMP	VBEND
WAIT_A_BIT	;TST.L	$140.W
	;BEQ.S	WAIT_A_BIT
	;CLR.L	$140.W
	ADDQ.L	#1,PNT_FAD2
	CMPI.L	#2,PNT_FAD2
	BNE.S	EXI
	ADDQ	#2,PNT_FAD
	CLR.L	PNT_FAD2
EXI	MOVE.L	#FAD1,MODMOD
	JMP	VBEND
PNT_FAD2	DC.L	0
TIME	DC.L	0
ENDED	ADDQ.L	#1,TIME
	MOVE.L	#WAIT2,MODMOD
	JMP	VBEND
WAIT2	;TST.L	$140.W
	;BEQ.S	WAIT2
	;CLR.L	$140.W
	CMPI.L	#50*4,TIME
	BNE.S	ENDED
	CLR.L	TIME
	CLR.L	PNT_FAD2
FAD2	LEA	FAD_OUT,A0
PNT_FADB	EQU	*+2
	LEA	0(A0),A0
	MOVE	(A0),D0
	CMPI	#-1,D0
	BEQ.S	ENDEDB
ADR_CHG2	EQU	*+2
	MOVE	D0,MODCOL
WAITB	MOVE.L	#WAITB__,MODMOD
	JMP	VBEND
WAITB__	;TST.L	$140.W
	;BEQ.S	WAITB
	;CLR.L	$140.W
	ADDQ.L	#1,PNT_FAD2
	CMPI.L	#2,PNT_FAD2
	BNE.S	EXIB
	ADDQ	#2,PNT_FADB
	CLR.L	PNT_FAD2
EXIB	MOVE.L	#FAD2,MODMOD
	JMP	VBEND
;	BRA.S	FAD2
ENDEDB	CLR.L	TIME
	CLR.L	PNT_FAD2
	MOVE.L	#LOOP_AFF_MES,MODMOD
	JMP	VBEND

TERMINê
CHG_VBL	EQU	*+2
	MOVE.L	#TERMINê,$70.W
	JMP	VBEND
RET	EQU	*+2
	JMP	RETOUR
TIMER
	CLR.B	$FFFFFA1B.W
MODCOL	EQU	*+2
	MOVE	#000,$FFFF8244.W
	MOVE	MODCOL,$FFFF8246.W
;	MOVE.B	#3,$FFFFFA21.W
;	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TIMER,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE

; a0: Pointer on packed Data
; a1: Pointer on destination of unpacked data
decrunch:	movem.l	d0-a6,-(sp)
	cmpi.l	#'Ice!',(a0)+
	bne.s	ice_03
	move.l	(a0)+,d0
	lea	-8(a0,d0.l),a5
	move.l	(a0)+,(sp)
	movea.l	a1,a4
	movea.l	a1,a6
	adda.l	(sp),a6
	movea.l	a6,a3
	bsr.s	ice_08
	bsr.s	ice_04
	bsr	ice_0c
	bcc.s	ice_03
	move.w	#$0f9f,d7
ice_00:	moveq	#3,d6
ice_01:	move.w	-(a3),d4
	moveq	#3,d5
ice_02:	add.w	d4,d4
	addx.w	d0,d0
	add.w	d4,d4
	addx.w	d1,d1
	add.w	d4,d4
	addx.w	d2,d2
	add.w	d4,d4
	addx.w	d3,d3
	dbra	d5,ice_02
	dbra	d6,ice_01
	movem.w	d0-d3,(a3)
	dbra	d7,ice_00
ice_03:	movem.l	(sp)+,d0-a6
	rts
ice_04:	bsr.s	ice_0c
	bcc.s	ice_07
	moveq	#0,d1
	bsr.s	ice_0c
	bcc.s	ice_06
	lea	ice_1a(pc),a1
	moveq	#4,d3
ice_05:	move.l	-(a1),d0
	bsr.s	ice_0f
	swap	d0
	cmp.w	d0,d1
	dbne	d3,ice_05
	add.l	20(a1),d1
ice_06:	move.b	-(a5),-(a6)
	dbra	d1,ice_06
ice_07:	cmpa.l	a4,a6
	bgt.s	ice_12
	rts
ice_08:	moveq	#3,d0
ice_09:	move.b	-(a5),d7
	ror.l	#8,d7
	dbra	d0,ice_09
	rts
ice_0a:	move.w	a5,d7
	btst	#0,d7
	bne.s	ice_0b
	move.l	-(a5),d7
	addx.l	d7,d7
	bra.s	ice_11
ice_0b:	move.l	-5(a5),d7
	lsl.l	#8,d7
	move.b	-(a5),d7
	subq.l	#3,a5
	add.l	d7,d7
	bset	#0,d7
	bra.s	ice_11
ice_0c:	add.l	d7,d7
	beq.s	ice_0d
	rts
ice_0d:	move.w	a5,d7
	btst	#0,d7
	bne.s	ice_0e
	move.l	-(a5),d7
	addx.l	d7,d7
	rts
ice_0e:	move.l	-5(a5),d7
	lsl.l	#8,d7
	move.b	-(a5),d7
	subq.l	#3,a5
	add.l	d7,d7
	bset	#0,d7
	rts
ice_0f:	moveq	#0,d1
ice_10:	add.l	d7,d7
	beq.s	ice_0a
ice_11:	addx.w	d1,d1
	dbra	d0,ice_10
	rts
ice_12:	lea	ice_1b(pc),a1
	moveq	#3,d2
ice_13:	bsr.s	ice_0c
	dbcc	d2,ice_13
	moveq	#0,d4
	moveq	#0,d1
	move.b	1(a1,d2.w),d0
	ext.w	d0
	bmi.s	ice_14
	bsr.s	ice_0f
ice_14:	move.b	6(a1,d2.w),d4
	add.w	d1,d4
	beq.s	ice_16
	lea	ice_1c(pc),a1
	moveq	#1,d2
ice_15:	bsr.s	ice_0c
	dbcc	d2,ice_15
	moveq	#0,d1
	move.b	1(a1,d2.w),d0
	ext.w	d0
	bsr.s	ice_0f
	add.w	d2,d2
	add.w	6(a1,d2.w),d1
	bra.s	ice_18
ice_16:	moveq	#0,d1
	moveq	#5,d0
	moveq	#0,d2
	bsr.s	ice_0c
	bcc.s	ice_17
	moveq	#8,d0
	moveq	#$40,d2
ice_17:	bsr.s	ice_0f
	add.w	d2,d1
ice_18:	lea	2(a6,d4.w),a1
	adda.w	d1,a1
	move.b	-(a1),-(a6)
ice_19:	move.b	-(a1),-(a6)
	dbra	d4,ice_19
	bra	ice_04
	DC.B	$7f,$ff,$00,$0e,$00,$ff,$00,$07
	DC.B	$00,$07,$00,$02,$00,$03,$00,$01
	DC.B	$00,$03,$00,$01
ice_1a:	DC.B	$00,$00,$01,$0d,$00,$00,$00,$0e
	DC.B	$00,$00,$00,$07,$00,$00,$00,$04
	DC.B	$00,$00,$00,$01
ice_1b:	DC.B	$09,$01,$00,$ff,$ff,$08,$04,$02
	DC.B	$01,$00
ice_1c:	DC.B	$0b,$04,$07,$00,$01,$20,$00,$00
	DC.B	$00,$20
**
	DS.L	256
NEWPILE	DS.L	1
MAIN
	CLR.L	-(SP)
	MOVE	#$20,-(sp)
	trap	#1
	addq.l	#6,sp
	CLR	-(SP)
	move.l	#-1,-(sp)
	move.l	#-1,-(sp)
	MOVE	#5,-(sp)
	trap	#14
	lea	12(sp),sp
	LEA	NEWPILE,A7
	MOVE.L	#TEXT,PT_TEXT
	CLR	PART_FONTE
	MOVE.W	#152,PT_BUF
	MOVE.L	#FONTS+34+(160*33*4)+6*16,SOV_ADR
	MOVE.W	#4,NO_DEC
	CLR	PT_AFF
	LEA	COD,A0
	LEA	TAB_D,A1
UN_DE_PLUS	MOVE.L	A0,(A1)+
CHERCHE	MOVE	(A0)+,D0
	CMPI	#$4E75,D0
	BNE.S	CHERCHE
	CMPI.L	#'FINI',(A1)
	BEQ.S	FINITO
	BRA.S	UN_DE_PLUS
FINITO

	move.l	#BUFFERSCR,d0	
	CLR.B	D0
	move.l	d0,SCREEN1
	add.l	#32000,d0	
	move.l	d0,SCREEN2
	MOVE.L	#END_COD,D0
	CLR.B	D0
	SUB.L	#32000,D0
	MOVE.L	D0,SCREEN3
	
	move.l	SCREEN1,d0
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)
	MOVE.L	#VGAND,$70.W
	CLR.L	$FFFF8240.W
	CLR.L	$FFFF8244.W
	CLR.L	$FFFF8248.W
	CLR.L	$FFFF824C.W
	CLR.L	$FFFF8250.W
	CLR.L	$FFFF8254.W
	CLR.L	$FFFF8258.W
	CLR.L	$FFFF825C.W
	LEA	GANDALF,A0
	MOVE.L	SCREEN3,A1
	LEA	-34(A1),A1
	JSR	decrunch

LP1	JSR	VSYNC
	LEA	PALG1,A0
MD1	EQU	*+2
	LEA	0(A0),A0
	CMPI	#-1,(A0)
	BEQ.S	ED1_
	MOVEM.L	(A0),D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	ADDQ.L	#1,MD2
	CMPI.L	#3,MD2
	BNE.S	LP1
	CLR.L	MD2
	ADD	#16*2,MD1
	BRA.S	LP1
MD2	DC.L	0
ED1_
;	MOVE.L	SCREEN3,A1
;	LEA	-34+2(A1),A1
;	MOVEM.L	(A1),D0-D7
;	MOVEM.L	D0-D7,$FFFF8240.W

	MOVEQ	#0,D0
	JSR	ZIK
			
	MOVE.L	#INTER_RTE,$68.W

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W
	
	MOVE.B	#$12,$FFFFFC02.W
	MOVE.L	#TIMER,$120.W
	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W

	LEA	MODCOL,A0
	LEA	SCREEN1,A2
	LEA	VBL_END,A3
	LEA	RETOUR,A4
	MOVE.L	A0,ADR_CHG
	MOVE.L	A0,ADR_CHG2
	MOVE.L	A2,SCR_CHG
	MOVE.L	A3,CHG_VBL
	MOVE.L	A4,RET
	MOVEM.L	D0-A6,REG_TXT
;	MOVE.L	#VBL_DEB,$70.W

	JSR	LINE_A_INIT
	MOVE	#1,24(A0)
	CLR	26(A0)
	CLR	28(A0)
	CLR	30(A0)
	MOVE	#0,36(A0)
	MOVE.L	#FILL,46(A0)
	MOVE	#1,50(A0)
	CLR	52(A0)
	MOVE	#1,54(A0)
	move	#1,56(A0)
	move	#1,58(A0)
	move	#319,60(A0)
	move	#199,62(A0)

	MOVE.L	#TABLE_DAT,ADR_TABL
	
	move.l	SCREEN3,d0
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)
	LEA	BUFCOD,A0
	MOVE.L	A0,SAVEA0
	LEA	TABLE,A4
	MOVE.L	A4,SAVEA4
	MOVEM.L	D0-A6,REG_GEN
	
	move	#$9999,modif
*************
	clr	x_angle
	clr	y_angle
	clr	z_angle

	CLR.L	POINTEUR

	MOVE	#2,x_angle
;	MOVE	#180*2,y_angle
;	MOVE	#70*2,z_angle
	
	move	#256,z_pt_fuite
	BRA.S	CONT1
CNT_POS	DC.L	0
CONT1	move.l	#NB_POS-1,d7
ici	JSR	GENERATION
	ADDQ.L	#1,CNT_POS
	CMPI.L	#10,CNT_POS
	BEQ.S	OUT_1
	DBF	D7,ici
OUT_1	
	SF	$FFFF8242.W
LP2	JSR	VSYNC
	LEA	PALG2,A0
MD1B	EQU	*+2
	LEA	0(A0),A0
	CMPI	#-1,(A0)
	BEQ.S	ED2_
	MOVEM.L	(A0),D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	ADDQ.L	#1,MD2B
	CMPI.L	#3,MD2B
	BNE.S	LP2
	CLR.L	MD2B
	ADD	#16*2,MD1B
	BRA.S	LP2
MD2B	DC.L	0
ED2_
	MOVE.L	SCREEN3,A0
	MOVE	#7999,D0
DEGAGE	CLR.L	(A0)+
	DBF	D0,DEGAGE
	
	move.l	#BUFFER,d0
	CLR.B	D0
	move.l	d0,SCR1
	add.l	#32000,d0
	move.l	d0,SCR2


	LEA	BIG_DATA,A0
	MOVE.L	SCR2,A1
	JSR	decrunch
	MOVEM.L	2(A1),D0-D7
	LEA	PAL_LOGO,A0
	MOVEM.L	D0-D7,(A0)

	MOVE	#7,D0
EFFDEBUT	CLR.L	(A1)+
	DBF	D0,EFFDEBUT
	CLR	(A1)+
	MOVEA.L	SCR1,A1
	LEA	160*12(A1),A1
	MOVEA.L	SCR2,A2
	LEA	34(A2),A2
	MOVE	#(40*56)-1,D0
AFF_LOGO	MOVE.L	(A2),(A1)+
	CLR.L	(A2)+
	DBF	D0,AFF_LOGO

	MOVEA.L	SCR1,A1
	LEA	160*(12+57)(A1),A1
	MOVEA.L	SCR1,A2
	LEA	160*(12+56)(A2),A2
	MOVE	#27,D1
AFF_LOGO1	MOVE	#39,D0
AFF_LOGO2	MOVE.L	(A2)+,(A1)+
	DBF	D0,AFF_LOGO2
	LEA	-480(A2),A2
	DBF	D1,AFF_LOGO1
	MOVEA.L	SCR1,A0
	MOVEA.L	SCR2,A1
	MOVE	#7999,D0
COPY	MOVE.L	(A0)+,(A1)+
	DBF	D0,COPY


;	MOVE.L	SCR1,A0
;	LEA	160*(192-43)(A0),A0
;N	SET	0
;	REPT	20
;	MOVE	#$FFFF,N(A0)
;N	SET	N+8
;	ENDR

	MOVE.L	SCR1,A0
	MOVE.L	SCR2,A1
	LEA	160*(192-42)+2(A0),A0
	LEA	160*(192-42)+2(A1),A1
	MOVE	#(20*42)-1,D0
DO_LIGNE	MOVE	#$FFFF,(A0)
	MOVE	#$FFFF,(A1)
	ADDQ.L	#8,A0
	ADDQ.L	#8,A1
	DBF	D0,DO_LIGNE


	MOVE.L	SCR1,A0
	MOVE.L	SCR2,A2
	LEA	192*160(A0),A1
	LEA	192*160(A2),A3
	MOVE	#7,D1
NDO_Y	MOVE	#19,D0
NDO_X	MOVE	#$FFFF,(A0)
	MOVE	#$FFFF,(A1)
	MOVE	#$FFFF,(A2)
	MOVE	#$FFFF,(A3)
	ADDQ.L	#8,A0
	ADDQ.L	#8,A1
	ADDQ.L	#8,A2
	ADDQ.L	#8,A3
	DBF	D0,NDO_X
	DBF	D1,NDO_Y

	MOVE.L	SCR1,A0
	MOVE.L	SCR2,A1
	MOVE	#(192-43)-1,D0
NDO_PIX	MOVE.B	#$FF,(A0)
	MOVE.B	#$FF,(A1)
	MOVE.B	#$FF,153(A0)
	MOVE.B	#$FF,153(A1)
	LEA	160(A0),A0
	LEA	160(A1),A1
	DBF	D0,NDO_PIX

	MOVE.L	SCR1,A0
	LEA	160*149(A0),A0
	MOVE.L	SCR2,A1
	LEA	160*149(A1),A1
	MOVE	#50,D0
DO_PIXU	MOVE.B	#$FF,4(A0)
	MOVE.B	#$FF,4(A1)
	MOVE.B	#$FF,153+4(A0)
	MOVE.B	#$FF,153+4(A1)
	LEA	160(A0),A0
	LEA	160(A1),A1
	DBF	D0,DO_PIXU
	
	LEA	TAB_D,A1
	MOVE.L	(A1),A1
	MOVEM.L	(A1),D0-D7
	MOVE.L	SCR1,A0
	LEA	160*50(A0),A0
	JSR	8*4(A1)
	MOVE.L	SCR2,A0
	LEA	160*50(A0),A0
	JSR	8*4(A1)
	
	LEA	TAB_D+4,A1
	MOVE.L	A1,MOD_A1
	MOVE.L	#VBL_IR,$70.W
	MOVE.L	#TB0_IR,$120.W

	move.l	#NB_POS-10-1,d7
ici2	JSR	GENERATION
	ADDQ.L	#1,CNT_POS
	CMPI.L	#53,CNT_POS
	BEQ.S	OUT_2
	DBF	D7,ici2
OUT_2
	MOVE.L	#TIMER,$120.W
	MOVE.L	#VBL_DEB,$70.W
	MOVE	#6,MOD_PLAN
	JSR	AFF_CADRE
	CLR.L	$FFFF8240.W
	CLR.L	$FFFF8244.W
	CLR.L	$FFFF8248.W
	CLR.L	$FFFF824C.W
	MOVE.L	#$00020002,$FFFF8250.W
	CLR.L	$FFFF8254.W
	CLR.L	$FFFF8258.W
	CLR.L	$FFFF825C.W
	LEA	BUFCOD2,A0
	MOVE.L	A0,SAVEA0

	move.l	#NB_POS-53-1,d7
ici3	JSR	GENERATION
;	ADDQ.L	#1,CNT_POS
;	CMPI.L	#53,CNT_POS
;	BEQ.S	OUT_2
	DBF	D7,ici3
;OUT_2
WAIT_VBL_	TST.L	$140.W
	BEQ.S	WAIT_VBL_
	CLR.L	$140.W
	ADDQ.L	#1,PETIT_CNT
	CMPI.L	#120,PETIT_CNT
	BNE.S	WAIT_VBL_
	MOVE.L	#VBL_ZIK,$70.W
	MOVE.L	SCREEN1,A0
	MOVEQ	#2,D0
	JSR	EFF_FACILE
	MOVE.L	SCREEN1,A0
	MOVEQ	#0,D0
	JSR	EFF_FACILE

	MOVE	#$002,$FFFF8244.W
	MOVE	#$002,$FFFF8246.W
	MOVE	#$002,$FFFF824C.W
	MOVE	#$002,$FFFF824E.W
	MOVE	#$002,$FFFF8254.W
	MOVE	#$002,$FFFF8256.W
	MOVE	#$002,$FFFF825C.W
	MOVE	#$002,$FFFF825E.W
	MOVE	#2,MOD_PLAN
	JSR	AFF_CADRE
	MOVE.L	SCREEN1,A0
	MOVEQ	#6,D0
	JSR	EFF_FACILE
	MOVEQ	#0,D0
	MOVE	D0,$FFFF8242.W
	MOVE	D0,$FFFF8248.W
	MOVE	D0,$FFFF824A.W
	MOVE	D0,$FFFF8250.W
	MOVE	D0,$FFFF8252.W
	MOVE	D0,$FFFF8258.W
	MOVE	D0,$FFFF825A.W
	
	BRA.S	JP2
PETIT_CNT	DC.L	0
JP2	

;0001	42	777
;0010	44	007
;0011	46	007
;0100	48
;0101	4A
;0110	4C
;0111	4E
;1000	50	777
;1001	52
;1010	54	007
;1011	56
;1100	58
;1101	5A
;1110	5C
;1111	5E
***********
AGAIN	;JSR	EFF
	CLR.L	T_I_M_E
	CLR.L	T_I_M_E2
	CLR.L	T_I_M_E2D
	CLR.L	T_I_M_ERY
	CLR	MOD_COUL
	CLR	MOD_COUL2
	CLR	MOD_COULB
	CLR	MOD_COUL2B
	MOVE.L	SCREEN1,A0
	LEA	TABLE,A1
	MOVE.L	(A1),A1
	MOVEM.L	(A1),D0-D7
	JSR	8*4(A1)
;	MOVE	#2,MOD_PLAN
;	JSR	AFF_CADRE

PAUSE	JSR	VSYNC
	ADDQ.L	#1,T_I_M_E2
T_I_M_E2	EQU	*+2
	MOVE.L	#0,D0
	CMPI.L	#50*2,D0
	BNE.S	PAUSE
*FAD1
FAD1T	JSR	VSYNC
	LEA	FAD_IN_,A0
MOD_COUL	EQU	*+2
	LEA	0(A0),A0
	CMPI	#-1,(A0)
	BEQ	END_FAD
	MOVE	(A0),D0
	MOVE	D0,$FFFF8242.W
	MOVE	D0,$FFFF8248.W
	MOVE	D0,$FFFF824A.W
	MOVE	D0,$FFFF8250.W
	MOVE	D0,$FFFF8252.W
	MOVE	D0,$FFFF8258.W
	MOVE	D0,$FFFF825A.W
	ADDQ	#1,MOD_COUL2
MOD_COUL2	EQU	*+2
	MOVE	#0,D0
	CMPI	#4,D0
	BNE.S	NON
	CLR	MOD_COUL2
	ADDQ	#2,MOD_COUL
NON	BRA	FAD1T
*ENDFAD
END_FAD	

PAUSED	JSR	VSYNC
	ADDQ.L	#1,T_I_M_E2D
T_I_M_E2D	EQU	*+2
	MOVE.L	#0,D0
	CMPI.L	#50*2,D0
	BNE.S	PAUSED
	
	MOVE	#2,MOD_PLAN2
	JSR	EFF_PLAN2

	MOVE.L	#TABLE+4,MOD_ADR_3D
VBL
	MOVE.L	SCREEN1,A0
MOD_ADR_3D	EQU	*+2
	LEA	MOD_ADR_3D,A1
	MOVE.L	(A1)+,A2
	TST.L	-4(A1)
	BNE	ROUL
	MOVE	#2,MOD_PLAN
	JSR	AFF_CADRE
NOT_SO_EASYH	JSR	VSYNC
	ADDQ.L	#1,T_I_M_ERY
T_I_M_ERY	EQU	*+2
	MOVE.L	#0,D0
	CMPI.L	#50*2,D0
	BNE.S	NOT_SO_EASYH
*FAD2
FAD2T	JSR	VSYNC
	LEA	FAD_OUT_,A0
MOD_COULB	EQU	*+2
	LEA	0(A0),A0
	CMPI	#-1,(A0)
	BEQ	END_FADB
	MOVE	(A0),D0
	MOVE	D0,$FFFF8242.W
	MOVE	D0,$FFFF8248.W
	MOVE	D0,$FFFF824A.W
	MOVE	D0,$FFFF8250.W
	MOVE	D0,$FFFF8252.W
	MOVE	D0,$FFFF8258.W
	MOVE	D0,$FFFF825A.W
	ADDQ	#1,MOD_COUL2B
MOD_COUL2B	EQU	*+2
	MOVE	#0,D0
	CMPI	#4,D0
	BNE.S	NONB
	CLR	MOD_COUL2B
	ADDQ	#2,MOD_COULB
NONB	BRA	FAD2T
*ENDFAD
END_FADB

NOT_SO_EASY	JSR	VSYNC
	ADDQ.L	#1,T_I_M_E
T_I_M_E	EQU	*+2
	MOVE.L	#0,D0
	CMPI.L	#50,D0
	BNE.S	NOT_SO_EASY
;	LEA	TABLE+4,A1
;	MOVE.L	(A1)+,A2
	BRA	AGAIN
ROUL	MOVEM.L	(A2),D0-D7
	JSR	8*4(A2)
	MOVE.L	A1,MOD_ADR_3D
	SF	$FFFF8240.W
	JSR	VSYNC
	BRA	VBL

VSYNC	TST.L	$140.W
	BEQ.S	VSYNC
	CLR.L	$140.W
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTS

AFF_CADRE	MOVE.L	SCREEN1,A0
MOD_PLAN	EQU	*+2
	LEA	0(A0),A0
	LEA	192*160(A0),A1
	MOVE	#7,D1
.DO_Y	MOVE	#19,D0
.DO_X	MOVE	#$FFFF,(A0)
	MOVE	#$FFFF,(A1)
	ADDQ.L	#8,A0
	ADDQ.L	#8,A1
	DBF	D0,.DO_X
	DBF	D1,.DO_Y

	MOVE.L	SCREEN1,A0
	ADD	MOD_PLAN,A0
	MOVE	#199,D0
.DO_PIX	MOVE.B	#$FF,(A0)
	MOVE.B	#$FF,153(A0)
	LEA	160(A0),A0
	DBF	D0,.DO_PIX
	RTS

EFF_PLAN2	MOVE.L	SCREEN1,A0
MOD_PLAN2	EQU	*+2
	LEA	0(A0),A0

	LEA	192*160(A0),A1
	MOVE	#7,D1
.DO_Y_	MOVE	#19,D0
.DO_X_	CLR	(A0)
	CLR	(A1)
	ADDQ.L	#8,A0
	ADDQ.L	#8,A1
	DBF	D0,.DO_X_
	DBF	D1,.DO_Y_

	MOVE.L	SCREEN1,A0
	ADD	MOD_PLAN,A0
	MOVE	#199,D0
.DO_PIX_	CLR.B	(A0)
	CLR.B	153(A0)
	LEA	160(A0),A0
	DBF	D0,.DO_PIX_
	RTS
EFF_FACILE
;A0=SCREEN A EFFACER
;D0=PLAN
	ADDA.L	D0,A0
	MOVE	#(20*200)-1,D0
.EF1_	CLR	(A0)
	ADDQ.L	#8,A0
	DBF	D0,.EF1_
	RTS
EFF	move.l	SCREEN1,a0
	MOVE	#3999,D0
.HA	CLR	(A0)
	ADDQ	#8,A0
	DBF	D0,.HA
	RTS
GENERATION	CMPI.L	#(NB_POS)-20-1,D7
	BNE.S	.CA_MARCHE
	CLR.L	FLAG2
.CA_MARCHE	CLR.L	POINTEUR
	LEA	DATA_X,A1
	jsr	trigo
	ADD.L	#2,POINTEUR2
	CLR.L	FLAG_PTS
	MOVE	#NB_PTS-1,D6
.DO_PTS	jsr	sp_to_pl
	jsr	ROUTINE
	DBF	D6,.DO_PTS
	MOVEM.L	D0-A6,-(SP)
	movem.l	REG_GEN,D0-A6
GEN
	JSR	EFF
	MOVE	#SEG-1,D6
.DO_SEG	MOVE.L	D6,-(SP)
	bsr	poly
	MOVE.L	(SP)+,D6
	DBF	D6,.DO_SEG

	MOVE	#3999,D0
	MOVE.L	SCREEN1,A0
	LEA	BUF2,A1
	LEA	BUF1,A2
.KL	MOVE	(A1),(A2)+
	MOVE	(A0),(A1)+
	ADDQ.L	#8,A0
	dbf	d0,.KL

	JSR	GENERE1
	MOVEQ	#0,D0
	MOVEQ	#0,D3
	LEA	DATAS,A0
	LEA	FINAL,A1
	SUBQ.L	#8,A0
.LOOPED	ADDQ.L	#8,A0
	MOVE.L	(A0),D2
	MOVE.L	4(A0),D1
	TST.L	(A0)
	BEQ.S	.OKVAZY
	CMP.L	D0,D1
	BLE.S	.LOOPED
	MOVE.L	D1,D0
	MOVE.L	D2,D4
;	MOVE.L	#1,4(A0)
	BRA.S	.LOOPED

.OKVAZY	
	LEA	DATAS,A0	;
	SUBQ.L	#8,A0
.BOUI	ADDQ.L	#8,A0	;
	CMP.L	#DATAS+8000,A0
	BEQ	.LP

	CMP.L	(A0),D4	;
	BNE.S	.BOUI	;
	CLR.L	4(A0)	;

	MOVE.L	D4,(A1)+
	MOVE.L	D0,(A1)+
	CMPI.L	#7,D3
	BEQ.S	.LP
	ADDQ.L	#1,D3
	MOVEQ	#0,D0
	LEA	DATAS,A0
	SUBQ.L	#8,A0
	BRA.S	.LOOPED
	
.LP	LEA	DATAS,A0
	MOVE	#1999,D0
P	CLR.L	(A0)+
	DBF	D0,P
	JSR	GENERE2

	MOVEM.L	D0-A6,REG_GEN
	MOVEM.L	(SP)+,D0-A6
	CLR.L	PNT

	ADD	#2*PAS,x_angle
;	ADD	#2*PAS,y_angle
;	ADD	#2*PAS,z_angle

ADR_TABL	EQU	*+2
	LEA	ADR_TABL,A1
	MOVE	(A1)+,D0
	MOVE	#100,MOD_Y
	SUB	D0,MOD_Y
	MOVE	#0,MOD_Z
	SUB	D0,MOD_Z
	MOVE.L	A1,ADR_TABL
	RTS	
SAVED0	DS.L	1
SAVEA0	DS.L	1
SAVEA4	DS.L	1
GENERE1
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVE.L	#-16+6,D7	OFFSET
	LEA	BUF1,A1
	LEA	BUF2,A2
	LEA	DATAS,A6
	
LOOP	ADD.L	#16,D7
	CMPI	#32000,D7
	BGT	FINI
	MOVE	(A1)+,D0
	MOVE	(A1)+,D1
	MOVE	(A2)+,D2
	MOVE	(A2)+,D3

	CMP	D0,D2
	BEQ.S	LOOP
	CMP	D1,D3
	BEQ.S	LOOP
	TST	D3
	BEQ.S	PLAN2_VIDE
MOVEL	
	MOVE	D2,D5
	SWAP	D5
	MOVE	D3,D5
	JSR	DO_IT
	BRA.S	LOOP
PLAN2_VIDE	TST	D2
	BNE.S	MOVEL
	BRA.S	LOOP

FINI	RTS

GENERE2
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D6
	MOVE.L	#-16+6,D7	OFFSET
	LEA	BUF1,A1
	LEA	BUF2,A2
	LEA	FINAL,A6
	MOVE.L	SAVEA0,A0
	MOVE.L	SAVEA4,A4
	MOVE.L	A0,(A4)+
	MOVE.L	A4,SAVEA4
	REPT	8
	MOVE.L	(A6)+,(A0)+
	MOVE.L	(A6)+,D0
	ENDR
	MOVEQ	#0,D0
WLOOP	ADD.L	#16,D7
	CMPI	#32000,D7
	BGT	WFINI
	MOVE	(A1)+,D0
	MOVE	(A1)+,D1
	MOVE	(A2)+,D2
	MOVE	(A2)+,D3

	CMP	D0,D2
	BEQ	WSAME_MOT1
	CMP	D1,D3
	BEQ.S	WSAME_MOT2
	TST	D3
	BEQ.S	WPLAN2_VIDE
WMOVEL	

	MOVE.B	#$40,D6
	LEA	FINAL,A6
	MOVE	D2,D5
	SWAP	D5
	MOVE	D3,D5
	SUBQ.L	#8,A6
TROUVE	ADDQ.L	#8,A6
	CMP.L	(A6),D5
	BEQ.S	C_LE_MEME
	ADDQ.B	#1,D6
	CMPI.B	#$48,D6
	BEQ.S	PADFRQ
	BRA.S	TROUVE
C_LE_MEME	MOVE.B	#$21,(A0)+
	MOVE.B	D6,(A0)+
	MOVE	D7,(A0)+
	BRA.S	WLOOP
PADFRQ	
	MOVE	#$217C,(A0)+
	MOVE	D2,(A0)+
	MOVE	D3,(A0)+
	MOVE	D7,(A0)+
	BRA.S	WLOOP
WPLAN2_VIDE	TST	D2
	BNE.S	WMOVEL
	MOVE	#$42A8,(A0)+
	MOVE	D7,(A0)+
	BRA.S	WLOOP

WSAME_MOT2	TST	D2
	BNE.S	WMOVE_PLAN1
WCLR_PLAN1	MOVE	#$4268,(A0)+
	MOVE	D7,(A0)+
	BRA.S	WLOOP
WMOVE_PLAN1	
	MOVE.B	#$40,D6
	LEA	FINAL,A6
	MOVE	D2,D5
	SUBQ.L	#8,A6
TROUVE2	ADDQ.L	#8,A6
	CMP	2(A6),D5
	BEQ.S	C_LE_MEME2
	ADDQ.B	#1,D6
	CMPI.B	#$48,D6
	BEQ.S	PADFRQ2
	BRA.S	TROUVE2
C_LE_MEME2	MOVE.B	#$31,(A0)+
	MOVE.B	D6,(A0)+
	MOVE	D7,(A0)+
	BRA	WLOOP
PADFRQ2

	MOVE	#$317C,(A0)+
	MOVE	D2,(A0)+
	MOVE	D7,(A0)+
	BRA	WLOOP

WSAME_MOT1	CMP	D1,D3
	BEQ	WLOOP
	TST	D3
	BNE.S	WMOVE_PLAN2
WCLR_PLAN2	MOVE	#$4268,(A0)+
	MOVE	D7,(A0)
	ADDQ	#2,(A0)+
	BRA	WLOOP
WMOVE_PLAN2	
	MOVE.B	#$40,D6
	LEA	FINAL,A6
	MOVE	D3,D5
	SUBQ.L	#8,A6
TROUVE3	ADDQ.L	#8,A6
	CMP	2(A6),D5
	BEQ.S	C_LE_MEME3
	ADDQ.B	#1,D6
	CMPI.B	#$48,D6
	BEQ.S	PADFRQ3
	BRA.S	TROUVE3
C_LE_MEME3	MOVE.B	#$31,(A0)+
	MOVE.B	D6,(A0)+
	MOVE	D7,(A0)
	ADDQ	#2,(A0)+
	BRA	WLOOP
PADFRQ3


	MOVE	#$317C,(A0)+
	MOVE	D3,(A0)+
	MOVE	D7,(A0)
	ADDQ	#2,(A0)+
	BRA	WLOOP
	
	
WFINI	MOVE	#$4E75,(A0)+
	MOVE.L	A0,SAVEA0
	RTS

DO_IT	MOVEM.L	D0-A6,-(SP)
	LEA	DATAS,A0
HERE	TST.L	(A0)
	BEQ.S	OKEY
	CMP.L	(A0),D5
	BEQ.S	IDEM
	ADDQ.L	#8,A0
	BRA.S	HERE
IDEM	ADDQ.L	#4,A0
	ADDQ.L	#1,(A0)+
	BRA.S	HER
	
OKEY	MOVE.L	D5,(A0)+
	MOVE.L	#1,(A0)+
HER	MOVEM.L	(SP)+,D0-A6
	RTS

trigo	movem.l d0-d7/a4-a6,-(a7)

	lea cos_tab,a4
	lea sin_tab,a5
	lea x_angle,a6
	MOVE (a6)+,d2
	MOVE (a6)+,d1
	MOVE (a6)+,d0
	MOVE 0(a5,d2.w),d5
	MOVE 0(a4,d2.w),d4
	MOVE 0(a5,d1.w),d3
	MOVE 0(a4,d1.w),d2
	MOVE 0(a5,d0.w),d1
	MOVE 0(a4,d0.w),d0
	lea var_3d,a6
	moveq #9,d7
sp_c	MOVE d3,d6
	NEG d6
	MOVE d6,(a6)+
sp_f	MOVE d2,d6
	muls d5,d6
	asr.l d7,d6
	MOVE d6,(a6)+
sp_i	MOVE d2,d6
	muls d4,d6
	asr.l d7,d6
	MOVE d6,(a6)+
sp_a	MOVE d0,d6
	muls d2,d6
	asr.l d7,d6
	MOVE d6,(a6)+
sp_d	MOVE d1,d6
	muls d4,d6
	asr.l d7,d6
	movea.w d6,a4
	MOVE d0,d6
	muls d3,d6
	asr.l d7,d6
	muls d5,d6
	asr.l d7,d6
	sub.w a4,d6
	MOVE d6,(a6)+	
sp_g	MOVE d1,d6
	muls d5,d6
	asr.l d7,d6
	movea.w d6,a4
	MOVE d0,d6
	muls d3,d6
	asr.l d7,d6
	muls d4,d6
	asr.l d7,d6
	ADD a4,d6
	MOVE d6,(a6)+	
sp_b	MOVE d1,d6
	muls d2,d6
	asr.l d7,d6
	MOVE d6,(a6)+
sp_e	MOVE d0,d6
	muls d4,d6
	asr.l d7,d6
	movea.w d6,a4
	MOVE d1,d6
	muls d3,d6
	asr.l d7,d6
	muls d5,d6
	asr.l d7,d6
	ADD a4,d6
	MOVE d6,(a6)+	
sp_h	MOVE d0,d6
	muls d5,d6
	asr.l d7,d6
	movea.w d6,a4
	MOVE d1,d6
	muls d3,d6
	asr.l d7,d6
	muls d4,d6
	asr.l d7,d6
	sub.w a4,d6
	MOVE d6,(a6)+	
	movem.l (a7)+,d0-d7/a4-a6
	rts

sp_to_pl movem.l d0-d7/a1-a6,-(a7)
	lea 	points,a6
	ADDA.L	POINTEUR,A6
	MOVE	(A6)+,D0
	MOVE	(A6)+,D1
	MOVE	(A6)+,D2
	ADD.L	#6,POINTEUR
	SUB	#160,D0
	SUB	#100,D1
	
	lea depla,a3
	moveq	#0,d3
	moveq	#0,d4
MOD_Z	EQU	*+2
	MOVE	#0,D5

;	move	d5,d4
;	add	d2,d4
;	cmpi.l	#255,d4
;	bgt.s	oh_no
;	bra.s	oh_yes
;	
;oh_no	sub	#255,D5
;oh_yes	clr.l	d4
	
	ext.l d3
	ext.l d4
	ext.l d5
	moveq #9,d6
	asl.l d6,d3
	asl.l d6,d4
	asl.l d6,d5
	movea.l d3,a1	
	movea.l d4,a2
	movea.l d5,a3
	lea var_3d,a6
	MOVE d0,d3
	MOVE d1,d4
	MOVE d2,d5
	muls (a6)+,d3
	muls (a6)+,d4
	muls (a6)+,d5
	add.l d4,d5
	add.l d3,d5
	add.l a3,d5
	move.l d5,a5
	divs z_pt_fuite,d5
	MOVE #512,d7
	sub.w d5,d7
	MOVE d0,d3
	MOVE d1,d4
	MOVE d2,d5
	muls (a6)+,d3
	muls (a6)+,d4
	muls (a6)+,d5
	add.l d4,d5
	add.l d3,d5
	add.l a1,d5
	move.l d5,a4
	divs d7,d5
	muls (a6)+,d0
	muls (a6)+,d1
	muls (a6)+,d2
	add.l d0,d1
	add.l d1,d2
	add.l a2,d2
	move.l d2,d6
	divs d7,d2
	lea xp,a6
	ADD #160,d5
	MOVE d5,(a6)+
MOD_Y	EQU	*+2
	ADD #100,d2
	MOVE d2,(a6)+
	move.l a4,d5
	move.l a5,d7
	moveq #9,d0
	asr.l d0,d5
	asr.l d0,d6
	asr.l d0,d7
	MOVE d5,(a6)+
	MOVE d6,(a6)+
	MOVE d7,(a6)+
	MOVE	D7,ZA	
	movem.l (a7)+,d0-d7/a1-a6
	rts

ROUTINE
	MOVE	xp,(A1)+
	MOVE	yp,(A1)+
	RTS

	
FLAG2	DC.L	1
poly:
	ADDQ.L	#1,FLAG_PTS
	CMPI.L	#(136/4),FLAG_PTS
	BLE.S	AFF_TRUC
	TST.L	FLAG2
	BEQ.S	AFF_TRUC
	RTS
AFF_TRUC	JSR	LINE_A_INIT
	
	LEA	DATA_X,A5
	ADDA.L	PNT,A5
	CMPI	#$9999,(A5)
	BNE.S	OKAY
	CLR.L	PNT
	LEA	DATA_X,A5
	BRA.S	OKAY2
OKAY	ADD.L	#4*4,PNT
OKAY2	
	moveq	#0,d3

	moveq	#0,d4
	moveq	#0,d5
	moveq	#0,d6
	moveq	#0,d7
	move	2(a5),d4
	move	6(a5),d5
	move	10(a5),d6
	move	14(a5),d7

	cmp	d4,d5
	bgt	d5_pg
	move	d4,d5
d5_pg	
	cmp	d6,d5
	bgt	d5_pg2
	move	d6,d5
d5_pg2	
	cmp	d7,d5
	bgt	d5_pg3
	move	d7,d5
d5_pg3	addq	#1,d5
	move	d5,ymax

	moveq	#0,d3

	moveq	#0,d4
	moveq	#0,d5
	moveq	#0,d6
	moveq	#0,d7
	move	2(a5),d4
	move	6(a5),d5
	move	10(a5),d6
	move	14(a5),d7

	cmp	d4,d5
	blt	d5_pgh
	move	d4,d5
d5_pgh
	cmp	d6,d5
	blt	d5_pg2h
	move	d6,d5
d5_pg2h	
	cmp	d7,d5
	blt	d5_pg3h
	move	d7,d5
d5_pg3h	subq	#1,d5
	move	d5,ymin
	
;	LEA	TAB,A5
	LEA	_PTSIN,A6
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	LEA	-4*4(A5),A5
	MOVE.L	(A5)+,(A6)+
	
	MOVE	ymin,D3
LOOP1	MOVE	D3,40(A0)
	MOVE.L	A0,-(SP)
;	DC	POLYGON
	cmp	#0,d3
	blt.s	saut
	cmp	#200,d3
	bge.s	saut
	JSR	LINE_A_POLYGON
saut	
	MOVE.L	(SP)+,A0
	ADDQ	#1,D3
	CMP	ymax,D3
	BNE	LOOP1

	RTS
ymin	ds	1
ymax	ds	1
LINE_A_INIT
	LEA	EMUL_LINEA,A0
	MOVE.L	A0,D0
	RTS

LINE_A_POLYGON
	MOVEM.L	D3-D7/A3-A5,-(SP)
	JSR	_FCA0D6
	MOVEM.L	(SP)+,D3-D7/A3-A5
	RTS

_FCA0D6
	MOVEQ	#3,D0
	
	LEA	_PTSIN,A0
	LEA	BUF16DA,A1
	CLR	PNT_261E
	
LFCA0F4	MOVE	6(A0),D1
	MOVE	2(A0),D2
	SUB	D2,D1
	BEQ.S	LFCA13E
	MOVE	EMUL_LINEA-$299A+$29C2,D3
	MOVE	D3,D4
	SUB	D2,D4
	SUB	6(A0),D3
	MOVE	D4,D2
	EOR	D3,D2
	BPL.S	LFCA13E
	MOVE	4(A0),D2
	SUB	0(A0),D2
	ADD	D2,D2
	MULS	D2,D4
	DIVS	D1,D4
	BPL.S	LFCA12E
	NEG	D4
	ADDQ	#1,D4
	ASR	#1,D4
	NEG	D4
	BRA.S	LFCA132
LFCA12E	ADDQ	#1,D4
	ASR	#1,D4
LFCA132	ADD	0(A0),D4
	MOVE	D4,(A1)+
	ADDQ	#1,PNT_261E
	
LFCA13E	LEA	4(A0),A0
	DBF	D0,LFCA0F4
	MOVE	PNT_261E,D0
	BNE.S	LFCA150
	RTS
LFCA150	
	LEA	BUF16DA,A0
	BSR	LFCA1DC
	MOVE	PNT_261E,D0
	ASR	#1,D0
	SUBQ	#1,D0
	TST	EMUL_LINEA+$29D0-$299A
	BNE.S	LFCA18A
LFCA16C	
	MOVE	(A1)+,EMUL_LINEA+$29C0-$299A
	MOVE	(A1)+,EMUL_LINEA+$29C4-$299A
	MOVE	D0,-(A7)
	MOVE.L	A1,-(A7)
	BSR	_FCA5F6
	MOVEA.L	(A7)+,A1
	MOVE	(A7)+,D0
	DBF	D0,LFCA16C
	RTS
LFCA18A	
	MOVE	(A1)+,EMUL_LINEA+$29C0-$299A
	MOVE	(A1)+,EMUL_LINEA+$29C4-$299A

	MOVE	EMUL_LINEA+$29D2-$299A,D1
	MOVE	EMUL_LINEA+$29C0-$299A,D2
	MOVE	EMUL_LINEA+$29C4-$299A,D3
	CMP	D1,D2
	BGE.S	LFCA1B6
	CMP	D1,D3
	BLT.S	LFCA1D6
	MOVE	D1,EMUL_LINEA+$29C0-$299A
LFCA1B6	
	MOVE	EMUL_LINEA+$29D6-$299A,D1
	CMP	D1,D3
	BLE.S	LFCA1CA
	CMP	D1,D2
	BGT.S	LFCA1D6
	MOVE	D1,EMUL_LINEA+$29C4-$299A
LFCA1CA	MOVE	D0,-(A7)
	MOVE.L	A1,-(A7)
	BSR	_FCA5F6
	MOVEA.L	(A7)+,A1
	MOVE	(A7)+,D0
LFCA1D6	DBF	D0,LFCA18A
	RTS
LFCA1DC	SUBQ	#2,D0
	BCS.S	LFCA1FC
	MOVE	D0,D1
	MOVEA.L	A0,A1
LFCA1E4	MOVE	D1,D0
	MOVEA.L	A1,A0
LFCA1E8	MOVE	(A0)+,D2
	CMP	(A0),D2
	BLE.S	LFCA1F4
	MOVE	(A0),-2(A0)
	MOVE	D2,(A0)
LFCA1F4	DBF	D0,LFCA1E8
	DBF	D1,LFCA1E4
LFCA1FC	RTS
	MOVEQ	#1,D1
	MOVE	6(A7),D0
	MULS	4(A7),D0
	BGE.S	LFCA20C
	NEG	D1
LFCA20C	MOVE	8(A7),D2
	DIVS	D2,D0
	TST	D2
	BGE.S	LFCA21A
	NEG	D1
	NEG	D2
LFCA21A	MOVE.L	D3,-(A7)
	MOVE.L	D0,D3
	BGE.S	LFCA222
	NEG.L	D3
LFCA222	SWAP	D3
	ADD	D3,D3
	CMP	D2,D3
	BLT.S	LFCA22C
	ADD	D1,D0
LFCA22C	MOVE.L	(A7)+,D3
	RTS
	MOVE	D2,-(A7)
	MOVE	D3,-(A7)
	MULS	#160,D1

	MOVE	EMUL_LINEA,D3
	MOVE.B	_FCA259(PC,D3.W),D3
	MOVE	D0,D2
	EXT.L	D2
	AND	#$FFF0,D2
	ASR	D3,D2
	ADD.L	D2,D1
	AND	#$F,D0
	MOVE	(A7)+,D3
	MOVE	(A7)+,D2
	RTS
_FCA259	DC.L	$03020001
	DC.L	$00000000

_FCA5D4	DC	$FFFF
_FCA5D6	DC.L	$7FFF3FFF
	DC.L	$1FFF0FFF
	DC.L	$07FF03FF
	DC.L	$01FF00FF
	DC.L	$007F003F
	DC.L	$001F000F
	DC.L	$00070003
	DC.L	$00010000

_FD1B86	DC.L	$00CC0130
	DC.L	$009C014A
	DC.L	$4E56FFEC

_FCA5F6
	LEA	EMUL_LINEA,A4
	MOVEM	$26(A4),D4-D6
	MOVE	D5,D0
	AND	$32(A4),D0
	ADD	D0,D0
	MOVEA.L	$2E(A4),A0
	ADDA	D0,A0
	TST	$34(A4)
	SNE	D0
	AND	#$20,D0
	MOVE	D4,D1
	ASR	#4,D1
	MOVE	D6,D2
	ASR	#4,D2
	MOVEQ	#$F,D3
	AND	D3,D4
	ADD	D4,D4
	MOVE	_FCA5D4(PC,D4.W),D4
	AND	D3,D6
	ADD	D6,D6
	MOVE	_FCA5D6(PC,D6.W),D6
	NOT	D6
	SUB	D1,D2
	BNE.S	LFCA63C
	AND	D6,D4
LFCA63C	MOVEA.L	$9A(A4),A5
LFD1B58
	MOVEA.L	SCREEN1,A1
	MULS	#160,D5
	ADDA.L	D5,A1
	MOVEA	D0,A2

	MOVE	#4,D7
	MOVE	D7,D0
	ADD	D0,D0
	MULS	D0,D1
	ADDA.L	D1,A1
	MOVEQ	#0,D7
	
	LEA	$18(A4),A3
	MOVE	$24(A4),D1
	ADD	D1,D1
TST	LEA	_FD1B86(PC,D1.W),A5
	ADDA	(A5),A5

LFD1C52

	NOT	D4
	NOT	D6
LFD1C56	MOVEA.L	A1,A4
	MOVE	D2,D5
	MOVE	(A0),D3
	ADDA	A2,A0
	TST	(A3)+
	BNE.S	LFD1C88
	MOVE	D4,D1
	AND	D1,(A1)
	ADDQ.L	#8,A1
	SUBQ	#1,D5
	BCS.S	LFD1C7E
	SUBQ	#1,D5
	BCS.S	LFD1C78
	CLR	D1
LFD1C70	ADDA	D0,A4
	MOVE	D1,(A4)
	DBF	D5,LFD1C70
LFD1C78	MOVE	D6,D1
	ADDA	D0,A4
	AND	D1,(A4)
LFD1C7E	DBF	D7,LFD1C56
	NOT	D4
	NOT	D6
	RTS

LFD1C88	MOVE	(A4),D1
	EOR	D3,D1
	AND	D4,D1
	EOR	D3,D1
	MOVE	D1,(A1)
	ADDQ.L	#8,A1
	SUBQ	#1,D5
	BCS.S	LFD1CAE
	SUBQ	#1,D5
	BCS.S	LFD1CA2
LFD1C9A	ADDA	D0,A4
	MOVE	D3,(A4)
	DBF	D5,LFD1C9A
LFD1CA2	ADDA	D0,A4
	MOVE	(A4),D1
	EOR	D3,D1
	AND	D6,D1
	EOR	D3,D1
	MOVE	D1,(A4)
LFD1CAE	DBF	D7,LFD1C56
	NOT	D4
	NOT	D6
	RTS

FILL	
	DC.L	-1
	DC.L	-1
	DC.L	-1
	DC.L	-1
	
TAB	
	DC	50,70
	DC	12,175
	DC	120,190
	DC	410,150

	DC	50,70

cos_tab	incbin '\COSIN.DAT'	;tables sinus cosinus
sin_tab	equ cos_tab+720

points	
*LETSGO
 DC 0,0,0
 DC 0,199,0
 DC 6,199,0
 DC 6,0,0
 
 DC 0,0,0
 DC 0,6,0
 DC 320,6,0
 DC 320,0,0

 DC 314,0,0
 DC 314,199,0
 DC 320,199,0
 DC 320,0,0
 
 DC 0,193,0
 DC 0,199,0
 DC 320,199,0
 DC 320,193,0
 
 DC 16,16,0
 DC 16,96,0
 DC 48,96,0
 DC 48,16,0
 DC 32,72,0
 DC 32,96,0
 DC 80,96,0
 DC 80,72,0
 DC 88,16,0
 DC 88,96,0
 DC 120,96,0
 DC 120,16,0
 DC 112,16,0
 DC 112,40,0
 DC 144,40,0
 DC 144,16,0
 DC 112,72,0
 DC 112,96,0
 DC 144,96,0
 DC 144,72,0
 DC 112,48,0
 DC 112,64,0
 DC 128,64,0
 DC 128,48,0
 DC 152,16,0
 DC 152,40,0
 DC 208,40,0
 DC 208,16,0
 DC 168,32,0
 DC 168,96,0
 DC 192,96,0
 DC 192,32,0
 DC 248,16,0
 DC 248,40,0
 DC 304,40,0
 DC 304,16,0
 DC 216,72,0
 DC 216,96,0
 DC 272,96,0
 DC 272,72,0
 DC 240,32,0
 DC 240,80,0
 DC 280,80,0
 DC 280,32,0
 DC 248,16,0
 DC 240,32,0
 DC 248,40,0
 DC 264,24,0
 DC 256,72,0
 DC 272,96,0
 DC 280,80,0
 DC 272,64,0
 DC 216,16,0
 DC 216,48,0
 DC 232,16,0
 DC 224,16,0
 DC 88,104,0
 DC 32,104,0
 DC 32,128,0
 DC 88,128,0
 DC 32,160,0
 DC 32,184,0
 DC 88,184,0
 DC 88,160,0
 DC 32,104,0
 DC 16,128,0
 DC 16,160,0
 DC 32,184,0
 DC 88,136,0
 DC 56,136,0
 DC 56,168,0
 DC 88,168,0
 DC 40,136,0
 DC 40,152,0
 DC 72,152,0
 DC 72,136,0
 DC 104,112,0
 DC 104,176,0
 DC 128,176,0
 DC 128,112,0
 DC 152,112,0
 DC 152,176,0
 DC 176,176,0
 DC 176,112,0
 DC 120,104,0
 DC 120,120,0
 DC 160,120,0
 DC 160,104,0
 DC 120,168,0
 DC 120,184,0
 DC 160,184,0
 DC 160,168,0
 DC 120,104,0
 DC 104,112,0
 DC 112,120,0
 DC 128,112,0
 DC 160,104,0
 DC 160,112,0
 DC 168,120,0
 DC 176,112,0
 DC 104,176,0
 DC 120,184,0
 DC 128,176,0
 DC 112,168,0
 DC 160,184,0
 DC 176,176,0
 DC 168,168,0
 DC 152,176,0
 DC 184,168,0
 DC 184,184,0
 DC 200,184,0
 DC 200,168,0
 DC 208,168,0
 DC 208,184,0
 DC 224,184,0
 DC 224,168,0
 DC 232,168,0
 DC 232,184,0
 DC 248,184,0
 DC 248,168,0
********
 DC 0,200,-0
 DC 0,200,-6
 DC 320,200,-6
 DC 320,200,-0
 
 DC 0,200,-0
 DC 0,200,-199
 DC 6,200,-199
 DC 6,200,-0
 
 DC 0,200,-193
 DC 0,200,-199
 DC 320,200,-199
 DC 320,200,-193
 
 DC 314,200,-0
 DC 314,200,-199
 DC 320,200,-199
 DC 320,200,-0
 
 DC 16,200,-48
 DC 16,200,-56
 DC 24,200,-56
 DC 24,200,-48
 DC 32,200,-48
 DC 32,200,-56
 DC 40,200,-56
 DC 40,200,-48
 DC 48,200,-48
 DC 48,200,-56
 DC 56,200,-56
 DC 56,200,-48
 DC 64,200,-16
 DC 64,200,-32
 DC 112,200,-32
 DC 112,200,-16
 DC 80,200,-24
 DC 80,200,-56
 DC 96,200,-56
 DC 96,200,-24
 DC 120,200,-16
 DC 120,200,-56
 DC 136,200,-56
 DC 136,200,-16
 DC 152,200,-16
 DC 152,200,-56
 DC 168,200,-56
 DC 168,200,-16
 DC 128,200,-16
 DC 128,200,-24
 DC 160,200,-24
 DC 160,200,-16
 DC 128,200,-48
 DC 128,200,-56
 DC 160,200,-56
 DC 160,200,-48
 DC 144,200,-64
 DC 144,200,-80
 DC 192,200,-80
 DC 192,200,-64
 DC 160,200,-72
 DC 160,200,-104
 DC 176,200,-104
 DC 176,200,-72
 DC 200,200,-64
 DC 200,200,-104
 DC 216,200,-104
 DC 216,200,-64
 DC 224,200,-64
 DC 224,200,-104
 DC 240,200,-104
 DC 240,200,-64
 DC 208,200,-80
 DC 208,200,-88
 DC 232,200,-88
 DC 232,200,-80
 DC 248,200,-64
 DC 248,200,-104
 DC 264,200,-104
 DC 264,200,-64
 DC 256,200,-64
 DC 256,200,-72
 DC 280,200,-72
 DC 280,200,-64
 DC 256,200,-96
 DC 256,200,-104
 DC 280,200,-104
 DC 280,200,-96
 DC 256,200,-80
 DC 256,200,-88
 DC 272,200,-88
 DC 272,200,-80
 DC 40,200,-112
 DC 40,200,-144
 DC 56,200,-144
 DC 56,200,-112
 DC 72,200,-112
 DC 72,200,-144
 DC 80,200,-144
 DC 80,200,-112
 DC 56,200,-112
 DC 56,200,-120
 DC 72,200,-144
 DC 72,200,-136
 DC 88,200,-112
 DC 88,200,-144
 DC 104,200,-144
 DC 104,200,-112
 DC 96,200,-112
 DC 96,200,-120
 DC 120,200,-120
 DC 120,200,-112
 DC 96,200,-136
 DC 96,200,-144
 DC 120,200,-144
 DC 120,200,-136
 DC 96,200,-124
 DC 96,200,-132
 DC 112,200,-132
 DC 112,200,-124
 DC 128,200,-112
 DC 128,200,-120
 DC 152,200,-144
 DC 152,200,-136
 DC 128,200,-136
 DC 128,200,-144
 DC 152,200,-120
 DC 152,200,-112
 DC 160,200,-112
 DC 160,200,-120
 DC 192,200,-120
 DC 192,200,-112
 DC 168,200,-116
 DC 168,200,-144
 DC 184,200,-144
 DC 184,200,-116
 DC 144,200,-152
 DC 144,200,-184
 DC 152,200,-184
 DC 152,200,-152
 DC 148,200,-152
 DC 148,200,-160
 DC 164,200,-160
 DC 164,200,-152
 DC 148,200,-168
 DC 148,200,-176
 DC 164,200,-176
 DC 164,200,-168
 DC 160,200,-156
 DC 160,200,-172
 DC 168,200,-172
 DC 168,200,-156
 DC 176,200,-152
 DC 176,200,-184
 DC 184,200,-184
 DC 184,200,-152
 DC 192,200,-152
 DC 192,200,-184
 DC 200,200,-184
 DC 200,200,-152
 DC 180,200,-152
 DC 180,200,-160
 DC 196,200,-160
 DC 196,200,-152
 DC 180,200,-168
 DC 180,200,-176
 DC 196,200,-176
 DC 196,200,-168
 DC 208,200,-152
 DC 208,200,-184
 DC 216,200,-184
 DC 216,200,-152
 DC 212,200,-152
 DC 212,200,-160
 DC 228,200,-160
 DC 228,200,-152
 DC 212,200,-168
 DC 212,200,-176
 DC 228,200,-176
 DC 228,200,-168
 DC 224,200,-156
 DC 224,200,-172
 DC 232,200,-172
 DC 232,200,-156
 DC 224,200,-184
 DC 232,200,-184
 DC 220,200,-172
 DC 212,200,-172
 DC 240,200,-152
 DC 240,200,-160
 DC 272,200,-160
 DC 272,200,-152
 DC 248,200,-156
 DC 248,200,-184
 DC 264,200,-184
 DC 264,200,-156
 DC 288,200,-176
 DC 288,200,-184
 DC 296,200,-184
 DC 296,200,-176
 DC 291,200,-168
 DC 299,200,-136
 DC 291,200,-128
 DC 283,200,-136

depla	
;	include	z2.dat
	dc	255
TABLE_DAT
 DC 1
 DC 2
 DC 3
 DC 4
 DC 5
 DC 6
 DC 7
 DC 8
 DC 10
 DC 11
 DC 12
 DC 13
 DC 14
 DC 15
 DC 16
 DC 17
 DC 18
 DC 20
 DC 21
 DC 22
 DC 23
 DC 24
 DC 25
 DC 26
 DC 27
 DC 28
 DC 29
 DC 31
 DC 32
 DC 33
 DC 34
 DC 35
 DC 36
 DC 37
 DC 38
 DC 39
 DC 41
 DC 42
 DC 43
 DC 44
 DC 45
 DC 46
 DC 47
 DC 48
 DC 49
 DC 51
 DC 52
 DC 53
 DC 54
 DC 55
 DC 56
 DC 57
 DC 58
 DC 59
 DC 61
 DC 62
 DC 63
 DC 64
 DC 65
 DC 66
 DC 67
 DC 68
 DC 70
 DC 71
 DC 72
 DC 73
 DC 74
 DC 75
 DC 76
 DC 77
 DC 78
 DC 80
 DC 81
 DC 82
 DC 83
 DC 84
 DC 85
 DC 86
 DC 87
 DC 88
 DC 90
 DC 91
 DC 92
 DC 93
 DC 94
 DC 95
 DC 96
 DC 97
 DC 98
 DC 100
 DC 100
 DC 100


PART1	DC	%1111111111111111
	DC	%0111111111111111
	DC	%0011111111111111
	DC	%0001111111111111
	DC	%0000111111111111
	DC	%0000011111111111
	DC	%0000001111111111
	DC	%0000000111111111
	DC	%0000000011111111
	DC	%0000000001111111
	DC	%0000000000111111
	DC	%0000000000011111
	DC	%0000000000001111
	DC	%0000000000000111
	DC	%0000000000000011
	DC	%0000000000000001

PART2	DC	%1000000000000000
	DC	%1100000000000000
	DC	%1110000000000000
	DC	%1111000000000000
	DC	%1111100000000000
	DC	%1111110000000000
	DC	%1111111000000000
	DC	%1111111100000000
	DC	%1111111110000000
	DC	%1111111111000000
	DC	%1111111111100000
	DC	%1111111111110000
	DC	%1111111111111000
	DC	%1111111111111100
	DC	%1111111111111110
	DC	%1111111111111111

PNTS
N	SET	32768
	REPT	16
	DC	N
N	SET	N/2
	ENDR

	*	'                    '
MESSAGE
	DC.B	'                    '
	DC.B	'   WELCOME IN OUR   '
	DC.B	'  LATEST MULTIPART  '
	DC.B	'       CALLED...    '
	DC.B	'                    '

	DC.B	'                    '
	DC.B	'                    '
	DC.B	'  <SUICIDAL LEVEL>  '
	DC.B	'                    '
	DC.B	'                    '

	DC.B	'                    '
	DC.B	'                    '
	DC.B	'  HARD-CODED BY...  '
	DC.B	'                    '
	DC.B	'                    '

	DC.B	'       -ZAPPY-      '
	DC.B	'                    '
	DC.B	' THE DEFINITELY MAD '
	DC.B	'      GFX-CODER     '
	DC.B	'                    '
	
	DC.B	'         AND        '
	DC.B	'       -ELRIC-      '
	DC.B	'      CHAOS AND     '
	DC.B	'     DRAGON LORD... '
	DC.B	'                    '

	DC.B	" IT'S TIME FOR THE  "
	DC.B	' WORLD TO KNOW THAT '
	DC.B	'     <HOLOCAUST>    '
	DC.B	'                    '
	DC.B	'    ! R U L E S !   '
	
	DC.B	'                    '
	DC.B	'                    '
	DC.B	'                    '
	DC.B	'                    '
	DC.B	'                    '
	EVEN

*****
CORRES
N	SET	0
	REPT	20
	DC.L	FONTES+N
N	SET	N+8
	ENDR

N	SET	0
	REPT	20
	DC.L	FONTES+N+(160*15)
N	SET	N+8
	ENDR

N	SET	0
	REPT	20
	DC.L	FONTES+N+(160*30)
N	SET	N+8
	ENDR
FONTES	INCBIN	FNT.PI1
	DS.B	160
	EVEN

FAD_IN
	DC	$000,$111,$222,$333,$444,$555,$666,$777
	DC	-1
FAD_OUT
	DC	$777,$666,$555,$444,$333,$222,$111,$000
	DC	-1
FAD_IN_
	DC	$000,$111,$222,$333,$444,$555,$666,$777
	DC	$667,$556,$445,$334,$223,$112,$002
	DC	-1
FAD_OUT_
	DC	$002,$112,$223,$334,$445,$556,$667
	DC	$777,$666,$555,$444,$333,$222,$111,$000
	DC	-1

***DEGAGE A PARTIR D'ICI
BUFCOD2

TB0_IR
	CLR.B	$FFFFFA1B.W
	MOVE	#$002,$FFFF8250.W
	MOVE	#$002,$FFFF8242.W
	MOVE.B	#4,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TB00,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE
TB00
	CLR.B	$FFFFFA1B.W
	MOVE	#$002,$FFFF8250.W
	MOVE.L	A1,USP
	LEA	FONTS+2,A1
	MOVE.L	(A1)+,$FFFF8240.W
	MOVE.L	(A1)+,$FFFF8244.W
	MOVE.L	(A1)+,$FFFF8248.W
	MOVE.L	(A1)+,$FFFF824C.W
	MOVE.L	USP,A1
	MOVE.B	#33,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TB0,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE
TB0
	CLR.B	$FFFFFA1B.W
	MOVE	#$111,$FFFF8246.W
	MOVE	#$111,$FFFF8254.W
	MOVE	#$001,$FFFF8244.W
	MOVE	#$002,$FFFF8242.W
	MOVE	#$002,$FFFF8248.W
	MOVE	#$002,$FFFF824A.W
	MOVE	#$002,$FFFF824C.W
	MOVE	#$002,$FFFF824E.W
	MOVE.B	#3,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TB1,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE
TB1
	CLR.B	$FFFFFA1B.W
	MOVE	#$222,$FFFF8246.W
	MOVE	#$222,$FFFF8254.W
	MOVE	#$002,$FFFF8244.W
	MOVE.B	#3,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TB2,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE
TB2
	CLR.B	$FFFFFA1B.W
	MOVE	#$333,$FFFF8246.W
	MOVE	#$333,$FFFF8254.W
	MOVE	#$003,$FFFF8244.W
	MOVE.B	#4,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TB3,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE
TB3
	CLR.B	$FFFFFA1B.W
	MOVE	#$444,$FFFF8246.W
	MOVE	#$444,$FFFF8254.W
	MOVE	#$004,$FFFF8244.W
	MOVE.B	#5,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TB4,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE
TB4
	CLR.B	$FFFFFA1B.W
	MOVE	#$555,$FFFF8246.W
	MOVE	#$555,$FFFF8254.W
	MOVE	#$005,$FFFF8244.W
	MOVE.B	#6,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TB5,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE
TB5
	CLR.B	$FFFFFA1B.W
	MOVE	#$666,$FFFF8246.W
	MOVE	#$666,$FFFF8254.W
	MOVE	#$006,$FFFF8244.W
	MOVE.B	#7,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TB6,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE
TB6
	CLR.B	$FFFFFA1B.W
	MOVE	#$777,$FFFF8246.W
	MOVE	#$777,$FFFF8254.W
	MOVE	#$007,$FFFF8244.W
	MOVE.B	#8,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TB7,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE
TB7
	CLR.B	$FFFFFA1B.W
;	MOVE	#$777,$FFFF8246.W
;	MOVE	#$777,$FFFF8254.W
	MOVE	#$017,$FFFF8244.W
;	MOVE.B	#4,$FFFFFA21.W
;	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TB0_IR,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE

VBL_IR	MOVEM.L	D0-A6,-(SP)
	CLR.B	$FFFFFA1B.W
	MOVE.B	#192-43-37-1-4,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	LEA	PAL_LOGO,A0
	MOVEM.L	(A0),D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

MOD_A1	EQU	*+2
	LEA	MOD_A1,A1
	MOVE.L	(A1)+,A2
	CMPI.L	#'FINI',-4(A1)
	BNE.S	ROULY
	LEA	TAB_D+4,A1
	MOVE.L	(A1)+,A2
ROULY	MOVEM.L	(A2),D0-D7
	MOVE.L	SCR1,A0
	LEA	160*50(A0),A0
	JSR	8*4(A2)
	MOVE.L	SCR2,A0
	LEA	160*50(A0),A0
	JSR	8*4(A2)
	MOVE.L	A1,MOD_A1
	JSR	GEST_SCROLL
	MOVE.L	SCR2,A1
	LEA	160*110(A1),A1
	MOVE.L	#$000000FF,D0
N	SET	0
	REPT	35
	MOVEP.L	D0,N(A1)
	MOVEP.L	D0,N+153(A1)
N	SET	N+160
	ENDR
	JSR	ZIK+8
	move.l	SCR1,d0
	MOVE.L	SCR2,SCR1
	MOVE.L	D0,SCR2
	CMPI.B	#$1,$FFFFFC02.W
	BNE.S	DS
	MOVE.L	SCREEN1,D0
DS	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)
	MOVEM.L	(SP)+,D0-A6
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE

GEST_SCROLL	MOVE.W	NO_DEC,D0
	JMP	ROUTS_DEC(PC,D0.W)
ROUTS_DEC	BRA	DEC_0
	BRA	DEC_1
	BRA	DEC_2
	BRA	DEC_3

GEST_AFFICH	
	MOVE.L	SCR2,A1
	LEA	160*112(A1),A1
	MOVE.W	PT_AFF,D0
	JMP	ROUTS_AFF(PC,D0.W)
ROUTS_AFF	BRA	AFF_20_00
	BRA	AFF_19_01
	BRA	AFF_18_02
	BRA	AFF_17_03
	BRA	AFF_16_04
	BRA	AFF_15_05
	BRA	AFF_14_06
	BRA	AFF_13_07
	BRA	AFF_12_08
	BRA	AFF_11_09
	BRA	AFF_10_10
	BRA	AFF_09_11
	BRA	AFF_08_12
	BRA	AFF_07_13
	BRA	AFF_06_14
	BRA	AFF_05_15
	BRA	AFF_04_16
	BRA	AFF_03_17
	BRA	AFF_02_18
	BRA	AFF_01_19

DEC_0	MOVE.L	PT_TEXT,A0
	LEA	CORES_FONTS,A1
	LEA	FONTS+34,A2
	LEA	BUFFER_SCROLL,A3
	ADDA.W	PT_BUF,A3
	MOVEQ	#0,D0
	MOVE.B	(A0),D0
	SUBI.B	#32,D0
	ADD.W	D0,D0
	ADDA.W	(A1,D0.W),A2	A2=ADRESSE DE LA FONTE
	ADD.W	PART_FONTE,A2	MOITIê GAUCHE OU DROITE:ON ADDITIONNE 8 OU 0
	MOVEQ	#HAUT_FONTE-1,D7
.RECOP_FONTE	MOVE.L	(A2)+,(A3)+
	MOVE.L	(A2)+,(A3)+
	LEA	152(A2),A2
	LEA	152(A3),A3
	DBRA	D7,.RECOP_FONTE
	MOVE.W	#4,NO_DEC
	LEA	BUFFER_SCROLL,A0
	BSR	GEST_AFFICH
	MOVE.W	PT_BUF,D0
	ADDQ.W	#8,D0
	CMPI.W	#160,D0
	BLT.S	.C_EST_BON_1
	MOVEQ	#0,D0
.C_EST_BON_1	MOVE.W	D0,PT_BUF
	MOVE.W	PT_AFF,D0
	ADDQ.W	#4,D0
	CMPI.W	#80,D0
	BLT.S	.C_EST_BON_2
	MOVEQ	#0,D0
.C_EST_BON_2	MOVE.W	D0,PT_AFF
	MOVE.L	PT_TEXT,A0
	MOVE.W	PART_FONTE,D0
	ADDQ.W	#8,D0
	CMPI.W	#LARG_FONTE/2,D0
	BLT.S	.C_EST_BON_3
	MOVEQ	#0,D0
	ADDQ.W	#1,A0
	CMPI.B	#-1,10(A0)
	BNE.S	.C_EST_BON_3
	LEA	TEXT,A0
.C_EST_BON_3	MOVE.W	D0,PART_FONTE
	MOVE.L	A0,PT_TEXT
	RTS

DEC_1	MOVE.L	PT_TEXT,A0
	LEA	CORES_FONTS,A1
	LEA	FONTS+34,A2
	LEA	BUFFER_SCROLL+(160*HAUT_FONTE),A3
	ADDA.W	PT_BUF,A3
	MOVEQ	#0,D0
	MOVE.B	(A0),D0
	SUBI.B	#32,D0
	ADD.W	D0,D0
	ADDA.W	(A1,D0.W),A2	A2=ADRESSE DE LA FONTE
	ADD.W	PART_FONTE,A2	MOITIê GAUCHE OU DROITE:ON ADDITIONNE 8 OU 0
	MOVE.L	SOV_ADR,A4
	MOVE.L	#$F000F,D5
	MOVE.L	#$FFF0FFF0,D6
	MOVEQ	#HAUT_FONTE-1,D7
.RECOP_FONTE	MOVE.L	(A4)+,D0	PLAN 1&2
	MOVE.L	(A4)+,D1	PLAN 3&4
	MOVE.L	(A2)+,D2	PLAN 1&2
	MOVE.L	(A2)+,D3	PLAN 3&4
	LSL.L	#4,D0
	LSL.L	#4,D1
	AND.L	D6,D0
	AND.L	D6,D1
	ROL.L	#4,D2
	ROL.L	#4,D3
	AND.L	D5,D2
	AND.L	D5,D3
	SWAP	D2
	SWAP	D3
	OR.L	D0,D2
	OR.L	D1,D3
	MOVE.L	D2,(A3)+
	MOVE.L	D3,(A3)+
	LEA	152(A2),A2
	LEA	152(A3),A3
	LEA	152(A4),A4
	DBRA	D7,.RECOP_FONTE
	MOVE.W	#8,NO_DEC
	LEA	BUFFER_SCROLL+(160*HAUT_FONTE),A0
	BSR	GEST_AFFICH
	RTS

DEC_2	MOVE.L	PT_TEXT,A0
	LEA	CORES_FONTS,A1
	LEA	FONTS+34,A2
	LEA	BUFFER_SCROLL+(160*HAUT_FONTE*2),A3
	ADDA.W	PT_BUF,A3
	MOVEQ	#0,D0
	MOVE.B	(A0),D0
	SUBI.B	#32,D0
	ADD.W	D0,D0
	ADDA.W	(A1,D0.W),A2	A2=ADRESSE DE LA FONTE
	ADD.W	PART_FONTE,A2	MOITIê GAUCHE OU DROITE:ON ADDITIONNE 8 OU 0
	MOVE.L	SOV_ADR,A4
	MOVEQ	#HAUT_FONTE-1,D7
.RECOP_FONTE	MOVEP.L	1(A4),D0
	MOVEP.L	(A2),D1
	MOVEP.L	D0,(A3)
	MOVEP.L	D1,1(A3)
	LEA	160(A2),A2
	LEA	160(A3),A3
	LEA	160(A4),A4
	DBRA	D7,.RECOP_FONTE
	MOVE.W	#12,NO_DEC
	LEA	BUFFER_SCROLL+(160*HAUT_FONTE*2),A0
	BSR	GEST_AFFICH
	RTS

DEC_3	MOVE.L	PT_TEXT,A0
	LEA	CORES_FONTS,A1
	LEA	FONTS+34,A2
	LEA	BUFFER_SCROLL+(160*HAUT_FONTE*3),A3
	ADDA.W	PT_BUF,A3
	MOVEQ	#0,D0
	MOVE.B	(A0),D0
	SUBI.B	#32,D0
	ADD.W	D0,D0
	ADDA.W	(A1,D0.W),A2	A2=ADRESSE DE LA FONTE
	ADD.W	PART_FONTE,A2	MOITIê GAUCHE OU DROITE:ON ADDITIONNE 8 OU 0
	MOVE.L	SOV_ADR,A4

	MOVE.L	A2,SOV_ADR	;TRES IMPORTANT

	MOVE.L	#$0FFF,D5
	MOVE.L	#$F000F000,D6
	MOVEQ	#HAUT_FONTE-1,D7
.RECOP_FONTE	MOVE.L	(A4)+,D0	PLAN 1&2
	MOVE.L	(A4)+,D1	PLAN 3&4
	MOVE.L	(A2)+,D2	PLAN 1&2
	MOVE.L	(A2)+,D3	PLAN 3&4
	ROR.L	#4,D0
	ROR.L	#4,D1
	AND.L	D6,D0
	AND.L	D6,D1
	SWAP	D0
	SWAP	D1
	LSR.L	#4,D2
	LSR.L	#4,D3
	AND.W	D5,D2
	AND.W	D5,D3
	OR.L	D0,D2
	OR.L	D1,D3
	MOVE.L	D2,(A3)+
	MOVE.L	D3,(A3)+
	LEA	152(A2),A2
	LEA	152(A3),A3
	LEA	152(A4),A4
	DBRA	D7,.RECOP_FONTE
	CLR.W	NO_DEC
	LEA	BUFFER_SCROLL+(160*HAUT_FONTE*3),A0
	BSR	GEST_AFFICH
	RTS

AFF_20_00	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,44(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,88(A1)
	MOVEM.L	(A0)+,D0-D6
	MOVEM.L	D0-D6,132(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_20_00+2
	RTS

AFF_19_01	MOVEQ	#HAUT_FONTE-1,D7
	MOVE.L	(A0)+,152(A1)
	MOVE.L	(A0)+,156(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,44(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,88(A1)
	MOVEM.L	(A0)+,D0-D4
	MOVEM.L	D0-D4,132(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_19_01+2
	RTS

AFF_18_02	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,144(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,44(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,88(A1)
	MOVEM.L	(A0)+,D0-D2
	MOVEM.L	D0-D2,132(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_18_02+2
	RTS

AFF_17_03	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D5
	MOVEM.L	D0-D5,136(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,44(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,88(A1)
	MOVE.L	(A0)+,132(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_17_03+2
	RTS

AFF_16_04	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2
	MOVEM.L	D0-D6/A2,128(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,44(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A4
	MOVEM.L	D0-D6/A2-A4,88(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_16_04+2
	RTS

AFF_15_05	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A4
	MOVEM.L	D0-D6/A2-A4,120(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,44(A1)
	MOVEM.L	(A0)+,D0-D6/A2
	MOVEM.L	D0-D6/A2,88(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_15_05+2
	RTS

AFF_14_06	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,112(A1)
	MOVE.L	(A0)+,156(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,44(A1)
	MOVEM.L	(A0)+,D0-D5
	MOVEM.L	D0-D5,88(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_14_06+2
	RTS

AFF_13_07	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,104(A1)
	MOVEM.L	(A0)+,D0-D2
	MOVEM.L	D0-D2,148(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,44(A1)
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,88(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_13_07+2
	RTS

AFF_12_08	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,96(A1)
	MOVEM.L	(A0)+,D0-D4
	MOVEM.L	D0-D4,140(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,44(A1)
	MOVE.L	(A0)+,88(A1)
	MOVE.L	(A0)+,92(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_12_08+2
	RTS

AFF_11_09	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,88(A1)
	MOVEM.L	(A0)+,D0-D6
	MOVEM.L	D0-D6,132(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,44(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_11_09+2
	RTS

AFF_10_10	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,80(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A3
	MOVEM.L	D0-D6/A2-A3,124(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A3
	MOVEM.L	D0-D6/A2-A3,44(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_10_10+2
	RTS

AFF_09_11	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,72(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,116(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D6
	MOVEM.L	D0-D6,44(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_09_11+2
	RTS

AFF_08_12	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,64(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,108(A1)
	MOVE.L	(A0)+,152(A1)
	MOVE.L	(A0)+,156(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D4
	MOVEM.L	D0-D4,44(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_08_12+2
	RTS

AFF_07_13	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,56(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,100(A1)
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,144(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVEM.L	(A0)+,D0-D2
	MOVEM.L	D0-D2,44(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_07_13+2
	RTS

AFF_06_14	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,48(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,92(A1)
	MOVEM.L	(A0)+,D0-D5
	MOVEM.L	D0-D5,136(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,(A1)
	MOVE.L	(A0)+,44(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_06_14+2
	RTS

AFF_05_15	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,40(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,84(A1)
	MOVEM.L	(A0)+,D0-D6/A2
	MOVEM.L	D0-D6/A2,128(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A4
	MOVEM.L	D0-D6/A2-A4,(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_05_15+2
	RTS

AFF_04_16	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,32(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,76(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A4
	MOVEM.L	D0-D6/A2-A4,120(A1)
	MOVEM.L	(A0)+,D0-D6/A2
	MOVEM.L	D0-D6/A2,(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_04_16+2
	RTS

AFF_03_17	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,24(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,68(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,112(A1)
	MOVE.L	(A0)+,156(A1)
	MOVEM.L	(A0)+,D0-D5
	MOVEM.L	D0-D5,(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_03_17+2
	RTS

AFF_02_18	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,16(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,60(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,104(A1)
	MOVEM.L	(A0)+,D0-D2
	MOVEM.L	D0-D2,148(A1)
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_02_18+2
	RTS

AFF_01_19	MOVEQ	#HAUT_FONTE-1,D7
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,8(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,52(A1)
	MOVEM.L	(A0)+,D0-D6/A2-A5
	MOVEM.L	D0-D6/A2-A5,96(A1)
	MOVEM.L	(A0)+,D0-D4
	MOVEM.L	D0-D4,140(A1)
	MOVE.L	(A0)+,(A1)
	MOVE.L	(A0)+,4(A1)
	LEA	160(A1),A1
	DBRA	D7,AFF_01_19+2
	RTS

PAL_LOGO	DS.L	8
BIG_DATA	INCBIN	LOGO.PAK
	EVEN
COD	INCBIN	DAMIER.COD
TAB_D	DS.L	65
	DC.L	'FINI'
FONTS	INCBIN	METAL.PI1
PALG1
	DCB	16,0
	DC	$000,$010,$111,$111,$111,$111,$111,$111
	DC	$111,$111,$111,$111,$111,$111,$111,$111
	DC	$000,$010,$222,$222,$221,$222,$222,$222
	DC	$222,$222,$222,$222,$222,$222,$222,$222
	DC	$000,$010,$333,$332,$331,$333,$333,$333
	DC	$333,$333,$333,$333,$333,$333,$333,$333
	DC	$000,$010,$443,$442,$431,$444,$444,$444
	DC	$444,$334,$433,$443,$444,$444,$444,$444
	DC	$000,$010,$553,$552,$431,$555,$555,$555
	DC	$455,$334,$433,$543,$554,$555,$555,$555
	DC	$000,$010,$663,$652,$431,$666,$666,$566
	DC	$455,$334,$433,$543,$654,$665,$665,$666
	DC	$000,$010,$663,$652,$431,$777,$677,$566
	DC	$455,$334,$433,$543,$654,$665,$765,$776
	DC	-1
PALG2
	DC	$000,$010,$663,$652,$431,$777,$677,$566
	DC	$455,$334,$433,$543,$654,$665,$765,$776
	DC	$000,$010,$663,$652,$431,$666,$666,$566
	DC	$455,$334,$433,$543,$654,$665,$665,$666
	DC	$000,$010,$553,$552,$431,$555,$555,$555
	DC	$455,$334,$433,$543,$554,$555,$555,$555
	DC	$000,$010,$443,$442,$431,$444,$444,$444
	DC	$444,$334,$433,$443,$444,$444,$444,$444
	DC	$000,$010,$333,$332,$331,$333,$333,$333
	DC	$333,$333,$333,$333,$333,$333,$333,$333
	DC	$000,$010,$222,$222,$221,$222,$222,$222
	DC	$222,$222,$222,$222,$222,$222,$222,$222
	DC	$000,$010,$111,$111,$111,$111,$111,$111
	DC	$111,$111,$111,$111,$111,$111,$111,$111
	DCB	16,0
	DC	-1
GANDALF	INCBIN	GANDALF.PAK
	EVEN

CORES_FONTS	
N	SET	0
N2	SET	160*(HAUT_FONTE+1)
N3	SET	160*(HAUT_FONTE+1)*2
N4	SET	160*(HAUT_FONTE+1)*3
N5	SET	160*(HAUT_FONTE+1)*4
	DC	N5+6*16	ESP
	DC	N5+6*16	!
	DC	N5+4*16	"
	DC	N5+6*16	#
	DC	N5+6*16	$
	DC	N5+6*16	%
	DC	N5+6*16	&
	DC	N5+3*16	'
	DC	N3+160-32	(
	DC	N3+160-16	)
	DC	N5+6*16	*
	DC	N5+6*16	+
	DC	N5+2*16	,
	DC	N5	-
	DC	N3+160-16*4	.
	DC	N5+6*16	/
NN	SET	0
	REPT	10
	DC	N4+NN
NN	SET	NN+16
	ENDR
	DC	N3+160-16*3	:
	DC	N5+160*5	;
	DC	N5+6*16	<
	DC	N5+6*16	=
	DC	N5+6*16	>
	DC	N5+6*16	?
	DC	N5+6*16	

	DC.W	N,N+16,N+32,N+48,N+64,N+80,N+96,N+112,N+128,N+144
	DC.W	N2,N2+16,N2+32,N2+48,N2+64,N2+80,N2+96,N2+112,N2+128,N2+144
	DC.W	N3,N3+16,N3+32,N3+48,N3+64,N3+80

TEXT
 DC.B "                        "
 DC.B "ON A DARKLING PLAIN, IN THE FIELDS OF DREAM, WE WILL RULE FOREVER... "
 DC.B "   CHAOS IS OUR KINGDOM, CHAOTIC DEMOS THE WAY OF EXPRESSING OURSELVES.        "
 DC.B " WELCOME, BRAVE ADVENTURERS...              "
	DC.B	"                      ",-1
	EVEN

	section bss
SCR1	DS.L	1
SCR2	DS.L	1
	DS.B	256
BUFFER	DS.B	32000*2
BUFFER_SCROLL	DS.L	40*HAUT_FONTE*4
PT_TEXT	DS.L	1
PT_BUF	DS.W	1
SOV_ADR	DS.L	1
NO_DEC	DS.W	1
PT_AFF	DS.W	1
PART_FONTE	DS.W	1

******
xe	ds 1
ye	ds 1
ze	ds 1
xdep	ds 1
ydep	ds 1
zdep	ds 1
xp	ds 1
yp	ds 1
new_xe	ds 1
new_ye	ds 1
new_ze	ds 1
x_angle	ds 1
y_angle	ds 1
z_angle	ds 1	
z_pt_fuite	ds 1
var_3d	ds 9
POINTEUR	DS.L	1
POINTEUR2	DS.L	1
DATA_X
	DS	NB_PTS*2
	DS.B	200
modif	Ds	1

SCREEN2	DS.L	1
SCREEN1	DS.L	1
SCREEN3	DS.L	1
FLAG_PTS	DS.L	1
ANC_VBL	DS.L	1
CNT	DS.L	1
CNT2	DS.L	1
ZA	DS	1	
PNT_X	DS.L	1
PNT_Y	DS.L	1
PNT	DS.L	1
REG_GEN	DS.L	16
*LINE_A_EMUL

PNT_261E	DS	1
EMUL_LINEA	DS.B	100
BUF16DA	DS.B	512
BUF299A	DS.B	512

_PTSIN	DS.B	100

	DS.B	256
BUFFERSCR	DS.B	32000
TABLE	DS.L	360
BUF1	DS.B	8000
BUF2	DS.B	8000
FINAL	DS.L	16
DATAS	DS.B	8000

BUFCOD	DS.B	205000
	;DS.B	373000-170000
END_COD
