PROG:
	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP	
	MOVE.L	D0,SAUV_SP

	CLR	-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.L	#-1,-(SP)
	MOVE	#5,-(SP)
	TRAP	#14
	ADDA.L	#12,SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W

	LEA	PALETTE2,A0
	LEA	PALETTE2,A3
	LEA	BUFFER,A1
	LEA	BUFFER2,A2
;	LEA	32*2(A2),A2

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	
	MOVE	#17,D5	
	
LA3	MOVE	(A0)+,D0
	MOVE	(A0)+,D1
	
	MOVE	(A3)+,D3
	MOVE	(A3)+,D3
	MOVE	(A3)+,D4
	MOVE	(A3)+,D4

	MOVE	#31,D2
ICI3	MOVE	D0,(A1)+
	MOVE	D3,(A2)+
	DBF	D2,ICI3

	MOVE	#31,D2
ICI4	MOVE	D1,(A2)+
	MOVE	D4,(A1)+
	DBF	D2,ICI4
	
	DBF	D5,LA3
	
	JSR	SET_SCR
	
	move.l	SCREEN1,d0	
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)

	CLR.L	D0
	CLR.L	D1

	MOVEA.L	SCREEN1,A0
	LEA	160(A0),A0
	MOVE	#6,D1
LA	MOVE	#198+30,D0
ICI	MOVE	#$FFFF,(A0)
	MOVE	#$FFFF,8(A0)
	MOVE	#$FFFF,8+2(A0)

	MOVE	#$FFFF,16(A0)
	MOVE	#$FFFF,16+2(A0)
	MOVE	#$FFFF,16+4(A0)
	
	MOVE	#$FFFF,16+8(A0)
	MOVE	#$FFFF,16+8+2(A0)
	MOVE	#$FFFF,16+8+4(A0)
	MOVE	#$FFFF,16+8+6(A0)

	LEA	230(A0),A0
	DBF	D0,ICI
	LEA	-23000+32(A0),A0
	LEA	-23000+230-6900(A0),A0
	DBF	D1,LA
	
	MOVE.B	#$12,$FFFFFC02.W
	DC.W	$A00A


	ANDI.B	#%11111000,$484.W

	CLR.L	D0
	CLR.L	D1
	CLR.L	D2

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL

;	MOVEQ	#1,D0
;	JSR	MUZAXX

	MOVE.L	$70.W,ANC_VBL
	MOVE.L	#VBLR,$70.W

BOUCLE	JMP	BOUCLE

SET_SCR:	move.l	#BUFFERSCR,d0	
	and.l	#$ffffff00,d0		
	move.l	d0,SCREEN1
	add.l	#23160,d0		
	move.l	d0,SCREEN
	rts				

	
VBLR	CLR	$FFFF8240.W
	CLR	$FFFF8242.W
	CLR	$FFFF8244.W
	CLR	$FFFF8246.W
	CLR	$FFFF8248.W
	CLR	$FFFF824A.W
	CLR	$FFFF824C.W
	CLR	$FFFF824E.W
	CLR	$FFFF8250.W
	CLR	$FFFF8252.W
	CLR	$FFFF8254.W
	CLR	$FFFF8256.W
	CLR	$FFFF8258.W
	CLR	$FFFF825A.W
	CLR	$FFFF825C.W
	CLR	$FFFF825E.W

	LEA	BUFFER,A6
	ADDA.L	POINT2,A6
	LEA	BUFFER2,A5
	ADDA.L	POINT2,A5
	ADD.L	#2,POINT2
	CMPI.L	#32*37*2,POINT2
	BNE.S	CA_ROULE
	CLR.L	POINT2
CA_ROULE

full	
	MOVE.L	#$FFFF8209,A3	
	MOVE.L	#$FFFF8260,A1	
	MOVE.L	#$FFFF820A,A0	
	MOVE	#199,D0		* NB DE LIGNES
	MOVEQ	#0,D1		
	MOVEQ	#0,D4
	MOVEQ	#2,D3
	MOVEQ	#$10,D6
	MOVE	#$2700,SR


SYNCHRO	MOVE.B	(A3),D7     * SYNCHRO
	BEQ.S	SYNCHRO
	SUB.W	D7,D6		
	LSL.W	D6,D1		

	REPT	97
	NOP	
	ENDR
	
FULLSCREEN

	REPT	199
	MOVE.B	D3,(A1)
	NOP
	MOVE.B	D4,(A1)
	MOVE	(a6),$ffff8242.W
	MOVE	(a6)+,$ffff8246.W
	MOVE	(a5),$FFFF824E.W
	MOVE	(a5)+,$FFFF825E.W
	dcb	77-4,$4E71
	MOVE.B	D4,(A0)
	MOVE.B	D3,(A0)
	dcb	13,$4E71
	MOVE.B	D3,(A1)
	NOP
	MOVE.B	D4,(A1)
	dcb	12,$4E71
	ENDR
	
	CMPI.B	#$39,$FFFFFC02.W
 	BEQ.S	FIN
	RTE

FIN	MOVE.L	ANC_VBL,$70.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W

	BSET	#0,$484.W
	BSET	#1,$484.W
	BSET	#2,$484.W

	MOVE.B	#8,$FFFFFC02.W

	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	
	SECTION	DATA

PALETTE2
	REPT	2
	DC.W	$100,$200,$300,$400,$500,$600,$700
	DC.W	$710,$720,$730,$740,$750,$760,$770
	DC.W	$771,$772,$773,$774,$775,$776,$777
	DC.W	$677,$577,$477,$377,$277,$177,$077
	DC.W	$066,$055,$044,$033,$022,$011,$001,$001
	ENDR


	SECTION	BSS
POINT2	DS.L	1
SCREEN	DS.L	1
SCREEN1	DS.L	1
ANC_PAL	DS.L	8
SAUV_SP	DS.L	1
ANC_VBL	DS.L	1
BUFFER	DS	32*37
BUFFER2	DS	32*37
		DS.B	256
BUFFERSCR	DS.B	64000
		DS.B	256
