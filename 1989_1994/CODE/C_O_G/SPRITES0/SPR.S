;X	EQU	6	;(COLONNES DE 16 PIXELS)+1
;Y	EQU	81	;HAUTEUR
NB_DECAL	EQU	8	;8 OU 16
NB_DEC	EQU	1	;1 POUR 8 ET 0 POUR 16
PAS	EQU	15*6	;PRISE DE POS DANS LA COURBE

	PEA	0.W
	MOVE	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP

	LEA	PIC,A0
	LEA	DECRUNCHED,A1
	JSR	decrunch

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVEQ	#1,D0
	JSR	ZIK
	
	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL

	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1

	JSR	PREP_ECR
	JSR	MAK_COD
	
	LEA	DECRUNCHED+34+120,A0
	BSR	PREDECALET
	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	MOVE	#7999,D7
RECOMEFF	CLR.L	(A0)+
	CLR.L	(A1)+
	DBRA	D7,RECOMEFF
	LEA	COURB,A0
	LEA	FIN_CRB,A1
	MOVE.L	-12(A1),-12(A0)
	MOVE.L	-8(A1),-8(A0)
	MOVE.L	-4(A1),-4(A0)	
	MOVE	#((PAS/6)*8)-2,D7
RECOP1	MOVE.L	(A0)+,(A1)+
	MOVE	(A0)+,(A1)+
	DBRA	D7,RECOP1
	MOVE.L	#-1,(A1)

	MOVE.L	#COURB,PNT_CRB

	MOVE.L	#FIN,$008
	MOVE.L	#FIN,$00C
	MOVE.L	#FIN,$010
	MOVE.L	#FIN,$014
	MOVE.L	#FIN,$018
	MOVE.L	#FIN,$01C
	MOVE.L	#FIN,$020

	ANDI.B	#%11111000,$484.W

	MOVEM.L	DECRUNCHED+2,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.B	$FFFFFA07.W,MFP
	MOVE.B	$FFFFFA09.W,MFP+1
	MOVE.B	$FFFFFA0F.W,MFP+2
	MOVE.B	$FFFFFA13.W,MFP+3
	MOVE.B	$FFFFFA1B.W,MFP+4
	MOVE.B	$FFFFFA21.W,MFP+5

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W

	MOVE.L	$70.W,ANC_VBL
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W

LOOP	JMP	LOOP

FIN	MOVE	#$2700,SR
	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W
	
	MOVE.B	MFP,$FFFFFA07.W
	MOVE.B	MFP+1,$FFFFFA09.W
	MOVE.B	MFP+2,$FFFFFA0F.W
	MOVE.B	MFP+3,$FFFFFA13.W
	MOVE.B	MFP+4,$FFFFFA1B.W
	MOVE.B	MFP+5,$FFFFFA21.W

	MOVE	#$2300,SR
	MOVE.B	SAUVEC,$FFFF8201.W
	MOVE.B	SAUVEC+1,$FFFF8203.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	
	ORI.B	#%00000111,$484.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP

VBLR	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	SUITV0
	ST	$FFFF8240.W
SUITV0	
	MOVE.L	PNT_CRB,A5
	SUBA.L	#12,A5
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVE.L	#0,A0
	MOVE.L	#0,A1
	MOVE.L	#0,A2
	MOVE.L	#0,A3

	MOVE.L	SCREEN2,A4
	ADDA.W	4(A5),A4
N	SET	0
	REPT	81
	MOVEM.L	D0-D7/A0-A3,N(A4)
N	SET	N+160
	ENDR
	LEA	PAS(A5),A5

	MOVE.L	SCREEN2,A4
	ADDA.W	4(A5),A4
N	SET	0
	REPT	81
	MOVEM.L	D0-D7/A0-A3,N(A4)
N	SET	N+160
	ENDR
	LEA	PAS(A5),A5

	MOVE.L	SCREEN2,A4
	ADDA.W	4(A5),A4
N	SET	0
	REPT	81
	MOVEM.L	D0-D7/A0-A3,N(A4)
N	SET	N+160
	ENDR
	LEA	PAS(A5),A5

	MOVE.L	SCREEN2,A4
	ADDA.W	4(A5),A4
N	SET	0
	REPT	81
	MOVEM.L	D0-D7/A0-A3,N(A4)
N	SET	N+160
	ENDR
	LEA	PAS(A5),A5

	MOVE.L	SCREEN2,A4
	ADDA.W	4(A5),A4
N	SET	0
	REPT	81
	MOVEM.L	D0-D7/A0-A3,N(A4)
N	SET	N+160
	ENDR

	MOVE.L	PNT_CRB,A5

	LEA	PREDEC_TAB,A4
	MOVE	(A5),D0
	LSR	#1,D0
	MOVE.L	(A4,D0.W),A0
	MOVE.L	SCREEN2,A1
	ADDA.W	4(A5),A1
N	SET	0
	REPT	81
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
N	SET	N+160
	ENDR

	MOVE.L	PNT_CRB,A5
	LEA	PAS(A5),A5
	MOVE.L	#0,A4

	MOVE.L	SCREEN2,A3

	LEA	TABLE_GEN,A6
	LEA	24*8(A6),A6
	ADDA.W	2(A5),A6
	MOVE.L	A3,A0
	ADDA.W	4(A5),A0
	MOVE.L	A3,A1
	ADDA.W	4(A5),A1
	MOVE.L	(A6)+,A2	;MSK
	MOVE.L	(A6),A6
	JSR	(A6)

	LEA	PAS(A5),A5

	LEA	TABLE_GEN,A6
	LEA	24*8(A6),A6
	ADDA.W	2(A5),A6
	MOVE.L	A3,A0
	ADDA.W	4(A5),A0
	MOVE.L	A3,A1
	ADDA.W	4(A5),A1
	MOVE.L	(A6)+,A2	;MSK
	MOVE.L	(A6),A6
	JSR	(A6)

	LEA	PAS(A5),A5

	LEA	TABLE_GEN,A6
	LEA	12*8(A6),A6
	ADDA.W	2(A5),A6
	MOVE.L	A3,A0
	ADDA.W	4(A5),A0
	MOVE.L	A3,A1
	ADDA.W	4(A5),A1
	MOVE.L	(A6)+,A2	;MSK
	MOVE.L	(A6),A6
	JSR	(A6)

	LEA	PAS(A5),A5

	LEA	TABLE_GEN,A6
	ADDA.W	2(A5),A6
	MOVE.L	A3,A0
	ADDA.W	4(A5),A0
	MOVE.L	A3,A1
	ADDA.W	4(A5),A1
	MOVE.L	(A6)+,A2	;MSK
	MOVE.L	(A6),A6
	JSR	(A6)

	SF	$FFFF8240.W
	BSR	SWAPEC

	ADDQ.L	#6,PNT_CRB

	MOVE.L	PNT_CRB,A0
	CMPI.W	#-1,(PAS*8)-6(A0)
	BNE.S	FINF
	MOVE.L	#COURB,PNT_CRB
FINF	JSR	ZIK+8
	
	CMPI.B	#$1,$FFFFFC02.W
	BEQ	FIN
;	CMPI.B	#$3B,$FFFFFC02.W
;	BEQ.S	SUITE_DEMO
	RTE
;SUITE_DEMO
;	MOVE	#$2300,SR
;	MOVE.L	#VBL_ZIK,$70.W
;	JSR	PREP_ECR
;	LEA	PIC2,A0
;	LEA	DECRUNCHED2,A1
;	JSR	decrunch
;	JSR	CHANGE_DATAS_ZERIOUL
;	JSR	MAK_COD_ZERIOUL
;	MOVE.L	#COURB,PNT_CRB
;	LEA	DECRUNCHED2+34,A0
;	BSR	PREDECALET
;	MOVEM.L	DECRUNCHED2+2,D0-D7
;	MOVEM.L	D0-D7,$FFFF8240.W
;	JSR	PREP_ECR
;	MOVE.L	#VBLR_ZERIOUL,$70.W
;LOOPA	JMP	LOOPA

VBL_ZIK	MOVEM.L	D0-A6,-(SP)
	JSR	ZIK+8
	MOVEM.L	(SP)+,D0-A6
	CMPI.B	#$1,$FFFFFC02.W
	BEQ	FIN
	RTE
	
TB0	RTE

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
MAK_COD	MOVE.L	#TABLE_SPR,ANNEX
	LEA	BUF_COD,A2
	LEA	TABLE_GEN,A6
	LEA	FIC_OPT,A3
	BSR 	CODING
	
	ADDQ.L	#4,ANNEX
	LEA	FIC_OPT+512,A3
	BSR	CODING

	ADDQ.L	#4,ANNEX
	LEA	FIC_OPT+1024,A3
	BSR	CODING

	RTS

CODING	
RECOMGENSPR	MOVE.L	ANNEX,A0
	MOVE.L	(A0),A0
	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A5
CHANGEY1	MOVE	#81-1,D6
CHANGE1
REAF2	MOVE	#6-2,D7
REAF1	MOVE.L	(A0),(A1)+
	MOVE.L	4(A0),(A1)+
	MOVE	(A0),D0
	MOVE	2(A0),D1
	MOVE	4(A0),D2
	OR.W	D2,D0
	OR.W	D1,D0
	NOT.W	D0
	MOVE	D0,(A5)+
	ADDQ.L	#8,A0
	DBRA	D7,REAF1
	CLR.L	(A1)+
	CLR.L	(A1)+
	MOVE	#-1,(A5)+
CHANGE2	LEA	160-((6-1)*8)(A0),A0
	DBRA	D6,REAF2

	MOVE.L	A3,(A6)+
	MOVE.L	A2,(A6)+
	BSR	COD_GEN
	MOVE.L	A2,(A6)+
	BSR	MAK_EFF
	REPT	NB_DECAL-1
	BSR	DECAL_SPR
	MOVE.L	A3,(A6)+
	MOVE.L	A2,(A6)+
	BSR	COD_GEN
	MOVE.L	A2,(A6)+
	BSR	MAK_EFF
	ENDR
	RTS

DECAL_SPR	MOVE.L	SCREEN2,A1	;MSK
	MOVE	#NB_DEC,D4
RECOM_DEC	MOVE.L	SCREEN1,A0	;SPR
CHANGEY2	MOVE	#81-1,D5
DEC3	MOVE	#3,D6
CHANGE3
DEC2	MOVE	#6-1,D7
DEC1	ROXR	(A0)
	ADDQ.L	#8,A0
	DBRA	D7,DEC1
CHANGE4	LEA	-((6*8)-2)(A0),A0
	DBRA	D6,DEC2
CHANGE5	LEA	(6-1)*8(A0),A0
	DBRA	D5,DEC3
	DBRA	D4,RECOM_DEC
	MOVE.L	SCREEN1,A0
CHANGEY3	MOVE	#81-1,D6
CHANGE6
MSK2	MOVE	#6-1,D7
MSK1	MOVE	(A0)+,D0
	MOVE	(A0)+,D1
	MOVE	(A0)+,D2
	ADDQ.L	#2,A0
	OR.W	D1,D0
	OR.W	D2,D0
	NOT.W	D0
	MOVE	D0,(A1)+
	DBRA	D7,MSK1
	DBRA	D6,MSK2
	RTS

COD_GEN	MOVE.L	SCREEN1,A0	;SPR
	MOVE.L	SCREEN2,A1	;MSK
	MOVE.L	#$4CDA00FF,(A2)+
CHANGEY4	MOVE	#81-1,D6
CHANGE7	
CDG2	MOVE	#6-1,D7
CDG1	BSR	ANDS
	DBRA	D7,CDG1
	MOVE	#$41E8,(A2)+
CHANGE8	MOVE	#160-(6*8),(A2)+
	BSR	OPTI_ADDQ_ANDS
 	DBRA	D6,CDG2
	LEA	32(A3),A3
	MOVE.L	SCREEN1,A0	;SPR
	MOVE.L	SCREEN2,A1	;MSK
	MOVE.L	#$4CDA00FF,(A2)+
CHANGEY5	MOVE	#81-1,D6
CHANGE9
CDG4	MOVE	#6-1,D7
CDG3	BSR	ORS
	DBRA	D7,CDG3
	MOVE	#$43E9,(A2)+
CHANGE10	MOVE	#160-(6*8),(A2)+
	BSR	OPTI_ADDQ_ORS
	DBRA	D6,CDG4
	LEA	32(A3),A3
	MOVE	#$4E75,(A2)+
	RTS

ANDS	TST.W	(A1)
	BEQ.S	NO_MASK
	CMPI.W	#-1,(A1)
	BEQ.S	NO_MASK
	;TST.L	0(A0)
	;BEQ	AND1
	MOVE	#$C158,D4
	MOVE	#$C198,D5
	CLR	ADD1
	MOVE	(A1),D0
	MOVE	#7,D3
CHERCHMSK	CMP.W     	(A3),D0
	BEQ.S	MSKTROUVê
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	ADDI.W	#$200,D5
	ADDQ.W	#4,ADD1
	DBRA	D3,CHERCHMSK
	LEA	-32(A3),A3
	MOVE	#$0298,(A2)+
	MOVE	(A1),(A2)+
	MOVE	(A1),(A2)+
	MOVE	#$0258,(A2)+
	MOVE	(A1),(A2)+
	MOVE	#$5448,(A2)+
;	ADDQ.W	#1,ADD
	BRA.S	FIN_MASK
MSKTROUVê	SUBA.W	ADD1,A3
	MOVE	D5,(A2)+
	MOVE	D4,(A2)+
	MOVE	#$5448,(A2)+
;	ADDQ.W	#1,ADD
	BRA.S	FIN_MASK
NO_MASK	MOVE	#$5048,(A2)+
	BSR	OPTI_ADDQ_ANDS
;	ADDQ.W	#1,ADD
FIN_MASK	ADDQ.L	#2,A1
	ADDQ.L	#8,A0
	RTS

AND1	MOVE	#$0258,(A2)+
	MOVE	(A1),(A2)+
	MOVE	#$5C48,(A2)+
	BRA	FIN_MASK

OPTI_ADDQ_ANDS:
	CLR	VARI
	MOVE	#$5048,D1	;ADDQ.W	#8,A0
	MOVE	#$5448,D2	;ADDQ.W	#2,A0
	MOVE	#$41E8,D3	;LEA	#IMM(A0),A0
CONTANDQ	CMP.W	-2(A2),D1
	BNE.S	ANDQ2
	ADDQ.W	#8,VARI
	SUBQ.L	#2,A2
	BRA	CONTANDQ
ANDQ2	CMP.W	-2(A2),D2
	BNE.S	ANDQ3
	ADDQ.W	#2,VARI
	SUBQ.L	#2,A2
	BRA	CONTANDQ
ANDQ3	CMP.W	-4(A2),D3
	BNE.S	FINANDQ
	MOVE	-2(A2),D4
	ADD.W	D4,VARI
	SUBQ.L	#4,A2
	BRA	CONTANDQ
FINANDQ	TST.W	VARI
	BEQ	SORTANDQ
	CMPI.W	#8,VARI
	BGT	CHANDQ2
	CMPI.W	#2,VARI
	BNE.S	CH1
	MOVE	#$5448,(A2)+
	BRA.S	SORTANDQ
CH1	CMPI.W	#4,VARI
	BNE.S	CH11
	MOVE	#$5848,(A2)+
	BRA.S	SORTANDQ
CH11	CMPI.W	#6,VARI
	BNE.S	CH111
	MOVE	#$5C48,(A2)+
	BRA.S	SORTANDQ
CH111	MOVE	#$5048,(A2)+
	BRA.S	SORTANDQ
CHANDQ2	MOVE	#$41E8,(A2)+
	MOVE	VARI,(A2)+
SORTANDQ	RTS

OPTI_ADDQ_ORS:
	CLR	VARI
	MOVE	#$5449,D0
	MOVE	#$5849,D1
	MOVE	#$5C49,D2
	MOVE	#$5049,D3
	MOVE	#$43E9,D4
CONTORQ	CMP.W	-2(A2),D0
	BNE.S	ORQ2
	ADDQ.W	#2,VARI
	SUBQ.L	#2,A2
	BRA.S	CONTORQ
ORQ2	CMP.W	-2(A2),D1
	BNE.S	ORQ3
	ADDQ.W	#4,VARI
	SUBQ.L	#2,A2
	BRA.S	CONTORQ
ORQ3	CMP.W	-2(A2),D2
	BNE.S	ORQ4
	ADDQ.W	#6,VARI
	SUBQ.L	#2,A2
	BRA.S	CONTORQ
ORQ4	CMP.W	-2(A2),D3
	BNE.S	ORQ5
	ADDQ.W	#8,VARI
	SUBQ.L	#2,A2
	BRA.S	CONTORQ
ORQ5	CMP.W	-4(A2),D4
	BNE.S	SORTORQ
	MOVE	-2(A2),D0
	ADD.W	D0,VARI
	MOVE	#$5449,D0
	SUBQ.L	#4,A2
	BRA.S	CONTORQ
SORTORQ	TST.W	VARI
	BEQ	FINORQ
	CMPI.W	#8,VARI
	BGT	CHORQ2
	CMPI.W	#2,VARI
	BNE.S	ORQ11
	MOVE	#$5449,(A2)+
	BRA	FINORQ
ORQ11	CMPI.W	#4,VARI
	BNE.S	ORQ111
	MOVE	#$5849,(A2)+
	BRA	FINORQ
ORQ111	CMPI.W	#6,VARI
	BNE.S	ORQ1111
	MOVE	#$5C49,(A2)+
	BRA	FINORQ
ORQ1111	MOVE	#$5049,(A2)+
	BRA	FINORQ
CHORQ2	MOVE	#$43E9,(A2)+
	MOVE	VARI,(A2)+
FINORQ	RTS

ORS	TST.W	(A1)
	BEQ	AFF_MOV
	CMPI.W	#-1,(A1)
	BEQ	NO_AFF
	TST.L	(A0)
	BEQ	AFF3
	TST.L	2(A0)
	BEQ	AFF1
	TST.W	(A0)
	BEQ	AFF23
	TST.W	4(A0)
	BEQ	AFF12
;	TST.W	2(A0)
;	BEQ	AFF13
	MOVEQ	#0,D0
	MOVE	(A0),D0
	CMP.W	2(A0),D0
	BEQ	COMPARE1
	BRA	NOT1SAME
APRES1COMP	MOVEQ	#0,D0
	MOVE	4(A0),D0
	CLR	ADD1
	MOVE	#$8159,D4
	MOVE	#7,D3
CHERCHSP1	CMP.L	(A3),D0
	BEQ.S	TROUVê1
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSP1
	LEA	-32(A3),A3
	MOVE	#$0059,(A2)+
	MOVE	4(A0),(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVê1	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

COMPARE1	CLR	ADD1
	SWAP	D0
	MOVE	2(A0),D0
	MOVE	#$8199,D4
	MOVE	#7,D3
CHERCHCOMP1	CMP.L	(A3),D0
	BEQ.S	TROUVêC1
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHCOMP1
	LEA	-32(A3),A3
	MOVE	#$0099,(A2)+
	MOVE.L	(A0),(A2)+
	BRA	APRES1COMP
TROUVêC1	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	BRA	APRES1COMP

NOT1SAME	MOVE	(A0),D0
	CLR	ADD1
	MOVE	#$8159,D4
	MOVE	#7,D3
CHERCHSPA	CMP.L	(A3),D0
	BEQ.S	TROUVêA
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSPA
	LEA	-32(A3),A3
	MOVE	#$0099,(A2)+
	MOVE.L	(A0),(A2)+
	BRA	APRES1COMP
TROUVêA	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	2(A0),D0
	CLR	ADD1
	MOVE	#$8159,D4
	MOVE	#7,D3
CHERCHSPB	CMP.L	(A3),D0
	BEQ.S	TROUVêB
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSPB
	LEA	-32(A3),A3
	MOVE	#$0059,(A2)+
	MOVE	2(A0),(A2)+
	BRA	APRES1COMP
TROUVêB	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	BRA	APRES1COMP


AFF13	MOVEQ	#0,D0
	MOVE	(A0),D0
	CLR	ADD1
	MOVE	#$8159,D4
	MOVE	#7,D3
CHERCHSP13	CMP.L	(A3),D0
	BEQ.S	TROUVê13
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSP13
	LEA	-32(A3),A3
	MOVE	#$0059,(A2)+
	MOVE	(A0),(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	CONT2
TROUVê13	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD

CONT2	MOVEQ	#0,D0
	MOVE	4(A0),D0
	CLR	ADD1
	MOVE	#$8159,D4
	MOVE	#7,D3
CHERCHSP1_3	CMP.L	(A3),D0
	BEQ.S	TROUVê1_3
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSP1_3
	LEA	-32(A3),A3
	MOVE	#$0059,(A2)+
	MOVE	4(A0),(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVê1_3	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	
	BRA	FIN_AFF

AFF3	MOVE	#$5849,(A2)+
	BSR	OPTI_ADDQ_ORS
;	ADDQ.W	#1,ADD
	MOVEQ	#0,D0
	MOVE	4(A0),D0
	CLR	ADD1
	MOVE	#$8159,D4
	MOVE	#7,D3
CHERCHSP3	CMP.L	(A3),D0
	BEQ.S	TROUVê3
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSP3
	LEA	-32(A3),A3
	MOVE	#$0059,(A2)+
	MOVE	4(A0),(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVê3	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

AFF1	MOVEQ	#0,D0
	MOVE	(A0),D0
	CLR	ADD1
	MOVE	#$8159,D4
	MOVE	#7,D3
CHERCHSP11	CMP.L	(A3),D0
	BEQ.S	TROUVê11
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSP11
	LEA	-32(A3),A3
	MOVE	#$0059,(A2)+
	MOVE	(A0),(A2)+
	MOVE	#$5C49,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVê11	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	#$5C49,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

AFF12	MOVEQ	#0,D0
	MOVE	(A0),D0
	CMP.W	2(A0),D0
	BEQ	COMPARE2
	MOVE	#$0099,(A2)+
	MOVE.L	(A0),(A2)+
APRES2COMP	MOVE	#$5849,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

COMPARE2	CLR	ADD1
	MOVE	#$8159,D4
	MOVE	#7,D3
CHERCHCOMP2	CMP.L	(A3),D0
	BEQ.S	TROUVêC2
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHCOMP2
	LEA	-32(A3),A3
	MOVE	#$0099,(A2)+
	MOVE.L	(A0),(A2)+
	BRA	APRES2COMP
TROUVêC2	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	D4,(A2)+
	BRA	APRES2COMP

AFF23	BSR	OPTI_ADDQ_ORS
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	TST.W	4(A0)
	BEQ	AFF2
	MOVE	#$0099,(A2)+
	MOVE.L	2(A0),(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
AFF2	MOVEQ	#0,D0
	MOVE	2(A0),D0
	CLR	ADD1
	MOVE	#$8159,D4
	MOVE	#7,D3
CHERCHSP2	CMP.L	(A3),D0
	BEQ.S	TROUVê2
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSP2
	LEA	-32(A3),A3
	MOVE	#$0059,(A2)+
	MOVE	2(A0),(A2)+
	MOVE	#$5849,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVê2	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	#$5849,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

AFF_MOV	TST.L	(A0)
	BEQ	MOV3
;	TST.L	2(A0)
;	BEQ	MOV1
	TST.W	(A0)
	BEQ	MOV23
	TST.W	4(A0)
	BEQ	MOV12
;	TST.W	2(A0)
;	BEQ	MOV13
	MOVE	(A0),D0
	CMP.W	2(A0),D0
	BEQ	COMPARE3
	MOVE	(A0),D0
	CLR	ADD1
	MOVE	#$32C0,D4
	MOVE	#7,D3
CHERCHMV66	CMP.L	(A3),D0
	BEQ.S	TROUVêMV66
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV66
	LEA	-32(A3),A3
	BRA	CONT6
TROUVêMV66	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+

	MOVE	2(A0),D0
	CLR	ADD1
	MOVE	#$32C0,D4
	MOVE	#7,D3
CHERCHMV77	CMP.L	(A3),D0
	BEQ.S	TROUVêMV77
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV77
	LEA	-32(A3),A3
	MOVE	#$32FC,(A2)+
	MOVE	2(A0),(A2)+
	BRA	APRES3COMP
TROUVêMV77	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	BRA	APRES3COMP

CONT6	MOVE	#$22FC,(A2)+
	MOVE.L	(A0),(A2)+
APRES3COMP	MOVEQ	#0,D0
	MOVE	4(A0),D0
	CLR	ADD1
	MOVE	#$32C0,D4
	MOVE	#7,D3
CHERCHMV33	CMP.L	(A3),D0
	BEQ.S	TROUVêMV33
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV33
	LEA	-32(A3),A3
	MOVE	#$32FC,(A2)+
	MOVE	4(A0),(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVêMV33	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

COMPARE3	SWAP	D0
	MOVE	2(A0),D0
	CLR	ADD1
	MOVE	#$22C0,D4
	MOVE	#7,D3
CHERCHCOMP3	CMP.L	(A3),D0
	BEQ.S	TROUVêC3
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#1,D4
	DBRA	D3,CHERCHCOMP3
	LEA	-32(A3),A3
	MOVE	#$22FC,(A2)+
	MOVE.L	(A0),(A2)+
	BRA	APRES3COMP
TROUVêC3	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	BRA	APRES3COMP


MOV13	MOVEQ	#0,D0
	MOVE	(A0),D0
	CLR	ADD1
	MOVE	#$32C0,D4
	MOVE	#7,D3
CHERCHMV13	CMP.L	(A3),D0
	BEQ.S	TROUVêMV13
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV13
	LEA	-32(A3),A3
	MOVE	#$32FC,(A2)+
	MOVE	(A0),(A2)+
	MOVE	#$32CC,(A2)+
	BRA	CONT3
TROUVêMV13	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	#$32CC,(A2)+
CONT3	MOVEQ	#0,D0
	MOVE	4(A0),D0
	CLR	ADD1
	MOVE	#$32C0,D4
	MOVE	#7,D3
CHERCHMV1_3	CMP.L	(A3),D0
	BEQ.S	TROUVêMV1_3
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV1_3
	LEA	-32(A3),A3
	MOVE	#$32FC,(A2)+
	MOVE	(A0),(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVêMV1_3	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
MOV3	MOVE	#$22CC,(A2)+
	MOVEQ	#0,D0
	MOVE	4(A0),D0
	CLR	ADD1
	MOVE	#$32C0,D4
	MOVE	#7,D3
CHERCHMV3	CMP.L	(A3),D0
	BEQ.S	TROUVêMV3
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV3
	LEA	-32(A3),A3
	MOVE	#$32FC,(A2)+
	MOVE	4(A0),(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVêMV3	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
MOV1	MOVEQ	#0,D0
	MOVE	(A0),D0
	CLR	ADD1
	MOVE	#$32C0,D4
	MOVE	#7,D3
CHERCHMV1	CMP.L	(A3),D0
	BEQ.S	TROUVêMV1
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV1
	LEA	-32(A3),A3
	MOVE	#$32FC,(A2)+
	MOVE	4(A0),(A2)+
	MOVE	#$22CC,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVêMV1	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	#$22CC,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
	MOVE	#$32FC,(A2)+
	MOVE	(A0),(A2)+
	MOVE	#$22CC,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF


MOV23	MOVE	#$32CC,(A2)+
	TST.W	4(A0)
	BEQ	MOV2

	MOVEQ	#0,D0
	MOVE	2(A0),D0
	CMP.W	4(A0),D0
	BEQ	COMPARE5
	MOVE	#$22FC,(A2)+
	MOVE.L	2(A0),(A2)+
APRES5COMP	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

COMPARE5	CLR	ADD1
	SWAP	D0
	MOVE	4(A0),D0
	MOVE	#$22C0,D4
	MOVE	#7,D3
CHERCHCOMP5	CMP.L	(A3),D0
	BEQ.S	TROUVêC5
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#1,D4
	DBRA	D3,CHERCHCOMP5
	LEA	-32(A3),A3
	MOVE	#$22FC,(A2)+
	MOVE.L	2(A0),(A2)+
	BRA	APRES5COMP
TROUVêC5	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	BRA	APRES5COMP

MOV2	MOVEQ	#0,D0
	MOVE	2(A0),D0
	CLR	ADD1
	MOVE	#$32C0,D4
	MOVE	#7,D3
CHERCHMV2	CMP.L	(A3),D0
	BEQ.S	TROUVêMV2
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV2
	LEA	-32(A3),A3
	MOVE	#$32FC,(A2)+
	MOVE	2(A0),(A2)+
	MOVE	#$32CC,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVêMV2	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	#$32CC,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF


MOV12	MOVEQ	#0,D0
	MOVE	(A0),D0
	CMP.W	2(A0),D0
	BEQ	COMPARE4
	MOVE	(A0),D0
	CLR	ADD1
	MOVE	#$32C0,D4
	MOVE	#7,D3
CHERCHMVD	CMP.L	(A3),D0
	BEQ.S	TROUVêMVD
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMVD
	LEA	-32(A3),A3
	MOVE	#$22FC,(A2)+
	MOVE.L	(A0),(A2)+
	MOVE	#$32CC,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVêMVD	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	#$32FC,(A2)+
	MOVE	2(A0),(A2)+
	MOVE	#$32CC,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
	
APRES4COMP	MOVE	#$32CC,(A2)+
	MOVE	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

COMPARE4	CLR	ADD1
	MOVE	#$32C0,D4
	MOVE	#7,D3
CHERCHCOMP4	CMP.L	(A3),D0
	BEQ.S	TROUVêC4
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#1,D4
	DBRA	D3,CHERCHCOMP4
	LEA	-32(A3),A3
	MOVE	#$22FC,(A2)+
	MOVE.L	(A0),(A2)+
	BRA	APRES4COMP
TROUVêC4	SUBA.W	ADD1,A3
	MOVE	D4,(A2)+
	MOVE	D4,(A2)+
	BRA	APRES4COMP

NO_AFF	MOVE	#$5049,(A2)+	
	BSR	OPTI_ADDQ_ORS
;	ADDQ.W	#1,ADD
FIN_AFF	ADDQ.L	#8,A0
	ADDQ.L	#2,A1
	RTS

PREDECALET	LEA	BUF_DEC,A1
	LEA	PREDEC_TAB,A6
CHANGEY6	MOVE	#81-1,D6
CHANGE11
RECOP2	MOVE	#6-2,D7
RECOP11	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,RECOP11
	CLR.L	(A1)+
	CLR.L	(A1)+
CHANGE12	LEA	160-((6-1)*8)(A0),A0
	DBRA	D6,RECOP2

	LEA	BUF_DEC,A0
	MOVE.L	A0,(A6)+
	MOVE	#6,D3
CHANGE13
CHANGEY7
DECPGEN	LEA	6*8*81(A0),A1
	MOVE.L	A1,(A6)+
CHANGEY8
CHANGE14	MOVE	#(6*81)-1,D7
RECOP3	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,RECOP3

	MOVE	#1,D4
CHANGEY9
DECPG1	MOVE	#81-1,D5
DECP3	MOVE	#3,D6
CHANGE15
DECP2	MOVE	#6-1,D7
DECP1	ROXR	(A0)
	ADDQ.L	#8,A0
	DBRA	D7,DECP1
CHANGE16	LEA	-((6*8)-2)(A0),A0
	DBRA	D6,DECP2
CHANGE17	LEA	(6-1)*8(A0),A0
	DBRA	D5,DECP3
CHANGEY10
CHANGE18	LEA	-(6*8*81)(A0),A0
	DBRA	D4,DECPG1
	DBRA	D3,DECPGEN
	RTS

MAK_EFF	TST.W	FLAG_EFF
	BEQ	FLAGEFF_OFF
	MOVE.L	SCREEN2,A0	;MSK
	MOVE.L	SCREEN1,A1	;SPR
CHANGEY11	MOVE	#81-1,D6
CHANGE19
RECOMEF2	MOVE	#6-1,D7
RECOMEF1	CMPI.W	#-1,(A0)
	BEQ	QQQ
	TST.L	(A1)
	BEQ	EFF3
	TST.L	2(A1)
	BEQ	EFF1
	TST.W	(A1)
	BEQ	EFF23
	TST.W	2(A1)
	BEQ	EFF13
	TST.W	4(A1)
	BEQ	EFF12
EFF_ALL	MOVE	#$20C0,(A2)+	;6 NOPS
	MOVE	#$30C0,(A2)+
	MOVE	#$5448,(A2)+
	BRA.S	NO_EFF
QQQ	MOVE	#$5048,(A2)+
	BSR	OPTI_ADDQ_EFF
NO_EFF	ADDQ.L	#8,A1
	ADDQ.L	#2,A0
	DBRA	D7,RECOMEF1
	MOVE	#$41E8,(A2)+
CHANGE20	MOVE	#160-(6*8),(A2)+
	BSR	OPTI_ADDQ_EFF
	DBRA	D6,RECOMEF2
	MOVE	#$4E75,(A2)+
FLAGEFF_OFF	RTS

EFF3	MOVE	#$5848,(A2)+
	BSR	OPTI_ADDQ_EFF
	MOVE	#$30C0,(A2)+	;4 NOPS
	MOVE	#$5448,(A2)+
	BRA	NO_EFF

EFF1	MOVE	#$30C0,(A2)+	;3 NOPS
	MOVE	#$5C48,(A2)+
	BRA	NO_EFF

EFF23	TST.W	4(A1)
	BEQ	EFF2
	MOVE	#$5448,(A2)+	;5 NOPS
	BSR	OPTI_ADDQ_EFF
	MOVE	#$20C0,(A2)+
	MOVE	#$5448,(A2)+
	BRA	NO_EFF
EFF2	MOVE	#$5448,(A2)+	;4 NOPS
	BSR	OPTI_ADDQ_EFF
	MOVE	#$30C0,(A2)+
	MOVE	#$5848,(A2)+
	BRA	NO_EFF

EFF13	MOVE	#$30C0,(A2)+	;6 NOPS
	MOVE	#$5448,(A2)+
	MOVE	#$30C0,(A2)+
	MOVE	#$5448,(A2)+
	BRA	NO_EFF

EFF12	MOVE	#$20C0,(A2)+	;4 NOPS
	MOVE	#$5848,(A2)+
	BRA	NO_EFF

OPTI_ADDQ_EFF
	MOVE	#$5448,D0
	MOVE	#$5848,D1
	MOVE	#$5C48,D2
	MOVE	#$5048,D3
	MOVE	#$41E8,D4
	CLR	ADD1
CONT_OPTEF	CMP.W	-2(A2),D0
	BNE.S	ADEF2
	ADDQ.W	#2,ADD1
	SUBQ.L	#2,A2
	BRA	CONT_OPTEF
ADEF2	CMP.W	-2(A2),D1
	BNE.S	ADEF3
	ADDQ.W	#4,ADD1
	SUBQ.L	#2,A2
	BRA	CONT_OPTEF
ADEF3	CMP.W	-2(A2),D2
	BNE.S	ADEF4
	ADDQ.W	#6,ADD1
	SUBQ.L	#2,A2
	BRA	CONT_OPTEF
ADEF4	CMP.W	-2(A2),D3
	BNE.S	ADEF5
	ADDQ.W	#8,ADD1
	SUBQ.L	#2,A2
	BRA	CONT_OPTEF
ADEF5	CMP.W	-4(A2),D4
	BNE.S	FIN_OPTEF
	MOVE	-2(A2),D0
	ADD.W	D0,ADD1
	MOVE	#$5448,D0
	SUBQ.L	#4,A2
	BRA	CONT_OPTEF
FIN_OPTEF	TST.W	ADD1
	BEQ	FINI_OPTEF
	CMPI.W	#8,ADD1
	BGT	MAK_LEAEF
	CMPI.W	#2,ADD1
	BNE.S	TESTEF2
	MOVE	#$5448,(A2)+
	BRA	FINI_OPTEF
TESTEF2	CMPI.W	#4,ADD1
	BNE.S	TESTEF3
	MOVE	#$5848,(A2)+
	BRA	FINI_OPTEF
TESTEF3	CMPI.W	#6,ADD1
	BNE.S	TESTEF4
	MOVE	#$5C48,(A2)+
	BRA	FINI_OPTEF
TESTEF4	MOVE	#$5048,(A2)+
	BRA	FINI_OPTEF
MAK_LEAEF	MOVE	#$41E8,(A2)+
	MOVE	ADD1,(A2)+
FINI_OPTEF	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	MOVE	#7999,D0
NETTOIE	CLR.L	(A0)+
	CLR.L	(A1)+
	DBF	D0,NETTOIE
	RTS
	
; a0: Pointer on packed Data
; a1: Pointer on destination of unpacked data
decrunch:	movem.l d0-a6,-(sp)
		cmpi.l	#'Ice!',(a0)+	; Is data packed?
		bne.s	ice_03		; no!
		move.l	(a0)+,d0	; read packed data
		lea	-8(a0,d0.l),a5
		move.l	(a0)+,(sp)
		movea.l a1,a4
		movea.l a1,a6
		adda.l	(sp),a6
		movea.l a6,a3
		bsr.s	ice_08
		bsr.s	ice_04
		bsr	ice_0c		;; Picture decrunch!
		bcc.s	ice_03		;; These marked lines may be
		MOVE	#$0f9f,d7	;; removed in your own sources
ice_00:		moveq	#3,d6		;; if you do not use the
ice_01:		MOVE	-(a3),d4	;; additional algorithm.
		moveq	#3,d5		;;
ice_02:		add.w	d4,d4		;;
		addx.w	d0,d0		;;
		add.w	d4,d4		;;
		addx.w	d1,d1		;;
		add.w	d4,d4		;;
		addx.w	d2,d2		;;
		add.w	d4,d4		;;
		addx.w	d3,d3		;;
		dbra	d5,ice_02	;;
		dbra	d6,ice_01	;;
		movem.w d0-d3,(a3)	;;
		dbra	d7,ice_00	;;
ice_03:		movem.l (sp)+,d0-a6
		rts
ice_04:		bsr.s	ice_0c
		bcc.s	ice_07
		moveq	#0,d1
		bsr.s	ice_0c
		bcc.s	ice_06
		lea	ice_1a(pc),a1
		moveq	#4,d3
ice_05:		move.l	-(a1),d0
		bsr.s	ice_0f
		swap	d0
		cmp.w	d0,d1
		dbne	d3,ice_05
		add.l	20(a1),d1
ice_06:		move.b	-(a5),-(a6)
		dbra	d1,ice_06
ice_07:		cmpa.l	a4,a6
		bgt.s	ice_12
		rts
ice_08:		moveq	#3,d0
ice_09:		move.b	-(a5),d7
		ror.l	#8,d7
		dbra	d0,ice_09
		rts
ice_0a:		MOVE	a5,d7
		btst	#0,d7
		bne.s	ice_0b
		move.l	-(a5),d7
		addx.l	d7,d7
		bra.s	ice_11
ice_0b:		move.l	-5(a5),d7
		lsl.l	#8,d7
		move.b	-(a5),d7
		subq.l	#3,a5
		add.l	d7,d7
		bset	#0,d7
		bra.s	ice_11
ice_0c:		add.l	d7,d7
		beq.s	ice_0d
		rts
ice_0d:		MOVE	a5,d7
		btst	#0,d7
		bne.s	ice_0e
		move.l	-(a5),d7
		addx.l	d7,d7
		rts
ice_0e:		move.l	-5(a5),d7
		lsl.l	#8,d7
		move.b	-(a5),d7
		subq.l	#3,a5
		add.l	d7,d7
		bset	#0,d7
		rts
ice_0f:		moveq	#0,d1
ice_10:		add.l	d7,d7
		beq.s	ice_0a
ice_11:		addx.w	d1,d1
		dbra	d0,ice_10
		rts
ice_12:		lea	ice_1b(pc),a1
		moveq	#3,d2
ice_13:		bsr.s	ice_0c
		dbcc	d2,ice_13
		moveq	#0,d4
		moveq	#0,d1
		move.b	1(a1,d2.w),d0
		ext.w	d0
		bmi.s	ice_14
		bsr.s	ice_0f
ice_14:		move.b	6(a1,d2.w),d4
		add.w	d1,d4
		beq.s	ice_16
		lea	ice_1c(pc),a1
		moveq	#1,d2
ice_15:		bsr.s	ice_0c
		dbcc	d2,ice_15
		moveq	#0,d1
		move.b	1(a1,d2.w),d0
		ext.w	d0
		bsr.s	ice_0f
		add.w	d2,d2
		add.w	6(a1,d2.w),d1
		bra.s	ice_18
ice_16:		moveq	#0,d1
		moveq	#5,d0
		moveq	#0,d2
		bsr.s	ice_0c
		bcc.s	ice_17
		moveq	#8,d0
		moveq	#$40,d2
ice_17:		bsr.s	ice_0f
		add.w	d2,d1
ice_18:		lea	2(a6,d4.w),a1
		adda.w	d1,a1
		move.b	-(a1),-(a6)
ice_19:		move.b	-(a1),-(a6)
		dbra	d4,ice_19
		bra	ice_04
		DC.B $7f,$ff,$00,$0e,$00,$ff,$00,$07
		DC.B $00,$07,$00,$02,$00,$03,$00,$01
		DC.B $00,$03,$00,$01
ice_1a:		DC.B $00,$00,$01,$0d,$00,$00,$00,$0e
		DC.B $00,$00,$00,$07,$00,$00,$00,$04
		DC.B $00,$00,$00,$01
ice_1b:		DC.B $09,$01,$00,$ff,$ff,$08,$04,$02
		DC.B $01,$00
ice_1c:		DC.B $0b,$04,$07,$00,$01,$20,$00,$00
		DC.B $00,$20
	
	SECTION	DATA
ZIK	INCBIN	MUSIX.MUS
PIC	INCBIN	zappy80_.PI1
FIC_OPT	INCBIN	zappy1.OPT

	DS.L	100
COURB	INCBIN	BIGGIE4.CRB
FIN_CRB	DS.L	200
	DC.W	-1

TABLE_SPR	DC.L	DECRUNCHED+34		Z
	DC.L	DECRUNCHED+34+40	A
	DC.L	DECRUNCHED+34+80	P

	SECTION	BSS
ANC_PAL	DS.L	8
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
SAUV_SP	DS.L	1
SAUVEC	DS.W	1
MFP	DS.B	8
	DS.B	256
BUFFER	DS.L	16000
	DS.B	256
SCREEN1	DS.L	1
SCREEN2	DS.L	1
PREDEC_TAB	DS.L	8
BUF_DEC	DS.L	(12*81)*8
PNT_CRB	DS.L	1
VARI	DS.W	1
ADD1	DS.W	1
ANNEX	DS.L	1
FLAG_EFF	DS	1

TABLE_GEN	DS.B	600
DECRUNCHED2
DECRUNCHED	DS.B	128*160+66
BUF_COD	DS.B	195000	ZAPPY=193618
;			ZERIOUL=193770
;			DAN=133978