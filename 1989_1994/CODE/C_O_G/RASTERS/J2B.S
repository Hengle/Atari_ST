START 	CLR.L	-(SP)		*superviseur
	MOVE	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP
		
	BSR	SAVE_INTS

	MOVE.W	#$2700,SR
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE.W	#$8800,A0
	MOVEQ	#$A,D0
SET_YM1	MOVE.B	D1,(A0)	;ON REMET LES 10 REGISTRES SONORES A ZERO
	CLR.B	2(A0)
	ADDQ.W	#1,D1
	DBRA	D0,SET_YM1

	MOVE.B	#7,(A0)
	MOVE.B	#$FF,2(A0)

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.B	#$20,$FFFFFA07.W
	MOVE.B	$FFFFFA07.W,$FFFFFA13.W
	MOVE.B	$FFFFFA09.W,$FFFFFA15.W
	MOVE.L	#PLAY_B,$134.W
	MOVE.B	#38,$FFFFFA1F.W
	MOVE.B	#1,$FFFFFA19.W
	MOVE.B	#$40,$FFFFFA17.W
	MOVE.L	#BEAT,BEAT1
	MOVE.L	#VBLR,$70.W

	LEA	BEAT,A0
	;MOVE.W	#((TEST_FIN-BEAT)/2)-1,D7
.CONT	MOVE.B	(A0),D0
	ADDI.B	#$80,D0
	MOVE.B	D0,(A0)+
	MOVE.B	(A0),D0
	ADDI.B	#$80,D0
	MOVE.B	D0,(A0)+
	CMP.L	#FINI,A0
	BNE.S	.CONT
	;DBRA	D7,.CONT

	LEA	BEAT,A0
	LEA	TEST_FIN,A1
	MOVE.W	#255,D7
.A	MOVE.B	(A0)+,(A1)+
	DBRA	D7,.A
	MOVE.L	BEAT1,A0
	MOVE.W	#$2300,SR

TEST	CMPI.B	#$39,$FFFFFC02.W
	BNE.S	TEST

	BSR	OLD_INTS

	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	TRAP	#1

SAUV_SP	DS.L	1

VBLR	CMPA.L	#TEST_FIN,A0
	BLE.S	.NO
	LEA	BEAT,A0
.NO	MOVE.W	#$8800,A1
	LEA	CODE,A2
	RTE

SAVE_INTS	MOVE.W	#$FA00,A0
	LEA	BUF_MFP,A1
	MOVE.B 	7(A0),(A1)+
	MOVE.B	9(A0),(A1)+
	MOVE.B	$13(A0),(A1)+
	MOVE.B	$19(A0),(A1)+
	MOVE.B	$1F(A0),(A1)+
	RTS	
	
OLD_INTS	LEA	BUF_MFP,A0
	MOVE.W	#$FA00,A1
	MOVE.B	(A0)+,7(A1)
	MOVE.B	(A0)+,9(A1)
	MOVE.B	(A0)+,$13(A1)
	MOVE.B	(A0)+,$19(A1)
	MOVE.B	(A0)+,$1F(A1)
	RTS

BUF_MFP	DS.B	6

	DS.L	256
STACK	DS.L	256

PLAY_B	;ST	$FFFF8240.W
	MOVEQ	#0,D0	;1
	MOVE.B	(A0)+,D0	;2
	LSL.W	#3,D0	;3
	MOVE.L	0(A2,D0.W),D1	;5
	MOVE.W	4(A2,D0.W),D2	;5
	MOVEP.L	D1,(A1)	;6
	MOVEP.W	D2,(A1)	;4     ;=26 NOPS
	;SF	$FFFF8240.W
	RTE	;5    ;=31 NOPS

BEAT1	DS.L	1
BEAT	INCBIN	b:\CHA_LA.SPL
TEST_FIN	DCB.B	256,0
FINI
CODE	INCBIN	TABLEREP.BIN
