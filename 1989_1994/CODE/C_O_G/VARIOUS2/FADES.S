***************************************************************************
* CE SOURCE COMPREND : - UN SOURCE DE FADE-UP STE                         *
*                      - UN SOURCE DE FADE-DOWN STE                       *
*                      - UN SOURCE DE FADE-ULTIME STE                     *
*                      - UN SOURCE DE FADE-UP STF                         *
*                      - UN SOURCE DE FADE-DOWN STF                       *
*                      - UN SOURCE DE FADE-ULTIME STF                     *
* TOTAL : 506 OCTETS !!
***************************************************************************

***FADE UP STE : AFFICHE PROGRESSIVEMENT LES COULEURS D'UNE IMAGE STE
***ENTRêES     : A0 ADRESSE DE LA PALETTE OPTIMALE
***            : A1 ADRESSE $FF8240 OU D'UN AUTRE BUFFER
***            : D7 NB DE COULEURS A "FADER"
***SORTIES     : D6 NB DE COULEURS DEJA "FADêE(S)"
FADE_UP_STE	MOVEQ	#0,D6
	MOVE.W	(A0)+,D0
	MOVE.W	D0,D1
	MOVE.W	D0,D2
	ANDI.W	#$000F,D0
	ANDI.W	#$00F0,D1
	ANDI.W	#$0F00,D2
	MOVE.W	(A1),D3
	MOVE.W	D3,D4
	MOVE.W	D3,D5
	ANDI.W	#$000F,D3
	ANDI.W	#$00F0,D4
	ANDI.W	#$0F00,D5
	CMP.W	D0,D3
	BEQ.S	.BLEU_OK
	BTST	#3,D3
	BEQ.S	.MIS_BLEU
	ADDQ.W	#1,D3
.MIS_BLEU	EOR.W	#8,D3
.BLEU_OK	CMP.W	D1,D4
	BEQ.S	.VERT_OK
	BTST	#7,D4
	BEQ.S	.MIS_VERT
	ADDI.W	#$10,D4
.MIS_VERT	EOR.W	#$80,D4
.VERT_OK	CMP.W	D2,D5
	BEQ.S	.ROUGE_OK
	BTST	#11,D5
	BEQ.S	.MIS_ROUGE
	ADDI.W	#$100,D5
.MIS_ROUGE	EOR.W	#$800,D5
.ROUGE_OK	OR.W	D4,D3
	OR.W	D5,D3
	MOVE.W	D3,(A1)+
	OR.W	D1,D0
	OR.W	D2,D0
	CMP.W	D3,D0
	BNE.S	.PAS_EGAL
	ADDQ.W	#1,D6
.PAS_EGAL	DBRA	D7,FADE_UP_STE+2
	RTS

***FADE DOWN STE : AMENE PROGRESSIVEMENT LA PALETTE D'1 IMAGE A 0
***ENTRêES       : A0 ADRESSE $FF8240 OU D'UN BUFFER QUELCONQUE
***              : D7 NB DE COULEURS A "FADER"
***SORTIES       : D6 NB DE COULEURS DEJA "FADêE(S)"
FADE_DOWN_STE	MOVEQ	#0,D6
	MOVE.W	(A0),D0
	MOVE.W	D0,D1
	MOVE.W	D0,D2
	ANDI.W	#$000F,D0
	ANDI.W	#$00F0,D1
	ANDI.W	#$0F00,D2
	TST.W	D0
	BEQ.S	.BLEU_OK
	BTST	#3,D0
	BNE.S	.MIS_BLEU
	SUBQ.W	#1,D0
.MIS_BLEU	EOR.W	#8,D0
.BLEU_OK	TST.W	D1
	BEQ.S	.VERT_OK
	BTST	#7,D1
	BNE.S	.MIS_VERT
	SUBI.W	#$10,D1
.MIS_VERT	EOR.W	#$80,D1
.VERT_OK	TST.W	D2
	BEQ.S	.ROUGE_OK
	BTST	#11,D2
	BNE.S	.MIS_ROUGE
	SUBI.W	#$100,D2
.MIS_ROUGE	EOR.W	#$800,D2
.ROUGE_OK	OR.W	D1,D0
	OR.W	D2,D0
	MOVE.W	D0,(A0)+
	BNE.S	.PAS_0
	ADDQ.W	#1,D6
.PAS_0	DBRA	D7,FADE_DOWN_STE+2
	RTS

***ULTIMATE FADE STE : AMENE CHACUNE DES COULEURS D'1 PALETTE A $FFF
***ENTRêES           : A0 ADRESSE $FF8240 OU D'UN BUFFER QUELCONQUE
***                  : D7 NB DE COULEURS A "FADER"
***SORTIE            : D6 NB DE COULEURS DEJA "FADêE(S)"
FADE_ULTIME_STE
	MOVEQ	#0,D6
	MOVE.W	(A0),D0
	MOVE.W	D0,D1
	MOVE.W	D0,D2
	ANDI.W	#$000F,D0
	ANDI.W	#$00F0,D1
	ANDI.W	#$0F00,D2
	CMPI.W	#$F,D0
	BEQ.S	.BLEU_OK
	BTST	#3,D0
	BEQ.S	.MIS_BLEU
	ADDQ.W	#1,D0
.MIS_BLEU	EOR.W	#8,D0
.BLEU_OK	CMPI.W	#$F0,D1
	BEQ.S	.VERT_OK
	BTST	#7,D1
	BEQ.S	.MIS_VERT
	ADDI.W	#$10,D1
.MIS_VERT	EOR.W	#$80,D1
.VERT_OK	CMPI.W	#$F00,D2
	BEQ.S	.ROUGE_OK
	BTST	#11,D2
	BEQ.S	.MIS_ROUGE
	ADDI.W	#$100,D2
.MIS_ROUGE	EOR.W	#$800,D2
.ROUGE_OK	OR.W	D1,D0
	OR.W	D2,D0
	MOVE.W	D0,(A0)+
	CMPI.W	#$FFF,D0
	BNE.S	.PAS_ENCORE_U
	ADDQ.W	#1,D6
.PAS_ENCORE_U	DBRA	D7,FADE_ULTIME_STE+2
	RTS

***ULTIMATE FADE : AMENE PROGRESSIVEMENT LES COULEURS D'1 PALETTE AU BLANC
***ENTRêES       : A0 ADRESSE $FF8240 OU D'UN BUFFER QUELCONQUE
***              : D7 NB DE COULEURS A "FADER"
***SORTIE        : D6 NB DE COULEURS DEJA "FADêE(S)"
FADE_ULTIME	MOVEQ	#0,D6
	MOVE.W	(A0),D0
	MOVE.W	D0,D1
	MOVE.W	D0,D2
	ANDI.W	#$000F,D0
	ANDI.W	#$00F0,D1
	ANDI.W	#$0F00,D2
	CMPI.W	#7,D0
	BEQ.S	.BLEU_OK
	ADDQ.W	#1,D0
.BLEU_OK	CMPI.W	#$70,D1
	BEQ.S	.VERT_OK
	ADDI.W	#$10,D1
.VERT_OK	CMPI.W	#$700,D2
	BEQ.S	.ROUGE_OK
	ADDI.W	#$100,D2
.ROUGE_OK	OR.W	D1,D0
	OR.W	D2,D0
	MOVE.W	D0,(A0)+
	CMPI.W	#$777,D0
	BNE.S	.PAS_ENCORE_U
	ADDQ.W	#1,D6
.PAS_ENCORE_U	DBRA	D7,FADE_ULTIME+2
	RTS

***FADE DOWN : RAMENE PROGRESSIVEMENT LES COULEURS D'UNE PALETTE A 0
***ENTRêES   : A0 ADRESSE $FF8240 OU D'UN BUFFER QUELCONQUE
***          : D7 NB DE COULEURS A "FADER"
***SORTIE    : D6 NB DE COULEURS DEJA "FADêE(S)"
FADE_DOWN	MOVEQ	#0,D6
	MOVE.W	(A0),D0
	MOVE.W	D0,D1
	MOVE.W	D0,D2
	ANDI.W	#$000F,D0
	ANDI.W	#$00F0,D1
	ANDI.W	#$0F00,D2
	TST.W	D0
	BEQ.S	.BLEU_OK
	SUBQ.W	#1,D0
.BLEU_OK	TST.W	D1
	BEQ.S	.VERT_OK
	SUBI.W	#$10,D1
.VERT_OK	TST.W	D2
	BEQ.S	.ROUGE_OK
	SUBI.W	#$100,D2
.ROUGE_OK	OR.W	D1,D0
	OR.W	D2,D0
	MOVE.W	D0,(A0)+
	BNE.S	.PAS_0
	ADDQ.W	#1,D6
.PAS_0	DBRA	D7,FADE_DOWN+2
	RTS

***FADE UP : AFFICHE PROGRESSIVEMENT LES COULEURS D'UNE IMAGE
***ENTRêES : A0 ADRESSE DE LA PALETTE DE L'IMAGE
***        : A1 ADRESSE $FF8240 OU D'UN BUFFER QUELCONQUE
***        : D7 NB DE COULEURS A "FADER"
***SORTIE  : D6 NB DE COULEURS DEJA "FADêES"
FADE_UP	MOVEQ	#0,D6
	MOVE.W	(A0)+,D0	;COULEUR VERS LAQUELLE ON DOIT
			;TENDRE
	MOVE.W	D0,D1
	MOVE.W	D0,D2
	ANDI.W	#$0007,D0
	ANDI.W	#$0070,D1
	ANDI.W	#$0700,D2
	MOVE.W	(A1),D3 	COULEUR ACTUELLE
	MOVE.W	D3,D4
	MOVE.W	D3,D5
	ANDI.W	#$0007,D3
	ANDI.W	#$0070,D4
	ANDI.W	#$0700,D5
	CMP.W	D3,D0
	BEQ.S	.BLEU_OK
	ADDQ.W	#1,D3
.BLEU_OK	CMP.W	D4,D1
	BEQ.S	.VERT_OK
	ADDI.W	#$10,D4
.VERT_OK	CMP.W	D5,D2
	BEQ.S	.ROUGE_OK
	ADDI.W	#$100,D5
.ROUGE_OK	OR.W	D4,D3
	OR.W	D5,D3
	MOVE.W	D3,(A1)+
	OR.W	D1,D0
	OR.W	D2,D0
	CMP.W	D3,D0
	BNE.S	.PAS_BON
	ADDQ.W	#1,D6
.PAS_BON	DBRA	D7,FADE_UP+2
	RTS