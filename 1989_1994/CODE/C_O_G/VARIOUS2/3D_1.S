	PEA	0.W
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP
	MOVE.L	D0,SAUV_SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#OFFSET_TABLE,-(SP)
	MOVE.L	#BUF_STARS,-(SP)
	MOVE.L	#DATA_STARS,-(SP)
	MOVE.L	#PT_STARS,-(SP)
	JSR	CALCULE_STARS

	MOVE.W	#$444,$FFFF8242.W
	MOVE.W	#$222,$FFFF8244.W
	MOVE.W	#$111,$FFFF8246.W

	JMP	VBL

	INCLUDE	K_STARS.S

VBL	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	PAS_TOUCHE
	ST	$FFFF8240.W
PAS_TOUCHE	MOVE.L	PT_STARS,A4
	MOVE.L	-4(A4),A0
	MOVE.L	$44E.W,A2	
	MOVEQ	#0,D0
	MOVE.W	(A0)+,D7
CONT_EFF	ADDQ.W	#4,A0
	MOVE.W	(A0)+,D1
	MOVE.L	D0,(A2,D1.W)
	DBRA	D7,CONT_EFF

	MOVE.L	(A4)+,A0
	MOVE.W	(A0)+,D7
CONT_AFF	MOVE.L	(A0)+,D0
	MOVE.W	(A0)+,D1
	OR.L	D0,(A2,D1.W)
	DBRA	D7,CONT_AFF

	CMPA.L	#FIN_TABLE,A4
	BNE.S	PAS_FINI_STARS
	LEA	OFFSET_TABLE,A4
PAS_FINI_STARS	MOVE.L	A4,PT_STARS
	SF	$FFFF8240.W
	MOVE.W	#$25,-(SP)
	TRAP	#14
	ADDQ.W	#2,SP
	CMPI.B	#$39,$FFFFFC02.W
	BNE	VBL
	
	MOVE.W	#$2,$FFFF8240.W
	MOVE.W	#$AAA,$FFFF8246.W

	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	TRAP	#1
	ADDQ.W	#2,SP

DATA_STARS	INCBIN	STARSR3D.STR
	EVEN

	SECTION	BSS
SAUV_SP	DS.L	1
PT_STARS	DS.L	1
	DS.L	2
OFFSET_TABLE	DS.L	(768/VITESSE)
FIN_TABLE
BUF_STARS	DS.B	89000