;PROGRAMME DE FABRICATION DE TABLES D'ETOILES 3D PRECALCULêES
;(c)F.W 1992
;COMMENCê LE 15-05-92 A 22 H 00, ACHEVê LE 16-05-92 A 23 H 15
;v.1.1 ROTATIONS AUTOUR DE Z DISPONIBLES !!
;FORMULE UTILISêE DANS CE PROGRAMME:
;TEMPS = 1.5 SEC POUR 200 ETOILES, VITESSE 8, SANS ROTATIONS !!
;X=Xe/Q     &     Y=YE/Q
;Q=|P|/(|P|+Ze)
;
PT_DEPART = 0
NB_STARS = 200		NOMBRE D'ETOILES (PAS TROP QUAND MEME)
VITESSE = 8		LA VITESSE EST COMPRISE ENTRE 0 ET 767
;
P = 256		LE POINT DE FUITE EST POSITIF
P_DEC = 8		2^P_DEC=P
X_CENTER = 160		CENTRE X ECRAN
Y_CENTER = 100		CENTRE Y ECRAN
;
LIM_Z_1 = 65		0-LIM_1_Z=PLAN 1       LIM_1_Z-LIM_2_Z=PLAN 2
LIM_Z_2 = 300		LIM_2_Z-512=PLAN 3
;
SHOW_TIME = 1
;
ROTATION = 1		1 POUR LES ROTATIONS AUTOUR DE Z, 0 SINON.
;
CLIP_HAUT = 0
CLIP_BAS = 199
CLIP_DROIT = 319
CLIP_GAUCHE = 0
;
;REMARQUE : PENSEZ A DEFINIR UNE BSS, COMPORTANT BUFSTARS DS.B XXXXXX,
;           PENSEZ A DEFINIR OFFSET_TABLE DS.L (768/VITESSE) ET DS.L 2 JUSTE AVANT POUR L'EFFACAGE
;           PENSER A DECLARER LE POINTEUR PT_STARS DS.L 1

;ENTREE : LES PARAMETRES SONT PASSêS PAR LA PILE :
;	MOVE.L #BUF_STARS,-(SP)
;	MOVE.L #DATA_STARS,-(SP)
;	MOVE.L #PT_STARS,-(SP)
;	MOVE.L #OFFSET_TABLE,-(SP)
;	JSR CALCULE_STARS
;
;EXEMPLE MEMOIRE : VITESSE            NB_STARS        MEMOIRE
;	       9                 50             21000
;	       9                 200            80000
;	       9                 250            100000
;	       9                 300            120000 
;	       8                 50             23000
;	       8                 200            90000
;	       8                 250            112000
;	       8                 300            135000
;ROTATION	       8	          200	           89000

	OPT	O+,W-

CALCULE_STARS	LINK	A5,#-20

	JMP	INIT_STARS

REVIENT_INIT	MOVE.L	16(A5),A4	BUFFER DE DESTINATION
	MOVE.L	20(A5),A2	ADRESSE DE LA TABLE D'OFFSET

	IFNE	ROTATION
	 MOVE.L	#TABLE_SINUS,PT_SINUS
	 MOVE.L	#TABLE_COSINUS,PT_COSINUS
	ENDC

	MOVE.W	#(768/VITESSE)-1,D6
RECOM_TABLE	MOVE.L	A4,(A2)+
	MOVE.L	A4,PT_SOV
	ADDQ.W	#2,A4
	CLR.W	COMPT
	MOVE.L	12(A5),A3
	LEA	PT_DEPART*10(A3),A3
	MOVE.W	#NB_STARS-1,D7
RECOM_CALCULE	
	IFNE	SHOW_TIME
	 NOT.W	$FFFF8240.W
	ENDC
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVE.W	(A3)+,D0	D0=Xe
	MOVE.W	(A3)+,D1	D1=Ye
	MOVE.W	(A3),D2	D2=Ze
	MOVE.W	D2,D5
	MOVE.W	D2,D4
	SUBI.W	#VITESSE,D5
	CMPI.W	#-256,D5
	BGT.S	PAS_CLIP_Z
	ADDI.W	#768,D5
	MOVE.W	4(A3),2(A3)
PAS_CLIP_Z	MOVE.W	D5,(A3)+

	IFNE	ROTATION
	 EXT.L	D0
	 EXT.L	D1
	 MOVEM.L	D2-D5,-(SP)
	 MOVE.L	PT_SINUS,A0
	 MOVE.L	PT_COSINUS,A1
	 MOVE.W	(A3),D5	OFFSET POUR SIN & COS
	 MOVE.W	(A0,D5.W),D2	SIN T
	 MOVE.W	(A1,D5.W),D3	COS_T
	 EXT.L	D3
	 EXT.L	D2
	 MULS.W	D0,D3	D3=XCOS T
	 MULS.W	D1,D2	D2=YSIN T
	 SUB.L	D2,D3	D3=X COS T -Y SIN T
	 MOVE.W	(A0,D5.W),D4	SIN T
	 MOVE.W	(A1,D5.W),D5	COS_T
	 MULS.W	D0,D4	D4=X SIN T
	 MULS.W	D1,D5	D5=Y COS T
	 ADD.L	D4,D5	D5=X SIN T + Y COS T
	 MOVE.L	D3,D0	D0=NEW X
	 MOVE.L	D5,D1	D1=NEW Y
	 MOVE.W	(A3),D5

	 ADDQ.W	#2,D5
	 CMPI.W	#720,D5
	 BLT.S	OK_REPLACE
	 MOVEQ	#0,D5
OK_REPLACE	 MOVE.W	D5,(A3)+
	 ADDQ.W	#2,A3
	 MOVEM.L	(SP)+,D2-D5
	ELSE
	 EXT.L	D0
	 EXT.L	D1
	 ASL.L	#P_DEC,D0	Xe=Xe*256
	 ASL.L	#P_DEC,D1	Ye=Ye*256
	 ADDQ.W	#4,A3
	ENDC

	MOVE.W	#P,D3	D3=|P|
	ADD.W	D3,D2	D2=|P|+Ze
	TST.W	D2
	BGT.S	PAS_Q_INF_0
	MOVE.W	#0,D0
	MOVE.W	#0,D1
	BRA	PAS_POZ
PAS_Q_INF_0	DIVS.W	D2,D0
	DIVS.W	D2,D1
	ADDI.W	#X_CENTER,D0
	ADDI.W	#Y_CENTER,D1

	CMPI.W	#CLIP_GAUCHE,D0
	BGE.S	X_POSITIF
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	BRA.S	PAS_POZ
X_POSITIF	CMPI.W	#CLIP_DROIT,D0
	BLE.S	X_CLIPPê
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	BRA.S	PAS_POZ
X_CLIPPê	CMPI.W	#CLIP_HAUT,D1
	BGE.S	Y_POSITIF
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	BRA.S	PAS_POZ
Y_POSITIF	CMPI.W	#CLIP_BAS,D1
	BLE.S	Y_CLIPPê
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	BRA.S	PAS_POZ
Y_CLIPPê
	LEA	TABLE_X,A0
	LEA	TABLE_Y,A1
	ASL.L	#2,D0
	MOVE.W	2(A0,D0.W),D2	INCREMENT
	MOVE.W	(A0,D0.W),D0	MOTIF
	ASL.L	#1,D1
	MOVE.W	(A1,D1.W),D1	Y*160
	ADD.W	D2,D1
	CMPI.W	#LIM_Z_1,D4
	BGE.S	SECOND_TEST
	SWAP	D0
	CLR.W	D0	MOTIF SUR LE PREMIER PLAN
	BRA.S	OK
SECOND_TEST	CMPI.W	#LIM_Z_2,D4
	BGE.S	THIRD_TEST
	SWAP	D0
	CLR.W	D0
	SWAP	D0	MOTIF SUR LE DEUXIEME PLAN
	BRA.S	OK
THIRD_TEST	MOVE.W	D0,-(SP)
	SWAP	D0
	MOVE.W	(SP)+,D0	MOTIF SUR LES 2 PREMIERS PLANS
OK	TST.L	D0
	BEQ.S	PAS_POZ
	MOVE.L	D0,(A4)+
	MOVE.W	D1,(A4)+
	ADDQ.W	#1,COMPT
PAS_POZ	DBRA	D7,RECOM_CALCULE
	MOVE.L	PT_SOV,A0
	MOVE.W	COMPT,(A0)
	SUBQ.W	#1,(A0)
	DBRA	D6,RECOM_TABLE

	MOVE.L	20(A5),A3
	LEA	(768/VITESSE)*4(A3),A2
	MOVE.L	-4(A2),-4(A3)

	MOVE.L	8(A5),A2
	MOVE.L	20(A5),(A2)

	UNLK	A5
	RTS

INIT_STARS	MOVE.L	12(A5),A0	BUFFER DE DESTINATION
	LEA	PT_DEPART*10(A0),A0
	MOVE.W	#NB_STARS-1,D5
REPEAT_STARS	MOVE.W	4(A0),D0	Z0
	MOVE.W	#(768/VITESSE)-1,D7
TEST_Z	SUBI.W	#VITESSE,D0
	CMPI.W	#-256,D0
	BGT.S	PAS_LOOP_Z
	ADDI.W	#768,D0	Z
	BRA.S	FINI_TEST_Z
PAS_LOOP_Z	DBRA	D7,TEST_Z
FINI_TEST_Z	ADDQ.W	#1,D7
	ADD.W	D7,D7
	MOVE.W	#718,D6
	SUB.W	D7,D6
	MOVE.W	D6,8(A0)
	LEA	10(A0),A0
	DBRA	D5,REPEAT_STARS
	JMP	REVIENT_INIT

TABLE_X	
N	SET	0
	REPT	20
	DC.W	32768,N,16384,N,8192,N,4096,N,2048,N,1024,N,512,N,256,N,128,N,64,N,32,N,16,N,8,N,4,N,2,N,1,N
N	SET	N+8
	ENDR

TABLE_Y
N	SET	0
	REPT	200
	DC.W	N*160
N	SET	N+1
	ENDR

	IFNE	ROTATION
TABLE_SINUS	INCBIN	SINUS.STR
	EVEN

TABLE_COSINUS	INCBIN	COSINUS.STR
	EVEN
PT_SINUS	DS.L	1
PT_COSINUS	DS.L	1
	ENDC

COMPT	DS.W	1
OFFSET	DS.L	1
PT_SOV	DS.L	1