	;TEL:39-83-41-77
	;ROUTINE DE DECALAGE DE SPRITES 1 PLAN TEMPS REELS

;LA LONGUEUR ET LA HAUTEUR SONT DECREMENTêS DE 1
LONG = 120-1
HAUT = 120-1

	PEA	0.W
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP
	MOVE.L	D0,SAUV_SP

	CLR.W	-(SP)
	PEA	-1.W	
	PEA	-1.W	
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.W	#163,POS_X
	MOVE.W	#30,POS_Y

VBL	MOVE.W	#$700,$FFFF8240.W
	MOVE.W	POS_X,D0	D0:X
	MOVE.W	POS_Y,D1	D1:Y
	MOVE.W	#LONG,D2	D2:LONGUEUR -1
	MOVE.W	#HAUT,D3	D3:HAUTEUR -1
	MOVE.L	$44E.W,A2	A1:BASE ECRAN
	LEA	GRAF_1P,A6	A6: ADRESSE DU SPRITE 1 PLAN
	JSR	AFF_SPR_1_P
	MOVE.W	#$777,$FFFF8240.W
	CMPI.B	#$3C,$FFFFFC02.W
	BNE.S	TEST_2
	ADDQ.W	#1,POS_X
TEST_2	CMPI.B	#$3B,$FFFFFC02.W
	BNE.S	TEST_3
	SUBQ.W	#1,POS_X
TEST_3
	MOVE.W	#$25,-(SP)
	TRAP	#14
	ADDQ.W	#2,SP
	CMPI.B	#$39,$FFFFFC02.W
	BNE.S	VBL

FIN	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	TRAP	#1
	ADDQ.W	#2,SP

POS_X	DS.W	1
POS_Y	DS.W	1
SAUV_SP	DS.L	1

GRAF_1P	INCBIN	ONEPLANE.GRF

	**************************************
	*ROUTINE GENERALE DE DECALAGES 1 PLAN*
	*ENTRêES... D0:POSITION X SUR L'ECRAN*
	*           D1:POSITION Y SUR L'ECRAN*
	*           D2:LONGUEUR-1 DU SPRITE  *
	*           D3:HAUTEUR-1 DU SPRITE   *
	*           A2:BASE ECRAN            *
	*           A6:ADRESSE DU SPRITE     *
	*  LES CLIPPING X ET Y SONT COMPRIS  *
	* v.0.3  13/05/92   22 H 20  pm      *
	**************************************
AFF_SPR_1_P	MOVE.W	D0,$140.W
	MOVEQ	#0,D5	INCREMENT GAUCHE & DROIT A 0
	MOVEQ	#0,D6
	MOVE.W	D2,D4
	ANDI.W	#$FFF0,D4
	LSR	#4,D4
	MOVE.W	D4,D7
	ADDQ.W	#1,D4
	ADD.W	D4,D4	D4:LONGUEUR EN OCTET D'UNE LIGNE

	TST.W	D0
	BGE.S	X_POSITIF
	MOVE.W	D0,D5
	ANDI.W	#15,D0	D0:DECALAGE
	NEG.W	D5
	SUBQ.W	#1,D5	COMPLEMENT A 2
	ANDI.W	#-16,D5
	LSR	#1,D5
	ADDQ.W	#8,D5	D5:NB D'OCTETS A NE PAS AFFICHER EN DEBUT DE SPRITE
	MOVEQ	#0,D0	X=0
	BRA.S	X_CLIPPêS
X_POSITIF	CMPI.W	#320,D0
	BGE	PAS_SPRITE
	MOVE.W	D0,D6
	ANDI.W	#15,D0	D0:DECALAGE
	ADD.W	D2,D6
	CMPI.W	#320,D6
	BLT.S	X_CLIPPêS
	SUBI.W	#319,D6	D6:NB DE PIXELS A RETIRER A DROITE DU SPRITE
	ANDI.W	#-16,D6
	LSR	#1,D6
	ADDQ.W	#8,D6	D6:NB D'OCTETS A IGNORER EN FIN DE LIGNE
	BRA.S	X_CLIPPêS+2

X_CLIPPêS	MOVEQ	#0,D6
	TST.W	D1
	BGE.S	Y_POSITIF
	NEG.W	D1	D1:NB DE LIGNES A NE PAS AFFICHER AU DEBUT DU SPRITE
	MULU.W	D1,D4
	ADDA.W	D4,A6	A6:NOUVELLE ADRESSE DU SPRITE CLIPPê EN HAUT
	MOVEQ	#0,D1	Y=0
	BRA.S	Y_CLIPPêS
Y_POSITIF	MOVE.W	D1,D4
	CMPI.W	#200,D4
	BGE	PAS_SPRITE
	ADD.W	D3,D4
	CMPI.W	#200,D4
	BLT.S	Y_CLIPPêS
	SUB.W	#199,D4
	SUB.W	D4,D3

Y_CLIPPêS	MOVE.W	D1,D4
	ADD.W	D1,D1
	ADD.W	D1,D1
	ADD.W	D4,D1
	LSL	#5,D1	D1:Y*160
	ADDA.W	D1,A2	A2:ADRESSE ECRAN DESTINATION
	MOVE.W	$140.W,D2
	ANDI.W	#-16,D2
	LSR	#1,D2
	ADDA.W	D2,A2

	;ARRIVê ICI D3 EST LA HAUTEUR DU SPRITE CLIPPê OU PAS
	;           A6 EST L'ADRESSE DU SPRITE CLIPPê OU PAS
	;           A2 EST L'ADRESSE ECRAN DE DESTINATION
	;           D5 EST A 0 OU DIFFERENT DE 0 SI CLIPPê A GAUCHE
	;           D6 EST A 0 OU DIFFERENT DE 0 SI CLIPPê A DROITE
	;           D0 EST LE DECALAGE

;	TST.W	D5
;	BNE.S	PAS_CLIP_GAUCHE
;	NOP

;	TST.W	D6
;	BNE.S	CLIP_NORMAL
;	NOP

CLIP_NORMAL	LEA	LEA_2+2,A0
	LEA	DBRA_2+2,A1

	LEA	TABLE_BRA_2,A4
	MOVE.W	#160,D4
	MOVE.W	D7,D1
	ADDQ.W	#1,D1
	LSL	#3,D1
	SUB.W	D1,D4
	MOVE.W	D4,(A0)

	ADD.W	D7,D7
	ADD.W	D7,D7	D7=NB DE BLOCS DE 16 *4
	MOVE.L	-4(A4,D7.W),A4
;	MOVE.L	A4,D1
;	MOVE.L	A1,D4
;	SUB.W	D4,D1
;	MOVE.W	D1,(A1)	

	MOVEQ	#0,D4	PRECAUTION
	MOVEQ	#0,D1
	LEA	TABLE_MASK,A0
	MOVE.W	D0,D2
	ADD.W	D2,D2
	ADD.W	D2,D2
	MOVE.L	(A0,D2.W),D5	MASK

ROUT_320_NRM	MOVE.L	(A6)+,D2
	ROR.L	D0,D2
	MOVE.L	D2,D4
	AND.L	D5,D2
	EOR.L	D2,D4
	MOVE.W	D2,8(A2)
	SWAP	D2
	MOVE.W	D2,(A2)
	LEA	16(A2),A2	

	JMP	(A4)

ROUT_288_NRM	MOVE.L	(A6)+,D2
	ROR.L	D0,D2
	MOVE.L	D2,D4
	AND.L	D5,D2	D2=MOTIF DE 32 PIXELS DECALê
	EOR.L	D2,D4
	OR.L	D1,D2
	MOVE.W	D2,8(A2)
	SWAP	D2
	MOVE.W	D2,(A2)
	LEA	16(A2),A2	

ROUT_256_NRM	MOVE.L	(A6)+,D2	PLAN 1 + PLAN 1
	ROR.L	D0,D2
	MOVE.L	D2,D1
	AND.L	D5,D2	D2=MOTIF DE 32 PIXELS DECALê
	EOR.L	D2,D1
	OR.L	D4,D2
	MOVE.W	D2,8(A2)
	SWAP	D2
	MOVE.W	D2,(A2)
	LEA	16(A2),A2


ROUT_224_NRM	MOVE.L	(A6)+,D2
	ROR.L	D0,D2
	MOVE.L	D2,D4
	AND.L	D5,D2	D2=MOTIF DE 32 PIXELS DECALê
	EOR.L	D2,D4
	OR.L	D1,D2
	MOVE.W	D2,8(A2)
	SWAP	D2
	MOVE.W	D2,(A2)
	LEA	16(A2),A2	

ROUT_192_NRM	MOVE.L	(A6)+,D2	PLAN 1 + PLAN 1
	ROR.L	D0,D2
	MOVE.L	D2,D1
	AND.L	D5,D2	D2=MOTIF DE 32 PIXELS DECALê
	EOR.L	D2,D1
	OR.L	D4,D2
	MOVE.W	D2,8(A2)
	SWAP	D2
	MOVE.W	D2,(A2)
	LEA	16(A2),A2

ROUT_160_NRM	MOVE.L	(A6)+,D2
	ROR.L	D0,D2
	MOVE.L	D2,D4
	AND.L	D5,D2	D2=MOTIF DE 32 PIXELS DECALê
	EOR.L	D2,D4
	OR.L	D1,D2
	MOVE.W	D2,8(A2)
	SWAP	D2
	MOVE.W	D2,(A2)
	LEA	16(A2),A2	

ROUT_128_NRM	MOVE.L	(A6)+,D2	PLAN 1 + PLAN 1
	ROR.L	D0,D2
	MOVE.L	D2,D1
	AND.L	D5,D2	D2=MOTIF DE 32 PIXELS DECALê
	EOR.L	D2,D1
	OR.L	D4,D2
	MOVE.W	D2,8(A2)
	SWAP	D2
	MOVE.W	D2,(A2)
	LEA	16(A2),A2

ROUT_96_NRM	MOVE.L	(A6)+,D2
	ROR.L	D0,D2
	MOVE.L	D2,D1
	AND.L	D5,D2	D2=MOTIF DE 32 PIXELS DECALê
	EOR.L	D2,D1
	OR.L	D4,D2
	MOVE.W	D2,8(A2)
	SWAP	D2
	MOVE.W	D2,(A2)
	LEA	16(A2),A2	

ROUT_64_NRM	MOVE.L	(A6)+,D2	PLAN 1 + PLAN 1
	ROR.L	D0,D2
	MOVE.L	D2,D4
	AND.L	D5,D2	D2=MOTIF DE 32 PIXELS DECALê
	EOR.L	D2,D4
	OR.L	D1,D2
	MOVE.W	D2,8(A2)
	SWAP	D2
	MOVE.W	D2,(A2)
	LEA	16(A2),A2

ROUT_32_NRM	MOVE.L	(A6)+,D2
	ROR.L	D0,D2
	MOVE.L	D2,D1
	AND.L	D5,D2	D2=MOTIF DE 32 PIXELS DECALê
	EOR.L	D2,D1
	OR.L	D4,D2
	MOVE.W	D2,8(A2)
	SWAP	D2
	MOVE.W	D2,(A2)
	LEA	16(A2),A2	
	MOVE.W	D1,8(A2)
	SWAP	D1
	MOVE.W	D1,(A2)

	MOVEQ	#0,D4
	MOVEQ	#0,D1
LEA_2	LEA	96(A2),A2		LE LEA EST AUTOMODIFIê
DBRA_2	DBRA	D3,ROUT_320_NRM	LE DBRA EST AUTOMODIFIê
	RTS

RIEN	DS.L	1

PAS_SPRITE	RTS

TABLE_BRA_2	DC.L	ROUT_288_NRM
	DC.L	ROUT_256_NRM,ROUT_224_NRM
	DC.L	ROUT_192_NRM,ROUT_160_NRM
	DC.L	ROUT_128_NRM,ROUT_96_NRM
	DC.L	ROUT_64_NRM,ROUT_32_NRM

TABLE_MASK	DC.L	$FFFFFFFF,$7FFFFFFF,$3FFFFFFF,$1FFFFFFF
	DC.L	$0FFFFFFF,$07FFFFFF,$03FFFFFF,$01FFFFFF
	DC.L	$00FFFFFF,$007FFFFF,$003FFFFF,$001FFFFF
	DC.L	$000FFFFF,$0007FFFF,$0003FFFF,$0001FFFF