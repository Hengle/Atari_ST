;MODE_TEXT = 1 : ON FAIT DES ESSAIS
;MODE_TEXT = 0 : ON EST DANS LA DEMO
MODE_TEXT = 0

	;TEXTEUR AVEC LES FONTES 'CURSIVES' DE NEOPAINT.
	;A0=ADRESSE DU TEXTE A AFFICHER
	;N.B.:
	;POUR LES CHANGEMENTS DE FONTES, DANS LE CAS DE L'OPTION
	;7 (CENTRAGE), TOUJOURS METTRE LE 8-->... AVANT!!!
	;-1=fin du texte
	;0 =changement de ligne
	;1 = ECRIRE SUR LE PLAN 1 (MODE PAR DêFAUT)
	;2 = ECRIRE SUR LE PLAN 2
	;3 = ECRIRE SUR LE PLAN 3
	;4 = ECRIRE SUR LE PLAN 4
	;5 = AFFICHAGE EN 'AND'
	;6 = AFFICHAGE EN 'OR' (MODE PAR DêFAUT)
	;7 = CENTRAGE DE LA LIGNE
	;8 = CHANGEMENT EN FONTES 'CURSIVE' (PAR DEFAUT)
	;9 = CHANGEMENT EN FONTES 'THYMES'
	;10 = CHANGEMENT EN FONTES 'PARK STREET'

MAIN
	IFNE	MODE_TEXT
	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	MOVEQ	#6,D7
	MOVE.W	#$2100,SR
.LOW	STOP	#$2100
	MOVE.B	#1,$FFFF8260.W
	STOP	#$2100
	CLR.B	$FFFF8260.W
	DBRA	D7,.LOW

	MOVE.W	#$2300,SR

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	LEA	TEXT,A0
	BSR	PUT_TEXT

J	JMP	J

TEXT	;dc.b	10,7,".,;-+?!()",34,"'"
	;dc.b	-1

	DC.B	9,7,"HOLOCAUST",0
	DC.B	8,7,"is very proud to present you",0
	DC.B	10,7,"their last production",0
	dc.b	8,7,"called:...",0
	dc.b	10,7,"BLOOD",0
	dc.b	8,7,"Enjoy it !!",0,0
	dc.b	9,7,"(c)1994 HC Prod."
	dc.b	-1

	DC.B	10,7,"ABCDEFGHIJKLMN",0
	DC.B	8,7,"OPQRSTUVWXYZ",0
	DC.B	9,7,"abcdefghijklmnopqrstuvwxyz",0
	DC.B	9,7,"Bonjour!  0123456789",0
	DC.B	9,7,"J'hallucine! (Bof!)",0
	DC.B	8,7,"Haha... A-+ hoho,hum?...",0
	DC.B	9,7,34,"YO",34,"A;a;",0
	DC.B	-1
	EVEN
	ENDC

PUT_TEXT	MOVE.L	A0,PT_TXT

	MOVEQ	#0,D7	;X ACTUEL
	MOVE.L	#0,A5	;Y_ACTUEL
	CLR.W	OFFSET_PLAN_NB_1
	CLR.W	OFFSET_PLAN_NB_2

	MOVE.L	#AFF_RT_OR_1_MOT,AFF_RT_1_MOT
	MOVE.L	#RT_AFF_OR,RT_AFF

	MOVE.L	#DIMENSIONS,PT_DIMENSION
	MOVE.L	#FONTS_1,PT_FONTS

NEXT_LETTER	MOVE.L	PT_TXT,A0
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0
	MOVE.L	A0,PT_TXT
	CMPI.B	#-1,D0
	BNE.S	.OK
	RTS
.OK	TST.B	D0
	BNE.S	.PAS_LIG_SUIV
	MOVEQ	#0,D7
	LEA	26(A5),A5	;LIGNE SUIVANTE
	BRA.S	NEXT_LETTER
.PAS_LIG_SUIV	CMPI.W	#4,D0
	BGT.S	.PAS_ACTION_PLANES
	SUBQ.W	#1,D0
	ADD.W	D0,D0
	MOVE.W	D0,OFFSET_PLAN_NB_1
	MOVE.W	D0,OFFSET_PLAN_NB_2
	BRA.S	NEXT_LETTER
.PAS_ACTION_PLANES
	CMPI.W	#6,D0
	BNE.S	.PAS_AFFICH_OR
	MOVE.L	#AFF_RT_OR_1_MOT,AFF_RT_1_MOT
	MOVE.L	#RT_AFF_OR,RT_AFF
	BRA.S	NEXT_LETTER
.PAS_AFFICH_OR	CMPI.W	#5,D0
	BNE.S	.PAS_AFFICH_AND
	MOVE.L	#AFF_RT_AND_1_MOT,AFF_RT_1_MOT
	MOVE.L	#RT_AFF_AND,RT_AFF
	BRA.S	NEXT_LETTER	
.PAS_AFFICH_AND	CMPI.W	#7,D0
	BNE.S	.PAS_CENTRAGE
	MOVE.L	A0,A2
	MOVEQ	#0,D6
.RECOM	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0
	BEQ.S	.FIN_LIG
	CMPI.B	#-1,D0
	BEQ.S	.FIN_LIG	
	SUBI.W	#32,D0
	BLT.S	.RECOM
	MULU.W	#10,D0
	MOVE.L	PT_DIMENSION,A1
	ADDA.W	D0,A1
	MOVE.W	(A1)+,D0
	MOVE.W	(A1),D1
	SUB.W	D0,D1
	ADDQ.W	#1,D1
	ADD.W	D1,D6
	BRA.S	.RECOM
	;ICI D6=LONGUEUR DU MSG
.FIN_LIG	MOVE.W	#320,D7
	SUB.W	D6,D7
	BGE.S	.OK_INF_320
	MOVEQ	#0,D7
.OK_INF_320	LSR.W	#1,D7	;D7=X CENTRê
	MOVE.L	A2,A0
	BRA	NEXT_LETTER
.PAS_CENTRAGE	CMPI.B	#8,D0
	BNE.S	.PAS_FONTES_CURSIVE
	MOVE.L	#DIMENSIONS,PT_DIMENSION
	MOVE.L	#FONTS_1,PT_FONTS
	BRA	NEXT_LETTER
.PAS_FONTES_CURSIVE
	CMPI.B	#9,D0
	BNE.S	.PAS_FONTES_THYMES
	MOVE.L	#DIMENSIONS2,PT_DIMENSION
	MOVE.L	#FONTS_2,PT_FONTS
	BRA	NEXT_LETTER
.PAS_FONTES_THYMES
	CMPI.B	#10,D0
	BNE.S	.PAS_FONTES_PARK_STREET
	MOVE.L	#DIMENSIONS3,PT_DIMENSION
	MOVE.L	#FONTS_3,PT_FONTS
	BRA	NEXT_LETTER
.PAS_FONTES_PARK_STREET
	SUBI.B	#32,D0
	MULU.W	#10,D0	;PROVISOIRE, PUTAIN!
	MOVE.L	PT_DIMENSION,A0
	ADDA.W	D0,A0
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;X2
	MOVE.W	(A0)+,D2	;Y1
	MOVE.W	(A0)+,D3	;Y2
	MOVE.W	D0,D6
	ANDI.W	#15,D6
	MOVE.W	D1,D4
	SUB.W	D0,D4	;LONGUEUR-1
	ADDQ.W	#1,D4
	MOVE.W	D4,-(SP)
	SUB.W	D2,D3	;HAUTEUR-1
	MULS.W	#40,D2
	MOVE.W	D0,D4
	MOVE.W	D1,D5
	ANDI.W	#15,D4
	ANDI.W	#15,D5
	ADD.W	D4,D4
	ADD.W	D5,D5
	LEA	MASK_DEPART,A2
	MOVE.W	(A2,D4.W),D4
	LEA	MASK_ARRIVêE,A2
	MOVE.W	(A2,D5.W),D5
	ANDI.W	#$FFF0,D0
	LSR.W	#4,D0
	ANDI.W	#$FFF0,D1
	LSR.W	#4,D1
	SUB.W	D0,D1	;NB DE MOTS-1
	BGT	SUP_1
	;ROUTINE POUR LES FONTES SUR 1 MOTS
	MOVE.W	D6,DEC_PRIM_1_MOT
	MOVE.L	PT_FONTS,A1
	MOVE.L	A1,A2
	ADD.W	D0,D0
	ADD.W	D0,D2
	ADDA.W	D2,A2	;A2=ADRESSE DE DEPART POUR LE POINTAGE DE LA FONTE

	LEA	BUF_FNT,A4
	MOVE.W	D3,HAUT_1_MOT
COPY_Y_1_MOT	MOVE.L	A2,A3
	MOVE.W	(A3)+,D0	;1er MOT
	AND.W	D4,D0
	AND.W	D5,D0
	MOVE.W	D0,(A4)+
	LEA	40(A2),A2
	DBRA	D3,COPY_Y_1_MOT

	LEA	BUF_FNT,A2
	ADDQ.W	#2,A2
DEC_PRIM_1_MOT = *+2
	MOVE.W	#$1234,D2
	MOVE.W	HAUT_1_MOT,D5
DECAL_Y_1_MOT	MOVE.L	A2,A1
	MOVEQ	#0,D0
	MOVE.W	-(A1),D0
	ROL.L	D2,D0
	MOVE.W	D0,(A1)
	ADDQ.W	#2,A2
	DBRA	D5,DECAL_Y_1_MOT

	MOVE.L	SCREEN2,A2
OFFSET_PLAN_NB_1 = *+2
	ADDA.W	#$1234,A2
	MOVE.W	(A0),D4
	ADD.W	A5,D4
	MULS.W	#160,D4
	ADDA.W	D4,A2
	MOVE.W	D7,D4
	ANDI.W	#15,D4	;DECALAGE
	MOVE.W	D7,D3
	ANDI.W	#$FFF0,D3
	LSR.W	#1,D3
	ADD.W	D3,A2
	LEA	BUF_FNT,A1
HAUT_1_MOT = *+2
	MOVE.W	#$1234,D6
AFF_RT_1_MOT = *+2
	JMP	$12345678

AFF_RT_AND_1_MOT
	MOVE.L	A2,A3
	MOVEQ	#0,D0
	MOVE.W	(A1)+,D0
	ROR.L	D4,D0
	NOT.W	D0
	AND.W	D0,(A3)
	ADDQ.W	#8,A3
	SWAP	D0
	NOT.W	D0
	AND.W	D0,(A3)
	LEA	160(A2),A2
	DBRA	D6,AFF_RT_AND_1_MOT
	BRA	CONJONCTURE

AFF_RT_OR_1_MOT	MOVE.L	A2,A3
	MOVEQ	#0,D0
	MOVE.W	(A1)+,D0
	ROR.L	D4,D0
	OR.W	D0,(A3)
	ADDQ.W	#8,A3
	SWAP	D0
	OR.W	D0,(A3)
	LEA	160(A2),A2
	DBRA	D6,AFF_RT_OR_1_MOT
	BRA	CONJONCTURE

	;ROUTINE POUR LES FONTES SUR PLUS DE 1 MOTS
SUP_1	MOVE.W	D6,DEC_PRIM
	ADD.W	D0,D0
	MOVE.W	D1,LONG
	SUBQ.W	#1,D1
	MOVEA.L	PT_FONTS,A1
	MOVE.L	A1,A2
	ADD.W	D0,D2
	ADDA.W	D2,A2	;A2=ADRESSE DE DEPART POUR LE POINTAGE DE LA FONTE
	LEA	BUF_FNT,A4
	MOVE.W	D3,HAUT
COPY_Y	MOVE.L	A2,A3
	MOVE.W	(A3)+,D0	;1er MOT
	AND.W	D4,D0
	MOVE.W	D0,(A4)+
	MOVE.W	D1,D6
COPY_X	MOVE.W	(A3)+,(A4)+
	DBRA	D6,COPY_X
	MOVE.W	-(A4),D0
	AND.W	D5,D0
	MOVE.W	D0,(A4)+
	LEA	40(A2),A2
	DBRA	D3,COPY_Y

	LEA	BUF_FNT,A2
	MOVE.W	D1,D3
	ADDQ.W	#2,D3
	ADD.W	D3,D3
	ADDA.W	D3,A2
DEC_PRIM = *+2
	MOVE.W	#$1234,D2
	MOVE.W	HAUT,D5
DECAL_Y	MOVE.L	A2,A1
	MOVEQ	#0,D4
	MOVE.W	D1,D6
	ADDQ.W	#1,D6
DECAL_X	MOVEQ	#0,D0
	MOVE.W	-(A1),D0
	ROL.L	D2,D0
	OR.W	D4,D0
	MOVE.W	D0,(A1)
	SWAP	D0
	MOVE.W	D0,D4
	DBRA	D6,DECAL_X
	ADDA.W	D3,A2
	DBRA	D5,DECAL_Y

	MOVE.L	SCREEN2,A2
OFFSET_PLAN_NB_2 = *+2
	ADDA.W	#$1234,A2
	MOVE.W	(A0)+,D4
	ADD.W	A5,D4
	MULS.W	#160,D4
	ADDA.W	D4,A2
	MOVE.W	D7,D4
	ANDI.W	#15,D4	;DECALAGE
	MOVE.W	D7,D3
	ANDI.W	#$FFF0,D3
	LSR.W	#1,D3
	ADD.W	D3,A2
	LEA	BUF_FNT,A1
HAUT = *+2
	MOVE.W	#$1234,D6
.AFFY	MOVE.L	A2,A3
	MOVEQ	#0,D1
LONG = *+2
	MOVE.W	#$1234,D5
RT_AFF = *+2
	JSR	$12345678
	DBRA	D6,.AFFY
	BRA	CONJONCTURE

RT_AFF_AND
.AFF	MOVEQ	#0,D0
	MOVE.W	(A1)+,D0
	ROR.L	D4,D0
	OR.W	D1,D0
	NOT.W	D0
	AND.W	D0,(A3)
	ADDQ.W	#8,A3
	SWAP	D0
	MOVE.W	D0,D1
	DBRA	D5,.AFF
	NOT.W	D1
	AND.W	D1,(A3)
	LEA	160(A2),A2
	RTS

RT_AFF_OR
.AFF	MOVEQ	#0,D0
	MOVE.W	(A1)+,D0
	ROR.L	D4,D0
	OR.W	D1,D0
	OR.W	D0,(A3)
	ADDQ.W	#8,A3
	SWAP	D0
	MOVE.W	D0,D1
	DBRA	D5,.AFF
	OR.W	D1,(A3)
	LEA	160(A2),A2
	RTS

CONJONCTURE	ADD.W	(SP)+,D7	;NOUVEAU X ACTUEL

	BRA	NEXT_LETTER

FONTS_1	INCBIN	FONTS.FNT	;FONTES 'CURSIVE'

FONTS_2	INCBIN	THYMES.1_P	;FONTES 'THYMES'

FONTS_3	INCBIN	PARKS.1_P	;FONTES 'PARK STREET'

MASK_DEPART	DC.W	$FFFF,$7FFF,$3FFF,$1FFF
	DC.W	$FFF,$3FF,$7FF,$1FF
	DC.W	$FF,$7F,$3F,$1F
	DC.W	$F,7,3,1

MASK_ARRIVêE	DC.W	$8000,$C000,$E000,$F000
	DC.W	$F800,$FC00,$FE00,$FF00
	DC.W	$FF80,$FFC0,$FFE0,$FFF0
	DC.W	$FFF8,$FFFC,$FFFE,$FFFF

BUF_FNT	DS.W	4*25

	;X DEPART, X ARRIVêE, Y DEPART, Y ARRIVêE
DIMENSIONS	DC.W	0,10,0,1,0	;SPACE
	DC.W	40,49,95,110,0	;!   *
	DC.W	0,1,0,1,0
	DC.W	110,128,97,109,2   ;#   *
	DC.W	0,1,0,1,0
	DC.W	0,1,0,1,0
	DC.W	0,1,0,1,0
	DC.W	96,101,95,101,0	;'   *
	DC.W	0,8,95,113,0	;(   *
	DC.W	9,17,95,113,0	;)   *
	DC.W	0,1,0,1,0
	DC.W	61,73,98,108,3	;+   *
	DC.W	23,27,107,111,12  ;,   *
	DC.W	74,81,103,104,8	;-   *
	DC.W	18,22,107,110,12  ;.   *
	DC.W	0,1,0,1,0
	DC.W	0,12,73,87,1	;0   *
	DC.W	16,25,73,87,1	;1   *
	DC.W	27,39,73,87,1	;2   *
	DC.W	40,55,73,87,1	;3   *
	DC.W	56,70,73,87,1	;4   *
	DC.W	71,85,73,87,1	;5   *
	DC.W	88,99,73,87,1	;6   *
	DC.W	104,116,73,87,1	;7   *
	DC.W	118,131,73,87,1	;8   *
	DC.W	135,147,73,87,1	;9   *
	DC.W	103,109,102,110,7   ;:   *
	DC.W	0,1,0,1,0
	DC.W	0,1,0,1,0
	DC.W	83,95,100,106,5	;=   *
	DC.W	0,1,0,1,0
	DC.W	29,39,95,110,0	;?   *
	DC.W	0,1,0,1,0
	DC.W	0,21,0,16,0	;A   *
	DC.W	22,39,0,15,0	;B   *
	DC.W	40,55,0,18,0	;C   *
	DC.W	57,75,0,15,0	;D   *
	DC.W	76,93,0,17,0	;E   *
	DC.W	94,110,0,19,0	;F   *
	DC.W	111,128,0,21,0	;G   *
	DC.W	129,150,0,15,0	;H   *
	DC.W	151,163,0,15,0	;I   *
	DC.W	164,179,0,20,0	;J   *
	DC.W	180,200,0,15,0	;K   *
	DC.W	201,219,0,17,0	;L   *
	DC.W	220,247,0,16,0	;M   *
	DC.W	248,267,0,16,0	;N   *
	DC.W	269,283,0,15,0	;O   *
	DC.W	284,301,0,16,0	;P   *
	DC.W	0,17,23,38,0	;Q   *
	DC.W	18,36,23,39,0	;R   *
	DC.W	37,55,23,38,0	;S   *
	DC.W	56,72,23,38,0	;T   *
	DC.W	73,93,23,38,0	;U   *
	DC.W	94,111,23,38,0	;V   *
	DC.W	112,135,23,38,0	;W   *
	DC.W	136,153,23,38,0	;X   *
	DC.W	154,171,23,43,0	;Y   *
	DC.W	172,187,23,43,0	;Z   *
	REPT	6
	DC.W	0,1,0,1,0
	ENDR
	DC.W	0,12,56,64,7	;a   *
	DC.W	13,23,49,64,0	;b   *
	DC.W	24,33,56,64,7	;c   *
	DC.W	34,46,51,64,2	;d   *
	DC.W	48,57,56,64,7	;e   *
	DC.W	58,68,51,69,2	;f   *
	DC.W	68,80,56,70,7	;g   *
	DC.W	81,92,49,64,0	;h   *
	DC.W	93,101,52,64,3	;i   *
	DC.W	102,112,52,69,3	;j   *
	DC.W	113,124,50,64,1	;k   *
	DC.W	125,133,51,64,2	;l   *
	DC.W	134,150,56,64,7	;m   *
	DC.W	151,162,56,64,7	;n   *
	DC.W	163,172,56,64,7	;o   *
	DC.W	173,186,52,69,3	;p   *
	DC.W	187,198,56,70,7	;q   *
	DC.W	199,207,56,64,7	;r   *
	DC.W	208,217,56,64,7	;s   *
	DC.W	218,227,53,64,4	;t   *
	DC.W	228,239,56,64,7	;u   *
	DC.W	240,250,56,64,7	;v   *
	DC.W	251,266,56,64,7	;w   *
	DC.W	266,275,56,64,7	;x   *
	DC.W	276,287,56,69,7	;y   *
	DC.W	288,298,56,69,7	;z   *
	REPT	7
	DC.W	0,1,0,1,0
	ENDR
	DC.W	50,59,98,110,3	;Ç   *

	;X DEPART, X ARRIVêE, Y DEPART, Y ARRIVêE
DIMENSIONS2	DC.W	0,10,0,1,0	;SPACE
	DC.W	187,191,66,83,-1 ;! *
	DC.W	193,202,66,72,0	;"
	DC.W	0,1,0,1,0   ;#
	DC.W	0,1,0,1,0
	DC.W	0,1,0,1,0
	DC.W	0,1,0,1,0
	DC.W	170,174,66,72,-1 ;' *
	DC.W	13,20,90,112,-1	;( *
	DC.W	21,28,90,112,-1	;) *
	DC.W	0,1,0,1,0
	DC.W	156,167,71,81,4	;+ *
	DC.W	135,139,79,86,12  ;, *
	DC.W	147,154,75,77,8	;- *
	DC.W	129,133,79,83,12  ;. *
	DC.W	0,1,0,1,0
	DC.W	9,19,67,83,0	;0 *
	DC.W	24,29,67,83,0	;1 *
	DC.W	33,43,67,83,0	;2 *
	DC.W	45,55,67,83,0	;3 *
	DC.W	57,68,67,83,0	;4 *
	DC.W	69,79,67,83,0	;5 *
	DC.W	81,91,67,83,0	;6 *
	DC.W	93,103,67,83,0	;7 *
	DC.W	105,115,67,83,0	;8 *
	DC.W	116,127,67,83,0	;9 *
	DC.W	0,1,0,1,0	;:
	DC.W	141,145,72,86,5	;; *
	DC.W	0,1,0,1,0
	DC.W	0,1,0,1,0	;=
	DC.W	0,1,0,1,0
	DC.W	175,185,66,83,-1 ;? *
	DC.W	0,1,0,1,0
	DC.W	8,24,13,29,0	;A *
	DC.W	26,40,13,29,0	;B *
	DC.W	42,55,13,29,0	;C *
	DC.W	58,73,13,29,0	;D *
	DC.W	75,87,13,29,0	;E *
	DC.W	90,101,13,29,0	;F *
	DC.W	103,119,13,29,0	;G *
	DC.W	120,135,13,29,0	;H *
	DC.W	137,143,13,29,0	;I *
	DC.W	144,152,13,29,0	;J *
	DC.W	154,170,13,29,0	;K *
	DC.W	171,183,13,29,0	;L *
	DC.W	186,205,13,29,0	;M *
	DC.W	207,222,13,29,0	;N *
	DC.W	224,239,13,29,0	;O *
	DC.W	241,253,13,29,0	;P *
	DC.W	254,269,13,32,0	;Q *
	DC.W	271,286,13,29,0	;R *
	DC.W	287,298,13,29,0	;S *
	DC.W	299,313,13,29,0	;T *
	DC.W	9,24,40,56,0	;U *
	DC.W	25,41,40,56,0	;V *
	DC.W	42,62,40,56,0	;W *
	DC.W	64,79,40,56,0	;X *
	DC.W	81,99,40,56,0	;Y *
	DC.W	100,113,40,56,0	;Z *
	REPT	6
	DC.W	0,1,0,1,0
	ENDR
	DC.W	29,40,96,107,5	;a *
	DC.W	41,51,90,107,-1	;b *
	DC.W	52,63,96,107,5	;c *
	DC.W	64,74,90,107,-1	;d *
	DC.W	75,85,96,107,5	;e *
	DC.W	86,93,90,107,-1	;f *
	DC.W	93,105,96,113,5	;g *
	DC.W	105,116,90,107,-1 ;h *
	DC.W	117,122,90,107,-1 ;i *
	DC.W	123,128,90,112,-1 ;j *
	DC.W	129,141,90,107,-1 ;k *
	DC.W	142,146,90,107,-1 ;l *
	DC.W	147,164,96,107,5 ;m *
	DC.W	165,176,96,107,5 ;n *
	DC.W	177,188,96,107,5 ;o *
	DC.W	189,200,96,112,5 ;p *
	DC.W	201,212,96,112,5 ;q *
	DC.W	213,221,96,107,5 ;r *
	DC.W	222,230,96,107,5 ;s *
	DC.W	231,237,92,107,1 ;t *
	DC.W	238,249,96,107,5 ;u *
	DC.W	250,260,96,107,5 ;v *
	DC.W	261,278,96,107,5 ;w *
	DC.W	279,289,96,107,5 ;x *
	DC.W	290,301,96,112,5 ;y *
	DC.W	302,311,96,107,5 ;z *
	REPT	7
	DC.W	0,1,0,1,0
	ENDR
	DC.W	50,59,98,110,3	;Ç

	;X DEPART, X ARRIVêE, Y DEPART, Y ARRIVêE
DIMENSIONS3	DC.W	0,10,0,1,0	;SPACE
	DC.W	153,159,64,81,-1 ;!
	DC.W	183,189,65,71,0	;"
	DC.W	0,1,0,1,0   ;#
	DC.W	0,1,0,1,0
	DC.W	0,1,0,1,0
	DC.W	0,1,0,1,0
	DC.W	192,196,65,71,-1 ;'
	DC.W	160,170,64,87,-1 ;(
	DC.W	170,180,64,87,-1 ;)
	DC.W	0,1,0,1,0
	DC.W	130,141,68,78,4	;+
	DC.W	110,114,79,83,12  ;,
	DC.W	122,128,76,77,8 ;-
	DC.W	106,109,79,81,12 ;.
	DC.W	0,1,0,1,0
	DC.W	8,16,72,81,0	;0
	DC.W	17,23,72,81,0	;1
	DC.W	24,32,72,81,0	;2
	DC.W	33,43,72,84,0	;3
	DC.W	43,54,72,86,0	;4
	DC.W	55,65,72,84,0	;5
	DC.W	66,75,67,81,0	;6
	DC.W	76,85,72,84,0	;7
	DC.W	86,96,69,81,0	;8
	DC.W	97,105,72,86,0	;9
	DC.W	0,1,0,1,0	;:
	DC.W	115,120,73,83,5 ;;
	DC.W	0,1,0,1,0
	DC.W	0,1,0,1,0	;=
	DC.W	0,1,0,1,0
	DC.W	143,151,64,81,-1 ;?
	DC.W	0,1,0,1,0
	DC.W	7,33,7,25,1	;A *
	DC.W	34,52,6,23,0	;B *
	DC.W	52,66,7,23,1	;C *
	DC.W	67,86,6,23,0	;D *
	DC.W	86,101,7,25,1	;E *
	DC.W	101,120,6,27,0	;F *
	DC.W	123,142,6,27,0	;G *
	DC.W	142,166,4,23,-2	;H *
	DC.W	168,183,6,23,0	;I *
	DC.W	184,200,6,31,0	;J *
	DC.W	202,225,6,27,0	;K *
	DC.W	227,246,7,23,1	;L *
	DC.W	246,274,6,25,0	;M *
	DC.W	274,304,7,23,1	;N *

	DC.W	8,24,35,52,0	;O *
	DC.W	24,46,36,52,1	;P *
	DC.W	47,64,35,53,0	;Q *
	DC.W	64,91,36,55,1	;R *
	DC.W	91,110,35,52,0	;S *
	DC.W	110,129,35,52,0	;T *
	DC.W	131,152,35,52,0	;U *
	DC.W	152,172,35,52,0	;V *
	DC.W	172,198,35,52,0	;W *
	DC.W	199,221,35,55,0	;X *
	DC.W	221,243,35,58,0	;Y *
	DC.W	244,262,35,52,0	;Z *
	REPT	6
	DC.W	0,1,0,1,0
	ENDR
	DC.W	8,17,103,110,10	;a *
	DC.W	18,28,93,110,0	;b *
	DC.W	29,36,103,110,10 ;c *
	DC.W	37,48,93,110,0	;d *
	DC.W	49,57,103,110,10 ;e *
	DC.W	57,68,93,118,0	;f *
	DC.W	68,77,103,118,10 ;g *
	DC.W	77,87,93,110,0 ;h *
	DC.W	87,92,99,110,6 ;i *
	DC.W	92,100,99,118,6 ;j *
	DC.W	100,109,93,110,0 ;k *
	DC.W	110,118,93,110,0 ;l *
	DC.W	118,131,103,110,10 ;m *
	DC.W	132,140,103,110,10 ;n *
	DC.W	141,149,103,110,10 ;o *
	DC.W	149,161,98,118,5 ;p *
	DC.W	161,169,103,118,10 ;q *
	DC.W	170,177,103,110,10 ;r *
	DC.W	178,185,103,110,10 ;s *
	DC.W	186,192,98,110,5 ;t *
	DC.W	193,202,103,110,10 ;u *
	DC.W	203,211,103,110,10 ;v *
	DC.W	212,224,103,110,10 ;w *
	DC.W	225,234,103,110,10 ;x *
	DC.W	234,243,103,118,10 ;y *
	DC.W	243,252,103,110,10 ;z *
	REPT	7
	DC.W	0,1,0,1,0
	ENDR
	DC.W	50,59,98,110,3	;Ç

PT_TXT	DS.L	1
PT_DIMENSION	DS.L	1
PT_FONTS	DS.L	1

	IFNE	MODE_TEXT
	SECTION	BSS
	DS.B	256
BUFFER	DS.B	32000+10000
SCREEN1	DS.L	1
	ENDC