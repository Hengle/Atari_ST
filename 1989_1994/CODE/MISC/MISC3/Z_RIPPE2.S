;Z-Ripper V1.1 - Version GEM (Ptermres)

	OPT	O+

 	CLR.L	-(SP)
 	MOVE	#$20,-(SP)
 	TRAP	#1
 	ADDQ.L	#6,SP

	;JMP	ROUTINE

	MOVE.B	$FFFF8260.W,MOD_SYNC

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,SAVE_PAL

	MOVE	#1,-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	PEA	MSG
	MOVE	#9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP	

	pea	buffer
	move	#$a,-(sp)
	trap	#1
	addq.l	#6,sp
	
	lea	buffer+2,a0
	lea	NAME,A1
	REPT	7
	MOVE.B	(A0)+,(A1)+
	ENDR

	lea	TABL_NAM,A1
	MOVEQ	#7,D0
COPY
	lea	buffer+2,a0
	REPT	7
	MOVE.B	(A0)+,(A1)+
	ENDR
	ADDQ	#7,A1
	DBF	D0,COPY

 	MOVE.L	#$31415926,$426.W
 	MOVE.L	#ROUTINE,$42A.W

	PEA	MSG2
	MOVE	#9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP	

TET 	CMPI.B	#$02,$FFFFFC02.W
 	BEQ	BOOTER
 	CMPI.B	#$03,$FFFFFC02.W
 	BNE.S	TET
* Retour au bureau

	MOVEM.L	SAVE_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

MOD_SYNC	EQU	*+3
	MOVE	#1,-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#VBLQ,$4D2.W

	MOVE.L	#20000,-(SP)
	MOVE	#$31,-(SP)
 	TRAP	#1

buffer	dc.b	8
	dc.b	0
	DC.B	'________'
* Boot
BOOT	DS.B	512
	EVEN
SAVE_PAL	DS.B	32
BOOTER
	MOVE	#1,-(SP)
	MOVE	#0,-(SP)
	MOVE	#0,-(SP)
	MOVE	#1,-(SP)
	MOVE	#0,-(SP)
	CLR.L	-(SP)
	MOVE.L	#BOOT,-(SP)
	MOVE	#8,-(SP)
	TRAP	#14
	ADDA.L	#20,SP
	JMP	BOOT

MSG	DC.B	'* Z-Ripper v1.1 * - Code by Zappy/Holocaust 18/11/91',$a,$d
	DC.B	'Nom de la sauvegarde ? (Sans extension..) ',$a,$d,$a,0
	EVEN
MSG2
	DC.B	'Version sp‚ciale GEM.',$a,$d,$a
	dc.b	'1...Ex‚cuter un boot (utilisez plutot la V1.0 pour ‡a)',$a,$d
	dc.b	'2.....................................Partir du bureau',0
	EVEN
**** 
VBLQ	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_T
	MOVE.L	A7,MOD_PILE+2
	MOVE	#1,FLAG_VBLQ
	MOVEM.L	D0-D7/A0-A6,SAVER
	BRA.S	ROUTINE
.NO_T	RTS
FLAG_VBLQ	DC	0
SAVER	DS.L	16
ROUTINE	;TST	FLAG_VBLQ
	;BNE.S	.NO_RESET

	LEA	D_PILE,A7
;.NO_RESET
	MOVE	#$2700,SR
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
**
	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,PAL_INIT
	LEA	SAUVEUR,A0
	MOVE.B	$FFFFFA07.W,(A0)+
	MOVE.B	$FFFFFA09.W,(A0)+
	MOVE.B	$FFFFFA0F.W,(A0)+
	MOVE.B	$FFFFFA13.W,(A0)+
	MOVE.B	$FFFFFA1B.W,(A0)+
	MOVE.B	$FFFFFA21.W,(A0)+
	MOVE.B	$FFFFFA15.W,(A0)+
	MOVE.B	$FFFFFA17.W,(A0)+
	MOVE.B	$FFFFFA19.W,(A0)+
	MOVE.B	$FFFFFA1F.W,(A0)+
	MOVE.B	$FFFF8201.W,(A0)+
	MOVE.B	$FFFF8203.W,(A0)+

	MOVE.B	$FFFFFA01.W,(A0)+
	MOVE.B	$FFFFFA03.W,(A0)+
	MOVE.B	$FFFFFA05.W,(A0)+
	MOVE.B	$FFFFFA0B.W,(A0)+
	MOVE.B	$FFFFFA0D.W,(A0)+
	MOVE.B	$FFFFFA11.W,(A0)+

	MOVE.L	$68.W,(A0)+
	MOVE.L	$70.W,(A0)+
	MOVE.L	$134.W,(A0)+
	MOVE.L	$120.W,(A0)+
	MOVE.L	$8.W,(A0)+
	MOVE.L	$C.W,(A0)+
	MOVE.L	$10.W,(A0)+
	MOVE.L	$14.W,(A0)+
**
ROUTINE2	
	LEA	D_PILE,A7
	MOVE	#$2700,SR
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.B	#2,$FFFF820A.W
	CLR.B	$FFFF8260.W

	MOVE.B	#$1E,$FFFFFA07.W
	MOVE.B	#$64,$FFFFFA09.W
	MOVE.B	#$00,$FFFFFA0F.W
	MOVE.B	#$1E,$FFFFFA13.W
	MOVE.B	#$00,$FFFFFA1B.W
	MOVE.B	#$47,$FFFFFA21.W
	MOVE.B	#$64,$FFFFFA15.W
	MOVE.B	#$48,$FFFFFA17.W
	MOVE.B	#$00,$FFFFFA19.W
	MOVE.B	#$FF,$FFFFFA1F.W

	move.L	#$707FFFF,$fFFf8800.W
	move	#$2420,$ffff8604.w
	move.b	#$00,$ffff8609.w
	move.b	#$7b,$ffff860b.w
	move.b	#$c4,$ffff860d.w
	MOVE.B	#$3,$FFFFFC04.W
	MOVE.B	#$95,$FFFFFC04.W
	MOVE.B	#$3,$FFFFFC00.W
	MOVE.B	#$96,$FFFFFC00.W
	MOVE	#$C8,D0
E	NOP
	DBF	D0,E
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	;MOVE.B	#$12,$FFFFFC02.W

	LEA	RTE2(PC),A0
	MOVE.L	A0,$68.W
	MOVE.L	A0,$134.W
	MOVE.L	A0,$120.W
	MOVE.L	A0,$70.W
	BRA.S	CONTINUE_GAIEMENT
RTE2	RTE
CONTINUE_GAIEMENT
DEBUT:	
 	MOVE.L	#$31415926,$426.W
 	MOVE.L	#ROUTINE,$42A.W

	;PEA	0.W
	;MOVE 	#$20,-(SP)
	;TRAP 	#1
	;ADDQ.L 	#6,SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W

	MOVE.L	#INTER_RTE,$68.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W

	LEA	DATA_HS,A0
	MOVE.L	A0,POIT
		
	MOVE.L	#VBLR_IR,$70.W
	MOVE.L	#TB0_IR,$120.W
	
	;MOVE.B	#$12,$FFFFFC02.W

	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	MOVE	#$2300,SR

BARRAGE
BOUCLE	BRA.S	BOUCLE
	MOVE	#$60FE,BOUCLE
	MOVE.W	#$2700,SR
	MOVE.L	#INTER_RTE,$70.W
	MOVE.L	#INTER_RTE,$134.W
	TST	FLAG_RET
	BEQ	SAUVE
	TST	FLAG_VBLQ
	BNE.S	.OK
	JMP	ROUTINE
.OK	CLR	FLAG_RET
	CLR	FLAG_VBLQ

	MOVEM.L	PAL_INIT,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	LEA	SAUVEUR,A0
	MOVE.B	(A0)+,$FFFFFA07.W
	MOVE.B	(A0)+,$FFFFFA09.W
	MOVE.B	(A0)+,$FFFFFA0F.W
	MOVE.B	(A0)+,$FFFFFA13.W
	MOVE.B	(A0)+,$FFFFFA1B.W
	MOVE.B	(A0)+,$FFFFFA21.W
	MOVE.B	(A0)+,$FFFFFA15.W
	MOVE.B	(A0)+,$FFFFFA17.W
	MOVE.B	(A0)+,$FFFFFA19.W
	MOVE.B	(A0)+,$FFFFFA1F.W
	MOVE.B	(A0)+,$FFFF8201.W
	MOVE.B	(A0)+,$FFFF8203.W

	MOVE.B	(A0)+,$FFFFFA01.W
	MOVE.B	(A0)+,$FFFFFA03.W
	MOVE.B	(A0)+,$FFFFFA05.W
	MOVE.B	(A0)+,$FFFFFA0B.W
	MOVE.B	(A0)+,$FFFFFA0D.W
	MOVE.B	(A0)+,$FFFFFA11.W

	MOVE.L	(A0)+,$68.W
	MOVE.L	(A0)+,$70.W
	MOVE.L	(A0)+,$134.W
	MOVE.L	(A0)+,$120.W
	MOVE.L	(A0)+,$8.W
	MOVE.L	(A0)+,$C.W
	MOVE.L	(A0)+,$10.W
	MOVE.L	(A0)+,$14.W
	MOVE.L	#VBLQ,$4D2.W
	MOVE	#$2300,SR
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W

MOD_PILE	LEA	$12345678,A7
	MOVEM.L	SAVER,D0-D7/A0-A6
;	MOVE	#$700,$FFFF8240.W
;.TOURN	CMPI.B	#$3B,$FFFFFC02.W
;	BNE.S	.TOURN
;	MOVEM.L	PAL_INIT,D0-D7
;	MOVEM.L	D0-D7,$FFFF8240.W
	RTS
**	
	
SAUVE
*Sauvegarde
	SUBA.L	A5,A5
	LEA	$FFFFFA01.W,A0
	MOVE.L	#$FF040000,D0
	MOVEP.L	D0,0(A0)
	MOVEQ	#0,D0
	MOVEP.L	D0,8(A0)
	MOVEQ	#$48,D0
	MOVEP.L	D0,$10(A0)

;	MOVE.L	#0,$84(A5)
;	MOVE.L	#0,$B4(A5)
;	MOVE.L	#0,$B8(A5)
*
	;MOVE.L	#$FE3CD8,$404(A5)
	;LEA	$472(A5),A0
	;MOVE.L	#$FC0FCA,(A0)+
	;MOVE.L	#$FC12B6,(A0)+
	;MOVE.L	#$FC1560,(A0)+
	;MOVE.L	#$FC117A,(A0)
*
	;LEA	$7B1A4(PC),A0
	;MOVE.L	A0,$70(A5)

	MOVE.W	#$2300,SR
	LEA	$FFFF8800.W,A0
	MOVE.B	#7,(A0)
	MOVE.B	#$C0,2(A0)
	MOVE.B	#$E,(A0)
	MOVE.B	#9,2(A0)

	CLR.W	-(A7)
	PEA	NAME(PC)
	MOVE.W	#$3C,-(A7)
	TRAP	#1
	ADDQ.L	#8,A7

	MOVE.W	D0,D7
	
	MOVE.L	MODIF,A6
	LEA	PROVIZOIR,A4
	MOVEQ	#17-1,D0
SAV	MOVE	(A6)+,(A4)+
	DBF	D0,SAV
	LEA	-34(A6),A6
	LEA	PALETTE,A4
	CLR	(A6)+
	REPT	8
	MOVE.L	(A4)+,(A6)+
	ENDR
	
MODIF	EQU	*+2
	PEA	$78000
	MOVE.L	#32066,-(SP)
	MOVE.W	D7,-(A7)
	MOVE.W	#$40,-(A7)
	TRAP	#1
	LEA	$C(A7),A7

	MOVE.W	D7,-(A7)
	MOVE.W	#$3E,-(A7)
	TRAP	#1
	ADDQ.L	#4,A7

	MOVEQ	#$7C,D0
LAB	STOP	#$2300
	DBF	D0,LAB

	MOVE.L	MODIF,A6
	LEA	PROVIZOIR,A4
	MOVEQ	#17-1,D0
SAV2	MOVE	(A4)+,(A6)+
	DBF	D0,SAV2

	LEA	TABL_NAM,A6
	OPT	O-
MODTN	EQU	*+2
	LEA	0(A6),A6
	OPT	O+
	TST.L	(A6)
	BEQ.S	.RESET
	ADD	#14,MODTN
	LEA	NAME,A4
	REPT	14
	MOVE.B	(A6)+,(A4)+
	ENDR
	JMP	ROUTINE2
.RESET	CLR.L	$42A.W
	CLR.L	$426.W
	MOVE.L	4.W,A0
	JMP	(A0)
FLAG_RET	DC	0
VBLR_IR
	CLR.B	$FFFFFA1B.W
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.B	#0,$FFFFFA19.W
	MOVE.B	#99,$FFFFFA1F.W		; BORDER HAUT
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W

	MOVE.B	#28,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W

	LEA	PALETTE,A0
INTER_RTE	RTE
PALETTE	DC	$000,$111,$222,$333,$444,$555,$666,$777
	DC	$100,$200,$300,$400,$500,$600,$700,$710
PAL_INIT	DS.B	32
INTER_TMA:	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE	#$2100,SR
full
	LEA	$FFFF8209.W,A3
	LEA	$FFFF8260.W,A6
	LEA	$FFFF820A.W,A5

	MOVEQ	#0,D0
	MOVEQ	#2,D1

	STOP	#$2100
	MOVE	#$2700,SR
	MOVE	#$2300,(SP)

	MOVEQ	#29,D2
SYNCHROA:	DBF	D2,SYNCHROA
	NOP

	MOVE.B	D0,(A5)
	REPT	6
	NOP
	ENDR
	MOVE.B	D1,(A5)

*******

	MOVEA.L	POIT,A2

	;MOVE.L	A6,USP
	MOVE.L	#$FFFF8209,A6
	MOVE.L	#$FFFF8260,A4
	MOVE.L	#$FFFF820A,A3
	MOVEQ	#0,D1		
	MOVEQ	#$10,D6
	MOVEQ	#3,D0
;	MOVE	#$2700,SR

SYNCHRO	MOVE.B	(A6),D7
	BEQ.S	SYNCHRO
	SUB.W	D7,D6		
	LSL.W	D6,D1		
	;MOVE.L	USP,A6
	DCB	97-3-4,$4E71

	REPT	9
	MOVE.L	(A2)+,A1	*3
	JSR	(A1)	*4
	ENDR
	
	DCB	7-1,$4E71

FULLSCREEN	NOP
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	17,$2090
	DCB	3,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71
	DBF	D0,FULLSCREEN
	MOVE.L	(A0)+,$FFFF8240.W
	MOVE.L	(A0)+,$FFFF8244.W
	MOVE.L	(A0)+,$FFFF8248.W
	MOVE.L	(A0)+,$FFFF824C.W
	MOVE.L	(A0)+,$FFFF8250.W
	MOVE.L	(A0)+,$FFFF8254.W
	MOVE.L	(A0)+,$FFFF8258.W
	MOVE.L	(A0)+,$FFFF825C.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	MOVE	#$2300,SR

	CMPI.B	#$50,$FFFFFC02.W
	BNE.S	PAS_HAUT
	ADD.L	#160,INC
PAS_HAUT	CMPI.B	#$48,$FFFFFC02.W
	BNE.S	PAS_BAS
	SUB.L	#160,INC
PAS_BAS
	CMPI.B	#$4E,$FFFFFC02.W
	BNE.S	PAS_HAUT2
	ADD.L	#160,Y_VALUE
PAS_HAUT2	CMPI.B	#$4A,$FFFFFC02.W
	BNE.S	PAS_BAS2
	SUB.L	#160,Y_VALUE
PAS_BAS2

	MOVE.L	INC,D0
	ADD.L	D0,Y_VALUE
	CMPI.B	#$4B,$FFFFFC02.W
	BNE.S	PAS_GA
	ADDQ.L	#2,Y_VALUE
PAS_GA	CMPI.B	#$4D,$FFFFFC02.W
	BNE.S	PAS_DR
	SUBQ.L	#2,Y_VALUE
PAS_DR

	MOVEQ	#0,D0	
	MOVEQ	#0,D1
	MOVE.L	Y_VALUE,D0

	LSR.L	#8,D0
	MOVE.L	D0,D1
	LSL.L	#8,D1

	MOVE.L	Y_VALUE,D0
	SUB.L	D1,D0

	LEA	DATA_HS,A0

	TST.L	D0
	BNE.S	DEJ
	LEA	RIEN,A0
	MOVE.L	A0,POIT
	BRA.S	OKAY
DEJ	SUBQ.L	#2,D0
	MULU	#18,D0
	ADDA.L	D0,A0
	MOVE.L	A0,POIT
		
OKAY
	MOVE.L	Y_VALUE,D0
	ADD.L	#1600+230*4+(28-14)*160-34,D0
	MOVE.L	D0,MODIF

	MOVEQ	#0,D0
	ADD.L	D1,D0
	lsr.l	#8,d0
	LEA	$FFFF8201.W,A0
	movep	d0,(a0)

	CMPI.B	#$3B,$FFFFFC02.W
	BNE.S	.NO_SAVE
	MOVE	#$4E71,BARRAGE
.NO_SAVE
	CMPI.B	#$3C,$FFFFFC02.W
	BNE.S	.NO_F2
	MOVE	#$4E71,BARRAGE
	MOVE	#1,FLAG_RET
.NO_F2
	CMPI.B	#$1,$FFFFFC02.W
	BNE.S	.NO_ESC
	MOVEM.L	PAL_INIT,D0-D7
	MOVEM.L	D0-D7,PALETTE
.NO_ESC	
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TAB
	MOVEM.L	PAL,D0-D7
	MOVEM.L	D0-D7,PALETTE
.NO_TAB

	CMPI.B	#$44,$FFFFFC02.W
	BNE.S	.NO_F10
	MOVE	#$700,$FFFF8240.W
	CLR.L	$426.W
	CLR.L	$42A.W
.NO_F10
	CMPI.B	#$39,$FFFFFC02.W
	BNE.S	.RTE
	CLR.L	INC
.RTE	RTE
PAL	DC	$000,$111,$222,$333,$444,$555,$666,$777
	DC	$000,$111,$222,$333,$444,$555,$666,$777
Y_VALUE	DC.L	0
INC	DC.L	0

TB0_IR	
	CLR.B	$FFFFFA1B.W
	MOVE	#$223,$FFFF8240.W
	MOVE.B	#200,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TB1_IR,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE			

TB1_IR	
	CLR	$FFFF8240.W
	DCB	127-4,$4E71
	CLR.B	$FFFF820A.W
	DCB	10,$4E71
	MOVE.B	#2,$FFFF820A.W
	CLR.B	$FFFFFA1B.W
	MOVE.L	#TB0_IR,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE			

FIN:	MOVE.L	4.W,A0
	JMP	(A0)

H0	
	DCB	115/5,$2090
	NOP
	NOP
	RTS

H70
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	3,$4E71
	DCB	17,$2090
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	2,$2090
	DCB	3,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	NOP
	RTS	=4 NOPS
H44
	DCB	3,$4E71
	DCB	18,$2090
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	2,$2090
	DCB	3,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	NOP
	RTS	=4 NOPS
H24
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	NOP
	DCB	17,$2090
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	3,$4E71
	DCB	2,$2090
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	REPT	3
	NOP
	ENDR
	RTS	=4 NOPS
H2
	NOP
	DCB	18,$2090
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	3,$4E71
	DCB	2,$2090
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	REPT	3
	NOP
	ENDR
	RTS	=4 NOPS
H80
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	7,$2090
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	3,$4E71
	DCB	9,$2090
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	3,$4E71
	DCB	2,$2090
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	NOP
	RTS
	 
	DATA

SAUVEUR	DS.B	12+6+8*4
	DS.B	160
DATA_HS
 DC.L H44,H44,H2,H2,H2,H80,H0,H0,H0
 DC.L H44,H44,H2,H2,H80,H0,H0,H0,H0
 DC.L H44,H44,H2,H80,H0,H0,H0,H0,H0
 DC.L H44,H44,H80,H0,H0,H0,H0,H0,H0
 DC.L H44,H24,H24,H2,H80,H0,H0,H0,H0
 DC.L H44,H24,H24,H80,H0,H0,H0,H0,H0
 DC.L H24,H2,H2,H2,H2,H2,H0,H0,H0
 DC.L H24,H2,H2,H2,H2,H0,H0,H0,H0
 DC.L H24,H2,H2,H2,H0,H0,H0,H0,H0
 DC.L H24,H2,H2,H0,H0,H0,H0,H0,H0
 DC.L H24,H2,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H0,H0,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H24,H24,H80,H80,H0,H0
 DC.L H70,H44,H2,H2,H2,H80,H0,H0,H0
 DC.L H70,H44,H2,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H2,H80,H0,H0,H0,H0,H0
 DC.L H44,H2,H2,H2,H2,H2,H0,H0,H0
 DC.L H44,H2,H2,H2,H2,H0,H0,H0,H0
 DC.L H44,H2,H2,H2,H0,H0,H0,H0,H0
 DC.L H44,H2,H2,H0,H0,H0,H0,H0,H0
 DC.L H44,H2,H0,H0,H0,H0,H0,H0,H0
 DC.L H44,H0,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H2,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H0,H0,H0,H0,H0,H0,H0
 DC.L H44,H44,H44,H2,H80,H0,H0,H0,H0
 DC.L H44,H44,H44,H80,H0,H0,H0,H0,H0
 DC.L H70,H70,H2,H2,H2,H80,H0,H0,H0
 DC.L H70,H70,H2,H2,H80,H0,H0,H0,H0
 DC.L H70,H70,H2,H80,H0,H0,H0,H0,H0
 DC.L H44,H24,H2,H2,H2,H2,H0,H0,H0
 DC.L H44,H24,H2,H2,H2,H0,H0,H0,H0
 DC.L H44,H24,H2,H2,H0,H0,H0,H0,H0
 DC.L H44,H24,H2,H0,H0,H0,H0,H0,H0
 DC.L H44,H24,H0,H0,H0,H0,H0,H0,H0
 DC.L H70,H0,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H44,H2,H2,H80,H0,H0,H0
 DC.L H70,H44,H44,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H44,H80,H0,H0,H0,H0,H0
 DC.L H44,H24,H24,H2,H80,H70,H0,H0,H0
 DC.L H70,H44,H24,H24,H80,H0,H0,H0,H0
 DC.L H44,H44,H2,H2,H0,H0,H0,H0,H0
 DC.L H44,H44,H2,H0,H0,H0,H0,H0,H0
 DC.L H44,H44,H0,H0,H0,H0,H0,H0,H0
 DC.L H70,H24,H2,H2,H0,H0,H0,H0,H0
 DC.L H70,H24,H2,H0,H0,H0,H0,H0,H0
 DC.L H70,H24,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H2,H2,H2,H80,H0,H0
 DC.L H70,H70,H44,H2,H2,H80,H0,H0,H0
 DC.L H70,H70,H44,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H2,H2,H2,H2,H2,H0,H0
 DC.L H70,H44,H2,H2,H2,H2,H0,H0,H0
 DC.L H70,H44,H2,H2,H2,H0,H0,H0,H0
 DC.L H70,H44,H2,H2,H0,H0,H0,H0,H0
 DC.L H70,H44,H2,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H24,H2,H2,H0,H0
 DC.L H24,H24,H24,H24,H24,H2,H0,H0,H0
 DC.L H24,H24,H24,H24,H24,H0,H0,H0,H0
 DC.L H70,H44,H44,H44,H80,H0,H0,H0,H0
 DC.L H70,H70,H70,H2,H2,H2,H80,H0,H0
 DC.L H70,H70,H70,H2,H2,H80,H0,H0,H0
 DC.L H70,H70,H70,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H24,H2,H2,H2,H2,H0,H0
 DC.L H44,H44,H44,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H2,H2,H0,H0,H0,H0
 DC.L H70,H44,H24,H2,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H0,H0,H0,H0,H0,H0
 DC.L H70,H70,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H70,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H2,H2,H80,H0,H0
 DC.L H70,H70,H44,H44,H2,H80,H0,H0,H0
 DC.L H70,H70,H44,H44,H80,H0,H0,H0,H0
 DC.L H70,H70,H44,H24,H24,H2,H80,H0,H0
 DC.L H70,H44,H44,H2,H2,H2,H0,H0,H0
 DC.L H70,H44,H44,H2,H2,H0,H0,H0,H0
 DC.L H70,H44,H44,H2,H0,H0,H0,H0,H0
 DC.L H70,H44,H44,H0,H0,H0,H0,H0,H0
 DC.L H70,H70,H24,H2,H2,H0,H0,H0,H0
 DC.L H70,H70,H24,H2,H0,H0,H0,H0,H0
 DC.L H70,H70,H24,H0,H0,H0,H0,H0,H0
 DC.L H70,H24,H24,H24,H24,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H24,H24,H24,H0,H0
 DC.L H70,H70,H70,H44,H2,H2,H80,H0,H0
 DC.L H44,H44,H44,H44,H2,H2,H0,H0,H0
 DC.L H44,H44,H44,H44,H2,H0,H0,H0,H0
 DC.L H44,H44,H44,H44,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H2,H2,H2,H0,H0,H0
 DC.L H70,H70,H44,H2,H2,H0,H0,H0,H0
 DC.L H70,H70,H44,H2,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H24,H24,H0,H0,H0,H0
 DC.L H70,H70,H24,H24,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H44,H80,H2,H0,H0
 DC.L H70,H70,H44,H44,H44,H80,H0,H0,H0
 DC.L H70,H70,H70,H44,H24,H2,H2,H80,H0
 DC.L H44,H44,H44,H44,H24,H2,H2,H0,H0
 DC.L H44,H44,H44,H44,H24,H2,H0,H0,H0
 DC.L H44,H44,H44,H44,H24,H0,H0,H0,H0
 DC.L H70,H44,H44,H44,H0,H0,H0,H0,H0
 DC.L H70,H70,H70,H2,H2,H2,H0,H0,H0
 DC.L H70,H70,H70,H2,H2,H0,H0,H0,H0
 DC.L H70,H70,H70,H2,H0,H0,H0,H0,H0
 DC.L H70,H70,H70,H0,H0,H0,H0,H0,H0
 DC.L H24,H70,H70,H24,H24,H0,H0,H0,H0
 DC.L H70,H24,H24,H24,H24,H24,H24,H0,H0
 DC.L H44,H44,H44,H44,H44,H2,H2,H0,H0
 DC.L H44,H44,H44,H44,H44,H2,H0,H0,H0
 DC.L H44,H44,H44,H44,H44,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H2,H2,H2,H0,H0
 DC.L H70,H70,H44,H44,H2,H2,H0,H0,H0
 DC.L H70,H70,H44,H44,H2,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H0,H0,H0,H0,H0
 DC.L H70,H44,H44,H24,H24,H24,H0,H0,H0
 DC.L H70,H70,H70,H24,H2,H0,H0,H0,H0
 DC.L H70,H70,H70,H24,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H70,H70,H0,H0,H0
 DC.L H44,H44,H44,H44,H44,H24,H2,H2,H2
 DC.L H44,H44,H44,H44,H44,H24,H2,H2,H0
 DC.L H70,H44,H44,H44,H44,H2,H2,H0,H0
 DC.L H70,H44,H44,H44,H44,H2,H0,H0,H0
 DC.L H70,H44,H44,H44,H44,H0,H0,H0,H0
 DC.L H70,H44,H44,H44,H24,H24,H2,H0,H0
 DC.L H70,H70,H70,H44,H2,H2,H0,H0,H0
 DC.L H70,H70,H70,H44,H2,H0,H0,H0,H0
 DC.L H70,H70,H70,H44,H0,H0,H0,H0,H0

RIEN	REPT	9
	DC.L	H0
	ENDR
PROVIZOIR	DS.B	34
NAME	DC.B	'_______1.PI1',0,0
	EVEN
TABL_NAM	DC.B	'_______2.PI1',0,0
	DC.B	'_______3.PI1',0,0
	DC.B	'_______4.PI1',0,0
	DC.B	'_______5.PI1',0,0
	DC.B	'_______6.PI1',0,0
	DC.B	'_______7.PI1',0,0
	DC.B	'_______8.PI1',0,0
	DC.B	'_______9.PI1',0,0
	DC.B	0,0,0,0,0,0
	EVEN
 	SECTION BSS
F_PILE	DS.L 	128
D_PILE	DS.L 	1
POIT	DS.L	1
	EVEN
