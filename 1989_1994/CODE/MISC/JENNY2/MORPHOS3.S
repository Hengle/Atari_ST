;fin: .S et tout

NB_PTS = 2032
NB_PASSES = 50
DEBUT:
	PEA	0.W
	MOVE 	#$20,-(SP)
	TRAP 	#1
	ADDQ.L 	#6,SP

	CLR.W 	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE 	#5,-(SP)
	TRAP 	#14
	LEA.L 	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W

	move.l	#BUFFER,d0
	CLR.B	D0
	move.l	d0,SCREEN1
	add.l	#32000,d0
	move.l	d0,SCREEN2

	move.l	SCREEN1,d0
;	MOVE.L	SCREEN2,SCREEN1
;	MOVE.L	D0,SCREEN2
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)

	MOVE.L	SCREEN1,A0
	LEA	IMG_DEP+34,A1
	MOVE	#7999,D0
T	MOVE.L	(A1)+,(A0)+
	DBF	D0,T

	MOVE.L	SCREEN1,A0
	LEA	FORME_DEP_X,A1
	LEA	FORME_DEP_Y,A2
	LEA	FORME_DEP,A3
	JSR	CALCUL
	MOVE.L	A1,SAVE_A1
	MOVE.L	A2,SAVE_A2
	MOVE.L	A3,SAVE_A3
	MOVE.L	COMPTE_PTS,SAVE_PTS
	CLR.L	COMPTE_PTS

	MOVE.L	SCREEN1,A0
	LEA	IMG_FIN+34,A1
	MOVE	#7999,D0
T2	MOVE.L	(A1)+,(A0)+
	DBF	D0,T2

	CLR	COORD_X
	CLR	COORD_Y
	MOVE.L	SCREEN1,A0
	LEA	FORME_FIN_X,A1
	LEA	FORME_FIN_Y,A2
	LEA	FORME_FIN,A3
	JSR	CALCUL
	MOVE.L	COMPTE_PTS,D0	;Nombre de pts forme finale
	MOVE.L	SAVE_PTS,D1	;Nombre de pts forme initiale
	CMP.L	D0,D1
	BEQ	IMPECCABLE	;TRES, TRES improbable!
	BGT	D1_PLUS_GRD
D0_PLUS_GRD	;d0=nb de pts de l'animation
	SUB.L	D1,D0	D0 = nb de pts manquants
	LEA	FORME_DEP_X,A3
	LEA	FORME_DEP_Y,A4
	LEA	FORME_DEP,A5
	MOVE.L	SAVE_A1,A1
	MOVE.L	SAVE_A2,A2
	MOVE.L	SAVE_A3,A6
	SUBQ.L	#1,D0
COMPLETE_IT	MOVE.B	(A3)+,(A1)+
	MOVE	(A4)+,(A2)+
	MOVE.L	(A5)+,(A6)+
	DBF	D0,COMPLETE_IT
	BRA	IMPECCABLE

D1_PLUS_GRD	;d1=nb de pts de l'animation
	SUB.L	D0,D1	D1 = nb de pts manquants
	MOVE.L	A3,A6
	LEA	FORME_FIN_X,A3
	LEA	FORME_FIN_Y,A4
	LEA	FORME_FIN,A5
	SUBQ.L	#1,D1
COMPLETE	MOVE.B	(A3)+,(A1)+
	MOVE	(A4)+,(A2)+
	MOVE.L	(A5)+,(A6)+
	DBF	D1,COMPLETE
IMPECCABLE	;Tout est bon pour commencer la mÇtamorphose...
******************************************************************
	LEA	FORME_DEP,A4
	LEA	FORME_FIN,A5

	MOVE	#NB_PTS-1,D4	NB_PTS
DO_A_LINE	
	LEA	BUFFER_COORD,A1
	ADDA.L	PETIT_PNT,A1
	LEA	BUFFER_COORD2,A2
	ADDA.L	PETIT_PNT,A2
	ADDA.L	PETIT_PNT,A2

	MOVEQ	#1,D5
DO_A_COORD	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE	(A4)+,D0
	MOVE	(A5)+,D1
	LEA	SOUS_BUFFER,A0
;MOD_R	EQU	*+2
;	LEA	0(A0),A0
MOD_T	EQU	*+2
	LEA	0(A0),A0
	JSR	DO_TRANS
	ADD	#2,MOD_T
	DBF	D5,DO_A_COORD
	CLR	MOD_T
;GERE SOUS BUFFER
	LEA	SOUS_BUFFER,A0
	MOVE	#NB_PASSES-1,D7

TRANS	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE	(A0)+,D0
	MOVE	(A0)+,D1
	MOVEM.L	D7/A0,-(SP)
	JSR	ROUTINE
	MOVEM.L	(SP)+,D7/A0
	LEA	NB_PTS(A1),A1
	LEA	NB_PTS*2(A2),A2
	DBF	D7,TRANS
	ADDQ.L	#1,PETIT_PNT
;	ADD	#NB_PASSES*8,MOD_R
	DBF	D4,DO_A_LINE
FINITO	BRA	ALL_IS_OVER

ROUTINE	
	MULU	#160,D1
	DIVS	#16,D0
	MOVE	D0,D2
	MULU	#8,D2
	ADD.L	D2,D1
	SWAP	D0
	CMPI	#$7,D0
	BGT.S	PLUS_UNZ
	BRA.S	PLUS_LOINZ
PLUS_UNZ	ADD.L	#1,D1
	SUB.L	#8,D0
PLUS_LOINZ
	LEA	KKZ,A0
	LSL	#3,D0
	JSR	(A0,D0.W)

;	CMPI	#-25,ZA
;	BGT.S	PLD
;	BRA.S	NOPLD
;PLD	ADD	#2,D1
;NOPLD
;	CMPI	#0,ZA
;	BGT.S	PLD2
;	BRA.S	NOPLD2
;PLD2	ADD	#2,D1
;NOPLD2	CMPI	#25,ZA
;	BGT.S	PLD3
;	BRA.S	NOPLD3
;PLD3	ADD	#2,D1
;NOPLD3
;
;	SUB.L	#23000,D1
	MOVE	D1,(A2)
	RTS
PETIT_PNT	DC.L	0
KKZ	MOVE.B	#%10000000,(A1)
	NOP
	RTS
	MOVE.B	#%01000000,(A1)
	NOP
	RTS
	MOVE.B	#%00100000,(A1)
	NOP
	RTS	
	MOVE.B	#%00010000,(A1)
	NOP
	RTS	
	MOVE.B	#%00001000,(A1)
	NOP
	RTS	
	MOVE.B	#%00000100,(A1)
	NOP
	RTS
	MOVE.B	#%00000010,(A1)
	NOP
	RTS
	MOVE.B	#%00000001,(A1)
	NOP
	RTS


***************************************************************
;	MOVE.L	#0,D0	DEPART
;	MOVE.L	#319,D1	ARRIVEE
;	LEA	COORD,A0
DO_TRANS
	MOVE.L	#NB_PASSES,D2	EN 'D2' PASSES
	MOVE.L	D2,D6
	MOVE	D2,MODIF
	MOVE	D2,MODIF_

	LSL.L	#8,D0
	LSL.L	#8,D1
	CMP.L	D0,D1
	BGT.S	ROUTY1
ROUTY2	;D1 PLUS PETIT
	MOVE.L	D0,D2
	SUB.L	D1,D0	D1=AMPLITUDE
	DIVU	D6,D0
	SWAP	D0
	CLR	D0
	SWAP	D0
MODIF_	EQU	*+2
	MOVE	#0,D7
	SUBQ	#1,D7
DO_IT_AGAIN_	SUB.L	D0,D2
	MOVE.L	D2,D3
	LSR.L	#8,D3	CA EN FAIT UN
	MOVE	D3,(A0)
	ADDQ.L	#4,A0
	DBF	D7,DO_IT_AGAIN_
FINI2	RTS
****
	
ROUTY1	;D0 PLUS PETIT
	MOVE.L	D0,D2
	SUB.L	D0,D1	D1=AMPLITUDE
	DIVU	D6,D1
	SWAP	D1
	CLR	D1
	SWAP	D1
MODIF	EQU	*+2
	MOVE	#0,D7
	SUBQ	#1,D7
DO_IT_AGAIN	ADD.L	D1,D2
	MOVE.L	D2,D3
	LSR.L	#8,D3	CA EN FAIT UN
	MOVE	D3,(A0)
	ADDQ.L	#4,A0
	DBF	D7,DO_IT_AGAIN
FINITED	RTS


FIN	MOVE.L	4.W,(A0)
	JMP	(A0)
SAVE_A1	DC.L	0
SAVE_A2	DC.L	0
SAVE_A3	DC.L	0
SAVE_PTS	DC.L	0
CALCUL
BOUCLE0	MOVE	(A0),D0
	BNE.S	PLAN_NON_VIDE
BOUCLE1	ADDQ.L	#8,A0
	ADD	#16,COORD_X
	CMPI	#320,COORD_X
	BNE.S	BOUCLE0
	CLR	COORD_X
	ADDQ	#1,COORD_Y
	CMPI	#200,COORD_Y
	BNE.S	BOUCLE0
	BRA	FINI
PLAN_NON_VIDE	MOVEQ	#15,D1
	MOVE	COORD_X,D2
LOOP0	BTST	D1,D0
	BNE.S	ALLUMê
BOUCLE2	ADDQ	#1,D2
	DBF	D1,LOOP0
	BRA.S	BOUCLE1
ALLUMê	ADDQ.L	#1,COMPTE_PTS
	MOVE	D2,(A3)+
	MOVE	COORD_Y,(A3)+
	MOVEM.L	D0-A0/A4-A6,-(SP)
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE	D2,D0
	MOVE	COORD_Y,D1

	MULU	#160,D1
	DIVS	#16,D0
	MOVE	D0,D2
	MULU	#8,D2
	ADD.L	D2,D1
	SWAP	D0
	CMPI	#$7,D0
	BGT.S	PLUS_UN
	BRA.S	PLUS_LOIN
PLUS_UN	ADD.L	#1,D1
	SUB.L	#8,D0
PLUS_LOIN
	LEA	KK,A0
	LSL	#3,D0
	JSR	(A0,D0.W)

;	CMPI	#-25,ZA
;	BGT.S	PLD
;	BRA.S	NOPLD
;PLD	ADD	#2,D1
;NOPLD
;	CMPI	#0,ZA
;	BGT.S	PLD2
;	BRA.S	NOPLD2
;PLD2	ADD	#2,D1
;NOPLD2	CMPI	#25,ZA
;	BGT.S	PLD3
;	BRA.S	NOPLD3
;PLD3	ADD	#2,D1
;NOPLD3
;
;	SUB.L	#23000,D1
	MOVE	D1,(A2)+
	MOVEM.L	(SP)+,D0-A0/A4-A6
	BRA.S	BOUCLE2

KK	MOVE.B	#%10000000,(A1)+
	NOP
	RTS
	MOVE.B	#%01000000,(A1)+
	NOP
	RTS
	MOVE.B	#%00100000,(A1)+
	NOP
	RTS	
	MOVE.B	#%00010000,(A1)+
	NOP
	RTS	
	MOVE.B	#%00001000,(A1)+
	NOP
	RTS	
	MOVE.B	#%00000100,(A1)+
	NOP
	RTS
	MOVE.B	#%00000010,(A1)+
	NOP
	RTS
	MOVE.B	#%00000001,(A1)+
	NOP
	RTS
FINI	RTS
ALL_IS_OVER	NOP
	JSR	EFF
	CLR.L	PNT_PLAN
	MOVE.L	#VBL_DEP,$70.W
BOUCLE	BRA.S	BOUCLE

VBL_DEP	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	NOT0
	MOVE	#$001,$FFFF8240.W
NOT0
	SF	$FFFF8242.W

	MOVEA.L	SCREEN1,A4
	JSR	EFF

	LEA	FORME_DEP_X,A0
	LEA	FORME_DEP_Y,A1
	
	REPT	NB_PTS
	MOVE.B	(A0)+,D0
	MOVE	(A1)+,D1
	OR.B	D0,(A4,D1.W)
	ENDR

	move.l	SCREEN1,d0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)
	
	ST	$ffff8240.w
	ADDQ.L	#1,TIME
	CMPI.L	#50,TIME
	BNE.S	CRACK
	CLR.L	TIME
	MOVE.L	#VBL_DO_IT,$70.W
CRACK	RTE
TIME	DC.L	0
VBL_DO_IT	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	NOTI
	MOVE	#$001,$FFFF8240.W
NOTI
	SF	$FFFF8242.W
;	MOVE	#$222,$FFFF8244.W
	
;	MOVE	#$222,$FFFF8246.W

;	MOVE	#$444,$FFFF8248.W

;	MOVE	#$444,$FFFF824A.W
;	MOVE	#$444,$FFFF824C.W
;	MOVE	#$444,$FFFF824E.W

;	MOVE	#$666,$FFFF8250.W
;	MOVE	#$666,$FFFF8252.W
;	MOVE	#$666,$FFFF8254.W
;	MOVE	#$666,$FFFF8256.W
;	MOVE	#$666,$FFFF8258.W
;	MOVE	#$666,$FFFF825A.W
;	MOVE	#$666,$FFFF825C.W
;	MOVE	#$666,$FFFF825E.W


	cmpi.b	#$1,$fffffc02.w
	beq	poi

	MOVEA.L	SCREEN1,A4

;	ADDA.L	PNT_PLAN,A4
;	ADDA.L	PNT_PLAN,A0
;	CMPI.L	#4,PNT_PLAN
;	BNE.S	JJ
;	ADDQ.L	#4,A0
;	BRA.S	JJJ
;JJ	SUBQ.L	#4,A0
;JJJ
	LEA	BUFFER_COORD2,A1
	ADD.L	CNT,A1
	ADD.L	CNT,A1

;	MOVEQ	#0,D0
	
;	REPT	NB_PTS
;	MOVE	(A1)+,D1
;	MOVE.B	D0,(A4,D1.W)
;	ENDR
	JSR	EFF

	ADD.L	#NB_PTS,CNT
	CMPI.L	#NB_PTS*NB_PASSES,CNT
	BNE.S	CA_TOURNE
	CLR.L	TIME
	CLR.L	CNT
	MOVE.L	#VBL_FIN,$70.W
	RTE
CA_TOURNE	LEA	BUFFER_COORD,A0
	ADD.L	CNT,A0
	LEA	BUFFER_COORD2,A1
	ADD.L	CNT,A1
	ADD.L	CNT,A1
;	CMPI.B	#-1,NB_PTS(A0)
;	BNE.S	OKOK
;	CLR.L	CNT
;	LEA	BUFFER_COORD2,A1
;	LEA	BUFFER_COORD,A0
OKOK	

	REPT	NB_PTS
	MOVE.B	(A0)+,D0
	MOVE	(A1)+,D1
	OR.B	D0,(A4,D1.W)
	ENDR


	MOVE.L	A0,-(SP)
	move.l	SCREEN1,d0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)
	MOVE.L	(SP)+,A0
poi
	ST	$FFFF8240.W
	
;	ADDQ.L	#2,PNT_PLAN
;	CMPI.L	#8,PNT_PLAN
;	BNE.S	DF
;	CLR.L	PNT_PLAN
DF	RTE
VBL_FIN	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	NOT1
	MOVE	#$001,$FFFF8240.W
NOT1
	SF	$FFFF8242.W

	MOVEA.L	SCREEN1,A4
	JSR	EFF

	LEA	FORME_FIN_X,A0
	LEA	FORME_FIN_Y,A1
	
	REPT	NB_PTS
	MOVE.B	(A0)+,D0
	MOVE	(A1)+,D1
	OR.B	D0,(A4,D1.W)
	ENDR

	move.l	SCREEN1,d0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)

	ST	$ffff8240.w
	ADDQ.L	#1,TIME
	CMPI.L	#50,TIME
	BNE.S	CRACK2
	CLR.L	TIME
	CLR.L	PNT_PLAN
	JSR	EFF
	move.l	SCREEN1,d0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	JSR	EFF

	MOVE.L	#VBL_DEP,$70.W
CRACK2	RTE

EFF	MOVEM.L	D0-A6,-(SP)
	movea.l	SCREEN1,a0

	moveq	#0,d0
	moveq	#0,d1
	moveq	#0,d2
	moveq	#0,d3
	moveq	#0,d4
	moveq	#0,d5
	moveq	#0,d6
	moveq	#0,d7
	sub.l	a1,a1
	sub.l	a2,a2
	sub.l	a3,a3
	sub.l	a4,a4
	sub.l	a5,a5
	sub.l	a6,a6
efecr1	
	MOVE	#199,D1
CLS
N	SET	0
	REPT	20
	MOVE	D0,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D1,CLS
	MOVEM.L	(SP)+,D0-A6
	RTS

	DATA
IMG_FIN	INCBIN	KIMAGURE.PI1
IMG_DEP	INCBIN	GEN4.PI1

;AI_CHAN.PI1 = 703 PTS
;ZAPPY.PI1 = 240
;CHOICE.PI1 = 708
;GEN4.PI1 = 1617
;GOKU1P.PI1 = 1946
;INTRO1.PI1 = 2948
;KIMAGURE.PI1 = 2032
;MADOKA.PI1 = 3345
	BSS
PNT_PLAN	DS.L	1
CNT	DS.L	1
COMPTE_PTS	DS.L	1
COORD_X	DS	1
COORD_Y	DS	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.B	32000*2
FORME_DEP_X	DS.B	NB_PTS
	EVEN
FORME_DEP_Y	DS.B	NB_PTS*2
FORME_FIN_X	DS.B	NB_PTS
	EVEN
FORME_FIN_Y	DS.B	NB_PTS*2
FORME_DEP	DS.B	NB_PTS*4
FORME_FIN	DS.B	NB_PTS*4
SOUS_BUFFER	DS.B	NB_PASSES*4
BUFFER_COORD	DS.B	NB_PASSES*NB_PTS
BUFFER_COORD2	DS.B	NB_PASSES*NB_PTS*2
