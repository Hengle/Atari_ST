	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP

	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1
	BSR	PREP_ECR

	CLR.W	-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008
	MOVE.L	#FIN,$00C
	MOVE.L	#FIN,$010
	MOVE.L	#FIN,$014
	MOVE.L	#FIN,$018
	MOVE.L	#FIN,$01C
	MOVE.L	#FIN,$020

	ANDI.B	#%11111000,$484.W

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL

	LEA	$FFFF8240.W,A0
	MOVE.W	#7,D7
EFF_PALET	CLR.L	(A0)+
	DBRA	D7,EFF_PALET

	MOVEQ	#1,D0
	JSR	MUZAXX
	MOVE.L	#MUZAXX+4,$4CE.W

	BSR	PRP1CRB
	BSR	PRP1FNT
	BSR	PRP1TXT
	MOVE.W	#0,PNT1

	BSR	PREPA_SPR
	MOVE.L	#CORES_POS,PNT_POS
	MOVE.L	#CORES_EC,PNT_EC
	MOVE.L	SCREEN1,EC_ACT
	MOVE.L	#CORES2EC,PNT2EC

	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	MOVE.W	#7999,D7
PREFAC	CLR.L	(A0)+
	CLR.L	(A1)+
	DBRA	D7,PREFAC

	REPT	6
	BSR	BOULE_WIZZ
	BSR	SWAPEC
	ENDR

;	MOVEM.L	SPRITES+2,D0-D7
;	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.B	$FFFFFA07.W,MFP
	MOVE.B	$FFFFFA09.W,MFP+1
	MOVE.B	$FFFFFA0F.W,MFP+2
	MOVE.B	$FFFFFA13.W,MFP+3
	MOVE.B	$FFFFFA1B.W,MFP+4
	MOVE.B	$FFFFFA21.W,MFP+5

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	BCLR	#3,$FFFFFA17.W

LOOP	JMP	LOOP

FIN	MOVE.W	#$2700,SR

	CLR.L	$4CE.W
	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W
	
	MOVE.B	MFP,$FFFFFA07.W
	MOVE.B	MFP+1,$FFFFFA09.W
	MOVE.B	MFP+2,$FFFFFA0F.W
	MOVE.B	MFP+3,$FFFFFA13.W
	MOVE.B	MFP+4,$FFFFFA1B.W
	MOVE.B	MFP+5,$FFFFFA21.W

	MOVE.W	#$2300,SR

	MOVE.B	SAUVEC,$FFFF8201.W
	MOVE.B	SAUVEC+1,$FFFF8203.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	
	ORI.B	#%00000111,$484.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S    *******
VBLR	MOVEM.L	SPRITES+2,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	MOVE.L	#$07770777,$FFFF8250.W
	MOVE.L	#$07770777,$FFFF8254.W
	MOVE.L	#$07770777,$FFFF8258.W
	MOVE.L	#$07770777,$FFFF825C.W
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	SUITV1
	ST	$FFFF8240.W
SUITV1	JSR	MUZAXX+4
	CLR.B	$FFFFFA1B.W
	MOVE.B	#191,$FFFFFA21.W
	MOVE.L	#TB0,$120.W
	MOVE.B	#8,$FFFFFA1B.W
	BSR	BOULE_WIZZ
	BSR	SWAPEC
	SF	$FFFF8240.W
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
	
TB0	CLR.L	$FFFF8240.W
	CLR.L	$FFFF8244.W
	CLR.L	$FFFF8248.W
	CLR.L	$FFFF824C.W
	CLR.L	$FFFF8250.W
	CLR.L	$FFFF8254.W
	CLR.L	$FFFF8258.W
	CLR.L	$FFFF825C.W
	RTE

TEST	MOVE.L	PNT_TXT,A2
	MOVEQ	#0,D0
	LEA	COURB,A0
	MOVE.L	EC_ACT,A1
	ADDQ.L	#6,A1
	MOVE.L	(A2)+,A3
	ADDA.W	PNT1,A3
	MOVE.L	(A3),A3
	JSR	(A3)
	REPT	9
	MOVE.L	(A2)+,A3
	MOVE.L	(A3),A3
	JSR	(A3)
	ENDR
	ADDQ.W	#4,PNT1
	CMPI.W	#64,PNT1
	BNE.S	FIN1
	CLR.W	PNT1
	ADDQ.L	#4,PNT_TXT
	MOVE.L	PNT_TXT,A0
	CMPI.L	#-1,80(A0)
	BNE.S	FIN1
	MOVE.L	#BUF_TEXT,PNT_TXT
FIN1	RTS

BOULE_WIZZ	MOVE.L	PNT2EC,A0
	MOVE.L	(A0),A0
	ADDA.L	SCREEN1,A0
	ADDQ.L	#6,A0
	MOVEQ	#0,D0
	JSR	EFFAC
	BSR	TEST

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	SUITV2
	MOVE.W	#$700,$FFFF8240.W

SUITV2	MOVE.L	PNT_POS,A0
	MOVE.L	(A0),A0
	MOVE.L	PNT_EC,A6
	MOVE.L	SCREEN1,A1
	ADDA.L	(A6)+,A1
	BSR	AFFICH
	MOVE.L	PNT_POS,A0
	MOVE.L	(A0),A0
	MOVE.L	SCREEN1,A1
	ADDA.L	(A6)+,A1
	BSR	AFFICH

	ADDQ.L	#4,PNT_POS
	MOVE.L	PNT_POS,A0
	CMPI.L	#-1,(A0)
	BNE.S	SUIT
	MOVE.L	#CORES_POS,PNT_POS
SUIT	ADDQ.L	#8,PNT_EC
	ADDQ.L	#4,PNT2EC
	MOVE.L	PNT_EC,A0
	CMPI.L	#-1,(A0)
	BNE.S	SUIT2
	MOVE.L	#CORES_EC,PNT_EC
	MOVE.L	#CORES2EC,PNT2EC
	MOVE.L	SCREEN1,EC_ACT
	SUBI.L	#160*32,EC_ACT
SUIT2	
	ADDI.L	#160*32,EC_ACT
	RTS

AFFICH
N	SET	0
	REPT	32
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+96(A1)
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,N+144(A1)
N	SET	N+160
	ENDR
	RTS

SWAPEC	MOVE.B	EC_ACT+1,$FFFF8201.W
	MOVE.B	EC_ACT+2,$FFFF8203.W
	MOVE.L	EC_ACT,A0		;SECURITY !!!!!
	CLR.L	32*160(A0)
	CLR.L	(32*160)+4(A0)
	RTS
	
PREPA_SPR	LEA	ADR_SPR,A0	;ADRESSE DES BOULES
	MOVE.L	SCREEN1,A1	;BUF_SPR
	MOVE.L	SCREEN2,A2	;BUF_MSK
	LEA	BUF_POS,A3	;BUFFER GENERAL
	LEA	COORDONNêES,A4	;COORDONNêES!!

	MOVE.W	#30,D6

RECOMGENGEN	MOVE.W	D6,ANNEXI

	CLR.W	NB_BOL
	MOVE.W	#9,D7
RECOM_GEN_SPR
	MOVE.L	SCREEN1,A1	;BUF_SPR
	MOVE.L	SCREEN2,A2	;BUF_MSK
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVE.W	(A4),D0	;No DU SPRITE(0-15)
	MOVE.W	2(A4),D1	;POSITION SUR L'êCRAN
	MOVE.W	4(A4),D2	;No DE LA BOULE(Z!)
	MOVE.L	(A0,D2.W),A5
	MOVE.W	#31,D6
PREAFBALL	MOVE.L	(A5),(A1)+
	MOVE.L	4(A5),(A1)+
	MOVE.L	8(A5),(A1)+
	MOVE.L	12(A5),(A1)+
	CLR.L	(A1)+
	CLR.L	(A1)+
	MOVE.W	(A5),D3
	MOVE.W	2(A5),D4
	MOVE.W	4(A5),D5
	OR.W	D4,D3
	OR.W	D5,D3
	NOT.W	D3
	MOVE.W	D3,(A2)+
	MOVE.W	8(A5),D3
	MOVE.W	10(A5),D4
	MOVE.W	12(A5),D5
	OR.W	D4,D3
	OR.W	D5,D3
	NOT.W	D3
	MOVE.W	D3,(A2)+
	MOVE.W	#-1,(A2)+
	LEA	160(A5),A5
	DBRA	D6,PREAFBALL
	CMPI.W	#-1,D0
	BEQ	NO_DECALAG
DEC_SPR	MOVE.L	SCREEN1,A1
N	SET	0
	REPT	32
	ROXR	N(A1)
	ROXR	N+8(A1)
	ROXR	N+16(A1)
	ROXR	N+2(A1)
	ROXR	N+10(A1)
	ROXR	N+18(A1)
	ROXR	N+4(A1)
	ROXR	N+12(A1)
	ROXR	N+20(A1)
N	SET	N+24
	ENDR
	DBRA	D0,DEC_SPR
	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
N	SET	0
	REPT	32
	MOVE.W	N(A1),D3
	MOVE.W	N+2(A1),D4
	MOVE.W	N+4(A1),D5
	OR.W	D4,D3
	OR.W	D5,D3
	NOT.W	D3
	MOVE.W	D3,(A2)+
	MOVE.W	N+8(A1),D3
	MOVE.W	N+10(A1),D4
	MOVE.W	N+12(A1),D5
	OR.W	D4,D3
	OR.W	D5,D3
	NOT.W	D3
	MOVE.W	D3,(A2)+
	MOVE.W	N+16(A1),D3
	MOVE.W	N+18(A1),D4
	MOVE.W	N+20(A1),D5
	OR.W	D4,D3
	OR.W	D5,D3
	NOT.W	D3
	MOVE.W	D3,(A2)+
N	SET	N+24
	ENDR
NO_DECALAG	MOVE.L	SCREEN1,A1	;SPR
	MOVE.L	SCREEN2,A2	;MSK
	MOVE.L	A3,A5
	ADDA.W	D1,A5
	TST.W	NB_BOL
	BEQ	AFFING
	REPT	32
	MOVE.W	(A2)+,D3
	MOVE.W	D3,D4
	SWAP	D3
	MOVE.W	D4,D3
	AND.L	D3,(A5)+
	AND.W	D3,(A5)+
	ADDQ.L	#2,A5
	MOVE.W	(A2)+,D3
	MOVE.W	D3,D4
	SWAP	D3
	MOVE.W	D4,D3
	AND.L	D3,(A5)+
	AND.W	D3,(A5)+
	ADDQ.L	#2,A5
	MOVE.W	(A2)+,D3
	MOVE.W	D3,D4
	SWAP	D3
	MOVE.W	D4,D3
	AND.L	D3,(A5)+
	AND.W	D3,(A5)+
	ADDQ.L	#2,A5
	LEA	136(A5),A5
	ENDR

	MOVE.L	SCREEN1,A1
	MOVE.L	A3,A5
	ADDA.W	D1,A5
	REPT	32
	MOVE.L	(A1)+,D3
	OR.L	D3,(A5)+
	MOVE.W	(A1)+,D3
	OR.W	D3,(A5)+
	ADDQ.L	#2,A5
	ADDQ.L	#2,A1
	MOVE.L	(A1)+,D3
	OR.L	D3,(A5)+
	MOVE.W	(A1)+,D3
	OR.W	D3,(A5)+
	ADDQ.L	#2,A5
	ADDQ.L	#2,A1
	MOVE.L	(A1)+,D3
	OR.L	D3,(A5)+
	MOVE.W	(A1)+,D3
	OR.W	D3,(A5)+
	ADDQ.L	#2,A5
	ADDQ.L	#2,A1
	LEA	136(A5),A5
	ENDR

	BRA	FIN1PRPSPR

AFFING	REPT	32
	MOVE.L	(A1)+,(A5)+
	MOVE.L	(A1)+,(A5)+
	MOVE.L	(A1)+,(A5)+
	MOVE.L	(A1)+,(A5)+
	MOVE.L	(A1)+,(A5)+
	MOVE.L	(A1)+,(A5)+
	LEA	136(A5),A5
	ENDR

FIN1PRPSPR	ADDQ.L	#6,A4
	ADDQ.W	#1,NB_BOL
	DBRA	D7,RECOM_GEN_SPR
	LEA	60(A4),A4
	LEA	5120(A3),A3
	MOVE.W	ANNEXI,D6
	DBRA	D6,RECOMGENGEN
	RTS

PRP1CRB	LEA	COURB,A0
	LEA	MOTIF,A1
	MOVE.W	#(2304)-1,D7
RECOM1CRB	MOVE.W	(A0),D0
	MOVE.W	(A1,D0.W),(A0)+
	ADDQ.L	#2,A0
	DBRA	D7,RECOM1CRB
	MOVE.W	#259,D7
EFF_CRB	CLR.L	(A0)+
	DBRA	D7,EFF_CRB
	RTS

PRP1FNT	LEA	FONT16X16,A0
	LEA	BUF_COD,A2
	LEA	TABL_ADR,A6
	MOVE.W	#58,D5
FNT3	MOVE.W	#15,D3
	MOVE.W	#15,D6
FNT2	CLR.W	CNT_ADD
	MOVE.L	A2,(A6)+
	MOVE.W	#15,D7
FNT1	MOVE.W	(A0)+,D0
	BTST	D3,D0
	BEQ.S	NO_AFF
	CMPI.W	#2,CNT_ADD
	BGE	OPT1
RT1	CLR.W	CNT_ADD
	MOVE.L	#$30183218,(A2)+
	MOVE.L	#$81711000,(A2)+
RETOUR	DBRA	D7,FNT1
	SUBQ.W	#1,D3
	LEA	-32(A0),A0
	CMPI.W	#2,CNT_ADD
	BGE	OPT2
RT2	DBRA	D6,FNT2
	MOVE.W	#$4E75,(A2)+
	LEA	32(A0),A0
	DBRA	D5,FNT3
	RTS

NO_AFF	MOVE.W	#$5888,(A2)+
	ADDQ.W	#1,CNT_ADD
	BRA	RETOUR

OPT1	MOVEQ	#0,D4
	MOVE.W	CNT_ADD,D4
	ADD.W	D4,D4
	SUBA.W	D4,A2
	MOVE.W	CNT_ADD,D4
	LSL	#2,D4	
	MOVE.W	#$41E8,(A2)+
	MOVE.W	D4,(A2)+
	BRA	RT1

OPT2	MOVEQ	#0,D4
	MOVE.W	CNT_ADD,D4
	ADD.W	D4,D4
	SUBA.W	D4,A2
	MOVE.W	CNT_ADD,D4
	LSL	#2,D4
	MOVE.W	#$41E8,(A2)+
	MOVE.W	D4,(A2)+
	BRA	RT2

PRP1TXT	LEA	TEXT1,A0
	LEA	BUF_TEXT,A1
RECOMTXT	CMPI.B	#-1,(A0)
	BEQ.S	FIN_TXT
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0
	SUBI.B	#32,D0
	LSL	#6,D0
	LEA	TABL_ADR,A6	
	ADDA.W	D0,A6
	MOVE.L	A6,(A1)+
	BRA.S	RECOMTXT
FIN_TXT	MOVE.L	#-1,(A1)+
	MOVE.L	#BUF_TEXT,PNT_TXT
	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
	SECTION	DATA
COORDONNêES	INCBIN	COOR_1.COR

SPRITES	INCBIN	BALL.PI1

MUZAXX	INCBIN	STRUCTUR.IMG

FONT16X16	INCBIN	FNT16X16.FNT

COURB	INCBIN	COOLCRB1.CRB
	DS.L	260

EFFAC	INCBIN	COOLCRB1.EFF

TEXT1	DC.B	"                  "
	DC.B	"WOW ! HOPE YOU LIKE THIS EFFECT I DID WITH MY "
	DC.B	"SCREWING BALLS AND WITH THE DOTS SCROLTEXT ON THEM."
	DC.B	"           I AM SURE THAT YOU HAVE EVER REMARKED THAT "
	DC.B	"THIS SCREEN IS BASED ON AN AMIGA ONE, A RAZOR ONE."
	DC.B	" IT WAS COOL AND WHEN I SAW THAT IT WAS POSSIBLE "
	DC.B	"TO MAKE ON OUR GOOD OLD ST, I HAVE DECIDED TO CONVERT IT "
	DC.B	"    HERE IS THE RESULT, I FIND IT IMPRESSIVE FOR MY "
	DC.B	"FAVOURITE MACHINE !!!"
	DC.B	"                  "
	DC.B	-1
	EVEN

MOTIF
N	SET	32768
	REPT	16
	DC.W	N
N	SET	N/2
	ENDR

ADR_SPR	
N	SET	0
	REPT	10
	DC.L	SPRITES+34+N
N	SET	N+16
	ENDR
N	SET	N+(160*32)
	REPT	6
	DC.L	SPRITES+34+N
N	SET	N+16
	ENDR

CORES_POS
N	SET	0
	REPT	31
	DC.L	BUF_POS+N
N	SET	N+5120
	ENDR
	DC.L	-1

CORES_EC	DC.L	224*160,224*160
	DC.L	0,256*160
	DC.L	32*160,288*160
	DC.L	64*160,320*160
	DC.L	96*160,352*160
	DC.L	128*160,384*160
	DC.L	160*160,416*160
 	DC.L	192*160,192*160
	DC.L	-1

CORES2EC	DC.L	224*160
	DC.L	0
	DC.L	32*160
	DC.L	64*160
	DC.L	96*160
	DC.L	128*160
	DC.L	160*160
	DC.L	192*160
	DC.L	-1

	SECTION	BSS
ANC_PAL	DS.L	8
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
SAUV_SP	DS.L	1
SAUVEC	DS.W	1
MFP	DS.B	8
	DS.B	256
BUFFER	DS.L	16000+2000
	DS.B	256
SCREEN1	DS.L	1
SCREEN2	DS.L	1
RETIR	DS.W	1
ANNEXI	DS.W	1
PNT_POS	DS.L	1
PNT_EC	DS.L	1
PNT2EC	DS.L	1
EC_ACT	DS.L	1
EC_PREC	DS.L	1
BUF_POS	DS.L	(40*32)*31	;158720 OCTETS
PNT1	DS.W	1
NB_BOL	DS.W	1
CNT_ADD	DS.W	1
PNT_TXT	DS.L	1
TABL_ADR	DS.L	16*59
BUF_TEXT	DS.L	4000
BUF_COD