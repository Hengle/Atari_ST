NB_LIG = 480

	CLR.L	-(SP)
	MOVE	#$20,-(SP)
	TRAP	#1
	ADDQ	#6,SP

	LEA	IMG+54,A0	D‚but palette
	LEA	PALETTE,A1
	MOVE	#256-1,D7
CREE_PALETTE	MOVEQ	#0,D4
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVE	#%11110000,D4
	MOVE.B	(A0)+,D0	R
	MOVE.B	(A0)+,D1	V
	MOVE.B	(A0)+,D2	B
	MOVE.B	(A0)+,D3	INUTILISEE
	AND.B	D4,D0	ON VIRE TOUT...
	AND.B	D4,D1
	AND.B	D4,D2
	LSR.B	#5,D0
	LSR.B	#5,D2
	LSR.B	#1,D1
	MOVEQ	#0,D3
	OR.B	D0,D3
	OR.B	D1,D3
	MOVE.B	D2,(A1)+
	MOVE.B	D3,(A1)+
	DBF	D7,CREE_PALETTE
;	TST.L	$5A0.W
;	BNE.S	STE
;	MOVEM.L	PALET_STF,D0-D7
;	MOVEM.L	D0-D7,PALETTE
;	BRA.S	FINI1
;STE	MOVEM.L	PALET_STE,D0-D7
;	MOVEM.L	D0-D7,PALETTE
FINI1	NOP
**********************************************************************

	LEA	PALETTES,A4
	LEA	32*479(A4),A4

	LEA	IMG+1082-4,A0
	MOVE.L	A0,A5
	LEA	PALETTE,A1
	MOVE	#480-1,D6

X	MOVE.L	A5,A0
	MOVE	#448-1,D7
.TR
	MOVEQ	#0,D0
	MOVEQ	#0,D5
	MOVE.B	(A0),D0
	ADD	D0,D0
	LEA	MINI_TABLO,A3
.CONT	CMPI	#$1234,(A3)
	BEQ	.ROOL
	CMPI	#$4242,(A3)+
	BNE.S	.CONT
	;MOVE	#$700,$FFFF8240.W
	;MOVE.B	#0,(A0)+
	;JMP	.FINI2
*** A PARTIR D'ICI: PROBLEME!   ESSAI DE SAUVETAGE....
	JMP	.PB
.TR2
	MOVEQ	#0,D0
	MOVEQ	#0,D5
	MOVE.B	(A0),D0
	ADD	D0,D0

.PB	MOVE	(A1,D0.W),D1
	LEA	MINI_TABLO,A3
	LEA	._SAVER,A2
	MOVE	#16-1,D3
.DO	MOVE	(A3)+,D0
	SUB	D1,D0
	BGT.S	.POSITIF
	NEG	D0
.POSITIF	MOVE	D0,(A2)+
	DBF	D3,.DO
**
	MOVE	#$FFF,D0
	MOVEQ	#0,D5
	MOVEQ	#0,D4
	LEA	._SAVER,A2
	MOVEQ	#15,D3
.Z	MOVE	(A2)+,D1
	CMP	D0,D1
	BGT.S	.G
	MOVE	D1,D0
	MOVE.B	D5,D4
.G	ADDQ.B	#1,D5
	DBF	D3,.Z
	
	MOVE.B	D4,(A0)+
	DBF	D7,.TR2
	JMP	.FINI2

._SAVER	DS.W	16
***
.ROOL	MOVE	(A1,D0.W),D1

;	CMPI	#$700,D1
;	BNE.S	.Q
;	MOVE	#$007,$FFFF8240.W
;.POPO	BRA.S	.POPO
;.Q	

	LEA	MINI_TABLO,A2
.RO	CMPI	#$1234,(A2)
	BEQ.S	.ROOL2
	CMP	(A2),D1
	BEQ.S	.ROOL3
	ADDQ	#2,A2
	ADDQ	#1,D5
	BRA.S	.RO
.ROOL2	MOVE	D1,(A3)
.ROOL3	MOVE.B	D5,(A0)+
	DBF	D7,.TR
.FINI2	NOP
	LEA	640(A5),A5
SUIT	LEA	MINI_TABLO,A2
	REPT	8
	MOVE.L	(A2)+,(A4)+
	ENDR
	LEA	-64(A4),A4
	LEA	MINI_TABLO,A2
	REPT	8
	MOVE.L	#$12341234,(A2)+
	ENDR
	DBF	D6,X
	MOVE	#$070,$FFFF8240.W
	JMP	OUF
MINI_TABLO	DCB	16,$1234
	DC	$4242
**********************************************************************
OUF
	LEA	IMG+1082-4,A0	DEBUT IMAGE? ESSAI...
	MOVE.L	A0,A5
	MOVE.L	A5,A4
	ADDA.L	#230*480,A4

	LEA	TABLE_RT,A1
	MOVEQ	#0,D5
	LEA	BIG_BUF,A3
	LEA	TABLE,A6

	MOVE	#%1000000000000000,D1
GO	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0

	MOVE.B	(A6,D0.W),D0

OK	EXT	D0
	ADD	D0,D0
	ADD	D0,D0
	LEA	(A1,D0.W),A2
	MOVE.L	(A2),A2
	JSR	(A2)
	LSR	#1,D1
	BNE.S	GO
	MOVE	#%1000000000000000,D1
	ADDQ	#8,A3

	ADDQ	#8,D5
	CMPI	#224,D5
	BNE.S	ROOL
**
	LEA	BIG_BUF,A3
	MOVEQ	#57-1,D5
.FQ	MOVE.L	(A3)+,(A5)+
	DBF	D5,.FQ
	MOVE	(A3)+,(A5)+
	LEA	BIG_BUF,A3

	LEA	BIG_BUF,A3
	MOVEQ	#57-1,D5
.FQE	CLR.L	(A3)+
	DBF	D5,.FQE
	CLR	(A3)+
	LEA	BIG_BUF,A3
**
	MOVEQ	#0,D5
	LEA	192(A0),A0
ROOL
	CMP.L	A4,A5
	BNE.S	GO

	MOVE	#$001,$FFFF8240.W
	JMP	DEBUT
	
RT_0	OR	D1,(A3)	0001
RT_00	RTS
RT_1	OR	D1,2(A3)	0010
	RTS
RT_2	OR	D1,(A3)	0011
	OR	D1,2(A3)
	RTS
RT_3	OR	D1,4(A3)	0100
	RTS
RT_4	OR	D1,4(A3)	0101
	OR	D1,(A3)
	RTS
RT_5	OR	D1,4(A3)	0110
	OR	D1,2(A3)
	RTS
RT_6	OR	D1,4(A3)	0111
	OR	D1,2(A3)
	OR	D1,(A3)
	RTS
RT_7	OR	D1,6(A3)	1000
	RTS
RT_8	OR	D1,6(A3)	1001
	OR	D1,(A3)
	RTS
RT_9	OR	D1,6(A3)	1010
	OR	D1,2(A3)
	RTS
RT_10	OR	D1,6(A3)	1011
	OR	D1,2(A3)
	OR	D1,(A3)
	RTS
RT_11	OR	D1,6(A3)	1100
	OR	D1,4(A3)
	RTS
RT_12	OR	D1,6(A3)	1101
	OR	D1,4(A3)
	OR	D1,(A3)
	RTS
RT_13	OR	D1,6(A3)	1110
	OR	D1,4(A3)
	OR	D1,2(A3)
	RTS
RT_14	OR	D1,6(A3)	1111
	OR	D1,4(A3)
	OR	D1,2(A3)
	OR	D1,(A3)
	RTS
TABLE
	REPT	16
	DC.B	0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15
	ENDR
DEBUT:

	LEA	DATA_HS,A0
	MOVE.L	A0,POIT
		
	move.l	#IMG,d0
	CLR.B	D0
	ADD.L	#440*256,D0
	move.l	d0,SCREEN1

	move.l	SCREEN1,d0
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)
******
	MOVE.L	SCREEN1,A0
	LEA	1600(A0),A0
	LEA	IMG+1082-4,A1
	ADDA.L	#230*479,A1
	MOVE	#480-1,D1
.G	MOVE.L	A0,A2
	MOVE.L	A1,A3
	MOVE	#28-1,D0
.H	MOVE.L	(A1)+,(A0)+
	MOVE.L	(A1)+,(A0)+
	DBF	D0,.H
	LEA	230(A2),A0
	LEA	-230(A3),A1
	DBF	D1,.G
******
	MOVE.L	SCREEN1,A1
	LEA	1600(A1),A1
	LEA	IMG,A0
	LEA	56*480(A0),A2
	LEA	56*480(A2),A3
	LEA	56*480(A3),A4
	MOVE	#480-1,D1
.G2
	MOVE.L	A1,A5
	MOVE	#28-1,D0
.H2	MOVE	(A1)+,(A0)+
	MOVE	(A1)+,(A2)+
	MOVE	(A1)+,(A3)+
	MOVE	(A1)+,(A4)+
	
	;MOVE.L	(A1)+,(A0)+
	;MOVE.L	(A1)+,(A0)+
	DBF	D0,.H2
	LEA	230(A5),A1
	DBF	D1,.G2
	
	LEA	IMG,A0
	MOVE.L	A0,A1
	ADDA.L	#(224*480)-1,A1
SAUVE	NOP
	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W

	MOVE.L	#INTER_RTE,$68.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W

	MOVE.L	#VBLR_IR,$70.W
	MOVE.L	#TB0_IR,$120.W

	MOVE.B	#$12,$FFFFFC02.W

	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W

BOUCLE	BRA.S	BOUCLE

VBLR_IR
	CLR.B	$FFFFFA1B.W
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.B	#0,$FFFFFA19.W
	MOVE.B	#99,$FFFFFA1F.W		; BORDER HAUT
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W

	LEA	PALETTES,A0
MODPAL	EQU	*+2
	ADDA.L	#0,A0
INTER_RTE	RTE

PALET_STF
	DC	$000,$001,$111,$212,$222,$323,$333,$434
	DC	$444,$545,$555,$656,$666,$767,$777,$777
PALET_STE
	DC	$000,$888,$111,$999,$222,$AAA,$333,$BBB
	DC	$444,$CCC,$555,$DDD,$666,$EEE,$777,$FFF

INTER_TMA:	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE	#$2100,SR
full
	LEA	$FFFF8209,A3
	LEA	$FFFF8260,A6
	LEA	$FFFF820A,A5

	MOVEQ	#0,D0
	MOVEQ	#2,D1

	STOP	#$2100
	MOVE	#$2700,SR
	MOVE	#$2300,(SP)

	MOVEQ	#29,D2
SYNCHROA:	DBF	D2,SYNCHROA
	NOP

	MOVE.B	D0,(A5)
	REPT	6
	NOP
	ENDR
	MOVE.B	D1,(A5)

*******

	MOVEA.L	POIT,A2

	MOVE.L	#$FFFF8209,A6
	MOVE.L	#$FFFF8260,A4
	MOVE.L	#$FFFF820A,A3
	MOVEQ	#0,D1		
	MOVEQ	#$10,D6
	MOVE.L	#228-12+2-1-1-1,D0

SYNCHRO	MOVE.B	(A6),D7
	BEQ.S	SYNCHRO
	SUB.W	D7,D6		
	LSL.W	D6,D1		
	DCB	97-3-4,$4E71

	REPT	9
	MOVE.L	(A2)+,A1	*3
	JSR	(A1)	*4
	ENDR
	
	DCB	7-1,$4E71
	MOVEQ	#42-1,D1

	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A0)+,$FFFF8240.W
	MOVE.L	(A0)+,$FFFF8244.W
	MOVE.L	(A0)+,$FFFF8248.W
	MOVE.L	(A0)+,$FFFF824C.W
	MOVE.L	(A0)+,$FFFF8250.W
	MOVE.L	(A0)+,$FFFF8254.W
	MOVE.L	(A0)+,$FFFF8258.W
	MOVE.L	(A0)+,$FFFF825C.W
	DCB	88-6*8,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12,$4E71

	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A0)+,$FFFF8240.W
	MOVE.L	(A0)+,$FFFF8244.W
	MOVE.L	(A0)+,$FFFF8248.W
	MOVE.L	(A0)+,$FFFF824C.W
	MOVE.L	(A0)+,$FFFF8250.W
	MOVE.L	(A0)+,$FFFF8254.W
	MOVE.L	(A0)+,$FFFF8258.W
	MOVE.L	(A0)+,$FFFF825C.W
	DCB	88-6*8,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-1,$4E71

FULLSCREEN	NOP
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A0)+,$FFFF8240.W
	MOVE.L	(A0)+,$FFFF8244.W
	MOVE.L	(A0)+,$FFFF8248.W
	MOVE.L	(A0)+,$FFFF824C.W
	MOVE.L	(A0)+,$FFFF8250.W
	MOVE.L	(A0)+,$FFFF8254.W
	MOVE.L	(A0)+,$FFFF8258.W
	MOVE.L	(A0)+,$FFFF825C.W
	DCB	88-6*8,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71
	DBF	D0,FULLSCREEN

	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A0)+,$FFFF8240.W
	MOVE.L	(A0)+,$FFFF8244.W
	MOVE.L	(A0)+,$FFFF8248.W
	MOVE.L	(A0)+,$FFFF824C.W
	MOVE.L	(A0)+,$FFFF8250.W
	MOVE.L	(A0)+,$FFFF8254.W
	MOVE.L	(A0)+,$FFFF8258.W
	MOVE.L	(A0)+,$FFFF825C.W
	DCB	88-6*8,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	;	ICI=116	NOPS

	CLR.B	$FFFF820A.W 
	DCB	8,$4E71

	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE	A4,(A3)
	MOVE.L	(A0)+,$FFFF8240.W
	MOVE.L	(A0)+,$FFFF8244.W
	MOVE.L	(A0)+,$FFFF8248.W
	MOVE.L	(A0)+,$FFFF824C.W
	MOVE.L	(A0)+,$FFFF8250.W
	MOVE.L	(A0)+,$FFFF8254.W
	MOVE.L	(A0)+,$FFFF8258.W
	MOVE.L	(A0)+,$FFFF825C.W
	DCB	88-2-6*8,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-1,$4E71
	
.F	NOP
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE.L	(A0)+,$FFFF8240.W
	MOVE.L	(A0)+,$FFFF8244.W
	MOVE.L	(A0)+,$FFFF8248.W
	MOVE.L	(A0)+,$FFFF824C.W
	MOVE.L	(A0)+,$FFFF8250.W
	MOVE.L	(A0)+,$FFFF8254.W
	MOVE.L	(A0)+,$FFFF8258.W
	MOVE.L	(A0)+,$FFFF825C.W
	DCB	88-6*8,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71
	DBF	D1,.F
SUITE
	CMPI.B	#$50,$FFFFFC02.W
	BNE.S	PAS_HAUT
	CMPI.L	#230*400,Y_VALUE
	BEQ.S	PAS_HAUT
	ADD.L	#230,Y_VALUE
	ADD.L	#32,MODPAL
PAS_HAUT	CMPI.B	#$48,$FFFFFC02.W
	BNE.S	PAS_BAS
	TST.L	Y_VALUE
	BEQ.S	PAS_BAS
	SUB.L	#230,Y_VALUE
	SUB.L	#32,MODPAL
PAS_BAS
	CMPI.B	#$4B,$FFFFFC02.W
	BNE.S	PAS_GA
	ADDQ.L	#8,Y_VALUE
PAS_GA	CMPI.B	#$4D,$FFFFFC02.W
	BNE.S	PAS_DR
	SUBQ.L	#8,Y_VALUE
PAS_DR
	MOVEQ	#0,D0	
	MOVEQ	#0,D1
	MOVE.L	Y_VALUE,D0
	
	LSR.L	#8,D0
	MOVE.L	D0,D1
	LSL.L	#8,D1

	MOVE.L	Y_VALUE,D0
	SUB.L	D1,D0

	LEA	DATA_HS,A0

	TST.L	D0
	BNE.S	DEJ
	LEA	RIEN,A0
	MOVE.L	A0,POIT
	BRA.S	OKAY
DEJ	SUBQ.L	#2,D0
	MULU	#18,D0
	ADDA.L	D0,A0
	MOVE.L	A0,POIT

OKAY
;	move.l	SCREEN1,d0
;	move.l	SCREEN2,SCREEN1		
;	move.l	d0,SCREEN2

	move.l	SCREEN1,d0
	ADD.L	D1,D0
	lsr.l	#8,d0
	LEA	$FFFF8201.W,A0
	movep	d0,(a0)

	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
Y_VALUE	DC.L	0

TB0_IR	RTE

FIN:	MOVE.L	4.W,A0
	JMP	(A0)

H0
	DCB	128-4-3-4,$4E71
	RTS

H70
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	3+17*5,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	NOP
	RTS	=4 NOPS
H44
	DCB	3+18*5,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	NOP
	RTS	=4 NOPS
H24
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	1+17*5,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	REPT	3
	NOP
	ENDR
	RTS	=4 NOPS
H2
	NOP
	DCB	18*5,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	REPT	3
	NOP
	ENDR
	RTS	=4 NOPS
H80
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	7*5,$4E71
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	3+9*5,$4E71

	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	NOP
	RTS
	 
	DATA
DATA_HS
 DC.L H44,H44,H2,H2,H2,H80,H0,H0,H0
 DC.L H44,H44,H2,H2,H80,H0,H0,H0,H0
 DC.L H44,H44,H2,H80,H0,H0,H0,H0,H0
 DC.L H44,H44,H80,H0,H0,H0,H0,H0,H0
 DC.L H44,H24,H24,H2,H80,H0,H0,H0,H0
 DC.L H44,H24,H24,H80,H0,H0,H0,H0,H0
 DC.L H24,H2,H2,H2,H2,H2,H0,H0,H0
 DC.L H24,H2,H2,H2,H2,H0,H0,H0,H0
 DC.L H24,H2,H2,H2,H0,H0,H0,H0,H0
 DC.L H24,H2,H2,H0,H0,H0,H0,H0,H0
 DC.L H24,H2,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H0,H0,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H24,H24,H80,H80,H0,H0
 DC.L H70,H44,H2,H2,H2,H80,H0,H0,H0
 DC.L H70,H44,H2,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H2,H80,H0,H0,H0,H0,H0
 DC.L H44,H2,H2,H2,H2,H2,H0,H0,H0
 DC.L H44,H2,H2,H2,H2,H0,H0,H0,H0
 DC.L H44,H2,H2,H2,H0,H0,H0,H0,H0
 DC.L H44,H2,H2,H0,H0,H0,H0,H0,H0
 DC.L H44,H2,H0,H0,H0,H0,H0,H0,H0
 DC.L H44,H0,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H2,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H0,H0,H0,H0,H0,H0,H0
 DC.L H44,H44,H44,H2,H80,H0,H0,H0,H0
 DC.L H44,H44,H44,H80,H0,H0,H0,H0,H0
 DC.L H70,H70,H2,H2,H2,H80,H0,H0,H0
 DC.L H70,H70,H2,H2,H80,H0,H0,H0,H0
 DC.L H70,H70,H2,H80,H0,H0,H0,H0,H0
 DC.L H44,H24,H2,H2,H2,H2,H0,H0,H0
 DC.L H44,H24,H2,H2,H2,H0,H0,H0,H0
 DC.L H44,H24,H2,H2,H0,H0,H0,H0,H0
 DC.L H44,H24,H2,H0,H0,H0,H0,H0,H0
 DC.L H44,H24,H0,H0,H0,H0,H0,H0,H0
 DC.L H70,H0,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H44,H2,H2,H80,H0,H0,H0
 DC.L H70,H44,H44,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H44,H80,H0,H0,H0,H0,H0
 DC.L H44,H24,H24,H2,H80,H70,H0,H0,H0
 DC.L H70,H44,H24,H24,H80,H0,H0,H0,H0
 DC.L H44,H44,H2,H2,H0,H0,H0,H0,H0
 DC.L H44,H44,H2,H0,H0,H0,H0,H0,H0
 DC.L H44,H44,H0,H0,H0,H0,H0,H0,H0
 DC.L H70,H24,H2,H2,H0,H0,H0,H0,H0
 DC.L H70,H24,H2,H0,H0,H0,H0,H0,H0
 DC.L H70,H24,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H2,H2,H2,H80,H0,H0
 DC.L H70,H70,H44,H2,H2,H80,H0,H0,H0
 DC.L H70,H70,H44,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H2,H2,H2,H2,H2,H0,H0
 DC.L H70,H44,H2,H2,H2,H2,H0,H0,H0
 DC.L H70,H44,H2,H2,H2,H0,H0,H0,H0
 DC.L H70,H44,H2,H2,H0,H0,H0,H0,H0
 DC.L H70,H44,H2,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H24,H2,H2,H0,H0
 DC.L H24,H24,H24,H24,H24,H2,H0,H0,H0
 DC.L H24,H24,H24,H24,H24,H0,H0,H0,H0
 DC.L H70,H44,H44,H44,H80,H0,H0,H0,H0
 DC.L H70,H70,H70,H2,H2,H2,H80,H0,H0
 DC.L H70,H70,H70,H2,H2,H80,H0,H0,H0
 DC.L H70,H70,H70,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H24,H2,H2,H2,H2,H0,H0
 DC.L H44,H44,H44,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H2,H2,H0,H0,H0,H0
 DC.L H70,H44,H24,H2,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H0,H0,H0,H0,H0,H0
 DC.L H70,H70,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H70,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H2,H2,H80,H0,H0
 DC.L H70,H70,H44,H44,H2,H80,H0,H0,H0
 DC.L H70,H70,H44,H44,H80,H0,H0,H0,H0
 DC.L H70,H70,H44,H24,H24,H2,H80,H0,H0
 DC.L H70,H44,H44,H2,H2,H2,H0,H0,H0
 DC.L H70,H44,H44,H2,H2,H0,H0,H0,H0
 DC.L H70,H44,H44,H2,H0,H0,H0,H0,H0
 DC.L H70,H44,H44,H0,H0,H0,H0,H0,H0
 DC.L H70,H70,H24,H2,H2,H0,H0,H0,H0
 DC.L H70,H70,H24,H2,H0,H0,H0,H0,H0
 DC.L H70,H70,H24,H0,H0,H0,H0,H0,H0
 DC.L H70,H24,H24,H24,H24,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H24,H24,H24,H0,H0
 DC.L H70,H70,H70,H44,H2,H2,H80,H0,H0
 DC.L H44,H44,H44,H44,H2,H2,H0,H0,H0
 DC.L H44,H44,H44,H44,H2,H0,H0,H0,H0
 DC.L H44,H44,H44,H44,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H2,H2,H2,H0,H0,H0
 DC.L H70,H70,H44,H2,H2,H0,H0,H0,H0
 DC.L H70,H70,H44,H2,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H24,H24,H0,H0,H0,H0
 DC.L H70,H70,H24,H24,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H44,H80,H2,H0,H0
 DC.L H70,H70,H44,H44,H44,H80,H0,H0,H0
 DC.L H70,H70,H70,H44,H24,H2,H2,H80,H0
 DC.L H44,H44,H44,H44,H24,H2,H2,H0,H0
 DC.L H44,H44,H44,H44,H24,H2,H0,H0,H0
 DC.L H44,H44,H44,H44,H24,H0,H0,H0,H0
 DC.L H70,H44,H44,H44,H0,H0,H0,H0,H0
 DC.L H70,H70,H70,H2,H2,H2,H0,H0,H0
 DC.L H70,H70,H70,H2,H2,H0,H0,H0,H0
 DC.L H70,H70,H70,H2,H0,H0,H0,H0,H0
 DC.L H70,H70,H70,H0,H0,H0,H0,H0,H0
 DC.L H24,H70,H70,H24,H24,H0,H0,H0,H0
 DC.L H70,H24,H24,H24,H24,H24,H24,H0,H0
 DC.L H44,H44,H44,H44,H44,H2,H2,H0,H0
 DC.L H44,H44,H44,H44,H44,H2,H0,H0,H0
 DC.L H44,H44,H44,H44,H44,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H2,H2,H2,H0,H0
 DC.L H70,H70,H44,H44,H2,H2,H0,H0,H0
 DC.L H70,H70,H44,H44,H2,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H0,H0,H0,H0,H0
 DC.L H70,H44,H44,H24,H24,H24,H0,H0,H0
 DC.L H70,H70,H70,H24,H2,H0,H0,H0,H0
 DC.L H70,H70,H70,H24,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H70,H70,H0,H0,H0
 DC.L H44,H44,H44,H44,H44,H24,H2,H2,H2
 DC.L H44,H44,H44,H44,H44,H24,H2,H2,H0
 DC.L H70,H44,H44,H44,H44,H2,H2,H0,H0
 DC.L H70,H44,H44,H44,H44,H2,H0,H0,H0
 DC.L H70,H44,H44,H44,H44,H0,H0,H0,H0
 DC.L H70,H44,H44,H44,H24,H24,H2,H0,H0
 DC.L H70,H70,H70,H44,H2,H2,H0,H0,H0
 DC.L H70,H70,H70,H44,H2,H0,H0,H0,H0
 DC.L H70,H70,H70,H44,H0,H0,H0,H0,H0

RIEN	REPT	9
	DC.L	H0
	ENDR

	DS.B	256
IMG	INCBIN	ST1.BMP

	DC.L	RT_00
TABLE_RT
	DC.L	RT_00
	DC.L	RT_0
	DC.L	RT_1
	DC.L	RT_2
	DC.L	RT_3
	DC.L	RT_4
	DC.L	RT_5
	DC.L	RT_6
	DC.L	RT_7
	DC.L	RT_8
	DC.L	RT_9
	DC.L	RT_10
	DC.L	RT_11
	DC.L	RT_12
	DC.L	RT_13
	DC.L	RT_14
	;DC.L	RT_14

 	SECTION BSS
POIT	DS.L	1
SCREEN1	DS.L	1
PALETTE	DS.B	2*256
PALETTES	DS.B	32*480
BIG_BUF	DS.B	230