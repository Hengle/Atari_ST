	*ENTIRELY CODED IN LESS THAN 1 HOUR

 	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP

	CLR.W	-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	ANDI.B	#%11111000,$484.W

	MOVE.L	#FIN,$008
	MOVE.L	#FIN,$00C
	MOVE.L	#FIN,$010
	MOVE.L	#FIN,$014
	MOVE.L	#FIN,$018
	MOVE.L	#FIN,$01C
	MOVE.L	#FIN,$020

	MOVEQ	#1,D0
	JSR	MUZAXX
	
	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,SAUV_PAL

	MOVE.B	$FFFFFA07.W,MFP
	MOVE.B	$FFFFFA09.W,MFP+1
	MOVE.B	$FFFFFA0F.W,MFP+2
	MOVE.B	$FFFFFA13.W,MFP+3
	MOVE.B	$FFFFFA1B.W,MFP+4
	MOVE.B	$FFFFFA21.W,MFP+5

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W

	MOVE.L	$70.W,ANC_VBL
	MOVE.L	#VBLR,$70.W
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	#TB0,$120.W
	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	MOVE.W	#$2300,SR

BOUCLE	JMP	BOUCLE

FIN	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W

	MOVE.B	MFP,$FFFFFA07.W
	MOVE.B	MFP+1,$FFFFFA09.W
	MOVE.B	MFP+2,$FFFFFA0F.W
	MOVE.B	MFP+3,$FFFFFA13.W
	MOVE.B	MFP+4,$FFFFFA1B.W
	MOVE.B	MFP+5,$FFFFFA21.W

	MOVEM.L	SAUV_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W

	ORI.B	#%00000111,$484.W

	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP

	*******	V B L  &  P R O C E D U R E S   *******
VBLR	MOVE.L	#2000,D0
BCL	DBRA	D0,BCL
	MOVE.W	#$7,$FFFF8240.W
	JSR	MUZAXX+8
	LEA	COUL,A6
	CLR.B	$FFFFFA1B.W
	MOVE.B	#1,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	BSR	AFF_TXT
	MOVE.W	#0,$FFFF8240.W
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	ADDI.L	#1,POINT
	MOVE.L	#VBLR2,$70.W
	RTE

VBLR2	MOVE.L	#2000,D0
BCL2	DBRA	D0,BCL2
	MOVE.W	#$7,$FFFF8240.W
	JSR	MUZAXX+8
	MOVE.W	#0,$FFFF8240.W
	LEA	COUL,A6
	CLR.B	$FFFFFA1B.W
	MOVE.B	#1,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	MOVE.L	#VBLR,$70.W
	RTE

TB0	;MOVE.W	#$700,$FFFF8240.W
	MOVE.W	(A6)+,$FFFF8242.W
	CLR.B	$FFFFFA1B.W
	MOVE.B	#1,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	#TB0,$120.W
	BCLR	#0,$FFFFFA0F.W
	;MOVE.W	#$000,$FFFF8240.W
	RTE

AFF_TXT	MOVEQ	#0,D0
	LEA	TEXT,A0
	ADDA.L	POINT,A0
	CMPI.B	#-1,(A0)
	BNE.S	SUITAF1
	CLR.L	POINT
	CLR.L	COORX
	LEA	TEXT,A0
SUITAF1	CMPI.B	#240,(A0)
	BNE.S	SUITAF2
	BRA	GODEB
SUITAF2	CMPI.B	#241,(A0)
	BNE.S	SUITAF3
	BRA	MONT
SUITAF3	CMPI.B	#242,(A0)
	BNE.S	SUITAF4
	BRA	DESC
SUITAF4	CMPI.B	#243,(A0)
	BNE.S	SUITAF5
	BRA	EFF_LIG
SUITAF5	CMPI.B	#244,(A0)
	BNE.S	SUITAF6
	BRA	STAY
SUITAF6	MOVE.B	(A0),D0
	SUBI.B	#32,D0
	BSR.S	AFF
FINAF	RTS
	
AFF	MOVE.L	$44E.W,A2
	MOVE.L	COORX,D1
	LEA	EC_PLAC,A1
	LSL.W	#2,D1
	ADDA.L	D1,A1
	ADDA.L	(A1),A2
	ADDA.L	COORY,A2
	LEA	FONT8_8,A1
	LSL.W	#3,D0
	ADDA.L	D0,A1
	MOVE.B	(A1)+,(A2)
	MOVE.B	(A1)+,160(A2)
	MOVE.B	(A1)+,320(A2)
	MOVE.B	(A1)+,480(A2)
	MOVE.B	(A1)+,640(A2)
	MOVE.B	(A1)+,800(A2)
	MOVE.B	(A1)+,960(A2)
	MOVE.B	(A1),1120(A2)
	ADDQ.L	#1,COORX
	CMPI.L	#40,COORX
	BLS.S	SUITF1
	SUBQ.L	#1,COORX
SUITF1	MOVE.L	COORY,D0
	BSR	CALC_PLAC
	BSR	AFF_CUR
	RTS
	
MONT	CMPI.L	#0,COORY
	BNE.S	SUITM
	MOVE.L	#8*160,COORY
SUITM	SUBI.L	#8*160,COORY
	MOVE.L	COORY,D0
	BSR	CALC_PLAC
	BSR	AFF_CUR
	MOVE.L	COORY,D0
	ADDI.L	#8*160,D0
	BSR	CALC_PLAC
	BSR	EFF_CUR
	BRA	FINAF

DESC	CMPI.L	#(15*8)*160,COORY
	BNE.S	SUITD
	MOVE.L	#(8*14)*160,COORY
SUITD	ADDI.L	#8*160,COORY
	MOVE.L	COORY,D0
	BSR	CALC_PLAC
	BSR	AFF_CUR
	MOVE.L	COORY,D0
	SUBI.L	#8*160,D0
	BSR	CALC_PLAC
	BSR	EFF_CUR	
	BRA	FINAF

EFF_LIG	CLR.L	COORX
	MOVE.L	$44E.W,A0
	ADDA.L	COORY,A0
N	SET	0
	REPT	8
	CLR.W	N(A0)
	CLR.W	8+N(A0)
	CLR.W	16+N(A0)
	CLR.W	24+N(A0)
	CLR.W	32+N(A0)
	CLR.W	40+N(A0)
	CLR.W	48+N(A0)
	CLR.W	56+N(A0)
	CLR.W	64+N(A0)
	CLR.W	72+N(A0)
	CLR.W	80+N(A0)
	CLR.W	88+N(A0)
	CLR.W	96+N(A0)
	CLR.W	104+N(A0)
	CLR.W	112+N(A0)
	CLR.W	120+N(A0)
	CLR.W	128+N(A0)
	CLR.W	136+N(A0)
	CLR.W	144+N(A0)
	CLR.W	152+N(A0)
N	SET	N+160
	ENDR
	BRA	FINAF

GODEB	MOVE.L	COORY,D0
	BSR.S	CALC_PLAC
	BSR.S	EFF_CUR
	ADDI.L	#8*160,COORY
	CLR.L	COORX
	MOVE.L	COORY,D0
	BSR.S	CALC_PLAC
	BSR.S	AFF_CUR
	BRA	FINAF	

STAY	MOVE.L	COORY,D0
	BSR.S	CALC_PLAC
	BSR.S	AFF_CUR
	RTS

CALC_PLAC	MOVE.L	$44E.W,A0
	ADDA.L	D0,A0
	LEA	EC_PLAC,A1
	MOVE.L	COORX,D1
	LSL.L	#2,D1
	ADDA.L	D1,A1
	ADDA.L	(A1),A0
	RTS

AFF_CUR	MOVE.B	#$FF,(A0)
	MOVE.B	#$FF,160(A0)
	MOVE.B	#$FF,320(A0)
	MOVE.B	#$FF,480(A0)
	MOVE.B	#$FF,640(A0)
	MOVE.B	#$FF,800(A0)
	MOVE.B	#$FF,960(A0)
	MOVE.B	#$FF,1120(A0)
	RTS

EFF_CUR	CLR.B	(A0)
	CLR.B	160(A0)
	CLR.B	320(A0)
	CLR.B	480(A0)
	CLR.B	640(A0)
	CLR.B	800(A0)
	CLR.B	960(A0)
	CLR.B	1120(A0)
	RTS

	SECTION	DATA
	;********* COMMENTAIRE POUR LA BONNE COMPREHENSION *********

	;	240 : LE CURSEUR VA AU DEBUT DE LA LIGNE SUIVANTE
	;	241 : LE CURSEUR MONTE D'UNE LIGNE
	;	242 : LE CURSEUR DESCEND D'UNE LIGNE
	;	243 : LA LIGNE SOUS LE CURSEUR EST EFFACE
	;	244 : FONCTION STAY(RESTE SUR PLACE)

TEXT	DC.B	"BONJOUR C'EST MOI FULBERT",240
	DC.B	"ET JE SUIS TRES CONTENT D'ETRE ICI",240
	DC.B	"JE SAIS JE SUIS BON ET TOUT ET TOUT",240
	DC.B	"MAIS ARRETEZ LES FLASH JE BRONZE "
	REPT	20
	DC.B	244,244
	ENDR	
	DC.B	240,"BON ALLEZ FINI DE RIRE ET PASSONS",240
	DC.B	"AUX CHOSES SERIEUSES",240
	DC.B	"PARLONS DE HOLOCAUST",240
	DC.B	"CE MERVEILLEUX GROUPE DONT JE FAIT ",240
	DC.B	"PARTIE "
	REPT	30
	DC.B	242,241
	ENDR	
	REPT	9
	DC.B	243,241
	ENDR
	DC.B	243
	DC.B	"COMME VOUS POUVEZ LE VOIR JE SUIS",240
	DC.B	"EN TRAIN DE CHANGER LES SEQUENCES",240
	DC.B	"ESCAPE "	
	REPT	25
	DC.B	244
	ENDR
	DC.B	240
	DC.B	"FAUT PAS CROIRE QUE CE SOIT DIFFICILE",240
	DC.B	"BIEN AU CONTRAIRE "
	REPT	25
	DC.B	244
	ENDR
	DC.B	240,"EN FAIT LE PLUS DUR EST DE TAPER",240
	DC.B	"LE TEXTE",240
	DC.B	"HAHAHAHAHAHAHAHAHAHAHAHAHAHAHAHA"
	REPT	25
	DC.B	244
	ENDR
	DC.B	243
	REPT	8
	DC.B	241,243
	ENDR
	DC.B	-1

FONT8_8	INCLUDE	FONT8_83.S

EC_PLAC	DC.L	0,1,8,9,16,17,24,25,32,33,40,41,48,49,56,57
	DC.L	64,65,72,73,80,81,88,89,96,97,104,105,112,113
	DC.L	120,121,128,129,136,137,144,145,152,153

COUL	REPT	35
	DC.W	$777,$666,$444,$333,$333,$444,$666,$777
	ENDR
MUZAXX	INCBIN	\MUS\CRANE.MUS

	SECTION	BSS
MFP	DS.B	8
SAUV_PAL	DS.L	8
SAUV_SP	DS.L	1
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
POINT	DS.L	1
COORX	DS.L	1
COORY	DS.L	1
FLAG	DS.L	1