
	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP

	MOVEQ	#1,D0
	JSR	MUZAXX
	MOVE.L	#MUZAXX+8,$4D2

	CLR.W	-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	ANDI.B	#%11111000,$484.W

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,SAUV_PAL
	MOVEM.L	PAL1,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	BSR	PREDEC

	MOVE.L	#FIN2,$008
	MOVE.L	#FIN2,$00C
	MOVE.L	#FIN2,$010
	MOVE.L	#FIN2,$014
	MOVE.L	#FIN2,$018
	MOVE.L	#FIN2,$01C

	CLR.L	$4D2.W
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	#VBLR,$70.W

	MOVE.L 	#$31415926,$426.W
	MOVE.L 	#RESET,$42A.W

BOUCLE	JMP	BOUCLE
	
FIN	MOVE.L	ANC_VBL,$70.W

	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W

	MOVEM.L	SAUV_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	ORI.B	#%00000111,$484.W

	ANDI.W	#%1101111111111111,SR

	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP

FIN2	CMPI.B	#$39,$FFFFFC02.W
	BNE	FIN2
	BRA	FIN

	*******	V B L  &  P R O C E D U R E S   *******
VBLR	MOVE.W	#$700,$FFFF8240.W
	BSR	AFF2
	MOVE.W	#$000,$FFFF8240.W
	JSR	MUZAXX+8
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE

AFF2	MOVEQ	#0,D0
	LEA	MVT2,A0
	LEA	CORES2,A1
	ADDA.L	AD,A0
	CMPI.B	#$FF,(A0)
	BNE	SUITK2
	CLR.L	AD
	LEA	MVT2,A0
SUITK2	MOVE.B	(A0),D0
	LSL.W	#2,D0
	ADDA.L	D0,A1
	MOVE.L	(A1),A0	
	MOVE.L	$44E.W,A1
N	SET	0
N2	SET	0
	REPT	88
	MOVEM.L	N(A0),D0-D7/A2-A6
	MOVEM.L	D0-D7/A2-A6,N2(A1)
	MOVEM.L	N+52(A0),D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N2+52(A1)
N	SET	N+(24*4)
N2	SET	N2+160
	ENDR
	ADDQ.L	#1,AD
	RTS

	*******   P R E D E C A L A G E S  1 6   ******* 
PREDEC	MOVE.L	#LOGO2+34+(111*160),A0
	BSR	REAFF1
	REPT	15
	MOVE.L	#BUF_LOG2,A0
	ADDA.L	AD,A0
	SUBA.L	#(24*4)*88,A0
	BSR	REAFF2
	BSR	DECAL2
	ENDR
	CLR.L	AD
	RTS

REAFF1	MOVE.L	#BUF_LOG2,A1
	MOVE.W	#87,D0
RECOM2	MOVEM.L	(A0),D1-D7/A2-A6
	MOVEM.L	D1-D7/A2-A6,(A1)
	MOVEM.L	48(A0),D1-D7/A2-A4
	MOVEM.L	D1-D7/A2-A4,48(A1)
	LEA	160(A0),A0
	LEA	24*4(A1),A1
	DBRA	D0,RECOM2
	ADDI.L	#(24*4)*88,AD
	RTS	
	
REAFF2	MOVE.L	#BUF_LOG2,A1
	ADDA.L	AD,A1
	MOVE.W	#87,D0
RECOM3	MOVEM.L	(A0),D1-D7/A2-A6
	MOVEM.L	D1-D7/A2-A6,(A1)
	MOVEM.L	48(A0),D1-D7/A2-A6
	MOVEM.L	D1-D7/A2-A6,48(A1)
	LEA	24*4(A0),A0
	LEA	24*4(A1),A1
	DBRA	D0,RECOM3
	ADDI.L	#(24*4)*88,AD
	RTS	

DECAL2	LEA	BUF_LOG2,A0
	ADDA.L	AD,A0	SUBA.L	#(24*4)*88,A0
	MOVE.L	#87,D1
DEC_NL	MOVE.L	#3,D0
DEC_PL	ROXR	(A0)
	ROXR	8(A0)
	ROXR	16(A0)
	ROXR	24(A0)
	ROXR	32(A0)
	ROXR	40(A0)
	ROXR	48(A0)
	ROXR	56(A0)
	ROXR	64(A0)
	ROXR	72(A0)
	ROXR	80(A0)
	ROXR	88(A0)
	ADDQ.L	#2,A0
	DBRA	D0,DEC_PL
	ADDA.L	#22*4,A0
	DBRA	D1,DEC_NL
	RTS

RESET	PEA	FINPR
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	JMP	RESET

	SECTION	DATA
LOGO2	INCBIN	HOLOLOGO.PI1
MUZAXX	INCBIN	ULM_REPLI.MUS

MVT2	DC.B	0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,14,13,12,11,10,9,8,7,6,5,4,3,2,1,$FF

PAL1	DC.W	$000,$000,$000,$000,$000,$000,$555,$444,$333,$222,$111,$700,$600,$500,$400,$300

CORES2	DC.L	BUF_LOG2,BUF_LOG2+(96*88),BUF_LOG2+(96*176),BUF_LOG2+(96*264),BUF_LOG2+(96*352),BUF_LOG2+(96*440),BUF_LOG2+(96*528),BUF_LOG2+(96*616)
	DC.L	BUF_LOG2+(96*704),BUF_LOG2+(96*792),BUF_LOG2+(96*880),BUF_LOG2+(96*968),BUF_LOG2+(96*1056),BUF_LOG2+(96*1144),BUF_LOG2+(96*1232),BUF_LOG2+(96*1320)	
	
FINPR	DC.B	"FIN,AU REVOIR",0

	SECTION	BSS
SAUV_PAL	DS.L	8
SAUV_SP	DS.L	1
ANC_VBL	DS.L	1
AD	DS.L	1
BUF_LOG2	DS.L	(24*88)*16