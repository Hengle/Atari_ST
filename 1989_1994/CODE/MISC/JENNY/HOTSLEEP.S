NB_PTS=104
PAS=2
NB_POS=360/PAS

DEBUT:	
	LEA.L 	D_PILE,A7
	PEA	0.W
	MOVE 	#$20,-(SP)
	TRAP 	#1
	ADDQ.L 	#6,SP

	MOVEQ	#1,D0
	JSR	ZIK

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
***
	clr	x_angle
	clr	y_angle
	clr	z_angle
	CLR.L	POINTEUR
	LEA	DATA_Y,A2
	LEA	DATA_X,A1
	move	#256,z_pt_fuite

	move.l	#NB_POS-1,d7
ici	CLR.L	POINTEUR
	jsr	trigo
	MOVE	#NB_PTS-1,D6
III	jsr	sp_to_pl
	jsr	ROUTINE
	DBF	D6,III
	ADD	#2*PAS,x_angle
	ADD	#2*PAS,y_angle
	dbf	d7,ici
	CLR.L	CNT
	MOVE.L	#NB_POS-2,CNT2
		
	LEA	DATA_X,A0
	LEA	DATA_Y,A1
	LEA	DATA_Y+(NB_PTS*(NB_POS-2)*2),A3
	MOVEM.L	A0-A3,DATAS_CUBE

***	
	MOVE.L	#INTER_RTE,$68.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W

	LEA	DATA_HS,A0
	MOVE.L	A0,POIT
		
	CLR.W 	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE 	#5,-(SP)
	TRAP 	#14
	LEA.L 	12(SP),SP

	move.l	#BUFFERSCR,d0
	CLR.B	D0
	move.l	d0,SCREEN1
	add.l	#64000,d0
	move.l	d0,SCREEN2

	move.l	SCREEN1,d0
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)

******
	LEA	PART1+34,A0
	MOVE.L	SCREEN1,A1
	LEA	32000(A1),A1
	LEA	31940(A1),A1
	LEA	1600(A1),A1
	MOVE	#199,D7
AFF_Y	MOVE	#(112/4)-1,D6
AFF_X	MOVE.L	(A0)+,(A1)+
	DBF	D6,AFF_X
	LEA	48(A0),A0
	LEA	70+48(A1),A1
	DBF	D7,AFF_Y
******
	LEA	PART2+34,A0
	MOVE.L	SCREEN1,A1
	LEA	31940(A1),A1
	LEA	32000(A1),A1
	LEA	1600+112(A1),A1
	MOVE	#199,D7
AFF_Y2	MOVE	#(112/4)-1,D6
AFF_X2	MOVE.L	(A0)+,(A1)+
	DBF	D6,AFF_X2
	LEA	48(A0),A0
	LEA	70+48(A1),A1
	DBF	D7,AFF_Y2
******
	LEA	PART3+34,A0
	MOVE.L	SCREEN1,A1
	LEA	31940(A1),A1
	LEA	32000(A1),A1
	LEA	1600(A1),A1
	LEA	230*100(A1),A1
	LEA	230*100(A1),A1
	MOVE	#199,D7
AFF_Y3	MOVE	#(112/4)-1,D6
AFF_X3	MOVE.L	(A0)+,(A1)+
	DBF	D6,AFF_X3
	LEA	48(A0),A0
	LEA	70+48(A1),A1
	DBF	D7,AFF_Y3
******
	LEA	PART4+34,A0
	MOVE.L	SCREEN1,A1
	LEA	31940(A1),A1
	LEA	32000(A1),A1
	LEA	1600+112(A1),A1
	LEA	230*100(A1),A1
	LEA	230*100(A1),A1
	MOVE	#199,D7
AFF_Y4	MOVE	#(112/4)-1,D6
AFF_X4	MOVE.L	(A0)+,(A1)+
	DBF	D6,AFF_X4
	LEA	48(A0),A0
	LEA	70+48(A1),A1
	DBF	D7,AFF_Y4
******
	MOVE.L	SCREEN1,A1
	LEA	31940(A1),A1
	LEA	32000(A1),A1
	LEA	1600(A1),A1
	LEA	230*100(A1),A1
	LEA	230*100(A1),A1
	LEA	230*100(A1),A1
	LEA	230*100(A1),A1
	MOVE	#99,D7
AFF_Y5	MOVE	#(112/4)-1,D6
AFF_X5	MOVE.L	#$FFFFFFFF,(A1)+
	CLR.L	(A1)+
	DBF	D6,AFF_X5
	ADDQ.L	#6,A0
	ADDQ.L	#6,A1
	DBF	D7,AFF_Y5
;	RESTE	34400-23000=11400 OCTETS LIBRES
;	DANS LE BUFFER DE 64*3K

******
;	LEA	PART5+34,A0
;	MOVE.L	SCREEN1,A1
;	LEA	1600(A1),A1
;	LEA	230*100(A1),A1
;	LEA	230*100(A1),A1
;	LEA	230*99(A1),A1
;	LEA	230*95(A1),A1
;	MOVE	#199,D7
;AFF_Y5	MOVE	#(112/4)-1,D6
;AFF_X5	MOVE.L	(A0)+,(A1)+
;	DBF	D6,AFF_X5
;	LEA	48(A0),A0
;	LEA	70+48(A1),A1
;	DBF	D7,AFF_Y5
******
;	LEA	PART6+34,A0
;	MOVE.L	SCREEN1,A1
;	LEA	1600+112(A1),A1
;	LEA	230*100(A1),A1
;	LEA	230*100(A1),A1
;	LEA	230*99(A1),A1
;	LEA	230*95(A1),A1
;	MOVE	#199,D7
;AFF_Y6	MOVE	#(112/4)-1,D6
;AFF_X6	MOVE.L	(A0)+,(A1)+
;	DBF	D6,AFF_X6
;	LEA	48(A0),A0
;	LEA	70+48(A1),A1
;	DBF	D7,AFF_Y6
******
	MOVE.L	#VBLR_IR,$70.W
	MOVE.L	#TB0_IR,$120.W
	
	MOVE.B	#$12,$FFFFFC02.W
	
	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W

BOUCLE	BRA.S	BOUCLE
	MOVE.L	SCREEN1,A0
	MOVE	#7999,D0
EFF_SCR1	CLR.L	(A0)+
	DBF	D0,EFF_SCR1
	MOVE.L	#LOOP_AFF_MES,MODMOD	Pour JSR LOOP_AFF_MES
	MOVE.L	#MESSAGE3,MOD_ADR	Nouveau message
	MOVE.L	#20*5,MODNB		New txts de 5 lignes
	CLR.L	FLAG_DREAM		Flag de fin de txt
	CLR	MODCOL		Coul de 8242
	CLR.L	MOD_GLOU
	CLR	HYP1		LEAS
	CLR	HYP2		LEAS
	CLR	HYP3		LEAS
	CLR	HYP4		LEAS
	CLR	HYP5		LEAS
	MOVE	#160*100,HYP6		LEAS
	MOVE	#160,MOD230		Non overscan
	CLR.L	FLAG_FIN
J	BRA.S	J
FLAG_FIN	DC.L	1
trigo	movem.l d0-d7/a4-a6,-(a7)

	lea cos_tab,a4
	lea sin_tab,a5
	lea x_angle,a6
	move.w (a6)+,d2
	move.w (a6)+,d1
	move.w (a6)+,d0
	
	move.w 0(a5,d2.w),d5
	move.w 0(a4,d2.w),d4
	move.w 0(a5,d1.w),d3
	move.w 0(a4,d1.w),d2
	move.w 0(a5,d0.w),d1
	move.w 0(a4,d0.w),d0

	lea var_3d,a6
	moveq #9,d7
sp_c	move.w d3,d6
	neg.w d6
	move.w d6,(a6)+
sp_f	move.w d2,d6
	muls d5,d6
	asr.l d7,d6
	move.w d6,(a6)+
sp_i	move.w d2,d6
	muls d4,d6
	asr.l d7,d6
	move.w d6,(a6)+
sp_a	move.w d0,d6
	muls d2,d6
	asr.l d7,d6
	move.w d6,(a6)+
sp_d	move.w d1,d6
	muls d4,d6
	asr.l d7,d6
	movea.w d6,a4
	move.w d0,d6
	muls d3,d6
	asr.l d7,d6
	muls d5,d6
	asr.l d7,d6
	sub.w a4,d6
	move.w d6,(a6)+	
sp_g	move.w d1,d6
	muls d5,d6
	asr.l d7,d6
	movea.w d6,a4
	move.w d0,d6
	muls d3,d6
	asr.l d7,d6
	muls d4,d6
	asr.l d7,d6
	add.w a4,d6
	move.w d6,(a6)+	
sp_b	move.w d1,d6
	muls d2,d6
	asr.l d7,d6
	move.w d6,(a6)+
sp_e	move.w d0,d6
	muls d4,d6
	asr.l d7,d6
	movea.w d6,a4
	move.w d1,d6
	muls d3,d6
	asr.l d7,d6
	muls d5,d6
	asr.l d7,d6
	add.w a4,d6
	move.w d6,(a6)+	
sp_h	move.w d0,d6
	muls d5,d6
	asr.l d7,d6
	movea.w d6,a4
	move.w d1,d6
	muls d3,d6
	asr.l d7,d6
	muls d4,d6
	asr.l d7,d6
	sub.w a4,d6
	move.w d6,(a6)+	
	movem.l (a7)+,d0-d7/a4-a6
	rts

sp_to_pl movem.l d0-d7/a1-a6,-(a7)
	lea points,a6
	ADDA.L	POINTEUR,A6
	move.w (a6)+,d0
	move.w (a6)+,d1
	move.w (a6)+,d2
	ADD.L	#6,POINTEUR
	
	lea depla,a3
	move.w (a3)+,d3	;xdep
	move.w (a3)+,d4	;ydep
	move.w (a3)+,d5	;zdep
	ext.l d3
	ext.l d4
	ext.l d5
	moveq #9,d6
	asl.l d6,d3 	;xdep*512
	asl.l d6,d4		;ydep*512
	asl.l d6,d5		;zdep*512
	movea.l d3,a1	
	movea.l d4,a2
	movea.l d5,a3
	
	lea var_3d,a6
	move.w d0,d3
	move.w d1,d4
	move.w d2,d5

	muls (a6)+,d3
	muls (a6)+,d4
	muls (a6)+,d5
	add.l d4,d5
	add.l d3,d5
	add.l a3,d5
	move.l d5,a5
	divs z_pt_fuite,d5

	move.w #512,d7
	sub.w d5,d7
	
	move.w d0,d3
	move.w d1,d4
	move.w d2,d5
		
	muls (a6)+,d3
	muls (a6)+,d4
	muls (a6)+,d5
	add.l d4,d5
	add.l d3,d5
	add.l a1,d5
	move.l d5,a4
	divs d7,d5
	muls (a6)+,d0
	muls (a6)+,d1
	muls (a6)+,d2
	add.l d0,d1
	add.l d1,d2
	add.l a2,d2
	move.l d2,d6
	divs d7,d2
	lea xp,a6
	add.w #160,d5
	move.w d5,(a6)+
	add.w #50,d2
	move.w d2,(a6)+
	move.l a4,d5
	move.l a5,d7
	moveq #9,d0
	asr.l d0,d5
	asr.l d0,d6
	asr.l d0,d7
	move.w d5,(a6)+
	move.w d6,(a6)+
	move.w d7,(a6)+
	MOVE	D7,ZA	
	movem.l (a7)+,d0-d7/a1-a6
	rts

ROUTINE
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVE	xp,D0
	MOVE	yp,d1
	MULU	#230,D1
	DIVS	#16,D0
	MOVE	D0,D2
	MULU	#8,D2
	ADD.L	D2,D1
	SWAP	D0
	CMPI	#$7,D0
	BGT.S	PLUS_UN
	BRA.S	PLUS_LOIN
PLUS_UN	ADD.L	#1,D1
	SUB.L	#8,D0
PLUS_LOIN
	LEA	KK,A0
	MULU	#6,D0
	JSR	0(A0,D0.W)

	TST	ZA
	BGT.S	PLD
	BRA.S	NOPLD
PLD	ADD	#2,D1
NOPLD	MOVE	D1,(A2)+
	RTS

KK	MOVE.B	#%10000000,(A1)+
	RTS
	MOVE.B	#%01000000,(A1)+
	RTS
	MOVE.B	#%00100000,(A1)+
	RTS	
	MOVE.B	#%00010000,(A1)+
	RTS	
	MOVE.B	#%00001000,(A1)+
	RTS	
	MOVE.B	#%00000100,(A1)+
	RTS
	MOVE.B	#%00000010,(A1)+
	RTS
	MOVE.B	#%00000001,(A1)+
	RTS

****DEBUT WRITER
LOOP_AFF_MES
MOD_ADR	EQU	*+2
	LEA	MESSAGE,A0
	CMPI.B	#-1,(A0)
	BEQ	TERMINê
;	MOVE	#(20*5)-1,D6
	MOVE.L	#ICI,MODMOD
	MOVE.L	A0,SAVEA0
ICI	MOVE.L	SAVEA0,A0
	MOVEQ	#0,D0
	MOVE.L	SCREEN1,A5
HYP1	EQU	*+2
	LEA	31940(A5),A5
MOD_GLOU	EQU	*+2
	ADDA.L	#32000,A5
HYP2	EQU	*+2
	LEA	1600(A5),A5
HYP3	EQU	*+2
	LEA	230*100(A5),A5
HYP4	EQU	*+2
	LEA	230*100(A5),A5
HYP5	EQU	*+2
	LEA	230*100(A5),A5
HYP6	EQU	*+2
	LEA	230*79+32(A5),A5
	LEA	CORRES,A2
MOD	EQU	*+2
	LEA	0(A5),A5
MOD2	EQU	*+2
	LEA	0(A5),A5
	MOVE.B	(A0)+,D0
	SUBI.B	#32,D0
;	TST.B	D0
;	BEQ.S	GO
;	SUBQ.B	#1,D0
GO	LSL.L	#2,D0
	ADDA.L	D0,A2
	MOVE.L	(A2),A2
	MOVEQ	#15,D0
AFF_A_LETTER	
;	MOVE	(A2),(A5)
MODCOD	NOP	$3A92
MODCOD2	MOVE	(A2),D7
	MOVE	D7,D4
	NOT	D7
	MOVE.L	#$FFFFFFFF,(A5)
	AND	D7,(A5)
	AND	D7,2(A5)
	OR	D4,(A5)
	LEA	160(A2),A2
MOD230	EQU	*+2
	LEA	230(A5),A5
	DBF	D0,AFF_A_LETTER
	ADDQ	#8,MOD
	CMPI	#160,MOD
	BNE.S	OKAY_MAN
	CLR	MOD
	ADD	#230*16,MOD2
OKAY_MAN	MOVE.L	A0,SAVEA0
	ADDQ.L	#1,CNT_LETTERS
MODNB	EQU	*+2
	CMPI.L	#20*5,CNT_LETTERS
	BEQ.S	FINITOTE
	RTS
CNT_LETTERS	DC.L	0
SAVEA0	DC.L	0
FINITOTE	MOVE.L	A0,MOD_ADR
	CLR.L	CNT_LETTERS
	CLR	MOD
	CLR	MOD2
	CLR	PNT_FAD
	CLR	PNT_FADB
	MOVE.L	#SUITE,MODMOD
	RTS
SUITE
CHG_FD1	EQU	*+2
FAD1	LEA	FAD_IN,A0
PNT_FAD	EQU	*+2
	LEA	0(A0),A0
	MOVE	(A0),D0
	CMPI	#-1,D0
	BEQ.S	ENDED
	MOVE	D0,MODCOL
	MOVE.L	#WAIT_A_BIT,MODMOD
	RTS

WAIT_A_BIT	;TST.L	VSYNC
	;BEQ.S	WAIT_A_BIT
	;CLR.L	VSYNC
	ADDQ.L	#1,PNT_FAD2
	CMPI.L	#4,PNT_FAD2
	BNE.S	EXI
	ADDQ	#2,PNT_FAD
	CLR.L	PNT_FAD2
	MOVE.L	#SUITE,MODMOD
EXI	RTS

PNT_FAD2	DC.L	0
TIME	DC.L	0
ENDED	ADDQ.L	#1,TIME
	MOVE.L	#WAIT2,MODMOD
	RTS
WAIT2	;TST.L	VSYNC
	;BEQ.S	WAIT2
	;CLR.L	VSYNC
	CMPI.L	#50*4,TIME
	BNE.S	ENDED
	CLR.L	TIME
	CLR.L	PNT_FAD2
	MOVE.L	#FAD2,MODMOD
	RTS
CHG_FD2	EQU	*+2
FAD2	LEA	FAD_OUT,A0
PNT_FADB	EQU	*+2
	LEA	0(A0),A0
	MOVE	(A0),D0
	CMPI	#-1,D0
	BEQ.S	ENDEDB
	MOVE	D0,MODCOL
WAITB	MOVE.L	#WAITB__,MODMOD
	RTS
WAITB__	;TST.L	VSYNC
	;BEQ.S	WAITB
	;CLR.L	VSYNC
	ADDQ.L	#1,PNT_FAD2
	CMPI.L	#4,PNT_FAD2
	BNE.S	EXIB
	ADDQ	#2,PNT_FADB
	CLR.L	PNT_FAD2
	MOVE.L	#FAD2,MODMOD
EXIB	RTS
ENDEDB	CLR.L	TIME
	CLR.L	PNT_FAD2
	MOVE.L	#LOOP_AFF_MES,MODMOD
	RTS
TERMINê	MOVE.L	#RTS,MODMOD
	MOVE.L	#1,FLAG_DREAM	
RTS	RTS
FLAG_DREAM	DC.L	0

TOUT_NOIR	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W
	RTS

VBLR_IR	;MOVEM.L	D0-A6,-(SP)
	CLR.B	$FFFFFA1B.W

;	MOVEM.L	PART1+2,D0-D7
	JSR	TOUT_NOIR
	MOVE.B	#0,$FFFFFA19.W
	MOVE.B	#99,$FFFFFA1F.W
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W
	;MOVE.L	#1,VSYNC
	;MOVEM.L	(SP)+,D0-A6
MOD_PAL2	NOP
	LEA	FAD_INITIAL,A5
ADDED	EQU	*+2
	LEA	0(A5),A5
	LEA	MOD_PAL,A6
	CMPI.L	#-1,(A5)
	BEQ.S	HC_RULEZ
	MOVE.L	A5,(A6)
	ADDQ.L	#1,WAITER
	CMPI.L	#3,WAITER
	BNE.S	SYU
	CLR.L	WAITER
	ADD	#32,ADDED
SYU	RTE
WAITER	DC.L	0
HC_RULEZ	MOVE	#$4E73,MOD_PAL2
INTER_RTE	RTE

INTER_TMA:	;MOVEM.L	D0-A6,-(SP)
	CLR.B	$FFFFFA07.W		; ARRET DU MFP POUR NE PAS
	CLR.B	$FFFFFA09.W		; ETRE GENE
	MOVE	#$2100,SR		; ON AUTORISE LA HBL
full
	LEA	$FFFF820A,A5

	MOVEQ	#0,D0
	MOVEQ	#2,D1

	STOP	#$2100		; ATTENTE DE LA PROCHAINE HBL
				; (FIXE A 16 CYCLES PRES ENVIRONS)
	MOVE	#$2700,SR		; ON COUPE TOUTE LES ITs
	MOVE	#$2300,(SP)		; AU RETOUR LE VBL SERA AUTORISEE

	MOVEQ	#29,D2		; ON ATTEND LE BON MOMENT
SYNCHROA:	DBF	D2,SYNCHROA
	NOP

	MOVE.B	D0,(A5)		; ET HOP! PLUS DE BORDER HAUT
	REPT	6
	NOP
	ENDR
	MOVE.B	D1,(A5)

*******
;	MOVEM.L	D0-A6,-(SP)
	MOVEA.L	POIT,A2

	MOVE.L	#$FFFF8209,A6
	MOVE.L	#$FFFF8260,A4
	MOVE.L	#$FFFF820A,A3
	MOVEQ	#0,D1
	MOVEQ	#$10,D6
	MOVE.L	#228-12+2-1-1,D0

SYNCHRO	MOVE.B	(A6),D7     * SYNCHRO
	BEQ.S	SYNCHRO
	SUB.W	D7,D6
	LSL.W	D6,D1
	;MOVE.L	USP,A6
	DCB	97-3-4,$4E71

	REPT	9
	MOVE.L	(A2)+,A1	*3
	JSR	(A1)		*4
	ENDR
	
	DCB	7-1,$4E71
	NOP
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	88-21,$4E71
MOD_PAL	EQU	*+4
	MOVEM.L	PART1+2,D1-D7/A0
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
;	DCB	13-9-4,$4E71
MODCOL	EQU	*+2
	MOVE	#$213,$FFFF8242.W
	
	MOVEM.L	D2-D4,$FFFF8244.W
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	MOVEM.L	D5-D7/A0,$FFFF8250.W
;	DCB	12-1,$4E71

FULLSCREEN	NOP
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	88,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71
	DBF	D0,FULLSCREEN

	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	88,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	;	ICI=116	NOPS

	CLR.B	$FFFF820A.W 
	DCB	8,$4E71

	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE	A4,(A3)
	DCB	88-2,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71
	MOVE.L	#29,D0
FULLOOP2	NOP
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	88,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71
	DBF	D0,FULLOOP2
SUITE_	JSR	TOUT_NOIR
	TST.L	FLAG_DREAM
	BEQ	_SOON_
	CMPI.L	#$943E,Y_VALUE		Valeur du Y dans la bulle.
	BNE.S	_SOON2_
	MOVE.L	#LOOP_AFF_MES,MODMOD	Pour JSR LOOP_AFF_MES
	MOVE.L	#MESSAGE2,MOD_ADR	Nouveau message
	MOVE	#$3A92,MODCOD		MOVE (A2),(A5)
	LEA	MODCOD2,A4		Nops... ( on
	MOVE.L	#$4E714E71,(A4)+	modifie la routine
	MOVE.L	#$4E714E71,(A4)+	d'affichage du
	MOVE.L	#$4E714E71,(A4)+	texte. )
	MOVE.L	#$4E714E71,(A4)+
	MOVE.L	#$4E714E71,(A4)+
	MOVE.L	#20*3,MODNB		New txts de 3 lignes
	MOVE.L	#VBLR2,$70.W		New Vbl
	CLR.L	FLAG_DREAM		Flag de fin de txt
	CLR	MODCOL		Coul de 8242
	MOVE.L	#FAD_IN2,CHG_FD1	New fadin
	MOVE.L	#FAD_OUT2,CHG_FD2	New fadout
_SOON2_	SUB.L	#230,Y_VALUE
	SUB.L	#230,MOD_GLOU
_SOON_	CMPI.B	#$48,$FFFFFC02.W
	BNE.S	PAS_HAUT
	ADD.L	#230,Y_VALUE
PAS_HAUT	CMPI.B	#$50,$FFFFFC02.W
	BNE.S	PAS_BAS
	TST.L	Y_VALUE
	BEQ.S	PAS_BAS
	SUB.L	#230,Y_VALUE
PAS_BAS
;	MOVEQ	#0,D0
	MOVE.L	Y_VALUE,D0
;	MOVE.L	D0,$2FFEC
	LSR.L	#8,D0
	MOVE.L	D0,D1
	LSL.L	#8,D1
	
	MOVE.L	Y_VALUE,D0
	SUB.L	D1,D0

	LEA	DATA_HS,A0

	TST.L	D0
	BNE.S	DEJ
	LEA	RIEN,A0
	MOVE.L	A0,POIT
	BRA.S	OKAY
DEJ	SUBQ.L	#2,D0
	MULU	#18,D0
	ADDA.L	D0,A0
	MOVE.L	A0,POIT
		
OKAY	move.l	SCREEN1,d0
;	move.l	SCREEN2,SCREEN1
;	move.l	d0,SCREEN2

	move.l	SCREEN1,d0
	ADD.L	D1,D0
	lsr.l	#8,d0
	LEA	$FFFF8201.W,A0
	movep	d0,(a0)
;	MOVEM.L	(SP)+,D0-A6

	JSR	TXT_ROUTY
	JSR	ZIK+8
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
Y_VALUE	DC.L	$0000B2CA+230*16+278*230
;$943E
TB0_IR	RTE
TXT_ROUTY
MODMOD	EQU	*+2
	JSR	LOOP_AFF_MES
	RTS
VBL_FINALE	MOVE	#$4E71,BOUCLE
	MOVEM.L	D0-A6,-(SP)
	JSR	ZIK+8
	move.l	SCREEN1,d0
	lsr.l	#8,d0
	move.l	#$ffff8201,a0
	movep	d0,(a0)
	TST.L	FLAG_FIN
	BNE.S	NOT_YET
	MOVE	MODCOL,$FFFF8242.W
	JSR	TXT_ROUTY
NOT_YET	MOVEM.L	(SP)+,D0-A6
	RTE
DATAS_CUBE	DS.L	4
VBLR2	CLR.B	$FFFFFA1B.W

	JSR	TOUT_NOIR

	MOVE.B	#0,$FFFFFA19.W
	MOVE.B	#99,$FFFFFA1F.W
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA2,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W

	MOVEM.L	DATAS_CUBE,A0-A3
	MOVE.L	SCREEN1,A2
	LEA	1600+32(A2),A2
	LEA	230*100(A2),A2
	LEA	230*100(A2),A2
	ADDQ.L	#1,CNT2

	CMPI.L	#NB_POS,CNT
	BNE.S	L

	LEA	DATA_X,A0
	LEA	DATA_Y,A1
	clr.l	CNT
L
	CMPI.L	#NB_POS,CNT2
	BNE.S	L2

	LEA	DATA_Y,A3
	CLR.L	CNT2
L2
	ADDQ.L	#1,CNT

	MOVEM.L	A0-A3,DATAS_CUBE
	
;	MOVEM.L	DATAS_CUBE,A0-A3	13
;	MOVE	#NB_PTS-1,D7
;EFF_PTS	MOVE	(A3)+,D1	2
;	MOVE.B	D0,0(A2,D1.W)	4
;	DBF	D7,EFF_PTS

;	MOVE	#NB_PTS-1,D7
;AFF_PTS	MOVE.B	(A0)+,D0
;	MOVE	(A1)+,D1
;	OR.B	D0,0(A2,D1.W)	5
;	DBF	D7,AFF_PTS
;poi
;	MOVEM.L	A0-A3,DATAS_CUBE	12

	TST.L	FLAG_DREAM
	BEQ	MODSCROL

MOD_PAL2B	NOP
	LEA	FAD_FINAL,A5
ADDEDB	EQU	*+2
	LEA	0(A5),A5
	LEA	MOD_PALB,A6
	CMPI.L	#-1,(A5)
	BEQ.S	HC_RULEZB
	MOVE.L	A5,(A6)
	ADDQ.L	#1,WAITERB
	CMPI.L	#3,WAITERB
	BNE.S	SYUB
	CLR.L	WAITERB
	ADD	#32,ADDEDB
SYUB	BRA.S	A_BIT_LATER
HC_RULEZB	MOVE	#$4E73,MOD_PAL2B
	MOVE.L	#VBL_FINALE,$70.W
A_BIT_LATER
	ADDQ.L	#1,CNT_FF
	CMPI.L	#3,CNT_FF
	BNE.S	MODSCROL
	CLR.L	CNT_FF
	TST	MOD_COL_P1
	BEQ.S	MP2
	SUBI	#$111,MOD_COL_P1
	CLR	MOD_COL_P4
MP2	TST	MOD_COL_P2
	BEQ.S	MODSCROL
	SUBI	#$111,MOD_COL_P2
MODSCROL	NOP
	CMPI.L	#8,NB_PART
	BNE.S	SUITV0
	BSR	TEST_LET
	CLR.L	NB_PART
SUITV0	BSR	SCROLL
	BSR	SCROLL
	RTE
WAITERB	DC.L	0
CNT_FF	DC.L	0
SCROLL	MOVE.L	SCREEN1,A0
	LEA	1600(A0),A0
	LEA	230*100(A0),A0
	LEA	230*100(A0),A0

	LEA	230*20+6-2(A0),A0
	LEA	BUF_LET,A1
	REPT	8
	ROXL	(A1)

	ROXL	226(A0)
	ROXL	218(A0)
	ROXL	210(A0)
	ROXL	202(A0)
	ROXL	194(A0)
	ROXL	186(A0)
	ROXL	178(A0)
	ROXL	170(A0)
	ROXL	162(A0)

	ROXL	154(A0)
	ROXL	146(A0)
	ROXL	138(A0)
	ROXL	130(A0)
	ROXL	122(A0)
	ROXL	114(A0)
	ROXL	106(A0)
	ROXL	98(A0)
	ROXL	90(A0)
	ROXL	82(A0)
	ROXL	74(A0)
	ROXL	66(A0)
	ROXL	58(A0)	
	ROXL	50(A0)
	ROXL	42(A0)
	ROXL	34(A0)
	ROXL	26(A0)
	ROXL	18(A0)
	ROXL	10(A0)
	ROXL	2(A0)
	LEA	230(A0),A0
	ADDQ.L	#2,A1
	ENDR
	ADDI.L	#1,NB_PART
	RTS

TEST_LET	MOVEQ	#0,D0
	LEA	TEXT1,A0	
	ADDA.L	POINT,A0
	LEA	FONT8_8,A1
	CMPI.B	#$FF,(A0)
	BNE	SUITTST
	CLR.L	POINT
	LEA	TEXT1,A0
	MOVE	#$4E73,MODSCROL
SUITTST	MOVE.B	(A0),D0	
	SUBI.B	#32,D0	
	LSL.W	#3,D0	
	ADDA.L	D0,A1	
	LEA	BUF_LET,A2
	MOVE.B	(A1)+,(A2)
	MOVE.B	(A1)+,2(A2)
	MOVE.B	(A1)+,4(A2)
	MOVE.B	(A1)+,6(A2)
	MOVE.B	(A1)+,8(A2)
	MOVE.B	(A1)+,10(A2)
	MOVE.B	(A1)+,12(A2)
	MOVE.B	(A1)+,14(A2)
	ADDI.L	#1,POINT
	RTS

INTER_TMA2
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE	#$2100,SR
	LEA	$FFFF820A,A5
	MOVEQ	#0,D0
	MOVEQ	#2,D1
	STOP	#$2100
	MOVE	#$2700,SR
	MOVE	#$2300,(SP)

	MOVEQ	#29,D2
SYNCHROB:	DBF	D2,SYNCHROB
	NOP

	MOVE.B	D0,(A5)
	REPT	6
	NOP
	ENDR
	MOVE.B	D1,(A5)

*******
	MOVEA.L	POIT,A2

	MOVE.L	#$FFFF8209,A6
	MOVE.L	#$FFFF8260,A4
	MOVE.L	#$FFFF820A,A3
	MOVEQ	#0,D1
	MOVEQ	#$10,D6

SYNCHRO_	MOVE.B	(A6),D7     * SYNCHRO
	BEQ.S	SYNCHRO_
	SUB.W	D7,D6
	LSL.W	D6,D1
	;MOVE.L	USP,A6
	DCB	97-3-4,$4E71

	REPT	9
	MOVE.L	(A2)+,A1	*3
	JSR	(A1)		*4
	ENDR
	
	DCB	7-1,$4E71
	MOVEQ	#0,D0
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	88-21,$4E71
MOD_PALB	EQU	*+4
	MOVEM.L	PART1+2,D1-D7/A0
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
;	DCB	4,$4E71
;MODCOL	EQU	*+2
;	MOVE	#$213,$FFFF8242.W
	MOVEM.L	D2-D4,$FFFF8244.W
	CLR	$FFFF8246.W
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	MOVEM.L	D5-D7/A0,$FFFF8250.W
;	DCB	12-1,$4E71

	NOP
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	88-4,$4E71
MOD_COL_P4	EQU	*+2
	MOVE	#$567,$FFFF8250.W
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	MOVEM.L	DATAS_CUBE,A0-A2/A5	13
;	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-1-8,$4E71
MOD_COL_P1	EQU	*+2
	MOVE	#$222,$FFFF8242.W
MOD_COL_P2	EQU	*+2
	MOVE	#$666,$FFFF8244.W

	REPT	6
	;NOP
	MOVEQ	#0,D0
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	88-6*14,$4E71	4NOPS
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13-12,$4E71
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	DCB	12-1-6,$4E71	17 FOIS
	ENDR

	NOP
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	88-6*2,$4E71
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE	(A5)+,D1	2
	MOVE.B	D0,0(A2,D1.W)	4
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-1,$4E71	104 FOIS

	REPT	9
	NOP
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	88-9*9,$4E71
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13-9,$4E71
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	DCB	12-1-9,$4E71
	ENDR

	NOP
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	88-5*9,$4E71
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	MOVE.B	(A0)+,D0	2
	MOVE	(A1)+,D1	2
	OR.B	D0,0(A2,D1.W)	5
	
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-1-3,$4E71
	MOVE.L	#228-12+2-1-1-1-17-40-14,D0

_FULLSCREEN	NOP
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	88,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71
	DBF	D0,_FULLSCREEN
*
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	MOVE	MODCOL,$FFFF8242.W
	MOVE.L	#40+14-1-1,D0
	DCB	88-3-6,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-1,$4E71

_FULLSCREEN2	NOP
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	88,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71
	DBF	D0,_FULLSCREEN2

	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	88,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	;	ICI=116	NOPS

	CLR.B	$FFFF820A.W 
	DCB	8,$4E71

	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	MOVE	A4,(A3)
	DCB	88-2,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71
	MOVE.L	#29,D0
FULLOOP	NOP
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	88,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	DCB	12-4,$4E71
	DBF	D0,FULLOOP
SUITE_2
	JSR	TOUT_NOIR
	MOVEM.L	A0-A2/A5,DATAS_CUBE	12
	MOVE.L	Y_VALUE,D0
	LSR.L	#8,D0
	MOVE.L	D0,D1
	LSL.L	#8,D1
	MOVE.L	Y_VALUE,D0
	SUB.L	D1,D0
	LEA	DATA_HS,A0
	TST.L	D0
	BNE.S	DEJY
	LEA	RIEN,A0
	MOVE.L	A0,POIT
	BRA.S	OKAYY
DEJY	SUBQ.L	#2,D0
	MULU	#18,D0
	ADDA.L	D0,A0
	MOVE.L	A0,POIT
		
OKAYY	move.l	SCREEN1,d0
;	move.l	SCREEN2,SCREEN1
;	move.l	d0,SCREEN2

	move.l	SCREEN1,d0
	ADD.L	D1,D0
	lsr.l	#8,d0
	LEA	$FFFF8201.W,A0
	movep	d0,(a0)
;	MOVEM.L	(SP)+,D0-A6

	JSR	TXT_ROUTY

	JSR	ZIK+8

	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE


FIN:	BRA.S	FIN

;GERE_PAL	SUBQ	#1,NUMERO_LIGNE	5
;	BEQ.S	CHANGE_COL		3/2
;	DCB	4*6-3+1,$4E71
;	BRA.S	RETOUR		3
;CHANGE_COL	MOVE.L	(A0)+,$FFFF8240.W	6
;	MOVE.L	(A0)+,$FFFF8244.W	6
;	MOVE.L	(A0)+,$FFFF8248.W	6
;	MOVE.L	(A0)+,$FFFF824C.W	6
;RETOUR	RTS			4

H0	
	DCB	128-4-3-4,$4E71
	RTS

H70
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	3+17*5,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	NOP
	RTS	=4 NOPS
H44
	DCB	3+18*5,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	NOP
	RTS	=4 NOPS
H24
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	1+17*5,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	REPT	3
	NOP
	ENDR
	RTS	=4 NOPS
H2
	NOP
	DCB	18*5,$4E71
	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	REPT	3
	NOP
	ENDR
	RTS	=4 NOPS
H80
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	7*5,$4E71
	MOVE	A4,(A4)
	MOVE.B	#0,(A4)
	DCB	3+9*5,$4E71

	MOVE.b	#0,(A3)
	MOVE	a4,(A3)
	DCB	13,$4E71
	MOVE	a4,(A4)
	MOVE.B	#0,(A4)
	NOP
	RTS
	 
	DATA

DATA_HS
 DC.L H44,H44,H2,H2,H2,H80,H0,H0,H0
 DC.L H44,H44,H2,H2,H80,H0,H0,H0,H0
 DC.L H44,H44,H2,H80,H0,H0,H0,H0,H0
 DC.L H44,H44,H80,H0,H0,H0,H0,H0,H0
 DC.L H44,H24,H24,H2,H80,H0,H0,H0,H0
 DC.L H44,H24,H24,H80,H0,H0,H0,H0,H0
 DC.L H24,H2,H2,H2,H2,H2,H0,H0,H0
 DC.L H24,H2,H2,H2,H2,H0,H0,H0,H0
 DC.L H24,H2,H2,H2,H0,H0,H0,H0,H0
 DC.L H24,H2,H2,H0,H0,H0,H0,H0,H0
 DC.L H24,H2,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H0,H0,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H24,H24,H80,H80,H0,H0
 DC.L H70,H44,H2,H2,H2,H80,H0,H0,H0
 DC.L H70,H44,H2,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H2,H80,H0,H0,H0,H0,H0
 DC.L H44,H2,H2,H2,H2,H2,H0,H0,H0
 DC.L H44,H2,H2,H2,H2,H0,H0,H0,H0
 DC.L H44,H2,H2,H2,H0,H0,H0,H0,H0
 DC.L H44,H2,H2,H0,H0,H0,H0,H0,H0
 DC.L H44,H2,H0,H0,H0,H0,H0,H0,H0
 DC.L H44,H0,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H2,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H0,H0,H0,H0,H0,H0,H0
 DC.L H44,H44,H44,H2,H80,H0,H0,H0,H0
 DC.L H44,H44,H44,H80,H0,H0,H0,H0,H0
 DC.L H70,H70,H2,H2,H2,H80,H0,H0,H0
 DC.L H70,H70,H2,H2,H80,H0,H0,H0,H0
 DC.L H70,H70,H2,H80,H0,H0,H0,H0,H0
 DC.L H44,H24,H2,H2,H2,H2,H0,H0,H0
 DC.L H44,H24,H2,H2,H2,H0,H0,H0,H0
 DC.L H44,H24,H2,H2,H0,H0,H0,H0,H0
 DC.L H44,H24,H2,H0,H0,H0,H0,H0,H0
 DC.L H44,H24,H0,H0,H0,H0,H0,H0,H0
 DC.L H70,H0,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H44,H2,H2,H80,H0,H0,H0
 DC.L H70,H44,H44,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H44,H80,H0,H0,H0,H0,H0
 DC.L H44,H24,H24,H2,H80,H70,H0,H0,H0
 DC.L H70,H44,H24,H24,H80,H0,H0,H0,H0
 DC.L H44,H44,H2,H2,H0,H0,H0,H0,H0
 DC.L H44,H44,H2,H0,H0,H0,H0,H0,H0
 DC.L H44,H44,H0,H0,H0,H0,H0,H0,H0
 DC.L H70,H24,H2,H2,H0,H0,H0,H0,H0
 DC.L H70,H24,H2,H0,H0,H0,H0,H0,H0
 DC.L H70,H24,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H2,H2,H2,H80,H0,H0
 DC.L H70,H70,H44,H2,H2,H80,H0,H0,H0
 DC.L H70,H70,H44,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H2,H2,H2,H2,H2,H0,H0
 DC.L H70,H44,H2,H2,H2,H2,H0,H0,H0
 DC.L H70,H44,H2,H2,H2,H0,H0,H0,H0
 DC.L H70,H44,H2,H2,H0,H0,H0,H0,H0
 DC.L H70,H44,H2,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H24,H2,H2,H0,H0
 DC.L H24,H24,H24,H24,H24,H2,H0,H0,H0
 DC.L H24,H24,H24,H24,H24,H0,H0,H0,H0
 DC.L H70,H44,H44,H44,H80,H0,H0,H0,H0
 DC.L H70,H70,H70,H2,H2,H2,H80,H0,H0
 DC.L H70,H70,H70,H2,H2,H80,H0,H0,H0
 DC.L H70,H70,H70,H2,H80,H0,H0,H0,H0
 DC.L H70,H44,H24,H2,H2,H2,H2,H0,H0
 DC.L H44,H44,H44,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H2,H2,H0,H0,H0,H0
 DC.L H70,H44,H24,H2,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H0,H0,H0,H0,H0,H0
 DC.L H70,H70,H0,H0,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H70,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H2,H2,H80,H0,H0
 DC.L H70,H70,H44,H44,H2,H80,H0,H0,H0
 DC.L H70,H70,H44,H44,H80,H0,H0,H0,H0
 DC.L H70,H70,H44,H24,H24,H2,H80,H0,H0
 DC.L H70,H44,H44,H2,H2,H2,H0,H0,H0
 DC.L H70,H44,H44,H2,H2,H0,H0,H0,H0
 DC.L H70,H44,H44,H2,H0,H0,H0,H0,H0
 DC.L H70,H44,H44,H0,H0,H0,H0,H0,H0
 DC.L H70,H70,H24,H2,H2,H0,H0,H0,H0
 DC.L H70,H70,H24,H2,H0,H0,H0,H0,H0
 DC.L H70,H70,H24,H0,H0,H0,H0,H0,H0
 DC.L H70,H24,H24,H24,H24,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H24,H24,H24,H0,H0
 DC.L H70,H70,H70,H44,H2,H2,H80,H0,H0
 DC.L H44,H44,H44,H44,H2,H2,H0,H0,H0
 DC.L H44,H44,H44,H44,H2,H0,H0,H0,H0
 DC.L H44,H44,H44,H44,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H2,H2,H2,H0,H0,H0
 DC.L H70,H70,H44,H2,H2,H0,H0,H0,H0
 DC.L H70,H70,H44,H2,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H0,H0,H0,H0,H0,H0
 DC.L H70,H44,H24,H24,H24,H0,H0,H0,H0
 DC.L H70,H70,H24,H24,H0,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H44,H80,H2,H0,H0
 DC.L H70,H70,H44,H44,H44,H80,H0,H0,H0
 DC.L H70,H70,H70,H44,H24,H2,H2,H80,H0
 DC.L H44,H44,H44,H44,H24,H2,H2,H0,H0
 DC.L H44,H44,H44,H44,H24,H2,H0,H0,H0
 DC.L H44,H44,H44,H44,H24,H0,H0,H0,H0
 DC.L H70,H44,H44,H44,H0,H0,H0,H0,H0
 DC.L H70,H70,H70,H2,H2,H2,H0,H0,H0
 DC.L H70,H70,H70,H2,H2,H0,H0,H0,H0
 DC.L H70,H70,H70,H2,H0,H0,H0,H0,H0
 DC.L H70,H70,H70,H0,H0,H0,H0,H0,H0
 DC.L H24,H70,H70,H24,H24,H0,H0,H0,H0
 DC.L H70,H24,H24,H24,H24,H24,H24,H0,H0
 DC.L H44,H44,H44,H44,H44,H2,H2,H0,H0
 DC.L H44,H44,H44,H44,H44,H2,H0,H0,H0
 DC.L H44,H44,H44,H44,H44,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H2,H2,H2,H0,H0
 DC.L H70,H70,H44,H44,H2,H2,H0,H0,H0
 DC.L H70,H70,H44,H44,H2,H0,H0,H0,H0
 DC.L H70,H70,H44,H44,H0,H0,H0,H0,H0
 DC.L H70,H44,H44,H24,H24,H24,H0,H0,H0
 DC.L H70,H70,H70,H24,H2,H0,H0,H0,H0
 DC.L H70,H70,H70,H24,H0,H0,H0,H0,H0
 DC.L H24,H24,H24,H24,H70,H70,H0,H0,H0
 DC.L H44,H44,H44,H44,H44,H24,H2,H2,H2
 DC.L H44,H44,H44,H44,H44,H24,H2,H2,H0
 DC.L H70,H44,H44,H44,H44,H2,H2,H0,H0
 DC.L H70,H44,H44,H44,H44,H2,H0,H0,H0
 DC.L H70,H44,H44,H44,H44,H0,H0,H0,H0
 DC.L H70,H44,H44,H44,H24,H24,H2,H0,H0
 DC.L H70,H70,H70,H44,H2,H2,H0,H0,H0
 DC.L H70,H70,H70,H44,H2,H0,H0,H0,H0
 DC.L H70,H70,H70,H44,H0,H0,H0,H0,H0

RIEN	REPT	9
	DC.L	H0
	ENDR

PART3	INCBIN	COLOR1.PI1
PART4	INCBIN	COLOR1B.PI1
PART1	INCBIN	bulle1.PI1
PART2	INCBIN	bulle2.PI1
;PART5	;INCBIN	P5B.PI1
;PART6	;INCBIN	P6B.PI1

;PAL0	
;N	SET	$000
;	REPT	7
;	DCB	8,N
;N	SET	N+$010
;	ENDR
;N	SET	$070
;	REPT	7
;	DCB	8,N
;N	SET	N+$001
;	ENDR
;N	SET	$077
;	REPT	8
;	DCB	8,N
;N	SET	N+$100
;	ENDR
;	DCB	400,$777
	
;PAL1	DC	$000,$037,$007,$004,$777,$600,$500,$400
;	DC	$210,$421,$532,$643,$754,$765,$213,$212

;PAL2	DC	$000,$734,$623,$512,$777,$412,$301,$324
;	DC	$210,$421,$532,$643,$754,$765,$213,$212
****DATAS WRITER
	*	'                    '
MESSAGE
	DC.B	'                    '
	DC.B	'                    '
	DC.B	'                    '
	DC.B	'                    '
	DC.B	'                    '

	DC.B	'                    '
	DC.B	'                    '
	DC.B	' HER NAME IS JENNY. '
	DC.B	'                    '
	DC.B	'                    '

	DC.B	'                    '
	DC.B	'                    '
	DC.B	'  SHE IS A CODER!!  '
	DC.B	'                    '
	DC.B	'                    '

	DC.B	'                    '
	DC.B	'   LOOK AT HER...   '
	DC.B	"  SHE'S SLEEPING... "
	DC.B	'                    '
	DC.B	'                    '

	DC.B	'EH? I WONDER IF SHE '
	DC.B	"IS DREAMING OF ME ? "
	DC.B	'                    '
	DC.B	'  ( ZAPPY ON        '
	DC.B	'        KEYBOARD...)'

	DC.B	'                    '
	DC.B	"    LET'S ENTER     "
	DC.B	'                    '
	DC.B	'   IN HER DREAMS !  '
	DC.B	'                    '

	DC.B	-1,-1,-1,-1
	DC.B	'BBBBBBBBBBBBBBBBBBBB'
	DC.B	'CCCCCCCCCCCCCCCCCCCC'
	DC.B	'DDDDDDDDDDDDDDDDDDDD'
	DC.B	'                    '
	DC.B	'                    '
	EVEN
MESSAGE2
	DC.B	'    WH...WHAT ?!?   '
	DC.B	'                    '
	DC.B	'                    '

	DC.B	'  SHE IS DREAMING   '
	DC.B	'        OF A        '
	DC.B	'     C U B E !!?    '

	DC.B	'                    '
	DC.B	'                    '
	DC.B	'                    '

	DC.B	" SHE'S DREAMING OF  "
	DC.B	'ENTERING OUR CREW!!!'
	DC.B	'                    '

	DC.B	'                    '
	DC.B	'   BUT... WAIT...   '
	DC.B	'                    '

	DC.B	'      THERE IS      '
	DC.B	'   SOMETHING ELSE...'
	DC.B	'                    '

	DC.B	-1,-1,-1,-1
	DC.B	'BBBBBBBBBBBBBBBBBBBB'
	DC.B	'CCCCCCCCCCCCCCCCCCCC'
	DC.B	'DDDDDDDDDDDDDDDDDDDD'
	EVEN
MESSAGE3
	DC.B	"LET'S GO DEEPER AND "
	DC.B	'DEEPER IN HER DREAM!'
	DC.B	'                    '
	DC.B	'                    '
	DC.B	'                    '

	DC.B	-1,-1,-1,-1
	DC.B	'BBBBBBBBBBBBBBBBBBBB'
	DC.B	'CCCCCCCCCCCCCCCCCCCC'
	DC.B	'DDDDDDDDDDDDDDDDDDDD'
	EVEN

CORRES
N	SET	0
	REPT	20
	DC.L	FONTES+N
N	SET	N+8
	ENDR

N	SET	0
	REPT	20
	DC.L	FONTES+N+(160*15)
N	SET	N+8
	ENDR

N	SET	0
	REPT	20
	DC.L	FONTES+N+(160*30)
N	SET	N+8
	ENDR
FONTES	INCBIN	FNT.COD
	DS.B	160
	EVEN
FAD_IN
	DC	$213,$324,$435,$546,$657,$767,$777
	DC	-1
FAD_OUT
	DC	$777,$767,$657,$546,$435,$324,$213
	DC	-1
FAD_IN2
	DC	$000,$111,$222,$333,$444,$555,$666,$777
	DC	-1
FAD_OUT2
	DC	$777,$666,$555,$444,$333,$222,$111,$000
	DC	-1
FAD_INITIAL
	DCB	16*5,0

	DC	$000,$001,$001,$000,$111,$000,$000,$000
	DC	$000,$000,$000,$000,$100,$100,$001,$000

	DC	$000,$002,$002,$000,$222,$100,$000,$000
	DC	$000,$000,$000,$100,$200,$210,$112,$000

	DC	$000,$003,$003,$000,$333,$200,$000,$100
	DC	$000,$000,$100,$200,$310,$321,$223,$000

	DC	$000,$004,$004,$000,$444,$300,$100,$200
	DC	$000,$100,$200,$310,$421,$432,$334,$000

	DC	$000,$015,$005,$001,$555,$400,$200,$300
	DC	$000,$200,$310,$421,$532,$543,$445,$000

	DC	$000,$026,$006,$102,$666,$500,$300,$400
	DC	$100,$310,$421,$532,$643,$654,$556,$101

	DC	$000,$026,$006,$102,$666,$500,$300,$400
	DC	$100,$310,$421,$532,$643,$654,$556,$101

	DC	$000,$037,$007,$213,$777,$600,$400,$500
	DC	$210,$421,$532,$643,$754,$765,$667,$212

	DC.L	-1
FAD_FINAL
	REPT	10
	DC	$000,$037,$007,$213,$777,$600,$400,$500
	DC	$210,$421,$532,$643,$754,$765,$667,$212
	ENDR
	DC	$000,$036,$006,$213,$776,$600,$400,$500
	DC	$210,$421,$532,$643,$754,$765,$666,$212
	DC	$000,$035,$005,$213,$775,$600,$400,$500
	DC	$210,$421,$532,$643,$754,$765,$665,$212
	DC	$000,$034,$004,$213,$774,$600,$400,$500
	DC	$210,$421,$532,$643,$754,$764,$664,$212
	DC	$000,$033,$003,$213,$773,$600,$400,$500
	DC	$210,$421,$532,$643,$753,$763,$663,$212
	DC	$000,$032,$002,$212,$772,$600,$400,$500
	DC	$210,$421,$532,$642,$752,$762,$662,$212
	DC	$000,$031,$001,$211,$771,$600,$400,$500
	DC	$210,$421,$531,$641,$751,$761,$661,$211
	DC	$000,$030,$000,$210,$770,$600,$400,$500
	DC	$210,$420,$530,$640,$750,$760,$660,$210
	DC	$000,$030,$000,$210,$760,$600,$400,$500
	DC	$210,$420,$530,$640,$750,$760,$660,$210
	DC	$000,$030,$000,$210,$750,$600,$400,$500
	DC	$210,$420,$530,$640,$750,$750,$650,$210
	DC	$000,$030,$000,$210,$740,$600,$400,$500
	DC	$210,$420,$530,$640,$740,$740,$640,$210
	DC	$000,$030,$000,$210,$730,$600,$400,$500
	DC	$210,$420,$530,$630,$730,$730,$630,$210
	DC	$000,$020,$000,$210,$720,$600,$400,$500
	DC	$210,$420,$520,$620,$720,$720,$620,$210
	DC	$000,$010,$000,$210,$710,$600,$400,$500
	DC	$210,$410,$510,$610,$710,$710,$610,$210
	DC	$000,$000,$000,$200,$700,$600,$400,$500
	DC	$200,$400,$500,$600,$700,$700,$600,$200
	DC	$000,$000,$000,$200,$600,$600,$400,$500
	DC	$200,$400,$500,$600,$600,$600,$600,$200
	DC	$000,$000,$000,$200,$500,$500,$400,$500
	DC	$200,$400,$500,$500,$500,$500,$500,$200
	DC	$000,$000,$000,$200,$400,$400,$400,$400
	DC	$200,$400,$400,$400,$400,$400,$400,$200
	DC	$000,$000,$000,$200,$300,$300,$300,$300
	DC	$200,$300,$300,$300,$300,$300,$300,$200
	DC	$000,$000,$000,$200,$200,$200,$200,$200
	DC	$200,$200,$200,$200,$200,$200,$200,$200
	DC	$000,$000,$000,$100,$100,$100,$100,$100
	DC	$100,$100,$100,$100,$100,$100,$100,$100
	DCB	16,0
	DC.L	-1

****FIN DATAS WRITER
****DEB DATAS CUBE
cos_tab	incbin 'COSIN.DAT'	;tables sinus cosinus
sin_tab	equ cos_tab+720
ZIK	INCBIN	MAX.MUS
points
 DC  -5,45,45,5,45,45
 DC  -15,45,45,15,45,45
 DC  -25,45,45,25,45,45
 DC  -35,45,45,35,45,45
 DC  -45,45,45,45,45,45

 DC  -5,-45,45,5,-45,45
 DC  -15,-45,45,15,-45,45
 DC  -25,-45,45,25,-45,45
 DC  -35,-45,45,35,-45,45
 DC  -45,-45,45,45,-45,45

 DC  45,-5,45,45,5,45
 DC  45,-15,45,45,15,45
 DC  45,-25,45,45,25,45
 DC  45,-35,45,45,35,45

 DC  -45,-5,45,-45,5,45
 DC  -45,-15,45,-45,15,45
 DC  -45,-25,45,-45,25,45
 DC  -45,-35,45,-45,35,45


 DC  -5,45,-45,5,45,-45
 DC  -15,45,-45,15,45,-45
 DC  -25,45,-45,25,45,-45
 DC  -35,45,-45,35,45,-45
 DC  -45,45,-45,45,45,-45

 DC  -5,-45,-45,5,-45,-45
 DC  -15,-45,-45,15,-45,-45
 DC  -25,-45,-45,25,-45,-45
 DC  -35,-45,-45,35,-45,-45
 DC  -45,-45,-45,45,-45,-45

 DC  45,-5,-45,45,5,-45
 DC  45,-15,-45,45,15,-45
 DC  45,-25,-45,45,25,-45
 DC  45,-35,-45,45,35,-45

 DC  -45,-5,-45,-45,5,-45
 DC  -45,-15,-45,-45,15,-45
 DC  -45,-25,-45,-45,25,-45
 DC  -45,-35,-45,-45,35,-45




 DC  -45,-45,-5,-45,-45,5
 DC  -45,-45,-15,-45,-45,15
 DC  -45,-45,-25,-45,-45,25
 DC  -45,-45,-35,-45,-45,35

 DC  -45,45,-5,-45,45,5
 DC  -45,45,-15,-45,45,15
 DC  -45,45,-25,-45,45,25
 DC  -45,45,-35,-45,45,35

 DC  45,-45,-5,45,-45,5
 DC  45,-45,-15,45,-45,15
 DC  45,-45,-25,45,-45,25
 DC  45,-45,-35,45,-45,35

 DC  45,45,-5,45,45,5
 DC  45,45,-15,45,45,15
 DC  45,45,-25,45,45,25
 DC  45,45,-35,45,45,35
	
depla	dc	0,0,0
***FIN DATAS CUBE
NB_PART	DC.L	8
TEXT1	
 DC.B "                                    "
 DC.B "                                    "
 DC.B "                                                  "
 DC.B "                                                  "
 DC.B "YO LAMERS!  THIS WAS CODED BY JENNY OF HOLOCAUST! "
 DC.B "                                    "
 DC.B "                                    "
 DC.B "                                    "
 DC.B "                                    "
	DC.B	"               ",$FF

FONT8_8	
	REPT	33
	DC.B	0,0,0,0,0,0,0,0
	ENDR
	DC.B	124,230,230,254,230,230,230,0	;A
	DC.B	252,230,230,252,230,230,252,0	;B
	DC.B	124,230,224,224,224,230,124,0	;C
	DC.B	252,230,230,230,230,230,252,0	;D
	DC.B	254,224,224,248,224,224,254,0	;E
	DC.B	254,224,224,248,224,224,224,0	;F
	DC.B	124,230,224,224,238,230,124,0	;G
	DC.B	230,230,230,254,230,230,230,0	;H
	DC.B	124,56,56,56,56,56,124,0	;I
	DC.B	14,14,14,14,14,206,124,0	;J
	DC.B	230,238,252,248,252,238,230,0	;K
	DC.B	224,224,224,224,224,224,254,0	;L
	DC.B	198,238,246,230,230,230,230,0	;M
	DC.B	252,230,230,230,230,230,230,0	;N
	DC.B	124,230,230,230,230,230,124,0	;O
	DC.B	252,230,230,252,224,224,224,0	;P
	DC.B	121,230,230,230,238,252,118,0	;Q
	DC.B	252,230,230,252,236,230,230,0	;R
	DC.B	124,230,224,124,14,206,124,0	;S
	DC.B	254,56,56,56,56,56,56,0	;T
	DC.B	230,230,230,230,230,230,124,0	;U
	DC.B	230,230,230,230,230,124,56,0	;V
	DC.B	230,230,230,230,254,238,198,0	;W
	DC.B	198,238,124,56,124,238,198,0	;X
	DC.B	230,230,230,124,56,56,56,0	;Y
	DC.B	254,206,28,56,112,230,254,0	;Z

 	SECTION BSS
**BSS CUBE

xe	ds.w 1
ye	ds.w 1
ze	ds.w 1
xdep	ds.w 1
ydep	ds.w 1
zdep	ds.w 1
xp	ds.w 1
yp	ds.w 1
new_xe	ds.w 1
new_ye	ds.w 1
new_ze	ds.w 1
x_angle	ds.w 1
y_angle	ds.w 1
z_angle	ds.w 1	
z_pt_fuite	ds.w 1
var_3d	ds.w 9
POINTEUR	DS.L	1

DATA_X
	DS.B	NB_POS*NB_PTS
DATA_Y
	DS	NB_POS*NB_PTS
CNT	DS.L	1
CNT2	DS.L	1
ZA	DS	1

**FIN BSS CUBE
POINT	DS.L	1
BUF_LET	DS.B	(1*8)*2
**
F_PILE	DS.L 	128
D_PILE	DS.L 	1
;VSYNC	DS.L	1
POIT	DS.L	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFERSCR	DS.B	64000*3
