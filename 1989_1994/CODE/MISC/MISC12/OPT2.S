NB_SPR	EQU	6

	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP

	CLR.W	-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	MOVEM.L	SPRITE_PIC+2,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W


	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1
	BSR	PREP_ECR

	MOVE.W	#4,X	;(NB DE COLONNES DE 16 PIXELS)+1
	MOVE.W	#55,Y
	MOVE.W	#8,NB_DECAL	;8 OU 16
	MOVE.W	#1,NB_DEC	;1 POUR 8 DECALAGES, 0 POUR 16
	BSR	MAK_OPTIMIZ

	MOVE.W	#1,VBL_FLAG

	ANDI.B	#%11111000,$484.W

	MOVE.L	$4D2.W,ADR_ZIK

	MOVE.W 	#$FA00,A0
	MOVE.B	7(A0),IERA
	MOVE.B	9(A0),IERB
	MOVE.B	$F(A0),ISRA
	MOVE.B	$11(A0),ISRB
	MOVE.B	$13(A0),IMRA
	MOVE.B	$15(A0),IMRB
	MOVE.B	$17(A0),VR
	MOVE.B	$19(A0),TACR
	MOVE.B	$1B(A0),TBCR
	MOVE.B	$1D(A0),TCDCR
	MOVE.B	$1F(A0),TADR
	MOVE.B	$21(A0),TBDR
	
	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	BCLR	#3,$FFFFFA17.W
	MOVE.W	#$2300,SR

	JMP	IT_VBL

FIN	MOVE.W	#$2700,SR
	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W
	
	MOVE.W	#$FA00,A0
	MOVE.B	IERA,7(A0)
	MOVE.B	IERB,9(A0)
	MOVE.B	ISRA,$F(A0)
	MOVE.B	ISRB,$11(A0)
	MOVE.B	IMRA,$13(A0)
	MOVE.B	IMRB,$15(A0)
	MOVE.B	VR,$17(A0)
	MOVE.B	TACR,$19(A0)
	MOVE.B	TBCR,$1B(A0)
	MOVE.B	TCDCR,$1D(A0)
	MOVE.B	TADR,$1F(A0)
	MOVE.B	TBDR,$21(A0)
	MOVE.W	#$2300,SR

	MOVE.B	SAUVEC,$FFFF8201.W
	MOVE.B	SAUVEC+1,$FFFF8203.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	TST.L	ADR_ZIK
	BEQ.S	SET_484
	ORI.B	#%00000110,$484.W
	BRA.S	SET_484+6
SET_484	ORI.B	#%00000111,$484.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S    *******
VBLR	CLR.W	VBL_FLAG
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
	
TB0	RTE

IT_VBL	TST.W	VBL_FLAG
	BNE.S	IT_VBL
	TST.L	ADR_ZIK
	BEQ.S	SUIT_VBL
	MOVE.L	ADR_ZIK,A0
	JSR	(A0)
SUIT_VBL

	MOVE.W	#1,VBL_FLAG
	BRA.S	IT_VBL

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
MAK_OPTIMIZ	MOVE.W	Y,D0
	BTST	#0,D0
	BNE.S	IMPAIR
	SUBQ.W	#1,Y
IMPAIR	MOVE.W	#160,D0
	MOVE.W	X,D1
	LSL	#3,D1
	SUB.W	D1,D0
	MOVE.W	D0,SAUT_FIN
	MOVE.W	X,D0
	MOVE.W	Y,D1
	ADDQ.W	#1,D1
	MULU.W	D0,D1
	MOVE.W	D1,TOTAL
	MOVE.W	Y,D1
	ADDQ.W	#1,D1
	MOVE.W	#2,D2
	DIVU.W	D2,D1
	MULU.W	D0,D1
	MOVE.W	D1,TOTAL2
	MOVE.W	Y,D1
	ADDQ.W	#1,D1
	MOVE.W	#4,D2
	DIVU.W	D2,D1
	MULU.W	D0,D1
	MOVE.W	D1,TOTAL4
	SUBQ.W	#1,X

	MOVE.L	#SPRIT_TAB,PNT_TAB
	MOVE.L	#BUF_FINAL,ADR

	MOVE.W	#NB_SPR-1,D7

RECOM_NB_SPR	MOVE.W	D7,ANNEX2

	MOVE.L	PNT_TAB,A0
	MOVE.L	(A0),A0		;ADRESSE DU SPRITE
	ADDQ.L	#4,PNT_TAB
	BSR	PREAFFICH
	BSR	ANALYZM
	BSR	ANALYZS
	MOVE.W	NB_DECAL,D7
RECOMY	MOVE.W	D7,ANNEX
	BSR	DECAL_SPR
	BSR	ANALYZM
	BSR	ANALYZS
	MOVE.W	ANNEX,D7
	DBRA	D7,RECOMY

	MOVE.W	ANNEX2,D7
	DBRA	D7,RECOM_NB_SPR
	RTS

DECAL_SPR	MOVEQ	#0,D0
	ROXR	D0
	MOVE.W	NB_DEC,D4
RECOM_DEC	LEA	BUF_SPR,A0
	MOVE.W	Y,D5
DEC3	MOVE.W	#3,D6
DEC2	MOVE.W	X,D7
DEC1	ROXR	(A0)
	ADDQ.W	#8,A0
	DBRA	D7,DEC1
	MOVEQ	#0,D0
	MOVE.W	X,D0
	ADDQ.W	#1,D0
	LSL	#3,D0
	SUBQ.W	#2,D0
	SUBA.W	D0,A0
	DBRA	D6,DEC2
	SUBQ.W	#6,D0
	ADDA.W	D0,A0
	DBRA	D5,DEC3
	DBRA	D4,RECOM_DEC
	LEA	BUF_SPR,A0
	LEA	BUF_MSK,A1
	MOVE.L	SCREEN1,A2
	LEA	80(A2),A3
	MOVE.W	Y,D6
MSK2	MOVE.W	X,D7
MSK1	MOVEM.W	(A0)+,D0-D3
	MOVEM.W	D0-D3,(A3)
	OR.W	D1,D0
	OR.W	D2,D0
	OR.W	D3,D0
	NOT.W	D0
	MOVE.W	D0,(A1)+
	MOVE.W	D0,(A2)
	ADDQ.W	#8,A2
	ADDQ.W	#8,A3
	DBRA	D7,MSK1
	ADDA.W	SAUT_FIN,A2
	ADDA.W	SAUT_FIN,A3
	DBRA	D6,MSK2
	RTS

ANALYZS	LEA	BUF_SPR,A5
	LEA	BUF_ITS,A1
	MOVE.L	A1,A4
	MOVE.W	TOTAL,D7
	SUBQ.W	#1,D7
	BSR	RECOM2
	LEA	BUF_SELECT,A6
	MOVE.L	A1,A3
	BSR	HIGHERS
	LEA	BUF_SPR,A5
	LEA	BUF2SELECT,A6
	REPT	2
	LEA	BUF_ITS,A1
	MOVE.L	A1,A4
	MOVE.W	TOTAL2,D7
	SUBQ.W	#1,D7
	BSR	RECOM2
	MOVE.L	A1,A3
	BSR	HIGHERS
	ENDR	
	LEA	BUF_SPR,A5
	LEA	BUF3SELECT,A6
	REPT	4
	LEA	BUF_ITS,A1
	MOVE.L	A1,A4
	MOVE.W	TOTAL4,D7
	SUBQ.W	#1,D7
	BSR	RECOM2
	MOVE.L	A1,A3
	BSR	HIGHERS
	ENDR	
	BSR	ORDONNE_SPR
	RTS

RECOM2	TST.L	(A5)
	BEQ.S	TEST3
	TST.W	2(A5)
	BNE.S	_2_PRESENT
	MOVEQ	#0,D0
	MOVE.W	(A5),D0
	LEA	TEST3,A3
	MOVE.L	A4,A2
	BRA.S	SEARCH2
_2_PRESENT	MOVE.L	(A5),D0
	LEA	TEST3,A3
	MOVE.L	A4,A2
	BRA.S	SEARCH
TEST3	ADDQ.W	#4,A5
	TST.W	(A5)
	BEQ.S	CONTINUE
	MOVEQ	#0,D0
	MOVE.W	(A5),D0
	LEA	CONTINUE,A3
	MOVE.L	A4,A2
	BRA.S	SEARCH2
CONTINUE	ADDQ.W	#4,A5
	DBRA	D7,RECOM2
	RTS
	
SEARCH	CMPA.L	A2,A1
	BEQ.S	FIN_1
	CMP.L	(A2),D0
	BEQ.S	FIN_2
	ADDQ.W	#8,A2
	BRA.S	SEARCH
FIN_1	MOVE.L	D0,(A1)+
	MOVE.L	#1,(A1)+
	JMP	(A3)
FIN_2	ADDQ.L	#1,4(A2)
	JMP	(A3)

SEARCH2	CMPA.L	A2,A1
	BEQ.S	FIN_12
	CMP.W	2(A2),D0
	BEQ.S	FIN_22
	ADDQ.W	#8,A2
	BRA.S	SEARCH2
FIN_12	MOVE.L	D0,(A1)+
	MOVE.L	#1,(A1)+
	JMP	(A3)
FIN_22	ADDQ.L	#1,4(A2)
	JMP	(A3)

ORDONNE_SPR	MOVE.L	ADR,A6
	LEA	BUF_SELECT,A0
	LEA	BUF2SELECT,A1
	LEA	BUF3SELECT,A2
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#7,D7
ADDIT_SPR1	ADD.L	4(A0),D0
	TST.W	(A0)
	BEQ.S	NO_L1
	ADD.L	4(A0),D0
NO_L1	ADDQ.W	#8,A0
	DBRA	D7,ADDIT_SPR1
	MOVEQ	#15,D7
ADDIT_SPR2	ADD.L	4(A1),D1
	TST.W	(A1)
	BEQ.S	NO_L2
	ADD.L	4(A1),D1
NO_L2	ADDQ.W	#8,A1
	DBRA	D7,ADDIT_SPR2
	MOVEQ	#31,D7
ADDIT_SPR3	ADD.L	4(A2),D2
	TST.W	(A2)
	BEQ.S	NO_L3
	ADD.L	4(A2),D2
NO_L3	ADDQ.W	#8,A2
	DBRA	D7,ADDIT_SPR3
	SUBI.W	#19,D0
	SUBI.W	#38,D1
	SUBI.W	#76,D2
	CMP.W	D0,D1
	BGT.S	_2_PLUS_GRAND
	CMP.W	D0,D2
	BGT.S	C_EST_LE_3
	BRA.S	C_EST_LE_1
_2_PLUS_GRAND	CMP.W	D1,D2
	BGT.S	C_EST_LE_3
	BRA.S	C_EST_LE_2
C_EST_LE_1	MOVE.W	#0,(A6)+
	MOVEQ	#7,D7
	LEA	-64(A0),A3
	BRA.S	RECOPY_SPR
C_EST_LE_2	MOVE.W	#1,(A6)+
	MOVEQ	#15,D7
	LEA	-128(A1),A3
	BRA.S	RECOPY_SPR
C_EST_LE_3	MOVE.W	#3,(A6)+
	MOVEQ	#31,D7
	LEA	-256(A2),A3
RECOPY_SPR	MOVE.L	(A3),(A6)+
	ADDQ.W	#8,A3
	DBRA	D7,RECOPY_SPR
	MOVE.L	A6,ADR
	RTS

ANALYZM	LEA	BUF_MSK,A5
	LEA	BUF_ITS,A1
	MOVE.L	A1,A4
	MOVE.W	TOTAL,D7
	SUBQ.W	#1,D7
	BSR	RECOM1
	LEA	BUF_SELECTM,A6
	MOVE.L	A1,A3
	BSR	HIGHERS
	LEA	BUF2SELECTM,A6
	LEA	BUF_MSK,A5
	REPT	2
	LEA	BUF_ITS,A1
	MOVE.W	TOTAL2,D7
	SUBQ.W	#1,D7
	BSR	RECOM1
	MOVE.L	A1,A3
	BSR	HIGHERS
	ENDR
	LEA	BUF3SELECTM,A6
	LEA	BUF_MSK,A5
	REPT	4
	LEA	BUF_ITS,A1
	MOVE.W	TOTAL4,D7
	SUBQ.W	#1,D7
	BSR	RECOM1
	MOVE.L	A1,A3
	BSR	HIGHERS
	ENDR
	BSR	ORDONNE
	RTS

ORDONNE	MOVE.L	ADR,A6
	LEA	BUF_SELECTM,A0
	LEA	BUF2SELECTM,A1
	LEA	BUF3SELECTM,A2
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#7,D7
ADDIT1	ADD.L	4(A0),D0
	ADDQ.W	#8,A0
	DBRA	D7,ADDIT1
	MOVEQ	#15,D7
ADDIT2	ADD.L	4(A1),D1
	ADDQ.W	#8,A1
	DBRA	D7,ADDIT2
	MOVEQ	#31,D7
ADDIT3	ADD.L	4(A2),D2
	ADDQ.W	#8,A2
	DBRA	D7,ADDIT3
	MULU.W	#3,D0
	MULU.W	#3,D1
	MULU.W	#3,D2
	SUBI.W	#19,D0
	SUBI.W	#38,D1
	SUBI.W	#76,D2
	CMP.W	D0,D1
	BGT.S	_2_PLUS_1
	CMP.W	D0,D2
	BGT.S	C_EST_3
	BRA.S	C_EST_1
_2_PLUS_1	CMP.W	D1,D2
	BGT.S	C_EST_3
	BRA.S	C_EST_2
C_EST_1	MOVE.W	#0,(A6)+
	MOVEQ	#7,D7
	LEA	-64(A0),A3
	BRA.S	MET_PLACE
C_EST_2	MOVE.W	#1,(A6)+
	MOVEQ	#15,D7
	LEA	-128(A1),A3
	BRA.S	MET_PLACE
C_EST_3	MOVE.W	#3,(A6)+
	MOVEQ	#31,D7
	LEA	-256(A2),A3
MET_PLACE	MOVE.L	(A3),(A6)+
	ADDQ.W	#8,A3
	DBRA	D7,MET_PLACE
	MOVE.L	A6,ADR
	RTS

HIGHERS	MOVEQ	#7,D5
SCAN2	MOVE.L	A4,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D1
OKK	CMPA.L	A3,A0
	BEQ.S	OK
	CMP.L	4(A0),D1
	BGE.S	NOTHING
	MOVE.L	(A0),D0
	MOVE.L	4(A0),D1
NOTHING	ADDQ.W	#8,A0
	BRA.S	OKK
OK	MOVE.L	A4,A0
PAS_BON	CMP.L	(A0),D0
	BEQ.S	SORTIE
	ADDQ.W	#8,A0
	BRA.S	PAS_BON
SORTIE	CLR.L	(A0)+
	CLR.L	(A0)+
	MOVE.L	D0,(A6)+
	MOVE.L	D1,(A6)+
	DBRA	D5,SCAN2
	RTS

RECOM1	TST.W	(A5)
	BEQ.S	CONTINUEM
	CMPI.W	#-1,(A5)
	BEQ.S	CONTINUEM
	MOVEQ	#0,D0
	MOVE.W	(A5),D0
	SWAP	D0
	MOVE.W	(A5),D0
	MOVE.L	A4,A2
	LEA	CONTINUEM,A3
	BRA.S	SEARCHM
CONTINUEM	ADDQ.W	#2,A5
	DBRA	D7,RECOM1
	RTS

SEARCHM	CMPA.L	A2,A1
	BEQ.S	FINM1
	CMP.L	(A2),D0
	BEQ.S	FINM2
	ADDQ.W	#8,A2
	BRA.S	SEARCHM
FINM1	MOVE.L	D0,(A1)+
	MOVE.L	#1,(A1)+
	JMP	(A3)
FINM2	ADDQ.L	#1,4(A2)
	JMP	(A3)

PREAFFICH	MOVE.L	SCREEN1,A1
	LEA	80(A1),A2
	LEA	BUF_SPR,A3
	LEA	BUF_MSK,A4
	MOVE.W	Y,D6
PREAFFY	MOVE.W	X,D7
	SUBQ.W	#1,D7
PREAFFX	MOVE.L	(A0),(A3)+
	MOVE.L	4(A0),(A3)+
	MOVEM.W	(A0),D0-D3
	MOVEM.W	D0-D3,(A2)
	OR.W	D1,D0
	OR.W	D2,D0
	OR.W	D3,D0
	NOT.W	D0
	MOVE.W	D0,(A4)+
	MOVE.W	D0,(A1)
	ADDQ.W	#8,A0
	ADDQ.W	#8,A1
	ADDQ.W	#8,A2
	DBRA	D7,PREAFFX
	CLR.L	(A3)+
	CLR.L	(A3)+
	CLR.L	(A2)
	CLR.L	4(A2)
	MOVE.W	#-1,(A4)+
	MOVE.W	#-1,(A1)
	MOVE.W	SAUT_FIN,D0
	ADDQ.W	#8,D0
	ADDA.W	D0,A0
	ADDA.W	D0,A1
	ADDA.W	D0,A2
	DBRA	D6,PREAFFY
	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
	SECTION	DATA
;SPRITE_PIC	INCBIN	TLB_SPR.PI1
SPRITE_PIC	INCBIN	BOUL3P.PI1

SPRIT_TAB	DC.L	SPRITE_PIC+34
	DC.L	SPRITE_PIC+34+24
	DC.L	SPRITE_PIC+34+48
	DC.L	SPRITE_PIC+34+72
	DC.L	SPRITE_PIC+34+96
	DC.L	SPRITE_PIC+34+120
	DC.L	SPRITE_PIC+34
	DC.L	SPRITE_PIC+34
	DC.L	SPRITE_PIC+34
	DC.L	SPRITE_PIC+34

	SECTION	BSS
ANC_PAL	DS.L	8
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
SAUV_SP	DS.L	1
SAUVEC	DS.W	1
IERA	DS.B	1
IERB	DS.B	1
ISRA	DS.B	1
ISRB	DS.B	1
IMRA	DS.B	1
IMRB	DS.B	1
VR	DS.B	1
TACR	DS.B	1
TBCR	DS.B	1
TCDCR	DS.B	1
TADR	DS.B	1
TBDR	DS.B	1
	DS.B	256
BUFFER	DS.L	16000
	DS.B	256
SCREEN1	DS.L	1
SCREEN2	DS.L	1
ADR_ZIK	DS.L	1
VBL_FLAG	DS.W	1
X	DS.W	1
Y	DS.W	1
SAUT_FIN	DS.W	1
TOTAL	DS.W	1
TOTAL2	DS.W	1
TOTAL4	DS.W	1
PNT_TAB	DS.L	1
ADR	DS.L	1
ANNEX	DS.W	1
ANNEX2	DS.W	1
NB_DECAL	DS.W	1
NB_DEC	DS.W	1
BUF_SPR	DS.L	5000
BUF_MSK	DS.L	5000
BUF_ITS	DS.L	10000
BUF_SELECTM	DS.L	16
BUF2SELECTM	DS.L	32
BUF3SELECTM	DS.L	64
BUF_SELECT	DS.L	16
BUF2SELECT	DS.L	32
BUF3SELECT	DS.L	64
BUF_FINAL	DS.L	10000