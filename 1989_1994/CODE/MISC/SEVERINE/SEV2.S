DELTA_X = 160	(PIX)
DELTA_Y = 127	(PIX)

PROG:
	CLR.L	-(SP)
	MOVE	#$20,-(SP)
	TRAP	#1
	ADDQ	#6,SP

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W
	
	MOVE.L	#$02100532,$FFFF8240.W
	CLR	TAILLE

DEBUT	MOVEQ	#0,D0	1er pixel
	LSL	#8,D0

	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVE	#DELTA_X,D7
	MOVE	D7,D6	D6=Taille sur l'‚cran
	;SUB	#20,D6
	SUB	TAILLE,D6
*
	MOVE	#DELTA_X,D3
	SUB	D6,D3
	LSR	#1,D3
*
	LSL	#8,D6

	DIVS	D7,D6

	LEA	MINI_BUF,A0
	MOVE	#160-1,D4
.DO_A_LINE
	ADD	D6,D0
	MOVE	D0,D1
	LSR	#8,D1
	ADD	D3,D1
	MOVE	D1,(A0)+
	DBF	D4,.DO_A_LINE

PART2	NOP

	LEA	GFX,A0
	LEA	TABLE_X,A2
	MOVE.L	$44E.W,A3
	MOVE	#127-1,D3
.Y	
	LEA	MINI_BUF,A1
	MOVE	#10-1,D7
.REDUC	MOVEQ	#15,D5
	MOVE	(A0)+,D6
.TST_A_BIT	
	BTST	D5,D6
	BEQ.S	.NUL
	MOVE	(A1),D0
	ADD	D0,D0
	ADD	D0,D0
	MOVE	(A2,D0.W),D1
	MOVE	2(A2,D0.W),D2
	OR	D1,(A3,D2.W)
	BRA.S	.SUIT
.NUL	MOVE	(A1),D0
	ADD	D0,D0
	ADD	D0,D0
	MOVE	(A2,D0.W),D1
	MOVE	2(A2,D0.W),D2
	NOT	D1
	AND	D1,(A3,D2.W)

.SUIT	ADDQ	#2,A1
	DBF	D5,.TST_A_BIT

	DBF	D7,.REDUC
	LEA	160(A3),A3
	DBF	D3,.Y
	
	MOVE.L	$44E.W,A0
MOD_BUF	LEA	BIG_BUF,A1
	MOVE	#127-1,D7
DO
N	SET	0
	REPT	10
	MOVE	N(A0),(A1)+
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D7,DO
	MOVE.L	A1,MOD_BUF+2
	
	ADDQ	#2,TAILLE
	CMPI	#100,TAILLE
	BEQ.S	FINI
	JSR	EFFACE
	JMP	DEBUT
FINI	;BRA.S	FINI

	CLR	TAILLE2
DEBUT2
	MOVEQ	#0,D0	1ere ligne
	LSL	#8,D0

	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVE	#DELTA_Y,D7
	MOVE	D7,D6	D6=Taille sur l'‚cran
	SUB	TAILLE2,D6
*
	MOVE	#DELTA_Y,D3
	SUB	D6,D3
	LSR	#1,D3
*
	LSL	#8,D6

	DIVS	D7,D6

MOD_BUFY	LEA	BUF_Y,A0
	MOVE	#127-1,D4
.DO_A_LINE
	ADD	D6,D0
	MOVE	D0,D1
	LSR	#8,D1
	ADD	D3,D1
	MULS	#160,D1
	MOVE	D1,(A0)+
	DBF	D4,.DO_A_LINE
	MOVE.L	A0,MOD_BUFY+2

	ADDQ	#2,TAILLE2
	CMPI	#100,TAILLE2
	BEQ.S	ON_Y_VA

	JMP	DEBUT2
ON_Y_VA
	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADD.L	#32000,D0
	MOVE.L	D0,SCREEN2
	
	MOVE.L	#VBL,$70.W
	
JUMP	BRA.S	JUMP
VBL	
	MOVE.L	#$02100532,$FFFF8240.W
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVE	#199,D7
.EFF
N	SET	0
	REPT	20
	MOVE	D0,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D7,.EFF

MOD_BUF2	LEA	BIG_BUF,A1
MOD_BUFY2	LEA	BUF_Y,A2
	MOVE	#127-1,D7
EDO
	MOVE.L	SCREEN2,A0
	ADD	(A2)+,A0
N	SET	0
	REPT	10
	MOVE	(A1)+,N(A0)
N	SET	N+8
	ENDR
	;LEA	160(A0),A0
	DBF	D7,EDO
	
	CMPI.B	#$3B,$FFFFFC02.W
	BEQ.S	.ROOL
	MOVE.L	A1,MOD_BUF2+2
	CMPI.L	#BIG_BUF+20*127*50,MOD_BUF2+2
	BNE.S	.ROOL
	MOVE.L	#BIG_BUF,MOD_BUF2+2
.ROOL
	CMPI.B	#$3C,$FFFFFC02.W
	BEQ.S	.ROOL2

	MOVE.L	A2,MOD_BUFY2+2
	CMPI.L	#BUF_Y+127*50*2,MOD_BUFY2+2
	BNE.S	.ROOL2
	MOVE.L	#BUF_Y,MOD_BUFY2+2
.ROOL2

	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.QUIT
	ST	$FFFF8240.W
.QUIT	RTE

FIN	MOVE.L	4.W,A0
	JMP	(A0)

EFFACE
	MOVE.L	$44E,A0
	MOVEQ	#0,D0
	MOVE	#7999,D7
.K	MOVE.L	D0,(A0)+
	DBF	D7,.K
	RTS

	DATA
GFX	INCBIN	SEV.GFX

TABLE_X
N	SET	0
	REPT	20
	DC	%1000000000000000,N
	DC	%100000000000000,N
	DC	%10000000000000,N
	DC	%1000000000000,N
	DC	%100000000000,N
	DC	%10000000000,N
	DC	%1000000000,N
	DC	%100000000,N
	DC	%10000000,N
	DC	%1000000,N
	DC	%100000,N
	DC	%10000,N
	DC	%1000,N
	DC	%100,N
	DC	%10,N
	DC	%1,N
N	SET	N+8
	ENDR

	BSS
*
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.B	64000
*
TAILLE	DS.W	1
TAILLE2	DS.W	1
MINI_BUF	DS.W	160
BUF_Y	DS.W	127*50
BIG_BUF	DS.B	20*127*50