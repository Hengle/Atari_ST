DELTA_X = 160	(PIX)
DELTA_Y = 127	(PIX)

PROG:
	CLR.L	-(SP)
	MOVE	#$20,-(SP)
	TRAP	#1
	ADDQ	#6,SP

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W
	
	MOVEQ	#0,D0
	MOVE.L	D0,$FFFF8240.W
	MOVE.L	D0,$FFFF8244.W
	MOVE.L	D0,$FFFF8248.W
	MOVE.L	D0,$FFFF824C.W
	MOVE.L	D0,$FFFF8250.W
	MOVE.L	D0,$FFFF8254.W
	MOVE.L	D0,$FFFF8258.W
	MOVE.L	D0,$FFFF825C.W

	MOVE.L	#BUF,D0
	CLR.B	D0
	ADD.L	#7936,D0
	MOVE.L	D0,SCREEN1
	MOVE.L	D0,SCREEN1B
	MOVE.L	#BUF2,D0
	CLR.B	D0
	ADD.L	#7936,D0
	MOVE.L	D0,SCREEN2
	JSR	PREP_ALL
	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
	MOVE	#7999,D7
.EFF	CLR.L	(A1)+
	CLR.L	(A2)+
	DBF	D7,.EFF

;	LEA	SEV+34,A0
;	MOVE.L	SCREEN1,A1
;	MOVE.L	SCREEN2,A2
;	LEA	40+160*40(A1),A1
;	LEA	40+160*40(A2),A2
;	MOVE	#(40*127)-1,D7
;.COPY	MOVE.L	(A0),(A1)+
;	MOVE.L	(A0)+,(A2)+
;	DBF	D7,.COPY

	MOVE.L	#VBL_INIT,$70.W
*	
	CLR	TAILLE

DEBUT	MOVEQ	#0,D0	1er pixel
	LSL	#8,D0

	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVE	#DELTA_X,D7
	MOVE	D7,D6	D6=Taille sur l'‚cran
	;SUB	#20,D6
	SUB	TAILLE,D6
*
	MOVE	#DELTA_X,D3
	SUB	D6,D3
	LSR	#1,D3
*
	LSL	#8,D6

	DIVS	D7,D6

	LEA	MINI_BUF,A0
	MOVE	#160-1,D4
.DO_A_LINE
	ADD	D6,D0
	MOVE	D0,D1
	LSR	#8,D1
	ADD	D3,D1
	MOVE	D1,(A0)+
	DBF	D4,.DO_A_LINE

PART2	NOP

	LEA	GFX,A0
	LEA	TABLE_X,A2
	MOVE.L	SCREEN1B,A3
	ADDQ	#6,A3
	MOVE	#127-1,D3
.Y	
	LEA	MINI_BUF,A1
	MOVE	#10-1,D7
.REDUC	MOVEQ	#15,D5
	MOVE	(A0)+,D6
.TST_A_BIT	
	BTST	D5,D6
	BEQ.S	.NUL
	MOVE	(A1),D0
	ADD	D0,D0
	ADD	D0,D0
	MOVE	(A2,D0.W),D1
	MOVE	2(A2,D0.W),D2
	OR	D1,(A3,D2.W)
	BRA.S	.SUIT
.NUL	MOVE	(A1),D0
	ADD	D0,D0
	ADD	D0,D0
	MOVE	(A2,D0.W),D1
	MOVE	2(A2,D0.W),D2
	NOT	D1
	AND	D1,(A3,D2.W)

.SUIT	ADDQ	#2,A1
	DBF	D5,.TST_A_BIT

	DBF	D7,.REDUC
	LEA	160(A3),A3
	DBF	D3,.Y
	
	MOVE.L	SCREEN1B,A0
	ADDQ	#6,A0
MOD_BUF	LEA	BIG_BUF,A1
	MOVE	#127-1,D7
DO
N	SET	0
	REPT	10
	MOVE	N(A0),(A1)+
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D7,DO
	MOVE.L	A1,MOD_BUF+2
	
	ADDQ	#2,TAILLE
	CMPI	#100,TAILLE
	BEQ.S	FINI
	JSR	EFFACE
	JMP	DEBUT
FINI	;BRA.S	FINI

	CLR	TAILLE2
DEBUT2
	MOVEQ	#0,D0	1ere ligne
	LSL	#8,D0

	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVE	#DELTA_Y,D7
	MOVE	D7,D6	D6=Taille sur l'‚cran
	SUB	TAILLE2,D6
*
	MOVE	#DELTA_Y,D3
	SUB	D6,D3
	LSR	#1,D3
*
	LSL	#8,D6

	DIVS	D7,D6

MOD_BUFY	LEA	BUF_Y,A0
	MOVE	#127-1,D4
.DO_A_LINE
	ADD	D6,D0
	MOVE	D0,D1
	LSR	#8,D1
	ADD	D3,D1
	MULS	#160,D1
	MOVE	D1,(A0)+
	DBF	D4,.DO_A_LINE
	MOVE.L	A0,MOD_BUFY+2

	ADDQ	#2,TAILLE2
	CMPI	#100,TAILLE2
	BEQ.S	ON_Y_VA

	JMP	DEBUT2
ON_Y_VA
	MOVE	#1,FLAGSEV
BOUCLE	BRA.S	BOUCLE
	MOVE.L	#INTER_RTE,$70.W
	MOVE	#4,T_F
	MOVE	#4,T_F2

	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
	MOVE	#7999,D7
.KILLEM	CLR.L	(A1)+
	CLR.L	(A2)+
	DBF	D7,.KILLEM
	MOVE.L	#VBL,$70.W
	
JUMP	BRA.S	JUMP
INTER_RTE	RTE
FAD_E	DC	$0,$0
	DC	$100,$100
	DC	$210,$210
	DC	$210,$321
	DC	$210,$432
	DC	$210,$532
	DC	$1234
VBL	
	LEA	FAD_E,A0
MODFE	LEA	0(A0),A0
	CMPI	#$1234,(A0)
	BEQ.S	.NORME
	SUBQ	#1,T_F
	BNE.S	.PE
	MOVE	#4,T_F
	ADDQ	#4,MODFE+2
.PE	MOVEM.W	(A0),D0-D1
	MOVE	D0,$FFFF8240.W
	MOVE	D1,$FFFF8242.W
	BRA.S	.ZE
.NORME	MOVE	#$210,$FFFF8240.W  0000
	MOVE	#$532,$FFFF8242.W  0001
.ZE

	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVE	#199,D7
.EFF
N	SET	0
	REPT	20
	MOVE	D0,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D7,.EFF

MOD_BUF2	LEA	BIG_BUF,A1
MOD_BUFY2	LEA	BUF_Y,A2
	MOVE	#127-1,D7
EDO
	MOVE.L	SCREEN2,A0
	ADD	(A2)+,A0
N	SET	0
	REPT	10
	MOVE	(A1)+,N(A0)
N	SET	N+8
	ENDR
	DBF	D7,EDO
*
	LEA	SIN,A0
MODSIN	EQU	*+2
	LEA	0(A0),A0
	CMPI	#$1234,(A0)
	BNE.S	.E
	CLR	MODSIN
	LEA	SIN,A0
.E	MOVE	(A0),D0
	EXT.L	D0
	DIVS	#10,D0
	ADD	#25,D0
	TST	D0
	BGE.S	.OK1
	MOVEQ	#0,D0
.OK1	CMPI	#50,D0
	BLT.S	.OK2
	MOVE	#49,D0
.OK2	MULU	#635,D0
	MOVEQ	#0,D1
	LEA	BIG_BUF,A1
	ADD.L	D0,D1
	ADD.L	D0,D1
	ADD.L	D0,D1
	ADD.L	D0,D1
	ADD.L	D1,A1
	MOVE.L	A1,MOD_BUF2+2
**
	LEA	SIN2,A0
MODSIN2	EQU	*+2
	LEA	0(A0),A0
	CMPI	#$1234,(A0)
	BNE.S	.E2
	CLR	MODSIN2
	LEA	SIN2,A0
.E2	MOVE	(A0),D0
	EXT.L	D0
	DIVS	#10,D0
	ADD	#25,D0
	TST	D0
	BGE.S	.OK1B
	MOVEQ	#0,D0
.OK1B	CMPI	#50,D0
	BLT.S	.OK2B
	MOVE	#49,D0
.OK2B	MULU	#127*2,D0
	LEA	BUF_Y,A1
	ADD	D0,A1
	MOVE.L	A1,MOD_BUFY2+2
	
*
	ADDQ	#2,MODSIN
	ADDQ	#2,MODSIN2


	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.QUIT
	ST	$FFFF8240.W
.QUIT	RTE

FIN	MOVE.L	4.W,A0
	JMP	(A0)
FAD_INIT	DC	0
	DC	$111
	DC	$222
	DC	$333
	DC	$444
	DC	$555
	DC	$666
	DC	$777
	DC	$1234
FAD_FIN	DC	$777
	DC	$666
	DC	$555
	DC	$444
	DC	$333
	DC	$222
	DC	$111
	DC	$000
	DC	$1234

VBL_INIT	MOVEM.L	D0-A6,-(SP)
	LEA	FAD_INIT,A0
MODFI	LEA	0(A0),A0
	CMPI	#$1234,(A0)
	BEQ.S	.NORM
	SUBQ	#1,T_F
	BNE.S	.P
	MOVE	#4,T_F
	ADDQ	#2,MODFI+2
.P	
	MOVE	(A0),$FFFF8242.W
	BRA.S	.Z
.NORM	MOVE	#$777,$FFFF8242.W
.Z
	CMPI	#120,TIME
	BLE.S	SAUT
	LEA	FAD_FIN,A0
MODFIF	LEA	0(A0),A0
	CMPI	#$1234,(A0)
	BEQ.S	.NORM2
	SUBQ	#1,T_F2
	BNE.S	.P
	MOVE	#4,T_F2
	ADDQ	#2,MODFIF+2
.P	
	MOVE	(A0),$FFFF8242.W
	BRA.S	.Z2
.NORM2	CLR	$FFFF8242.W
	CLR	TIME
	MOVE	#4,T_F
	MOVE	#4,T_F2
	CLR	MODFI+2
	CLR	MODFIF+2
	MOVEM.L	(SP)+,D0-A6
	RTE
.Z2
SAUT
	MOVE.B	SCREEN2+1,$FFFF8201.W
	MOVE.B	SCREEN2+2,$FFFF8203.W
	
	TST	TIME
	BNE	.NO
	LEA	SCEN,A0
MOD_SCEN	EQU	*+2
	LEA	0(A0),A0
	CMPI	#$1234,(A0)
	BNE.S	.KH
	LEA	SEV+34,A0
	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
	LEA	40+160*40(A1),A1
	LEA	40+160*40(A2),A2
	MOVE	#(40*127)-1,D7
.COPY	MOVE.L	(A0),(A1)+
	MOVE.L	(A0)+,(A2)+
	DBF	D7,.COPY
	MOVE.L	#VBL_SEV,$70.W
	CLR	TIME
	MOVE	#4,T_F
	MOVE	#4,T_F2
	MOVEM.L	(SP)+,D0-A6
	RTE
.KH	ADDQ	#4,MOD_SCEN
	MOVE	(A0)+,MD1
	MOVE	(A0)+,MD2

	MOVE.L	SCREEN2,A0
	LEA	160*50(A0),A0
	LEA	NAMES,A1
MD1	EQU	*+2
	LEA	0(A1),A1
MD2	EQU	*+2
	MOVE	#40-1,D7
.PR
N	SET	0
	REPT	20
	MOVE	(A1)+,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D7,.PR
	MOVE	#7-1,D7
.KILL_R
N	SET	0
	REPT	20
	CLR	N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D7,.KILL_R
.NO
	ADDQ	#1,TIME
	MOVEM.L	(SP)+,D0-A6
	RTE
TIME	DC	0
SCEN	DC	0,51-1
	DC	40*53,51-1
	DC	40*105,51-1
	DC	40*157,43-1
	DC	$1234
EFFACE
	MOVE.L	SCREEN1B,A0
	ADDQ	#6,A0
	MOVEQ	#0,D0
	MOVE	#128-1,D7
.K
N	SET	0
	REPT	10
	MOVE	D0,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D7,.K
	RTS
*
FAD_SEV
	DC	$100,$100,$100
	DC	$210,$200,$200
	DC	$210,$310,$300
	DC	$210,$421,$400
	DC	$210,$532,$500
	DC	$210,$532,$600
	DC	$210,$532,$700
	DC	$1234
T_F	DC	4
T_F2	DC	4
FAD_SEV2
	DC	$210,$532,$700
	DC	$210,$532,$600
	DC	$210,$532,$500
	DC	$210,$421,$400
	DC	$210,$310,$300
	DC	$210,$200,$200
	DC	$100,$100,$100
	DC	0,0,0
	DC	$1234
FLAGSEV	DC	0
VBL_SEV	MOVEM.L	D0-A6,-(SP)
	TST	FLAGSEV
	BNE.S	DEUZ
	LEA	FAD_SEV,A0
MODF	LEA	0(A0),A0
	CMPI	#$1234,(A0)
	BEQ.S	.NORM
	SUBQ	#1,T_F
	BNE.S	.P
	MOVE	#4,T_F
	ADDQ	#6,MODF+2
.P	MOVEM.W	(A0),D0-D2
	MOVE	D0,$FFFF8240.W
	MOVE	D1,$FFFF8242.W
	MOVE	D2,$FFFF8244.W
	MOVE	D2,$FFFF8246.W
	BRA.S	.Z
.NORM	MOVE	#$210,$FFFF8240.W  0000
	MOVE	#$532,$FFFF8242.W  0001
	MOVE	#$700,$FFFF8244.W  0010
	MOVE	#$700,$FFFF8246.W  0011
.Z	MOVE	#$7,$FFFF8248.W  0100
	MOVE	#$7,$FFFF824A.W  0101
	MOVE	#$7,$FFFF824C.W  0110
	MOVE	#$7,$FFFF824E.W  0111
	MOVEM.L	$FFFF8240.W,D0-D3
	MOVEM.L	D0-D3,$FFFF8250.W
	BRA.S	ERT
DEUZ
	LEA	FAD_SEV2,A0
MODF2	LEA	0(A0),A0
	CMPI	#$1234,(A0)
	BEQ.S	.NORM2
	SUBQ	#1,T_F2
	BNE.S	.P2
	MOVE	#4,T_F2
	ADDQ	#6,MODF2+2
.P2	MOVEM.W	(A0),D0-D2
	MOVE	D0,$FFFF8240.W
	MOVE	D1,$FFFF8242.W
	MOVE	D2,$FFFF8244.W
	MOVE	D2,$FFFF8246.W
	BRA.S	.Z2
.NORM2	CLR	$FFFF8240.W
	CLR	$FFFF8242.W
	CLR	$FFFF8244.W
	CLR	$FFFF8246.W
	MOVE	#$4E71,BOUCLE
.Z2	MOVE	#$7,$FFFF8248.W  0100
	MOVE	#$7,$FFFF824A.W  0101
	MOVE	#$7,$FFFF824C.W  0110
	MOVE	#$7,$FFFF824E.W  0111
	MOVEM.L	$FFFF8240.W,D0-D3
	MOVEM.L	D0-D3,$FFFF8250.W

ERT	MOVE.L	SCREEN1,A0
	MOVEQ	#0,D0
	MOVE	#200-1,D7
.EFF
N	SET	0
	REPT	20
	MOVE	D0,N+2(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D7,.EFF

	JSR	GERE_COEURS
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.RTE
	ST	$FFFF8240.W
.RTE	MOVEM.L	(SP)+,D0-A6
	ADDQ	#1,TIME
	RTE

GERE_COEURS
	LEA	TABLO_X,A0
	LEA	TABLO_Y,A1
	LEA	ANGLES,A2
	LEA	CORES_X,A3
	LEA	SIN,A4
.GO	MOVE	(A0)+,D0
	CMPI	#$1234,D0
	BEQ.S	.FINI
	MOVE.L	SCREEN1,A6
	LEA	BUF_SPR1,A5
	MOVE	(A1),D1
	CMPI	#-50,D1
	BNE.S	.ROL
	MOVE	#201,D1
.ROL	SUBQ	#1,D1
	MOVE	D1,(A1)+
	MULS	#160,D1
	ADD	D1,A6
	MOVEQ	#0,D1
	MOVE	(A2),D1
	CMPI	#98,D1
	BNE.S	.ROOL
	MOVEQ	#-1,D1
.ROOL	ADDQ	#1,D1
	MOVE	D1,(A2)+
	ADD	D1,D1
	MOVE	(A4,D1.W),D1
	EXT.L	D1
	DIVS	#12,D1
	ADD	D1,D0
	
	ADD	D0,D0
	ADD	D0,D0
	ADD	2(A3,D0.W),A6
	MOVE	(A3,D0.W),D0
	MULU	#4*36*2,D0
	ADD	D0,A5
	MOVE	#36-1,D7
.RECOP	MOVE	(A5)+,2(A6)
	MOVE	(A5)+,2+8(A6)
	MOVE	(A5)+,2+16(A6)
	MOVE	(A5)+,2+24(A6)
	LEA	160(A6),A6
	DBF	D7,.RECOP
	BRA.S	.GO
.FINI	RTS
PREP_ALL
	MOVE.L	SCREEN1,A0
	MOVE	#36-1,D7
	LEA	SEV+34+127*160,A1
.PIPO
	MOVE	(A1),(A0)
	MOVE	8(A1),8(A0)
	MOVE	16(A1),16(A0)
	LEA	160(A0),A0
	LEA	160(A1),A1
	DBF	D7,.PIPO

	MOVEQ	#15,D4
MOD	LEA	BUF_SPR1,A0
	MOVE.L	SCREEN1,A1
	MOVE	#36-1,D7
.COP
	MOVE	(A1),(A0)+
	MOVE	8(A1),(A0)+
	MOVE	16(A1),(A0)+
	MOVE	24(A1),(A0)+
	LEA	160(A1),A1
	DBF	D7,.COP
	MOVE.L	A0,MOD+2

	MOVE.L	SCREEN1,A1
	MOVE	#36-1,D7
.SHIFT
N	SET	0
	REPT	8
	ROXR	N(A1)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	DBF	D7,.SHIFT

	;MOVE.L	D4,-(SP)
	;MOVE	#7,-(SP)
	;TRAP	#1
	;ADDQ	#2,SP
	;MOVE.L	(SP)+,D4
	DBF	D4,MOD
	RTS

	DATA
GFX	INCBIN	SEV.GFX

TABLE_X
N	SET	0
	REPT	20
	DC	%1000000000000000,N
	DC	%100000000000000,N
	DC	%10000000000000,N
	DC	%1000000000000,N
	DC	%100000000000,N
	DC	%10000000000,N
	DC	%1000000000,N
	DC	%100000000,N
	DC	%10000000,N
	DC	%1000000,N
	DC	%100000,N
	DC	%10000,N
	DC	%1000,N
	DC	%100,N
	DC	%10,N
	DC	%1,N
N	SET	N+8
	ENDR

SIN	INCBIN	SIN.DAT
	DC	$1234
SIN2	INCBIN	SIN2.DAT
	DC	$1234
*

SEV	INCBIN	SEV.PI1
	
CORES_X
N	SET	0
	REPT	20
	DC	0,N
	DC	1,N
	DC	2,N
	DC	3,N
	DC	4,N
	DC	5,N
	DC	6,N
	DC	7,N
	DC	8,N
	DC	9,N
	DC	10,N
	DC	11,N
	DC	12,N
	DC	13,N
	DC	14,N
	DC	15,N
N	SET	N+8
	ENDR

TABLO_X	DC	50,200,80,150,220
	DC	$1234

TABLO_Y	DC	50,200,-25,100,150

ANGLES	DC	0,20,40,60,80

NAMES	INCBIN	NAMES.DAT

	BSS
TAILLE	DS.W	1
TAILLE2	DS.W	1
MINI_BUF	DS.W	160
BUF_Y	DS.W	127*50
BIG_BUF	DS.B	20*127*50
**
	DS.B	256
BUF	DS.B	32000+16000
	DS.B	256
BUF2	DS.B	32000+16000
SCREEN1	DS.L	1
SCREEN1B	DS.L	1
SCREEN2	DS.L	1
BUF_SPR1	DS.W	4*36*16
