	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP
	
	CLR.W	-(SP)
	MOVE.L	#-1,-(SP)	
	MOVE.L	#-1,-(SP)	
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP
	
	MOVE.L	#FIN,$008
	MOVE.L	#FIN,$00C
	MOVE.L	#FIN,$010
	MOVE.L	#FIN,$014
	MOVE.L	#FIN,$018
	MOVE.L	#FIN,$01C
	MOVE.L	#FIN,$020
	MOVE.L	#FIN,$024
	
	ANDI.B	#%11111000,$484.W
	
	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	MOVE.W	#$000,$FFFF8240.W
	MOVE.W	#$000,$FFFF8246.W

	MOVE.B	$FFFFFA07.W,MFP
	MOVE.B	$FFFFFA09.W,MFP+1
	MOVE.B	$FFFFFA0F.W,MFP+2
	MOVE.B	$FFFFFA13.W,MFP+3
	MOVE.B	$FFFFFA1B.W,MFP+4
	MOVE.B	$FFFFFA21.W,MFP+5

	BSR	PREP_TAB1
	CLR.L	POS_TB
	MOVEQ	#1,D0
	JSR	MUZAXX
	
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	
LOOP	JMP	LOOP

FIN	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W
	
	MOVE.B	MFP,$FFFFFA07.W
	MOVE.B	MFP+1,$FFFFFA09.W
	MOVE.B	MFP+2,$FFFFFA0F.W
	MOVE.B	MFP+3,$FFFFFA13.W
	MOVE.B	MFP+4,$FFFFFA1B.W
	MOVE.B	MFP+5,$FFFFFA21.W

	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W		
	
	ORI.B	#%00000111,$484.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S   *******
VBLR	JSR	MUZAXX+8
	LEA	TABROUG1,A6
	ADDA.L	POS_TB,A6
	CMPI.W	#-1,1200(A6)
	BNE	SUITRAST
	CLR.L	POS_TB
	LEA	TABROUG1,A6
SUITRAST	CLR.B	$FFFFFA1B.W
	MOVE.L	#TB0,$120.W
	MOVE.B	#2,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	ADDI.L	#200,POS_TB
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
	
TB0	MOVE.W	(A6)+,$FFFF8240.W
	MOVE.B	#2,$FFFFFA21.W
	BCLR	#0,$FFFFFA0F.W
	RTE

PREP_TAB1	LEA	TABROUG1,A0
	LEA	MVT1,A2
SUITPRP1	CMPI.W	#-1,12(A2)
	BEQ	FINPREP1
	MOVEQ	#5,D5
	LEA	COUL,A1
SERP	MOVE.L	A0,A5
	MOVEQ	#0,D0
	MOVE.W	(A2),D0
	MOVE.L	D0,D1
	ADD.L	D0,D0
	ADDA.L	D0,A5
	MOVEQ	#12,D7
PCOUL	MOVE.W	(A1)+,(A5)+
	DBRA	D7,PCOUL
	ADDQ.L	#4,A2
	DBRA	D5,SERP
	LEA	200(A0),A0
	SUBA.L	#22,A2
	BRA	SUITPRP1	
FINPREP1	MOVE.W	#-1,(A0)+
	RTS

	SECTION	DATA
MUZAXX	INCBIN	STORM.MUS

COUL	DC.W	$011,$022,$033,$044,$055,$066,$077,$066,$055,$044,$033,$022,$011
	DC.W	$110,$220,$330,$440,$550,$660,$770,$660,$550,$440,$330,$220,$110
	DC.W	$111,$222,$333,$444,$555,$666,$777,$666,$555,$444,$333,$222,$111
	DC.W	1,2,3,4,5,6,7,6,5,4,3,2,1
	DC.W	$010,$020,$030,$040,$050,$060,$070,$060,$050,$040,$030,$020,$010
	DC.W	$100,$200,$300,$400,$500,$600,$700,$600,$500,$400,$300,$200,$100 
	
MVT1	DC.W	1,2,3,4,5,7,9,11,13,15,18,21,24,27,30,34,38,42,45,48,51,54,57,60,62,64,66,68,70,72,73,74,75,76,77,78,79,80
	DC.W	80,79,78,77,76,75,74,73,72,70,68,66,64,62,60,57,54,51,48,45,42,38,34,30,27,24,21,18,15,13,11,9,7,5,4,3,2,1
	DC.W	1,2,3,4,5,7,9,11,13,15,18,21
	DC.W	-1

	SECTION	BSS
ANC_PAL	DS.L	8
SAUV_SP	DS.L	1
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
POS_TB	DS.L	1
MFP	DS.B	8
TABROUG1	DS.W	100*100