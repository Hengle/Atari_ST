	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP
	
	CLR.W	-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP
	
	MOVE.L	#FIN,$008
	MOVE.L	#FIN,$00C
	MOVE.L	#FIN,$010
	MOVE.L	#FIN,$014
	MOVE.L	#FIN,$018
	MOVE.L	#FIN,$01C
	MOVE.L	#FIN,$020
	MOVE.L	#FIN,$024
	
	ANDI.B	#%11111000,$484.W
	
	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1
	
	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	MOVEM.L	FONT1+2,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	
	BSR	PREP_ECR
	MOVE.L	#FONT1+34+(160*132)+112,SOV_LET
	CLR.L	POS_LET
	CLR.B	SCRP1

	MOVE.B	$FFFFFA07.W,MFP
	MOVE.B	$FFFFFA09.W,MFP+1
	MOVE.B	$FFFFFA0F.W,MFP+2
	MOVE.B	$FFFFFA13.W,MFP+3
	MOVE.B	$FFFFFA1B.W,MFP+4
	MOVE.B	$FFFFFA21.W,MFP+5
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W

	MOVEQ	#1,D0
	JSR	MUZAXX
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	#VBLR,$70.W
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	#TB0,$120.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	
LOOP	JMP	LOOP

FIN	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W
	
	MOVE.B	MFP,$FFFFFA07.W
	MOVE.B	MFP+1,$FFFFFA09.W
	MOVE.B	MFP+2,$FFFFFA0F.W
	MOVE.B	MFP+3,$FFFFFA13.W
	MOVE.B	MFP+4,$FFFFFA1B.W
	MOVE.B	MFP+5,$FFFFFA21.W
	
	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.B	SAUVEC,$FFFF8201.W
	MOVE.B	SAUVEC+1,$FFFF8203.W
	
	ORI.B	#%00000111,$484.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S   *******
VBLR	MOVE.W	#$000,$FFFF8240.W
	CLR.B	$FFFFFA1B.W
	MOVE.B	#199,$FFFFFA21.W
	MOVE.L	#TB0,$120.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.W	#$700,$FFFF8240.W
	BRA	SCROL8X8
SUITV0	BSR	SWAPEC
	JSR	MUZAXX+8
	MOVE.W	#$000,$FFFF8240.W
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
	
SCROL8X8	MOVE.L	SCREEN2,A0
	ADDA.L	#200*160,A0
	BSR	DECAL
	CMPI.B	#1,SCRP1
	BLO	PART1
	BEQ	PART2
	CMPI.B	#3,SCRP1
	BLO	PART3
	BEQ	PART4

TB0	CLR.B	$FFFFFA1B.W
	MOVE.L	FONT1+2,$FFFF8240.W
	MOVE.L	FONT1+6,$FFFF8244.W
	MOVE.L	FONT1+10,$FFFF8248.W
	MOVE.L	FONT1+14,$FFFF824C.W
	MOVE.L	FONT1+18,$FFFF8250.W
	MOVE.L	FONT1+22,$FFFF8254.W
	MOVE.L	FONT1+26,$FFFF8258.W
	MOVE.L	FONT1+30,$FFFF825C.W
	REPT	55
	NOP
	ENDR
	CLR.B	$FFFF820A.W
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	MOVE.B	#2,$FFFF820A.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.B	#1,$FFFFFA21.W
	BCLR	#0,$FFFFFA0F.W
	RTE
	
DECAL	
N	SET	0
	REPT	33
	MOVEM.L	N+8(A0),D0-D7/A2-A6
	MOVEM.L	D0-D7/A2-A6,N(A0)
	MOVEM.L	N+60(A0),D0-D7/A2-A6
	MOVEM.L	D0-D7/A2-A6,N+52(A0)
	MOVEM.L	N+112(A0),D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+104(A0)
N	SET	N+160
	ENDR
	RTS

PART1	MOVE.L	SOV_LET,A0
	MOVE.L	SCREEN2,A1
	ADDA.L	#152+(200*160),A1
N	SET	0
	REPT	33
	MOVE.B	N+9(A0),N(A1)
	MOVE.B	N+11(A0),N+2(A1)
	MOVE.B	N+13(A0),N+4(A1)
	MOVE.B	N+15(A0),N+6(A1)
N	SET	N+160
	ENDR
	BSR	NEW_LET1
	MOVE.L	SOV_LET,A0
	MOVE.L	SCREEN2,A1
	ADDA.L	#153+(200*160),A1
N	SET	0
	REPT	33
	MOVE.B	N(A0),N(A1)
	MOVE.B	N+2(A0),N+2(A1)
	MOVE.B	N+4(A0),N+4(A1)
	MOVE.B	N+6(A0),N+6(A1)
N	SET	N+160
	ENDR
	ADDQ.B	#1,SCRP1
	BRA	SUITV0

PART2	MOVE.L	SOV_LET,A0
	MOVE.L	SCREEN2,A1
	ADDA.L	#152+(200*160),A1
N	SET	0
	REPT	33
	MOVE.L	N(A0),N(A1)
	MOVE.L	N+4(A0),N+4(A1)
N	SET	N+160
	ENDR
	ADDQ.B	#1,SCRP1
	BRA	SUITV0

PART3	MOVE.L	SOV_LET,A0
	MOVE.L	SCREEN2,A1
	ADDA.L	#152+(200*160),A1
N	SET	0
	REPT	33
	MOVE.B	N+1(A0),N(A1)
	MOVE.B	N+8(A0),N+1(A1)
	MOVE.B	N+3(A0),N+2(A1)
	MOVE.B	N+10(A0),N+3(A1)
	MOVE.B	N+5(A0),N+4(A1)
	MOVE.B	N+12(A0),N+5(A1)
	MOVE.B	N+7(A0),N+6(A1)
	MOVE.B	N+14(A0),N+7(A1)
N	SET	N+160
	ENDR
	ADDQ.B	#1,SCRP1
	BRA	SUITV0
	
PART4	MOVE.L	SOV_LET,A0
	MOVE.L	SCREEN2,A1
	ADDA.L	#152+(200*160),A1
N	SET	0
	REPT	33
	MOVE.L	N+8(A0),N(A1)
	MOVE.L	N+12(A0),N+4(A1)
N	SET	N+160
	ENDR
	CLR.B	SCRP1
	BRA	SUITV0
	
NEW_LET1	LEA	TEXT1,A0
	ADDA.L	POS_LET,A0
	CMPI.B	#$FF,(A0)
	BNE	SUITNL1
	CLR.L	POS_LET
	LEA	TEXT1,A0
SUITNL1	MOVEQ	#0,D0
	MOVE.B	(A0),D0
	SUBI.B	#32,D0
	LSL.W	#2,D0
	LEA	CORES1,A1
	ADDA.L	D0,A1
	MOVE.L	(A1),SOV_LET
	ADDQ.L	#1,POS_LET
	RTS
	
SWAPEC	MOVE.L	SCREEN2,D0
	MOVE.L	SCREEN1,SCREEN2
	MOVE.L	D0,SCREEN1
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
PREP_ECR	MOVE.L	#BUFFER,D0
	ANDI.B	#0,D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000+(1280*8),D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
	SECTION	DATA
FONT1	INCBIN	FNT_ZAP.PI1

MUZAXX	INCBIN	STORM.MUS

TEXT1	DC.B	"ABCDEFGHIJKLMNOPQRSTUVWXYZ   !'(),-./    0123456789   :?   WRAP   ",$FF
	EVEN

CORES1	INCLUDE	CORESZAP.S	
	
	SECTION	BSS
ANC_PAL	DS.L	8
SAUV_SP	DS.L	1
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
POS_LET	DS.L	1
SOV_LET	DS.L	1
MFP	DS.B	8
SCRP1	DS.B	1
	EVEN
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.L	24000
	DS.B	256
SAUVEC	DS.W	1	