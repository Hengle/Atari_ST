	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP
	
	CLR.W	-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP
	
	MOVE.L	#FIN,$008
	MOVE.L	#FIN,$00C
	MOVE.L	#FIN,$010
	MOVE.L	#FIN,$014
	MOVE.L	#FIN,$018
	MOVE.L	#FIN,$01C
	MOVE.L	#FIN,$020
	MOVE.L	#FIN,$024
	
	ANDI.B	#%11111000,$484.W
	
	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	MOVE.L	#$00000777,$FFFF8240.W
	
	BSR	PREP_BUF
	CLR.W	OFF_SET
	MOVE.L	#BOUG3,LOD_PROC	

	MOVEQ	#2,D0
	JSR	MUZAXX
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	#VBLR,$70.W
	
LOOP	JMP	LOOP

FIN	MOVE.L	ANC_VBL,$70.W

	ORI.B	#%00000111,$484.W
	
	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#2,SP	
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S   *******
VBLR	CMPI.B	#$F,$FFFFFC02.W
	BNE	SUITV0
	MOVE.W	#$700,COULT
SUITV0	MOVE.W	COULT,$FFFF8240.W
	MOVE.L	LOD_PROC,A0
	JSR	(A0)
	MOVE.W	#$000,$FFFF8240.W
	JSR	MUZAXX+8
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	CLR.W	COULT
	RTE
	
BOUG1	LEA	BUF_ECC,A0
	ADDA.W	OFF_SET,A0
	MOVE.L	$44E.W,A1
	ADDA.L	#1600,A1
	JSR	BUF_COD1
	CMPI.W	#0,OFF_SET
	BNE	SUITBG1
	MOVE.L	#BOUG3,LOD_PROC
	MOVE.W	#40,OFF_SET
SUITBG1	SUBI.W	#40,OFF_SET
	RTS

BOUG2	LEA	BUF_ECC2,A0
	ADDA.W	OFF_SET,A0
	MOVE.L	$44E.W,A1
	ADDA.L	#1600,A1
	JSR	BUF_COD1
	CMPI.W	#40*180,OFF_SET
	BNE	SUITBG2
	MOVE.L	#BOUG4,LOD_PROC
	CLR.W	OFF_SET
SUITBG2	ADDI.W	#40,OFF_SET
	RTS

BOUG3	LEA	PICT,A0
	ADDA.W	OFF_SET,A0
	MOVE.L	$44E.W,A1
	ADDA.L	#1600,A1
	JSR	BUF_COD2
	CMPI.W	#40,OFF_SET
	BNE	SUITBG3
	MOVE.L	#BOUG2,LOD_PROC
	MOVE.W	#-2,OFF_SET
SUITBG3	ADDI.W	#2,OFF_SET
	RTS

BOUG4	MOVE.L	#PICT+(80*180),A0
	ADDA.W	OFF_SET,A0
	MOVE.L	$44E.W,A1
	ADDA.L	#1600,A1
	JSR	BUF_COD2
	CMPI.W	#0,OFF_SET
	BNE	SUITBG4
	MOVE.L	#BOUG1,LOD_PROC
	MOVE.W	#(40*180)+2,OFF_SET
SUITBG4	SUBQ.W	#2,OFF_SET
	RTS

PREP_BUF	LEA	PICT,A0
	LEA	BUF_ECC,A1
	MOVEQ	#1,D5
PREPG	MOVE.W	#359,D7
PREPG1	MOVEQ	#19,D6
PREPB1	MOVE.W	(A0)+,(A1)+
	DBRA	D6,PREPB1	
	LEA	40(A0),A0
	DBRA	D7,PREPG1
	MOVE.L	#PICT+40,A0
	DBRA	D5,PREPG
 	BSR	PREP_COD1
	BSR	PREP_COD2
	RTS

PREP_COD1	LEA	BUF_COD1,A0
	MOVEQ	#0,D2
	MOVE.W	#3599,D7
PREPC1	MOVE.W	#$3358,(A0)+
	MOVE.W	D2,(A0)+
	ADDQ.W	#8,D2
	DBRA	D7,PREPC1
	MOVE.W	#$4E75,(A0)+
	RTS

PREP_COD2	LEA	BUF_COD2,A0
	MOVEQ	#0,D2
	MOVE.W	#179,D7
PREPC2G	MOVE.W	#19,D6
PREPC2	MOVE.W	#$3358,(A0)+
	MOVE.W	D2,(A0)+
	ADDQ.W	#8,D2
	DBRA	D6,PREPC2
	MOVE.L	#$41E80028,(A0)+
	DBRA	D7,PREPC2G
	MOVE.W	#$4E75,(A0)+
	RTS
	
	SECTION	DATA
PICT	INCBIN	ANC_PICT.H_C

MUZAXX	INCBIN	ASTAROD.MUS

	SECTION	BSS
ANC_PAL	DS.L	8
SAUV_SP	DS.L	1
ANC_VBL	DS.L	1
OFF_SET	DS.W	1
COULT	DS.W	1
LOD_PROC	DS.L	1
BUF_ECC	DS.W	(20*180)*2
BUF_ECC2	DS.W	(20*180)*2
BUF_COD1	DS.W	(40*180)+1
BUF_COD2	DS.W	(42*180)+1