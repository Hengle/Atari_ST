CAR_RES	EQU	500

	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP

	CLR.W	-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008
	MOVE.L	#FIN,$00C
	MOVE.L	#FIN,$010
	MOVE.L	#FIN,$014
	MOVE.L	#FIN,$018
	MOVE.L	#FIN,$01C
	MOVE.L	#FIN,$020

	ANDI.B	#%11111000,$484.W

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,SAUV_PAL
	MOVE.W	#$000,$FFFF8240.W
	MOVE.W	#$037,$FFFF8244.W
	
	BSR	PREP_BUF
	BSR	PREDEC

	MOVE.L	$70.W,ANC_VBL
	MOVE.L	#VBLR,$70.W

LOOP	JMP	LOOP

FIN	MOVE.L	ANC_VBL,$70.W

	MOVEM.L	SAUV_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	ORI.B	#%0000111,$484.W

	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP

	*******	V B L  &  P R O C E D U R E S   *******
VBLR	MOVE.W	#2000,D0
BCL	DBRA	D0,BCL
	MOVE.W	#$700,$FFFF8240.W
	BSR	BOUG
	MOVE.W	#$000,$FFFF8240.W
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE

AFF_LIGN	
N	SET	0
N2	SET	0
	REPT	8
	MOVE.W	N2(A0),N(A1)
	MOVE.W	2+N2(A0),8+N(A1)
	MOVE.W	4+N2(A0),16+N(A1)
	MOVE.W	6+N2(A0),24+N(A1)
	MOVE.W	8+N2(A0),32+N(A1)
	MOVE.W	10+N2(A0),40+N(A1)
	MOVE.W	12+N2(A0),48+N(A1)
	MOVE.W	14+N2(A0),56+N(A1)
	MOVE.W	16+N2(A0),64+N(A1)
	MOVE.W	18+N2(A0),72+N(A1)
N	SET	N+160
N2	SET	N2+CAR_RES
	ENDR
	RTS

BOUG	LEA	MVT,A0
	ADDA.L	AD,A0
	CMPI.W	#-1,(A0)
	BNE	SUITBG1
	CLR.L	AD
	LEA	MVT,A0
SUITBG1	MOVEQ	#0,D0	
	MOVEQ	#0,D1
	MOVE.W	(A0),D0
	MOVE.W	D0,D1
	ANDI.W	#%0000000000001111,D0
	LSL.B	#2,D0
	LEA	CORES1,A1
	ADDA.L	D0,A1
	MOVE.L	(A1),A0
	ANDI.W	#%1111111111110000,D1
	LSR.W	#3,D1	
	ADDA.L	D1,A0	
	MOVE.L	$44E.W,A1
	LEA	80(A1),A1
	ADDQ.L	#2,A1
	BSR	AFF_LIGN	
	ADDQ.L	#2,AD
	RTS

PREP_BUF	MOVE.W	#499,D1
	LEA	TEXT,A0
	LEA	BUFFERG,A1
RECOMPRP	LEA	FNT8_8,A2
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0	
	SUBI.B	#32,D0	
	LSL.W	#3,D0
	ADDA.L	D0,A2
	MOVE.B	(A2)+,(A1)
	MOVE.B	(A2)+,CAR_RES(A1)		
	MOVE.B	(A2)+,CAR_RES*2(A1)		
	MOVE.B	(A2)+,CAR_RES*3(A1)		
	MOVE.B	(A2)+,CAR_RES*4(A1)		
	MOVE.B	(A2)+,CAR_RES*5(A1)		
	MOVE.B	(A2)+,CAR_RES*6(A1)		
	MOVE.B	(A2)+,CAR_RES*7(A1)		
	ADDQ.L	#1,A1
	DBRA	D1,RECOMPRP
	CLR.L	AD
	RTS

PREDEC	LEA	BUFFERG,A0
	MOVE.W	#15,D1
RECOMDG	MOVE.W	#(250*8)-1,D0
	MOVE.L	A0,A2
	ADDA.L	#500*8,A2
RECOP1	MOVE.W	(A0)+,(A2)+
	DBRA	D0,RECOP1
	MOVE.W	#(250*8)-1,D0
DECAL	ROXR	(A0)+
	DBRA	D0,DECAL	
	SUBA.L	#500*8,A0
	DBRA	D1,RECOMDG
	RTS


	SECTION	DATA
FNT8_8	INCLUDE	FONT8_83.S

TEXT	REPT	42 
	DC.B	" "
	ENDR
	DC.B	"HERE IT IS !! I HAVE FINALLY ACHIEVED THIS WONDERFUL"
	DC.B	" CODES IN ABOUT THREE HOURS. THIS IS THE SCROLLINE "
	DC.B	"MASSACRE PART TWO DEMO. MY NAME IS H.O FROM HOLOCAUST"
	DC.B	",A GREAT NEW GROUP THAT EVERYBODY NOW KNOW!!   NO ???"
	DC.B	"  WELL WE AREN'T THE BEST 'COZ WE HAVEN'T THE FASTEST"
	DC.B	" CODES ON THE ST !.....FUCK LAMERS AND WRAP  IIIIIIIIIIIIIIIII"
	REPT	185
	DC.B	" "
	ENDR
	DC.B	-1

CORES1	DC.L	BUFFERG+(7500*8),BUFFERG+(7000*8)
	DC.L	BUFFERG+(6500*8),BUFFERG+(6000*8),BUFFERG+(5500*8),BUFFERG+(5000*8),BUFFERG+(4500*8),BUFFERG+(4000*8)
	DC.L	BUFFERG+(3500*8),BUFFERG+(3000*8),BUFFERG+(2500*8),BUFFERG+(2000*8),BUFFERG+(1500*8),BUFFERG+(1000*8)
	DC.L	BUFFERG+(500*8),BUFFERG

MVT	
N	SET	0
	REPT	1520
	DC.W	N
N	SET	N+2
	ENDR
;	DC.B	0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,14,13,12,11,10
	DC.W	-1

	SECTION	BSS
SAUV_PAL	DS.L	8
ANC_VBL	DS.L	1
SAUV_SP	DS.L	1
AD	DS.L	1
BUFFERG	DS.B	(CAR_RES*8)*16