	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP
	
	CLR.W	-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP
	
	MOVE.L	#FIN,$008
	MOVE.L	#FIN,$00C
	MOVE.L	#FIN,$010
	MOVE.L	#FIN,$014
	MOVE.L	#FIN,$018
	MOVE.L	#FIN,$01C
	MOVE.L	#FIN,$020
	MOVE.L	#FIN,$024
	
	ANDI.B	#%11111000,$484.W
	
	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	MOVE.L	#$00000526,$FFFF8240.W
	
	BSR	PRPBUFEQ1
	MOVEQ	#1,D0
	JSR	MUZAXX
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	#VBLR,$70.W
	
LOOP	JMP	LOOP

FIN	MOVE.L	ANC_VBL,$70.W

	ORI.B	#%00000111,$484.W
	
	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#2,SP	
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S   *******
VBLR	BSR	EQUA1
	JSR	MUZAXX+8
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE

EQUA1	LEA	BUFEQ1,A0
	MOVEQ	#0,D0
	MOVE.B	#8,$FFFF8800.W
	MOVE.B	$FFFF8800.W,D0
	LSL.L	#7,D0
	ADDA.L	D0,A0	
	MOVE.L	$44E.W,A1
	ADDA.L	#(160*40)+8,A1
	BSR	AFF_EQU
	LEA	BUFEQ1,A0
	MOVEQ	#0,D0
	MOVE.B	#9,$FFFF8800.W
	MOVE.B	$FFFF8800.W,D0
	LSL.L	#7,D0
	ADDA.L	D0,A0	
	MOVE.L	$44E.W,A1
	ADDA.L	#(160*40)+32,A1
	BSR	AFF_EQU
	LEA	BUFEQ1,A0
	MOVEQ	#0,D0
	MOVE.B	#$A,$FFFF8800.W
	MOVE.B	$FFFF8800.W,D0
	LSL.L	#7,D0
	ADDA.L	D0,A0	
	MOVE.L	$44E.W,A1
	ADDA.L	#(160*40)+56,A1
	BSR	AFF_EQU
	RTS
	
AFF_EQU
N	SET	0
	REPT	16
	MOVE.B	(A0)+,N(A1)
	MOVE.B	(A0)+,N+160(A1)
	MOVE.B	(A0)+,N+320(A1)
	MOVE.B	(A0)+,N+480(A1)
	MOVE.B	(A0)+,N+640(A1)
	MOVE.B	(A0)+,N+800(A1)
	MOVE.B	(A0)+,N+960(A1)
	MOVE.B	(A0)+,N+1120(A1)
N	SET	N+1440
	ENDR
	RTS
		
PRPBUFEQ1	LEA	BUFEQ1,A0
	LEA	GRAPH,A1
	MOVEQ	#15,D5
EFF_BF1	CLR.L	(A0)+
	CLR.L	(A0)+
	DBRA	D5,EFF_BF1
	MOVEQ	#14,D7
	MOVEQ	#0,D6
	MOVEQ	#14,D4
PRP_BFG	MOVE.W	D7,D5
EFF_BF	CLR.L	(A0)+
	CLR.L	(A0)+
	DBRA	D5,EFF_BF
	MOVE.W	D6,D5
AFF_BF	MOVE.L	(A1),(A0)+
	MOVE.L	4(A1),(A0)+
	DBRA	D5,AFF_BF
	SUBQ.L	#1,D7
	ADDQ.L	#1,D6
	DBRA	D4,PRP_BFG
	RTS			
	
	SECTION	DATA
MUZAXX	INCBIN	STORM.MUS

GRAPH	;DC.B	24,24,36,36,66,66,129,129	;CHAPEAU
	DC.B	24,36,66,129,231,36,36,60	;FLECHE VIDE
	;DC.B	24,36,90,189,255,60,60,60	;FLECHE PLEINE

	SECTION	BSS
ANC_PAL	DS.L	8
SAUV_SP	DS.L	1
ANC_VBL	DS.L	1
BUFEQ1	DS.L	32*16