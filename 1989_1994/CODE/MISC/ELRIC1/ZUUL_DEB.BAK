	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP
	
	CLR.W	-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP
	
	MOVE.L	#FIN,$008
	MOVE.L	#FIN,$00C
	MOVE.L	#FIN,$010
	MOVE.L	#FIN,$014
	MOVE.L	#FIN,$018
	MOVE.L	#FIN,$01C
	MOVE.L	#FIN,$020
	MOVE.L	#FIN,$024
	
	ANDI.B	#%11111000,$484.W
	
	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	MOVE.W	#$000,$FFFF8240.W
	MOVE.W	#$777,$FFFF8240.W
	
	BSR	PREP_BUF
	BSR	PREPCOD1
	BSR	PREP_ECR
	CLR.W	OFF_SET
	MOVE.L	#BOUG3,LOD_PROC
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	#VBLR,$70.W
	
LOOP	JMP	LOOP

FIN	MOVE.L	ANC_VBL,$70.W

	ORI.B	#%00000111,$484.W
	
	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#2,SP	
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S   *******
VBLR	MOVE.L	LOD_PROC,A0
	JSR	(A0)
	BSR	SWAPEC
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE

BOUG1	LEA	BUF_GRF1,A0
	ADDA.W	OFF_SET,A0
	MOVE.L	SCREEN2,A1
	JSR	BUFCOD1
	CMPI.W	#20*80,OFF_SET
	BNE	SUITBG1
	CLR.W	OFF_SET
	MOVE.L	#BOUG4,LOD_PROC
SUITBG1	ADDI.W	#20,OFF_SET
	RTS

BOUG2	LEA	BUF_GRF2,A0
	ADDA.W	OFF_SET,A0
	MOVE.L	SCREEN2,A1
	JSR	BUFCOD1
	CMPI.W	#0,OFF_SET
	BNE	SUITBG2
	MOVE.W	#20,OFF_SET
	MOVE.L	#BOUG3,LOD_PROC
SUITBG2	SUBI.W	#20,OFF_SET
	RTS

BOUG3	LEA	GRAPH,A0
	ADDA.W	OFF_SET,A0
	MOVE.L	SCREEN2,A1
	JSR	BUFCOD2
	CMPI.W	#20,OFF_SET
	BNE	SUITBG3
	MOVE.W	#-2,OFF_SET
	MOVE.L	#BOUG1,LOD_PROC
SUITBG3	ADDQ.W	#2,OFF_SET
	RTS

BOUG4	MOVE.L	#GRAPH+(40*80),A0
	ADDA.W	OFF_SET,A0
	MOVE.L	SCREEN2,A1
	JSR	BUFCOD2
	CMPI.W	#0,OFF_SET
	BNE	SUITBG4
	MOVE.W	#(80*40)+2,OFF_SET
	MOVE.L	#BOUG2,LOD_PROC
SUITBG4	SUBQ.W	#2,OFF_SET
	RTS
	
SWAPEC	MOVE.L	SCREEN2,D0
	MOVE.L	SCREEN1,SCREEN2
	MOVE.L	D0,SCREEN1
	MOVE.B	SCREEN2+1,$FFFF8201.W
	MOVE.B	SCREEN2+2,$FFFF8203.W
	RTS

PREP_BUF	LEA	GRAPH,A0
	LEA	BUF_GRF1,A1
	MOVEQ	#1,D7
RECOMTOUT	MOVE.W	#159,D1
AFFT1	MOVEQ	#9,D0
RCLIG1	MOVE.W	(A0)+,(A1)+
	DBRA	D0,RCLIG1
	LEA	20(A0),A0
	DBRA	D1,AFFT1
	MOVE.L	#GRAPH+20,A0
	LEA	BUF_GRF2,A1
	DBRA	D7,RECOMTOUT
	RTS

PREPCOD1	LEA	BUFCOD1,A0
	MOVEQ	#0,D2
	MOVEQ	#79,D1
RECOMCDG	MOVEQ	#9,D0
RECOMCD1	MOVE.W	#$3358,(A0)+
	MOVE.W	D2,(A0)+
	ADDQ.W	#8,D2
	DBRA	D0,RECOMCD1
	ADDI.W	#80,D2
	DBRA	D1,RECOMCDG
	MOVE.W	#$4E75,(A0)
	BSR	PREPCOD2
	RTS

PREPCOD2	LEA	BUFCOD2,A0
	MOVEQ	#0,D2
	MOVEQ	#79,D0
RECOMCDG2	MOVE.W	#9,D1
RECOMCD2	MOVE.W	#$3358,(A0)+
	MOVE.W	D2,(A0)+
	ADDQ.W	#8,D2
	DBRA	D1,RECOMCD2
	ADDI.W	#80,D2
	MOVE.L	#$41E80014,(A0)+
	DBRA	D0,RECOMCDG2
	MOVE.W	#$4E75,(A0)
	RTS
	
PREP_ECR	MOVE.L	#BUFFER,D0
	ANDI.B	#0,D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

	SECTION	DATA
GRAPH	INCBIN	ZUUL_1.H_C

	SECTION	BSS
ANC_PAL	DS.L	8
SAUV_SP	DS.L	1
ANC_VBL	DS.L	1
LOD_PROC	DS.L	1
OFF_SET	DS.W	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.L	16000
	DS.B	256
BUF_GRF1	DS.W	1600
BUF_GRF2	DS.W	1600
BUFCOD1	DS.W	1600+1
BUFCOD2	DS.W	(22*80)+1