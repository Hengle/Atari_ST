	PEA	0.W
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1
	BSR	PREP_ECR

	MOVE.L	#SUIT_FADE,PNT_FADES
	MOVE.L	#FADE3COLORS,PNT_COLORS

	MOVE.W	#1,VBL_FLAG

	ANDI.B	#%11111000,$484.W

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	MOVE.W	#$8240,A0
	MOVEQ	#7,D7
EFF_COLORS	CLR.L	(A0)+
	DBRA	D7,EFF_COLORS

	MOVE.W 	#$FA00,A0
	MOVE.B	7(A0),IERA
	MOVE.B	9(A0),IERB
	MOVE.B	$F(A0),ISRA
	MOVE.B	$11(A0),ISRB
	MOVE.B	$13(A0),IMRA
	MOVE.B	$15(A0),IMRB
	MOVE.B	$17(A0),VR
	MOVE.B	$19(A0),TACR
	MOVE.B	$1B(A0),TBCR
	MOVE.B	$1D(A0),TCDCR
	MOVE.B	$1F(A0),TADR
	MOVE.B	$21(A0),TBDR
	
	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	BCLR	#3,$FFFFFA17.W
	MOVE.W	#$2300,SR

	MOVE.W	#1,VBL_FLAG
	MOVE.W	#5,TEMPO
	JMP	MAK_FADE

FIN	MOVE.W	#$2700,SR
	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W
	
	MOVE.W	#$FA00,A0
	MOVE.B	IERA,7(A0)
	MOVE.B	IERB,9(A0)
	MOVE.B	ISRA,$F(A0)
	MOVE.B	ISRB,$11(A0)
	MOVE.B	IMRA,$13(A0)
	MOVE.B	IMRB,$15(A0)
	MOVE.B	VR,$17(A0)
	MOVE.B	TACR,$19(A0)
	MOVE.B	TBCR,$1B(A0)
	MOVE.B	TCDCR,$1D(A0)
	MOVE.B	TADR,$1F(A0)
	MOVE.B	TBDR,$21(A0)
	MOVE.W	#$2300,SR

	MOVE.B	SAUVEC,$FFFF8201.W
	MOVE.B	SAUVEC+1,$FFFF8203.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	ORI.B	#%00000111,$484.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S    *******
VBLR	CLR.W	VBL_FLAG
	MOVE.B	#200,$FFFFFA21.W
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
	
TB0	RTE

VBL_FADE_PRES	TST.W	VBL_FLAG
	BNE.S	VBL_FADE_PRES

	MOVE.L	PNT_COLORS,A0
	TST.W	TEMPO
	BNE.S	NO_FADE_ROUGE
	MOVE.W	#5,TEMPO
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,$FFFF8242.W
	CMPI.L	#-1,(A0)
	BNE.S	NO_FADE_ROUGE
	MOVE.W	#100,TEMPO
	MOVE.L	#FIN_FADE_3,PNT_COLORS
	MOVE.W	#1,VBL_FLAG
	BRA.S	VBL2FADE_PRES
NO_FADE_ROUGE	SUBQ.W	#1,TEMPO
	MOVE.L	A0,PNT_COLORS

	MOVE.W	#1,VBL_FLAG
	BRA.S	VBL_FADE_PRES

VBL2FADE_PRES	TST.W	VBL_FLAG
	BNE.S	VBL2FADE_PRES

	MOVE.L	PNT_COLORS,A0
	TST.W	TEMPO
	BNE.S	NO2FADE_ROUGE
	MOVE.W	#5,TEMPO
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,$FFFF8242.W
	LEA	-32(A0),A0
	CMPA.L	#FADE3COLORS-16,A0
	BNE.S	NO2FADE_ROUGE
	MOVE.W	#30,TEMPO
	MOVE.L	#FADE3COLORS,PNT_COLORS
	JMP	MAK_FADE
NO2FADE_ROUGE	SUBQ.W	#1,TEMPO
	MOVE.L	A0,PNT_COLORS

	MOVE.W	#1,VBL_FLAG
	BRA.S	VBL2FADE_PRES

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

MAK_FADE	MOVE.L	SCREEN1,A0
	MOVE.W	#7999,D7
NETTOIE_ZCRIN	CLR.L	(A0)+
	DBRA	D7,NETTOIE_ZCRIN
	MOVE.L	PNT_FADES,A0
	MOVE.L	SCREEN1,A1
	ADDA.W	(A0)+,A1
	MOVE.W	(A0)+,D6

RECOM_FADE	MOVE.L	(A0)+,A2
	MOVE.W	(A0)+,D0
	MOVE.W	D0,D3
	MOVE.W	#160,D4
	LSL	#3,D3
	SUB.W	D3,D4
	LSR	#1,D4
	ADDA.W	D4,A1
	MOVE.W	(A0)+,D1
AFF_FADE_Y	MOVE.W	D0,D2
	SUBQ.W	#1,D2
AFF_FADE_X	MOVE.L	(A2)+,(A1)+
	MOVE.L	(A2)+,(A1)+
	DBRA	D2,AFF_FADE_X
	MOVE.W	D0,D3
	LSL	#3,D3
	MOVE.W	#160,D4
	SUB.W	D3,D4
	ADDA.W	D4,A2
	ADDA.W	D4,A1
	DBRA	D1,AFF_FADE_Y
	LSR	#1,D4
	SUBA.W	D4,A1
	LEA	160*4(A1),A1
	DBRA	D6,RECOM_FADE
	CMPI.L	#-2,(A0)
	BEQ.S	AFF_GEN_4
	CMPI.L	#-1,(A0)
	BNE.S	PROCHAIN_FADE
	LEA	-22(A0),A0
PROCHAIN_FADE	MOVE.L	A0,PNT_FADES
	MOVE.W	#1,VBL_FLAG
	BRA	VBL_FADE_PRES
	
AFF_GEN_4	LEA	PIC_FADE+34+80+(160*12),A0
	MOVE.L	SCREEN1,A1
	LEA	(160*80)+8(A1),A1
	MOVE.W	#52,D6
AFF_M1Y	MOVE.W	#19,D7
AFF_M1	MOVE.L	(A0)+,(A1)+
	DBRA	D7,AFF_M1
	LEA	80(A0),A0
	LEA	80(A1),A1
	DBRA	D6,AFF_M1Y
	LEA	PIC_FADE+34+80+(160*70),A0
	MOVE.L	SCREEN1,A1
	LEA	(160*82)+88(A1),A1
	MOVE.W	#55,D6
AFF_M2Y	MOVE.W	#19,D7
AFF_M2	MOVE.L	(A0)+,(A1)+
	DBRA	D7,AFF_M2
	LEA	80(A0),A0
	LEA	80(A1),A1
	DBRA	D6,AFF_M2Y
	ADDQ.W	#4,A0
	BRA.S	PROCHAIN_FADE

	SECTION	DATA
PIC_FADE	INCBIN	FADES.PI1

;FADE_COLORS	DC.W	$000,$100,$200,$300,$411,$522,$633,$744,$755,$766,$777
;	DC.L	-1

;FADE2COLORS	DC.W	$766,$755,$744,$633,$522,$411,$300,$200,$100,$000
;	DC.L	-1

FADE3COLORS	DC.W	$000,$000,$000,$000,$000,$000,$000,$000
	DC.W	$000,$000,$000,$100,$100,$100,$100,$100
	DC.W	$000,$000,$000,$200,$200,$200,$200,$200
	DC.W	$000,$000,$100,$300,$300,$300,$300,$300
	DC.W	$000,$000,$200,$400,$400,$400,$400,$411
	DC.W	$000,$100,$300,$500,$510,$500,$500,$522
	DC.W	$000,$200,$400,$511,$620,$610,$600,$633
	DC.W	$100,$300,$411,$522,$730,$720,$700,$744
	DC.W	$200,$311,$422,$533,$740,$730,$710,$755
	DC.W	$211,$322,$433,$544,$750,$740,$720,$766
FIN_FADE_3	DC.W	$222,$333,$444,$555,$760,$750,$730,$777
	DC.L	-1

SUIT_FADE	
	DC.W	160*60,4
	DC.L	PIC_FADE+34
	DC.W	8,11
	DC.L	PIC_FADE+34+(160*12)
	DC.W	2,11
	DC.L	PIC_FADE+34+(160*24)
	DC.W	8,11
	DC.L	PIC_FADE+34+(160*36)
	DC.W	2,10
	DC.L	PIC_FADE+34+(160*47)
	DC.W	6,11

	DC.W	160*60,4
	DC.L	PIC_FADE+34+(160*59)
	DC.W	6,11
	DC.L	PIC_FADE+34+(160*12)
	DC.W	2,11
	DC.L	PIC_FADE+34+(160*71)
	DC.W	10,11
	DC.L	PIC_FADE+34+(160*36)
	DC.W	2,10
	DC.L	PIC_FADE+34+(160*83)
	DC.W	10,11

	DC.W	160*60,2
	DC.L	PIC_FADE+34+(160*95)
	DC.W	10,11
	DC.L	PIC_FADE+34+(160*12)
	DC.W	2,11
	DC.L	PIC_FADE+34+(160*107)
	DC.W	6,11

	DC.W	160*60,2
	DC.L	PIC_FADE+34+(160*119)
	DC.W	6,11
	DC.L	PIC_FADE+34+(160*12)
	DC.W	2,11
	DC.L	PIC_FADE+34+(160*47)
	DC.W	6,11

	DC.W	160*60,2
	DC.L	PIC_FADE+34+(160*131)
	DC.W	6,11
	DC.L	PIC_FADE+34+(160*12)
	DC.W	2,11
	DC.L	PIC_FADE+34+(160*24)
 	DC.W	8,11

	DC.W	160*60,2
	DC.L	PIC_FADE+34+(160*143)
	DC.W	4,11
	DC.L	PIC_FADE+34+(160*12)
	DC.W	2,11
	DC.L	PIC_FADE+34+(160*155)
	DC.W	8,11

	DC.W	160*60,0
	DC.L	PIC_FADE+34+(160*167)
	DC.W	10,11
	DC.L	-2
	DC.L	-1

	SECTION	BSS
ANC_PAL	DS.L	8
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
SAUV_SP	DS.L	1
SAUVEC	DS.W	1
IERA	DS.B	1
IERB	DS.B	1
ISRA	DS.B	1
ISRB	DS.B	1
IMRA	DS.B	1
IMRB	DS.B	1
VR	DS.B	1
TACR	DS.B	1
TBCR	DS.B	1
TCDCR	DS.B	1
TADR	DS.B	1
TBDR	DS.B	1
	DS.B	256
BUFFER	DS.L	16000
	DS.B	256
SCREEN1	DS.L	1
SCREEN2	DS.L	1
VBL_FLAG	DS.W	1
ADR_1	DS.L	1
ADR_2	DS.L	1
PNT_FADES	DS.L	1
TEMPO	DS.W	1
PNT_COLORS	DS.L	1