	*DECOMPACTEUR DE .PC1
	*PARAMETRES A TRANSMETTRE:
	*               -ADRESSE DE LA .PC1 DANS A0
	*               -ADRESSE DU BUFFER DE DESTINATION DANS A1
	*STYMOX 1991
	
decomp_pc1	CLR.L	NB_OCT
	LEA	BUF_LIG,A2
RECOM_CDE	MOVEQ	#0,D7
	MOVE.B	(A0)+,D7
	CMPI.B	#0,D7
	BGE	AFF_NB
	NEG.B	D7
	MOVE.B	(A0),D4
RECOP1	MOVE.B	D4,(A2)+
	ADDQ.W	#1,NB_OCT
	DBRA	D7,RECOP1
	ADDQ.W	#1,A0
	CMPI.W	#160,NB_OCT
	BLT	RECOM_CDE
	BRA	FIN_LIG
AFF_NB	MOVE.B	(A0)+,(A2)+
	ADDQ.W	#1,NB_OCT
	DBRA	D7,AFF_NB
	CMPI.W	#160,NB_OCT
	BLT	RECOM_CDE
FIN_LIG	MOVEQ	#0,D7
	MOVEQ	#0,D6
	LEA	BUF_LIG,A2
	MOVE.W	#3,D6
REORGANIZ2	MOVE.W	#19,D7
REORGANIZ1	MOVE.W	(A2)+,(A1)
	ADDQ.L	#8,A1
	DBRA	D7,REORGANIZ1
	LEA	-158(A1),A1
	DBRA	D6,REORGANIZ2
	LEA	152(A1),A1
	ADDQ.W	#1,NB_LIG
	CLR.W	NB_OCT
	LEA	BUF_LIG,A2
	CMPI.W	#200,NB_LIG
	BLT	RECOM_CDE
	RTS

NB_OCT	DS.W	1
NB_LIG	DS.W	1
BUF_LIG	DS.B	160
