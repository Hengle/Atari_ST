;	ANDI.L	#$5EF25EF2,(A0)+	$0298 
;	ANDI.W	#$5EF2,(A0)+		$0258
;	AND.L	D0,(A0)+		$C198+$200
;	AND.W	D0,(A0)+		$C158+$200
;	ADDQ.W	#2,A0		$5448
;	ADDQ.W	#4,A0		$5848
;	ADDQ.W	#6,A0		$5C48
;	ADDQ.W	#8,A0		$5048
;	LEA	64(A0),A0		$41E8 0040
;	ORI.L	#$5EF25EF2,(A0)+	$0098
;	ORI.W	#$5EF2,(A0)+		$0058
;	OR.L	D0,(A0)+		$8198+$200
;	OR.W	D0,(A0)+		$8158+$200
;	MOVE.L	#$5EF25EF2,(A0)+	$20FC
;	MOVE.W	#$5EF2,(A0)+		$30FC
;	MOVE.L	D0,(A0)+		$20C0+1
;	MOVE.W	D0,(A0)+		$30C0+1
;	MOVE.L	A1,(A0)+		$20C9
;	MOVE.W	A1,(A0)+		$30C9

	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP
	MOVE.L	D0,SAUV_SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,SAUV_PAL
	MOVEM.L	PIC+2,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	;LEA	PIC+34,A0
	;LEA	BUF_PIC,A1
	;BSR	decomp_pc1

;	LEA	PIC+34+(160*50),A0
;	MOVE.L	$44E.W,A1
;	MOVE.W	#154,D6
;COPYII	MOVE.W	#19,D7
;COPY	MOVE.L	(A0),(A1)
;	ADDQ.W	#8,A0
;	ADDQ.W	#8,A1
;	DBRA	D7,COPY
;;	LEA	64(A0),A0
; 	LEA	64(A1),A1
;	DBRA	D6,COPYII

;	MOVE.W	#7,-(SP)
;	TRAP	#1
;	ADDQ.W	#2,SP
;	BRA	FIN

	LEA	PIC+34,A0
	LEA	BUF_MSK,A1
	LEA	BUF_SPR,A2
	MOVE.W	#199,D6
MSK_2	MOVE.W	#19,D7
MSK_1	MOVE.L	(A0),(A2)+
	MOVEM.W	(A0)+,D0-D3
	OR.W	D1,D0
	OR.W	D2,D0
	OR.W	D3,D0
	NOT.W	D0
	MOVE.W	D0,(A1)+
	DBRA	D7,MSK_1
;	LEA	64(A0),A0
	DBRA	D6,MSK_2

	LEA	BUF_SELECT,A2
	
	MOVEQ	#0,D4
	LEA	BUF_MSK,A5
	LEA	BUF_ITS,A1
	MOVE.W	#(20*200)-1,D7
	BSR	RECOM_AND
	LEA	BUF_FINALM,A6
	BSR	HIGHERS
	MULU.W	#3,D4
	MOVE.W	D4,POS_RESULT
MAK_MASK2	MOVEQ	#0,D4
	LEA	BUF_MSK,A5
	LEA	BUF2FINALM,A6
	REPT	2
	LEA	BUF_ITS,A1
	MOVE.W	#(20*100)-1,D7
	BSR	RECOM_AND
	BSR	HIGHERS
	ENDR
	MULU.W	#3,D4
	MOVE.W	D4,POS_RESULT+2
MAK_MASK4	MOVEQ	#0,D4
	LEA	BUF_MSK,A5
	LEA	BUF4FINALM,A6
	REPT	4
	LEA	BUF_ITS,A1
	MOVE.W	#(20*50)-1,D7
	BSR	RECOM_AND
	BSR	HIGHERS
	ENDR
	MULU.W	#3,D4
	MOVE.W	D4,POS_RESULT+4
MAK_MASK8	MOVEQ	#0,D4
	LEA	BUF_MSK,A5
	LEA	BUF8FINALM,A6
	REPT	8
	LEA	BUF_ITS,A1
	MOVE.W	#(20*25)-1,D7
	BSR	RECOM_AND
	BSR	HIGHERS
	ENDR
	MULU.W	#3,D4
	MOVE.W	D4,POS_RESULT+6
	BSR	SELECTIONNE
	BSR	MAK_COD_ANDS
	MOVE.W	#$41E8,(A2)+
	MOVE.W	#-((160*200)),D0
	MOVE.W	D0,(A2)+
	BSR	MAK_COD_ORS
;	BSR	SAUV_FIC
	BRA	ATT

MAK_COD_ORS	MOVE.L	#$4CDA000F,(A2)+
	LEA	BUF_SPR,A0
	LEA	BUF_MSK,A1
	MOVE.W	#199,D6
RECOMANCOR_2	MOVEQ	#19,D7
RECOMANCOR_1	CMPI.W	#-1,(A1)
	BEQ	PAS_ORS
	TST.W	(A1)
	BEQ	AFF_MOV
	TST.L	(A0)
	BEQ	PAS_ORS
	TST.W	(A0)
	BEQ	OR_2
	TST.W	2(A0)
	BEQ	OR_1
	MOVE.W	#$0098,(A2)+
	MOVE.L	(A0),(A2)+
	MOVE.W	#$5848,(A2)+
	BRA	FIN_ORS
OR_1	MOVE.W	#$0058,(A2)+
	MOVE.W	(A0),(A2)+
	MOVE.W	#$5C48,(A2)+
	BRA	FIN_ORS
OR_2	MOVE.W	#$5448,(A2)+
	BSR	OPTI_ADDQ
	MOVE.W	#$0058,(A2)+
	MOVE.W	2(A0),(A2)+
	MOVE.W	#$5848,(A2)+
	BRA	FIN_ORS
AFF_MOV	TST.L	(A0)
	BEQ.S	PAS_ORS
	TST.W	(A0)
	BEQ	MOV_2
	TST.W	2(A0)
	BEQ	MOV_1
	MOVE.W	#$20FC,(A2)+
	MOVE.L	(A0),(A2)+
	MOVE.W	#$5848,(A2)+
	BRA	FIN_ORS
MOV_1	MOVE.W	#$30FC,(A2)+
	MOVE.W	(A0),(A2)+
	MOVE.W	#$30C9,(A2)+
	MOVE.W	#$5848,(A2)+
	BRA	FIN_ORS
MOV_2	MOVE.W	#$30C9,(A2)+
	MOVE.W	#$30FC,(A2)+
	MOVE.W	2(A0),(A2)+
	MOVE.W	#$5848,(A2)+
	BRA	FIN_ORS
PAS_ORS	MOVE.W	#$5048,(A2)+	
	BSR	OPTI_ADDQ
FIN_ORS	ADDQ.W	#2,A1
	ADDQ.W	#4,A0
	DBRA	D7,RECOMANCOR_1
	;MOVE.L	#$41E80040,(A2)+
	BSR	OPTI_ADDQ
	DBRA	D6,RECOMANCOR_2
	MOVE.W	#$4E75,(A2)+
	RTS

MAK_COD_ANDS	LEA	BUF_COD,A2
	LEA	BUF_MSK,A0
	LEA	BUF_SPR,A1
	LEA	BUF_SELECT,A3
	MOVE.W	#199,ANNEX6
	MOVEQ	#0,D5
	MOVE.W	(A3)+,D7
	BEQ.S	RECOMANC_3
	MOVE.W	#99,ANNEX6
	MOVEQ	#1,D5
	CMPI.W	#1,D7
	BEQ.S	RECOMANC_3
	MOVE.W	#49,ANNEX6
	MOVEQ	#3,D5
	CMPI.W	#3,D7
	BEQ.S	RECOMANC_3
	MOVE.W	#24,ANNEX6
	MOVEQ	#7,D5
RECOMANC_3	MOVE.L	#$4CDA00FF,(A2)+
	MOVE.W	ANNEX6,D6
RECOMANC_2	MOVEQ	#19,D7
RECOMANC_1	CMPI.W	#-1,(A0)
	BEQ	PAS_ANDS
	TST.W	(A0)
	BEQ	PAS_ANDS
	MOVE.W	(A0),D0
	SWAP	D0
	MOVE.W	(A0),D0
;	TST.W	(A1)
;	BEQ	AND_2
;	TST.W	2(A1)
;	BEQ	AND_1
AND_TOUT	MOVE.L	#$0298C198,D2
	MOVE.W	(A0),D0
	MOVEQ	#0,D1
	MOVEQ	#7,D3
SHR_TOUT	CMP.W	2(A3),D0
	BEQ.S	TROUVê_TOUT
	ADDQ.W	#4,A3
	ADDI.W	#$200,D2
	ADDQ.W	#4,D1
	DBRA	D3,SHR_TOUT
	LEA	-32(A3),A3
	SWAP	D2
	MOVE.W	D2,(A2)+
	MOVE.W	D0,(A2)+
	MOVE.W	D0,(A2)+
	MOVE.W	#$5848,(A2)+
	BRA	FIN_ANDS
TROUVê_TOUT	SUBA.W	D1,A3
	MOVE.W	D2,(A2)+
	MOVE.W	#$5848,(A2)+
	BRA	FIN_ANDS
AND_1	MOVE.L	#$0258C158,D2
	MOVE.W	(A0),D0
	LEA	BUF_SELECT,A3
	MOVEQ	#7,D3
SHR_1	CMP.W	2(A3),D0
	BEQ.S	TROUVê_1
	ADDQ.W	#4,A3
	ADDI.W	#$200,D2
	DBRA	D3,SHR_1
	SWAP	D2
	MOVE.W	D2,(A2)+
	MOVE.W	D0,(A2)+
	MOVE.W	#$5C48,(A2)+
	BRA	FIN_ANDS
TROUVê_1	MOVE.W	D2,(A2)+
	MOVE.W	#$5C48,(A2)+
	BRA	FIN_ANDS
AND_2	MOVE.W	#$5448,(A2)+
	MOVE.L	#$0258C158,D2
	MOVE.W	(A0),D0
	LEA	BUF_SELECT,A3
	MOVEQ	#7,D3
SHR_2	CMP.W	2(A3),D0
	BEQ.S	TROUVê_1
	ADDQ.W	#4,A3
	ADDI.W	#$200,D2
	DBRA	D3,SHR_1
	SWAP	D2
	MOVE.W	D2,(A2)+
	MOVE.W	D0,(A2)+
	MOVE.W	#$5848,(A2)+
	BRA	FIN_ANDS
TROUVê_2	MOVE.W	D2,(A2)+
	MOVE.W	#$5848,(A2)+
	BRA	FIN_ANDS
PAS_ANDS	MOVE.W	#$5048,(A2)+
	BSR	OPTI_ADDQ
FIN_ANDS	ADDQ.W	#2,A0
	ADDQ.W	#4,A1
	DBRA	D7,RECOMANC_1
	;MOVE.W	#$41E8,(A2)+
	;MOVE.W	#64,(A2)+
	;BSR	OPTI_ADDQ
	DBRA	D6,RECOMANC_2
	LEA	32(A3),A3
	DBRA	D5,RECOMANC_3
	RTS

OPTI_ADDQ	MOVEQ	#0,D1
RECOM_OPTI	CMPI.W	#$5448,-2(A2)
	BNE.S	TEST_2
	ADDQ.W	#2,D1
	SUBQ.W	#2,A2
	BRA.S	RECOM_OPTI
TEST_2	CMPI.W	#$5848,-2(A2)
	BNE.S	TEST_3
	ADDQ.W	#4,D1
	SUBQ.W	#2,A2
	BRA.S	RECOM_OPTI
TEST_3	CMPI.W	#$5C48,-2(A2)
	BNE.S	TEST_4
	ADDQ.W	#6,D1
	SUBQ.W	#2,A2
	BRA.S	RECOM_OPTI
TEST_4	CMPI.W	#$5048,-2(A2)
	BNE.S	TEST_5
	ADDQ.W	#8,D1
	SUBQ.W	#2,A2
	BRA.S	RECOM_OPTI
TEST_5	CMPI.W	#$41E8,-4(A2)
	BNE.S	FIN_TEST
	ADD.W	-2(A2),D1
	SUBQ.W	#4,A2
	BRA.S	RECOM_OPTI
FIN_TEST	TST.W	D1
	BEQ.S	FIN_OPTI
	CMPI.W	#8,D1
	BGT.S	MAK_LEA
	BEQ.S	OPTI_8
OPTI_2	CMPI.W	#2,D1
	BNE.S	OPTI_4
	MOVE.W	#$5448,(A2)+
	BRA.S	FIN_OPTI
OPTI_4	CMPI.W	#4,D1
	BNE.S	OPTI_6
	MOVE.W	#$5848,(A2)+
	BRA.S	FIN_OPTI
OPTI_6	MOVE.W	#$5C48,(A2)+
	BRA.S	FIN_OPTI
OPTI_8	MOVE.W	#$5048,(A2)+
	BRA.S	FIN_OPTI
MAK_LEA	MOVE.W	#$41E8,(A2)+
	MOVE.W	D1,(A2)+
FIN_OPTI	RTS

RECOM_AND	CMPI.W	#-1,(A5)
	BEQ	NO_ANDS
	TST.W	(A5)
	BEQ	NO_ANDS
	MOVE.W	(A5),D0
	LEA	NO_ANDS,A4
	BRA.S	SEARCH
NO_ANDS	ADDQ.W	#2,A5
	DBRA	D7,RECOM_AND
	RTS

SEARCH	LEA	BUF_ITS,A3
RECOM_SHR	CMPA.L	A3,A1
	BEQ.S	FIN_1
	CMP.W	(A3),D0
	BEQ.S	FIN_2
	ADDQ.W	#8,A3
	BRA.S	RECOM_SHR
FIN_1	MOVE.W	D0,D1
	SWAP	D0
	MOVE.W	D1,D0
	MOVE.L	D0,(A1)+
	MOVE.L	#1,(A1)+
	JMP	(A4)
FIN_2	ADDQ.L	#1,4(A3)
	JMP	(A4)

HIGHERS	MOVEQ	#7,D7
RECOM_GEN_H	LEA	BUF_ITS,A0
	CMPA.L	A0,A1
	BEQ	NO_NO_NO
	MOVE.L	(A0)+,D0
	MOVE.L	(A0)+,D1
RECOM_HIG	CMPA.L	A0,A1
	BEQ.S	FINITO
	CMP.L	4(A0),D1
	BGE.S	PLUS_GRAND
	MOVE.L	(A0),D0
	MOVE.L	4(A0),D1
PLUS_GRAND	ADDQ.W	#8,A0
	BRA.S	RECOM_HIG	
FINITO	MOVE.L	D0,(A6)+
	MOVE.L	D1,(A6)+
	ADD.L	D1,D4
	LEA	BUF_ITS,A0
RECOM_FIND	CMP.L	(A0),D0
	BEQ.S	TROUVê
	ADDQ.W	#8,A0
	BRA.S	RECOM_FIND
TROUVê	CLR.L	(A0)+
	CLR.L	(A0)+
	DBRA	D7,RECOM_GEN_H
NO_NO_NO	RTS

SELECTIONNE	LEA	POS_RESULT,A0
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	MOVE.W	(A0)+,D2
	MOVE.W	(A0)+,D3
	SUBI.W	#19,D0
	SUBI.W	#38,D1
	SUBI.W	#76,D2
	SUBI.W	#152,D3
	CMP.W	D0,D1
	BGE.S	_2_PLUS
	CMP.W	D0,D2
	BGE.S	_4_PLUS
	CMP.W	D0,D3
	BGE.S	_8_PLUS
	MOVE.W	#0,(A2)+
	LEA	BUF_FINALM,A0
	MOVEQ	#7,D7
	BRA.S	REMPLIT
_2_PLUS	CMP.W	D1,D2
	BGE.S	_4_PLUS
	CMP.W	D1,D3
	BGE.S	_8_PLUS
	MOVE.W	#1,(A2)+
	LEA	BUF2FINALM,A0
	MOVEQ	#15,D7
	BRA.S	REMPLIT
_4_PLUS	CMP.W	D2,D3
	BGE.S	_8_PLUS
	MOVE.W	#3,(A2)+
	LEA	BUF4FINALM,A0
	MOVEQ	#31,D7
	BRA.S	REMPLIT
_8_PLUS	MOVE.W	#7,(A2)+
	LEA	BUF8FINALM,A0
	MOVEQ	#63,D7
REMPLIT	MOVE.L	(A0),(A2)+
	ADDQ.W	#8,A0
	DBRA	D7,REMPLIT
	RTS

;	LEA	BUF_MSK,A0
;	MOVE.L	$44E.W,A1
;	MOVE.W	#154,D6
;MASK2	MOVE.W	#11,D7
;MASK1	MOVE.W	(A0)+,(A1)
;	ADDQ.W	#8,A1
;	DBRA	D7,MASK1
;	LEA	64(A1),A1
;	DBRA	D6,MASK2

SAUV_FIC	LEA	BUF_COD,A0
	SUBA.L	A0,A2
	MOVE.L	A2,ADR
	CLR.W	-(SP)
	PEA	FIC_NAME
	MOVE.W	#60,-(SP)
	TRAP	#1
	ADDQ.W	#8,SP
	MOVE.W	D0,HANDLE

	PEA	BUF_COD
	MOVE.L	ADR,-(SP)
	MOVE.W	HANDLE,-(SP)
	MOVE.W	#64,-(SP)
	TRAP	#1
	LEA	12(SP),SP

	MOVE.W	HANDLE,-(SP)
	MOVE.W	#$3E,-(SP)
	TRAP	#1
	ADDQ.W	#4,SP
	RTS

ATT	MOVE.W	#(20*200)-1,D7
	MOVE.L	$44E.W,A0
RECOMMMM	MOVE.L	#-1,(A0)+
	CLR.L	(A0)+
	DBRA	D7,RECOMMMM
VBL	MOVE.W	#$333,$FFFF8240.W
	MOVE.L	$44E.W,A0
	LEA	BUF_SELECT+2,A2
	JSR	BUF_COD
	SF	$FFFF8240.W
	MOVE.W	#$25,-(SP)
	TRAP	#14
	ADDQ.W	#2,SP
	CMPI.B	#$39,$FFFFFC02.W
	BNE.S	VBL

FIN	MOVEM.L	SAUV_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	TRAP	#1
	ADDQ.W	#2,SP

;	INCLUDE	DEC_PC1.COD

	SECTION	DATA
PIC	INCBIN	ULTIMATE.PI1

FIC_NAME	DC.B	"2_TETE.COD",0
	EVEN

	SECTION	BSS
SAUV_SP	DS.L	1
SAUV_PAL	DS.L	8
ANNEX6	DS.W	1
HANDLE	DS.W	1
ADR	DS.L	1
POS_RESULT	DS.L	10
BUF_PIC	DS.B	32000*2
BUF_MSK	DS.B	8000*2
BUF_SPR	DS.B	32000*2
BUF_ITS	DS.B	32000*2
BUF_FINALM	DS.L	16
BUF2FINALM	DS.L	32
BUF4FINALM	DS.L	64
BUF8FINALM	DS.L	128
BUF_SELECT	DS.L	10000
BUF_COD	DS.B	20000