	*** ROUTINE DE GESTION DE LA SOURIS

	PEA	0.W
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1
	BSR	PREP_ECR
	MOVE.L	SCREEN1,A0
	OR.W	#$8000,80+(160*100)(A0)
	MOVE.W	#80+(160*100),ANC_Y

	MOVEQ	#1,D0
	JSR	MUZAXX

	MOVE.W	#1,VBL_FLAG

	ANDI.B	#%11111000,$484.W

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	SF	$FFFF8240.W

	MOVE.W 	#$FA00,A0
	MOVE.B	7(A0),IERA
	MOVE.B	9(A0),IERB
	MOVE.B	$F(A0),ISRA
	MOVE.B	$11(A0),ISRB
	MOVE.B	$13(A0),IMRA
	MOVE.B	$15(A0),IMRB
	MOVE.B	$17(A0),VR
	MOVE.B	$19(A0),TACR
	MOVE.B	$1B(A0),TBCR
	MOVE.B	$1D(A0),TCDCR
	MOVE.B	$1F(A0),TADR
	MOVE.B	$21(A0),TBDR

	MOVE.B	#$80,$FFFFFC02.W
	MOVE.B	#$01,$FFFFFC02.W
	MOVE.B	#$8,$FFFFFC02.W	;POSITION SOURIS EN RELATIF
	
	MOVE.W	#160,POS_X
	MOVE.W	#100,POS_Y
	CLR.W	RIGHT_ONE
	CLR.W	LEFT_ONE

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	BSET	#6,$FFFFFA09.W	;BIT 4 DU PORT E/S ?????
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	$118.W,ANC_CLAV
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	MOVE.L	#CLAV_ROUT,$118.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	BCLR	#3,$FFFFFA17.W
	MOVE.W	#$2300,SR

	JMP	IT_VBL

FIN	MOVE.W	#$2700,SR
	MOVE.B	#$80,$FFFFFC02.W	;CE RESET CLAVIER SUPPRIME
	MOVE.B	#$01,$FFFFFC02.W	;LA DERNIERE REPEAT TOUCHE
	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W
	MOVE.L	ANC_CLAV,$118.W
	
	MOVE.W	#$FA00,A0
	MOVE.B	IERA,7(A0)
	MOVE.B	IERB,9(A0)
	MOVE.B	ISRA,$F(A0)
	MOVE.B	ISRB,$11(A0)
	MOVE.B	IMRA,$13(A0)
	MOVE.B	IMRB,$15(A0)
	MOVE.B	VR,$17(A0)
	MOVE.B	TACR,$19(A0)
	MOVE.B	TBCR,$1B(A0)
	MOVE.B	TCDCR,$1D(A0)
	MOVE.B	TADR,$1F(A0)
	MOVE.B	TBDR,$21(A0)
	MOVE.W	#$2300,SR

	MOVE.B	SAUVEC,$FFFF8201.W
	MOVE.B	SAUVEC+1,$FFFF8203.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	ORI.B	#%00000111,$484.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S    *******
VBLR	CLR.W	VBL_FLAG
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
	
TB0	RTE

CLAV_ROUT	;ST	$FFFF8240.W
	MOVE.B	$FFFFFC02.W,PRISE
	MOVE.B	PRISE,ANNEX
	ANDI.B	#%11111100,PRISE
	CMPI.B	#$F8,PRISE	;TEST DU HEADER
	BNE	NO_MOUSE
	ANDI.B	#%00000011,ANNEX
	TST.B	ANNEX
	BNE.S	TEST_B1
	CLR.W	LEFT_ONE
	CLR.W	RIGHT_ONE
;	SF	$FFFF8240.W
	BRA.S	FIN_BUTTONS
TEST_B1	CMPI.B	#1,ANNEX
	BNE.S	TEST_B2
	MOVE.W	#$7,$FFFF8240.W
	MOVE.W	#1,RIGHT_ONE
	CLR.W	LEFT_ONE
	BRA.S	FIN_BUTTONS
TEST_B2	CMPI.B	#2,ANNEX
	BNE.S	TEST_B12
	MOVE.W	#$700,$FFFF8240.W
	MOVE.W	#1,LEFT_ONE
	CLR.W	RIGHT_ONE
	BRA.S	FIN_BUTTONS
TEST_B12	MOVE.W	#$777,$FFFF8240.W
	MOVE.W	#1,RIGHT_ONE
	MOVE.W	#1,LEFT_ONE
FIN_BUTTONS	MOVE.L	#TAK_X_CLAV,$118.W
NO_MOUSE	SF	$FFFF8240.W
	RTE

TAK_X_CLAV	;MOVE.W	#$700,$FFFF8240.W
	MOVEM.L	D7/A6,-(SP)
	MOVEQ	#0,D7
	MOVE.B	$FFFFFC02.W,D7
	EXT.W	D7
	ADD.W	D7,POS_X
	MOVE.W	POS_X,D7
	ANDI.W	#15,D7
	ADD.W	D7,D7
	LEA	PNTS,A6
	MOVE.W	(A6,D7.W),ANNEX	;MOTIF DU POINT DANS ANNEX
	MOVE.L	#TAK_Y_CLAV,$118.W
	MOVEM.L	(SP)+,D7/A6
	SF	$FFFF8240.W
	RTE

TAK_Y_CLAV	;MOVE.W	#7,$FFFF8240.W
	MOVEM.L	D1/D6/D7/A6,-(SP)
	MOVEQ	#0,D7
	MOVE.B	$FFFFFC02.W,D7
	EXT.W	D7
	ADD.W	D7,POS_Y
	MOVE.W	POS_Y,D7
	MULU.W	#160,D7
	MOVE.W	D7,D6
	MOVE.W	POS_X,D7
	ANDI.W	#-16,D7
	LSR	#1,D7
	ADD.W	D6,D7
	MOVE.W	ANC_Y,D1
	MOVE.L	SCREEN1,A6
	MOVE.W	#0,(A6,D1.W)
	MOVE.W	D7,ANC_Y
	MOVE.L	SCREEN1,A6
	ADDA.W	D7,A6
	MOVE.W	ANNEX,D7
	OR.W	D7,(A6)
	MOVE.L	#CLAV_ROUT,$118.W
	MOVEM.L	(SP)+,D1/D6/D7/A6
	SF	$FFFF8240.W
	RTE

IT_VBL	TST.W	VBL_FLAG
	BNE.S	IT_VBL

;	MOVE.W	#2000,

	MOVE.W	#$700,$FFFF8242.W
;	ST	$FFFF8240.W
	JSR	MUZAXX+8
	;BSR	SWAPEC
;	SF	$FFFF8240.W
	MOVE.W	#1,VBL_FLAG
	BRA.S	IT_VBL

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
	SECTION	DATA
MUZAXX	INCBIN	COPPER.MUS
	EVEN

PNTS
N	SET	32768
	REPT	16
	DC.W	N
N	SET	N/2
	ENDR
	SECTION	BSS
ANC_PAL	DS.L	8
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
ANC_CLAV	DS.L	1
SAUV_SP	DS.L	1
SAUVEC	DS.W	1
IERA	DS.B	1
IERB	DS.B	1
ISRA	DS.B	1
ISRB	DS.B	1
IMRA	DS.B	1
IMRB	DS.B	1
VR	DS.B	1
TACR	DS.B	1
TBCR	DS.B	1
TCDCR	DS.B	1
TADR	DS.B	1
TBDR	DS.B	1
	DS.B	256
BUFFER	DS.L	16000
	DS.B	256
SCREEN1	DS.L	1
SCREEN2	DS.L	1
VBL_FLAG	DS.W	1
LEFT_ONE	DS.W	1
RIGHT_ONE	DS.W	1
ANC_X	DS.W	1
ANC_Y	DS.W	1
POS_X	DS.W	1
POS_Y	DS.W	1
ANNEX	DS.W	1
ANNEX2	DS.W	1
PRISE	DS.B	1
	EVEN