	*** ROUTINE DE GESTION DU JOYSTICK

	PEA	0.W
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1
	BSR	PREP_ECR

	MOVE.W	#1,VBL_FLAG

	ANDI.B	#%11111000,$484.W

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL

	MOVE.W 	#$FA00,A0
	MOVE.B	7(A0),IERA
	MOVE.B	9(A0),IERB
	MOVE.B	$F(A0),ISRA
	MOVE.B	$11(A0),ISRB
	MOVE.B	$13(A0),IMRA
	MOVE.B	$15(A0),IMRB
	MOVE.B	$17(A0),VR
	MOVE.B	$19(A0),TACR
	MOVE.B	$1B(A0),TBCR
	MOVE.B	$1D(A0),TCDCR
	MOVE.B	$1F(A0),TADR
	MOVE.B	$21(A0),TBDR

	MOVE.B	#$80,$FFFFFC02.W
	MOVE.B	#$01,$FFFFFC02.W
	MOVE.B	#$14,$FFFFFC02.W
	
	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	BSET	#6,$FFFFFA09.W	;BIT 4 DU PORT E/S ?????
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	$118.W,ANC_CLAV
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	MOVE.L	#CLAV_ROUT,$118.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	BCLR	#3,$FFFFFA17.W
	MOVE.W	#$2300,SR

	JMP	IT_VBL

FIN	MOVE.W	#$2700,SR
	MOVE.B	#$80,$FFFFFC02.W	;CE RESET CLAVIER SUPPRIME
	MOVE.B	#$01,$FFFFFC02.W	;LA DERNIERE REPEAT TOUCHE
	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W
	MOVE.L	ANC_CLAV,$118.W
	
	MOVE.W	#$FA00,A0
	MOVE.B	IERA,7(A0)
	MOVE.B	IERB,9(A0)
	MOVE.B	ISRA,$F(A0)
	MOVE.B	ISRB,$11(A0)
	MOVE.B	IMRA,$13(A0)
	MOVE.B	IMRB,$15(A0)
	MOVE.B	VR,$17(A0)
	MOVE.B	TACR,$19(A0)
	MOVE.B	TBCR,$1B(A0)
	MOVE.B	TCDCR,$1D(A0)
	MOVE.B	TADR,$1F(A0)
	MOVE.B	TBDR,$21(A0)
	MOVE.W	#$2300,SR

	MOVE.B	SAUVEC,$FFFF8201.W
	MOVE.B	SAUVEC+1,$FFFF8203.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	ORI.B	#%00000111,$484.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S    *******
VBLR	CLR.W	VBL_FLAG
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
	
TB0	RTE

CLAV_ROUT	MOVE.B	$FFFFFC02.W,D0
	CMPI.B	#$FE,D0
	BEQ.S	JOY_0
	CMPI.B	#$FF,D0
	BNE.S	NO_JOY
	MOVE.L	#TAK1CLAV,$118.W
NO_JOY	RTE

JOY_0	MOVE.L	#TAK0CLAV,$118.W
	RTE

TAK0CLAV	MOVE.B	$FFFFFC02.W,D0
	MOVE.L	#CLAV_ROUT,$118.W
	RTE

TAK1CLAV	MOVEQ	#0,D0
	MOVE.B	$FFFFFC02.W,D0
	BTST	#7,D0
	BEQ.S	NO_FIRE
	MOVE.W	#1,FIRE
	BRA.S	NO_FIRE+6
NO_FIRE	CLR.W	FIRE
	ANDI.B	#%00001111,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	CORES_CLAV,A0
	MOVE.L	(A0,D0.W),A0
	JMP	(A0)

HAUT	MOVE.W	#3,D0
	TST.W	FIRE
	BEQ	AFF_COLOR
	ADDI.W	#4,D0
	BRA	AFF_COLOR

BAS	MOVE.W	#$303,D0
	TST.W	FIRE
	BEQ.S	AFF_COLOR
	ADDI.W	#$404,D0
	BRA	AFF_COLOR

DROITE	MOVE.W	#$300,D0
	TST.W	FIRE
	BEQ.S	AFF_COLOR
	ADDI.W	#$400,D0
	BRA	AFF_COLOR

GAUCHE	MOVE.W	#$30,D0
	TST.W	FIRE
	BEQ.S	AFF_COLOR
	ADDI.W	#$40,D0
	BRA	AFF_COLOR

HAUT_GAUCHE	MOVE.W	#$330,D0
	TST.W	FIRE
	BEQ.S	AFF_COLOR
	ADDI.W	#$440,D0
	BRA	AFF_COLOR

BAS_GAUCHE	MOVE.W	#$33,D0
	TST.W	FIRE
	BEQ.S	AFF_COLOR
	ADDI.W	#$44,D0
	BRA	AFF_COLOR

HAUT_DROITE	MOVE.W	#$333,D0
	TST.W	FIRE
	BEQ.S	AFF_COLOR
	ADDI.W	#$444,D0
	BRA	AFF_COLOR

BAS_DROITE	MOVE.W	#$320,D0
	TST.W	FIRE
	BEQ.S	AFF_COLOR
	ADDI.W	#$440,D0
;	BRA	AFF_COLOR

AFF_COLOR	MOVE.W	D0,$FFFF8240.W
	MOVE.L	#CLAV_ROUT,$118.W
	RTE

NO_DIR	TST.W	FIRE
	BEQ.S	NO_TRUE
	MOVE.W	#$147,$FFFF8240.W
	MOVE.L	#CLAV_ROUT,$118.W
	RTE

NO_TRUE	SF	$FFFF8240.W
	MOVE.L	#CLAV_ROUT,$118.W
	RTE

IT_VBL	TST.W	VBL_FLAG
	BNE.S	IT_VBL

	MOVE.W	#1,VBL_FLAG
	BRA.S	IT_VBL

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
	SECTION	DATA
CORES_CLAV	DC.L	NO_DIR
	DC.L	HAUT
	DC.L	BAS
	DC.L	NO_DIR
	DC.L	GAUCHE
	DC.L	HAUT_GAUCHE
	DC.L	BAS_GAUCHE
	DC.L	NO_DIR
	DC.L	DROITE
	DC.L	HAUT_DROITE
	DC.L	BAS_DROITE
	REPT	5
	DC.L	NO_DIR
	ENDR

	SECTION	BSS
ANC_PAL	DS.L	8
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
ANC_CLAV	DS.L	1
SAUV_SP	DS.L	1
SAUVEC	DS.W	1
IERA	DS.B	1
IERB	DS.B	1
ISRA	DS.B	1
ISRB	DS.B	1
IMRA	DS.B	1
IMRB	DS.B	1
VR	DS.B	1
TACR	DS.B	1
TBCR	DS.B	1
TCDCR	DS.B	1
TADR	DS.B	1
TBDR	DS.B	1
	DS.B	256
BUFFER	DS.L	16000
	DS.B	256
SCREEN1	DS.L	1
SCREEN2	DS.L	1
VBL_FLAG	DS.W	1
FIRE	DS.W	1