NB_STARS	EQU	85
NB2STARS	EQU	50
NB3STARS	EQU	40

	PEA	0.W
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1
	BSR	PREP_ECR

	MOVEQ	#1,D0
	JSR	MUZAXX

	LEA	PIC+34,A0
	MOVE.L	SCREEN2,A1
	JSR	decomp_pc1

	MOVE.L	#FADE_ULT,PNT_FADE
	MOVE.L	#TRAJET_1,PNT1TRAJET
	MOVE.L	#TRAJET_2,PNT2TRAJET
	MOVE.L	#TRAJET_3,PNT3TRAJET
	MOVE.L	#TRAJET_4,PNT4TRAJET

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	MOVE.W	#$8240,A0
	MOVEQ	#7,D7
RECOM_EFF_C	CLR.L	(A0)+
	DBRA	D7,RECOM_EFF_C

	MOVE.L	SCREEN2,A0
	MOVE.L	SCREEN1,A1
	LEA	56+(160*28)(A1),A1
	MOVEQ	#11,D6
PREAFFTHE2	MOVEQ	#4,D7
PREAFFTHE1	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,PREAFFTHE1
	LEA	120(A0),A0
	LEA	120(A1),A1
	DBRA	D6,PREAFFTHE2

	LEA	TABLE_DEC,A6
	MOVE.L	SCREEN2,A0
	LEA	160*43(A0),A0
	LEA	BUF_DEST,A1
	MOVE.W	#21-1,D6	;HAUTEUR
AFF_ULT12	MOVEQ	#19,D7
AFF_ULT1	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,AFF_ULT1
	CLR.L	(A1)+
	CLR.L	(A1)+
	DBRA	D6,AFF_ULT12
	LEA	BUF_DEST,A0
	MOVE.L	A0,(A6)+
	MOVEQ	#6,D3
DEC_GEN	MOVE.L	A1,(A6)+
	MOVE.W	#(21*21)-1,D7
AFF_ULT2	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,AFF_ULT2
	MOVEQ	#1,D4
DEC_ULT4	MOVEQ	#20,D5
DEC_ULT3	MOVEQ	#3,D6
DEC_ULT2	MOVEQ	#20,D7
DEC_ULT1	ROXR	(A0)
	ADDQ.W	#8,A0
	DBRA	D7,DEC_ULT1
	LEA	-166(A0),A0
	DBRA	D6,DEC_ULT2
	LEA	160(A0),A0
	DBRA	D5,DEC_ULT3
	LEA	-(21*168)(A0),A0
	DBRA	D4,DEC_ULT4
	DBRA	D3,DEC_GEN

	LEA	TABLE2DEC,A6
	MOVE.L	SCREEN2,A0
	LEA	160*64(A0),A0
	MOVE.W	#21-1,D6	;HAUTEUR
AFF2ULT12	MOVEQ	#19,D7
AFF2ULT1	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,AFF2ULT1
	CLR.L	(A1)+
	CLR.L	(A1)+
	DBRA	D6,AFF2ULT12
	LEA	-(168*21)(A1),A0
	MOVE.L	A0,(A6)+
	MOVEQ	#6,D3
DEC2GEN	MOVE.L	A1,(A6)+
	MOVE.W	#(21*21)-1,D7
AFF2ULT2	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,AFF2ULT2
	MOVEQ	#1,D4
DEC2ULT4	MOVEQ	#20,D5
DEC2ULT3	MOVEQ	#3,D6
DEC2ULT2	MOVEQ	#20,D7
DEC2ULT1	ROXR	(A0)
	ADDQ.W	#8,A0
	DBRA	D7,DEC2ULT1
	LEA	-166(A0),A0
	DBRA	D6,DEC2ULT2
	LEA	160(A0),A0
	DBRA	D5,DEC2ULT3
	LEA	-(21*168)(A0),A0
	DBRA	D4,DEC2ULT4
	DBRA	D3,DEC2GEN

	BSR	MAK_COD_FIGHT

	MOVE.L	#SINUS_1,PNT1STARS
	MOVE.L	#SINUS_2,PNT2STARS

	MOVE.W	#2,ANNEX

	MOVE.W	#1,VBL_FLAG

	ANDI.B	#%11111000,$484.W

	MOVE.W 	#$FA00,A0
	MOVE.B	7(A0),IERA
	MOVE.B	9(A0),IERB
	MOVE.B	$F(A0),ISRA
	MOVE.B	$11(A0),ISRB
	MOVE.B	$13(A0),IMRA
	MOVE.B	$15(A0),IMRB
	MOVE.B	$17(A0),VR
	MOVE.B	$19(A0),TACR
	MOVE.B	$1B(A0),TBCR
	MOVE.B	$1D(A0),TCDCR
	MOVE.B	$1F(A0),TADR
	MOVE.B	$21(A0),TBDR
	
	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	BCLR	#3,$FFFFFA17.W
	MOVE.W	#$2300,SR

	;JMP	TEST
	JMP	ULT_1_VBL

FIN	MOVE.W	#$2700,SR
	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W
	
	MOVE.W	#$FA00,A0
	MOVE.B	IERA,7(A0)
	MOVE.B	IERB,9(A0)
	MOVE.B	ISRA,$F(A0)
	MOVE.B	ISRB,$11(A0)
	MOVE.B	IMRA,$13(A0)
	MOVE.B	IMRB,$15(A0)
	MOVE.B	VR,$17(A0)
	MOVE.B	TACR,$19(A0)
	MOVE.B	TBCR,$1B(A0)
	MOVE.B	TCDCR,$1D(A0)
	MOVE.B	TADR,$1F(A0)
	MOVE.B	TBDR,$21(A0)
	MOVE.W	#$2300,SR

	MOVE.B	SAUVEC,$FFFF8201.W
	MOVE.B	SAUVEC+1,$FFFF8203.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	ORI.B	#%00000111,$484.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S    *******
VBLR	CLR.W	VBL_FLAG
	JSR	MUZAXX+8
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
	
TB0	RTE

ULT_1_VBL	TST.W	VBL_FLAG
	BNE.S	ULT_1_VBL

	BSR	AFF_TRAJET

	MOVE.L	PNT_FADE,A0
	TST.W	ANNEX
	BNE.S	FIN_FD1
	MOVE.W	#2,ANNEX
	MOVE.W	#$8240,A1
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,(A1)

	CMPA.L	#FIN_FADE,A0
	BNE.S	END_2
	LEA	PALET_BLANCS,A0
END_2	CMPI.L	#-1,(A0)
	BNE.S	END
	MOVE.W	#64,ANNEX
	MOVE.L	#SUCC_ROUTIN,ANNEX2
	MOVE.W	#1,VBL_FLAG
	BRA	ULT_2_VBL
END	MOVE.L	A0,PNT_FADE
FIN_FD1	SUBQ.W	#1,ANNEX

	MOVE.W	#1,VBL_FLAG
	BRA	ULT_1_VBL

ULT_2_VBL	TST.W	VBL_FLAG
	BNE.S	ULT_2_VBL

	BSR	AFF_TRAJET

	MOVE.L	PNT1STARS,A0
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	TABLE_DEC,A0
	MOVE.L	(A0,D0.W),A0
	LSR	#3,D1
	ADD.W	D1,D1
	ADD.W	D1,D1
	LEA	SUCC_ROUTIN,A1
	MOVE.L	(A1,D1.W),A2
	MOVE.L	SCREEN1,A1
	LEA	160*43(A1),A1
	JSR	(A2)

	MOVE.L	PNT2STARS,A0
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	TABLE2DEC,A0
	MOVE.L	(A0,D0.W),A0
	SUBA.W	D1,A0
	MOVE.W	D1,D2
	LSR	#3,D1
	ADD.W	D1,D1
	ADD.W	D1,D1
	LEA	SUCC2ROUTIN,A1
	MOVE.L	(A1,D1.W),A2
	MOVE.L	SCREEN1,A1
	LEA	160*64(A1),A1
	ADDA.W	D2,A1
	JSR	(A2)

	;160*43,160*64

	ADDQ.L	#4,PNT1STARS
	ADDQ.L	#4,PNT2STARS
 	MOVE.L	PNT2STARS,A0
	CMPI.L	#-1,(A0)
	BNE.S	PAS_FIN_ROUT
	MOVE.W	#1,VBL_FLAG
	BRA	ULT_3_VBL

PAS_FIN_ROUT	MOVE.W	#1,VBL_FLAG
	BRA	ULT_2_VBL

ULT_3_VBL	TST.W	VBL_FLAG
	BNE.S	ULT_3_VBL

	BSR	AFF_TRAJET

	SUBQ.W	#1,ANNEX
	TST.W	ANNEX
	BNE.S	SUITTT

	MOVE.L	#CIRCULE,PNT1STARS
	MOVE.W	#1,VBL_FLAG
	BRA	ULT_4_VBL

SUITTT	MOVE.W	#1,VBL_FLAG
	BRA	ULT_3_VBL

ULT_4_VBL	TST.W	VBL_FLAG
	BNE.S	ULT_4_VBL

	BSR	AFF_TRAJET

	MOVE.L	PNT1STARS,A2
	LEA	TABLE_ADR,A0
	ADDA.W	(A2)+,A0
	MOVE.L	(A0),A1
	MOVE.L	SCREEN1,A0
	ADDA.W	(A2)+,A0
	JSR	(A1)

	CMPI.L	#-1,(A2)
	BNE.S	FIGHT_OK
	SUBQ.W	#4,A2
FIGHT_OK	MOVE.L	A2,PNT1STARS

	MOVE.W	#1,VBL_FLAG
	BRA	ULT_4_VBL

ROUTINE_1
N	SET	0
	REPT	21
	LEA	160(A0),A0
	MOVE.L	(A0)+,N(A1)
	MOVE.L	(A0)+,N+4(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_2
N	SET	0
	REPT	21
	LEA	152(A0),A0
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,N(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_3
N	SET	0
	REPT	21
	LEA	144(A0),A0
	MOVEM.L	(A0)+,D0-D5
	MOVEM.L	D0-D5,N(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_4
N	SET	0
	REPT	21
	LEA	136(A0),A0
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,N(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_5
N	SET	0
	REPT	21
	LEA	128(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A3
	MOVEM.L	D0-D7/A2-A3,N(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_6
N	SET	0
	REPT	21
	LEA	120(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_7
N	SET	0
	REPT	21
	LEA	112(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVE.L	(A0)+,N+48(A1)
	MOVE.L	(A0)+,N+52(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_8
N	SET	0
	REPT	21
	LEA	104(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,N+48(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_9
N	SET	0
	REPT	21
	LEA	96(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D5
	MOVEM.L	D0-D5,N+48(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_10
N	SET	0
	REPT	21
	LEA	88(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,N+48(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_11
N	SET	0
	REPT	21
	LEA	80(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A3
	MOVEM.L	D0-D7/A2-A3,N+48(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_12
N	SET	0
	REPT	21
	LEA	72(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_13
N	SET	0
	REPT	21
	LEA	64(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVE.L	(A0)+,N+96(A1)
	MOVE.L	(A0)+,N+100(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_14
N	SET	0
	REPT	21
	LEA	56(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,N+96(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_15
N	SET	0
	REPT	21
	LEA	48(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D5
	MOVEM.L	D0-D5,N+96(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_16
N	SET	0
	REPT	21
	LEA	40(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,N+96(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_17
N	SET	0
	REPT	21
	LEA	32(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A3
	MOVEM.L	D0-D7/A2-A3,N+96(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_18
N	SET	0
	REPT	21
	LEA	24(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+96(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_19
N	SET	0
	REPT	21
	LEA	16(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+96(A1)
	MOVE.L	(A0)+,N+144(A1)
	MOVE.L	(A0)+,N+148(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_20
N	SET	0
	REPT	21
	ADDQ.W	#8,A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+96(A1)
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,N+144(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_21
N	SET	0
	REPT	21
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+96(A1)
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,N+144(A1)
	ADDQ.W	#8,A0
N	SET	N+160
	ENDR
	RTS

IT_VBL	TST.W	VBL_FLAG
	BNE.S	IT_VBL

	MOVE.W	#$222,$FFFF8242.W
	MOVE.W	#$444,$FFFF8244.W
	MOVE.W	#$777,$FFFF8246.W

	MOVE.W	#2000,D0
BCL	DBRA	D0,BCL

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	SUITV0
	ST	$FFFF8240.W

SUITV0	MOVE.L	PNT1STARS,A0
	MOVE.L	-8(A0),A0
	MOVE.L	SCREEN2,A1
	REPT	NB_STARS
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	NOT.W	D0
	AND.W	D0,(A1,D1.W)
	ENDR
	MOVE.L	PNT2STARS,A0
	MOVE.L	-8(A0),A0
	MOVE.L	SCREEN2,A1
	ADDQ.W	#2,A1
	REPT	NB2STARS
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	NOT.W	D0
	AND.W	D0,(A1,D1.W)
	ENDR
	MOVE.L	PNT3STARS,A0
	MOVE.L	-8(A0),A0
	MOVE.L	SCREEN2,A1
	MOVE.L	SCREEN2,A2
	ADDQ.W	#2,A2
	REPT	NB3STARS
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	NOT.W	D0
	AND.W	D0,(A1,D1.W)
	AND.W	D0,(A2,D1.W)
	ENDR
	MOVE.L	PNT1STARS,A0
	MOVE.L	(A0),A0
	MOVE.L	SCREEN2,A1
	REPT	NB_STARS
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	OR.W	D0,(A1,D1.W)
	ENDR
	MOVE.L	PNT2STARS,A0
	MOVE.L	(A0),A0
	MOVE.L	SCREEN2,A1
	ADDQ.W	#2,A1
	REPT	NB2STARS
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	OR.W	D0,(A1,D1.W)
	ENDR
	MOVE.L	PNT3STARS,A0
	MOVE.L	(A0),A0
	MOVE.L	SCREEN2,A1
	MOVE.L	SCREEN2,A2
	ADDQ.W	#2,A2
	REPT	NB3STARS
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	OR.W	D0,(A1,D1.W)
	OR.W	D0,(A2,D1.W)
	ENDR

	ADDQ.L	#4,PNT1STARS
	MOVE.L	PNT1STARS,A0
	CMPI.L	#-1,(A0)
	BNE.S	SUIT_1
	MOVE.L	#TABLE_ADR,PNT1STARS
SUIT_1	ADDQ.L	#4,PNT2STARS
	MOVE.L	PNT2STARS,A0
	CMPI.L	#-1,(A0)
	BNE.S	SUIT_2
	MOVE.L	#TABLE2ADR,PNT2STARS
SUIT_2	ADDQ.L	#4,PNT3STARS
	MOVE.L	PNT3STARS,A0
	CMPI.L	#-1,(A0)
	BNE.S	SUIT_3
	MOVE.L	#TABLE3ADR,PNT3STARS
SUIT_3
	BSR	SWAPEC
	SF	$FFFF8240.W
	MOVE.W	#1,VBL_FLAG
	BRA	IT_VBL

AFF_TRAJET	MOVE.L	PNT1TRAJET,A1
	MOVE.W	(A1)+,D0
	MOVE.W	(A1)+,D1
	MOVE.W	(A1)+,D2
	MOVE.L	SCREEN1,A0
	ADDQ.W	#6,A0
	JSR	REMPLIT
	MOVE.L	PNT2TRAJET,A1
	MOVE.W	(A1)+,D0
	MOVE.W	(A1)+,D1
	MOVE.W	(A1)+,D2
	MOVE.L	SCREEN1,A0
	ADDQ.W	#6,A0
	JSR	REMPLIT
	MOVE.L	PNT3TRAJET,A1
	MOVE.W	(A1)+,D0
	MOVE.W	(A1)+,D1
	MOVE.W	(A1)+,D2
	MOVE.L	SCREEN1,A0
	ADDQ.W	#6,A0
	JSR	REMPLIT
	MOVE.L	PNT4TRAJET,A1
	MOVE.W	(A1)+,D0
	MOVE.W	(A1)+,D1
	MOVE.W	(A1)+,D2
	MOVE.L	SCREEN1,A0
	ADDQ.W	#6,A0
	JSR	REMPLIT

	ADDQ.L	#6,PNT1TRAJET
	ADDQ.L	#6,PNT2TRAJET
	ADDQ.L	#6,PNT3TRAJET
	ADDQ.L	#6,PNT4TRAJET
	MOVE.L	PNT1TRAJET,A1
	CMPI.L	#-1,(A1)
	BNE.S	CONT1TRAJET
	MOVE.W	#1,VBL_FLAG
	JMP	PREP_ETOIL_V

CONT1TRAJET	MOVE.L	A1,PNT1TRAJET
	MOVE.L	PNT2TRAJET,A1
	CMPI.L	#-1,(A1)
	BNE.S	CONT2TRAJET
	SUBQ.W	#6,A1

CONT2TRAJET	MOVE.L	A1,PNT2TRAJET 
	MOVE.L	PNT3TRAJET,A1
	CMPI.L	#-1,(A1)
	BNE.S	CONT3TRAJET
	SUBQ.W	#6,A1

CONT3TRAJET	MOVE.L	A1,PNT3TRAJET 
	MOVE.L	PNT4TRAJET,A1
	CMPI.L	#-1,(A1)
	BNE.S	CONT4TRAJET
	SUBQ.W	#6,A1

CONT4TRAJET	MOVE.L	A1,PNT4TRAJET 
	RTS

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

MAK_COD_FIGHT	MOVE.L	SCREEN2,A0
	LEA	32+(160*85)+(160*51)(A0),A0
	LEA	COD_FIGHT,A1
	LEA	TABLE_ADR,A6
	MOVEQ	#50,D6
MAKE_2	MOVE.L	A1,(A6)+
	MOVEQ	#23,D7
MAKE_1	MOVE.W	#$20FC,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,MAKE_1
	MOVE.W	#$41E8,(A1)+
	MOVE.W	#-(96+160),(A1)+
	LEA	-(160+96)(A0),A0
	DBRA	D6,MAKE_2
	MOVE.W	#$4E75,(A1)
	RTS

PREP_ETOIL_V	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
	MOVE.W	#(20*200)-1,D7
EFF_SCR	MOVE.L	(A1)+,(A2)+
	MOVE.L	(A1)+,(A2)+
	DBRA	D7,EFF_SCR

	BSR	ETOIL_V
	MOVE.L	#TABLE_ADR,PNT1STARS
	MOVE.L	#TABLE2ADR,PNT2STARS
	MOVE.L	#TABLE3ADR,PNT3STARS

	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
	MOVE.W	#(20*200)-1,D7
EFF2SCR	MOVE.L	(A1)+,(A2)+
	MOVE.L	(A1)+,(A2)+
	DBRA	D7,EFF2SCR

	BSR	SWAPEC

	MOVE.W	#1,VBL_FLAG
	BRA	IT_VBL

ETOIL_V	;LEA	PIC+34,A0
	MOVE.L	SCREEN2,A1
	MOVE.W	#(20*200)-1,D7
MAK_MASK_SCR	MOVEM.W	(A1),D0-D3
	OR.W	D1,D0
	OR.W	D2,D0
	OR.W	D3,D0
	NOT.W	D0
	MOVE.W	D0,(A1)
	CLR.W	2(A1)
	CLR.L	4(A1)
	ADDQ.W	#8,A1
	DBRA	D7,MAK_MASK_SCR
	LEA	PNTS,A4
	LEA	CORES,A5
	LEA	BUF_DEST,A2
	
	LEA	TABLE_ADR,A6
	MOVE.W	#199,D6	;NB DE PHASES-1 (200,100 OU 50)
	MOVE.W	#NB_STARS-1,D5	;NB D'ETOILES-1
	LEA	COOR_ETOIL_V,A3
	MOVE.W	#160,ANNEX
	MOVE.L	#TABLE_ADR,ANNEX2
	BSR	STARS_DEC2
	LEA	TABLE2ADR,A6
	MOVE.W	#99,D6	;NB DE PHASES-1 (200,100 OU 50)
	MOVE.W	#NB2STARS-1,D5	;NB D'ETOILES-1
	LEA	COOR_ETOIL_V+(NB_STARS*6),A3
	MOVE.W	#160*2,ANNEX
	MOVE.L	#TABLE2ADR,ANNEX2
	BSR	STARS_DEC2
	LEA	TABLE3ADR,A6
	MOVE.W	#49,D6	;NB DE PHASES-1 (200,100 OU 50)
	MOVE.W	#NB3STARS-1,D5	;NB D'ETOILES-1
	LEA	COOR_ETOIL_V+((NB_STARS+NB2STARS)*6),A3
	MOVE.W	#160*4,ANNEX
	MOVE.L	#TABLE3ADR,ANNEX2
	BSR	STARS_DEC2
	RTS

STARS_DEC2	MOVE.L	A2,(A6)+
	MOVE.W	D5,D7
	MOVE.L	A3,A0
STARS_DEC1	MOVE.W	(A0)+,D0	;X AND 15
	MOVE.W	(A0)+,D1	;(X AND -16)/2
	MOVE.W	(A0),D2	;Y*160
	SUB.W	ANNEX,D2
	CMPI.W	#-160,D2
	BNE.S	NO_DEP
	MOVE.W	#160*199,D2
NO_DEP	CMPI.W	#-160*2,D2
	BNE.S	NO2DEP
	MOVE.W	#160*198,D2
NO2DEP	CMPI.W	#-160*3,D2
	BNE.S	NO3DEP
	MOVE.W	#160*197,D2
NO3DEP	CMPI.W	#-160*4,D2
	BNE.S	NO4DEP
	MOVE.W	#160*196,D2
NO4DEP	MOVE.W	D2,(A0)+
 	MOVE.L	SCREEN2,A1
	ADD.W	D1,A1
	ADD.W	D2,A1
	MOVE.W	(A1),D3
	MOVE.W	D0,D4
	ADD.W	D4,D4
	MOVE.W	(A5,D4.W),D4
	BTST	D4,D3
	BNE.S	AFFICH
	MOVE.L	#0,(A2)+
SUIT	DBRA	D7,STARS_DEC1
	DBRA	D6,STARS_DEC2
	MOVE.L	ANNEX2,A0
	MOVE.L	-8(A6),-8(A0)
	MOVE.L	-4(A6),-4(A0)
	MOVE.L	#-1,(A6)
	RTS

AFFICH	ADD.W	D0,D0
	MOVE.W	(A4,D0.W),(A2)+
	ADD.W	D2,D1
	MOVE.W	D1,(A2)+
	BRA	SUIT

	INCLUDE	DEC_PC1.COD

	INCLUDE	REMPLISS.S
	
	SECTION	DATA
PIC	INCBIN	ULTIMAT2.PC1

COOR_ETOIL_V	INCBIN	STARS_V.STR

SINUS_1	INCBIN	SINUS_1.ULT
	DC.L	-1

SINUS_2	INCBIN	SINUS_2.ULT
	DC.L	-1

MUZAXX	INCBIN	B:MUSIX.MUS
	EVEN

CORES	DC.W	15,14,13,12,11,10,9,8,7,6,5,4,3,2,1,0

FADE_ULT	DC.W	0,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000
	DC.W	0,$000,$000,$000,$110,$110,$110,$110,$000,$100,$000,$000,$000,$100,$000,$111
	DC.W	0,$111,$000,$000,$220,$220,$220,$220,$000,$200,$100,$000,$000,$210,$000,$222
	DC.W	0,$222,$111,$000,$331,$330,$330,$330,$000,$310,$200,$100,$000,$320,$000,$333
	DC.W	0,$333,$222,$000,$442,$440,$440,$440,$000,$420,$310,$200,$100,$430,$100,$444
	DC.W	0,$444,$333,$111,$553,$551,$550,$550,$100,$530,$420,$310,$200,$540,$200,$555
	DC.W	0,$555,$444,$222,$664,$662,$661,$660,$200,$640,$530,$420,$310,$650,$300,$666
FIN_FADE	DC.L	-1

PALET_BLANCS	DC.W	0,$666,$555,$333,$775,$773,$772,$770,$310,$750,$640,$530,$420,$760,$400,$777
	DC.W	0,$777,$666,$444,$776,$774,$773,$771,$421,$761,$751,$641,$531,$771,$511,$777
	DC.W	0,$777,$777,$555,$777,$775,$774,$772,$532,$772,$762,$752,$642,$772,$622,$777
	DC.W	0,$777,$777,$666,$777,$776,$775,$773,$643,$773,$773,$763,$753,$773,$733,$777
	DC.W	0,$777,$777,$777,$777,$777,$776,$774,$754,$774,$774,$774,$764,$774,$744,$777
	DC.W	0,$777,$777,$777,$777,$777,$777,$775,$765,$775,$775,$775,$775,$775,$755,$777
	DC.W	0,$777,$777,$777,$777,$777,$777,$776,$776,$776,$776,$776,$776,$776,$766,$777
	DC.W	0,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777

	DC.W	0,$777,$777,$777,$777,$777,$777,$776,$776,$776,$776,$776,$776,$776,$766,$777
	DC.W	0,$777,$777,$777,$777,$777,$777,$775,$765,$775,$775,$775,$775,$775,$755,$777
	DC.W	0,$777,$777,$777,$777,$777,$776,$774,$754,$774,$774,$774,$764,$774,$744,$777
	DC.W	0,$777,$777,$666,$777,$776,$775,$773,$643,$773,$773,$763,$753,$773,$733,$777
	DC.W	0,$777,$777,$555,$777,$775,$774,$772,$532,$772,$762,$752,$642,$772,$622,$777
	DC.W	0,$777,$666,$444,$776,$774,$773,$771,$421,$761,$751,$641,$531,$771,$511,$777
	DC.W	0,$666,$555,$333,$775,$773,$772,$770,$310,$750,$640,$530,$420,$760,$400,$777
	DC.L	-1

SUCC_ROUTIN	DC.L	ROUTINE_1,ROUTINE_2,ROUTINE_3,ROUTINE_4,ROUTINE_5,ROUTINE_6
	DC.L	ROUTINE_7,ROUTINE_8,ROUTINE_9,ROUTINE_10,ROUTINE_11,ROUTINE_12
	DC.L	ROUTINE_13,ROUTINE_14,ROUTINE_15,ROUTINE_16,ROUTINE_17
	DC.L	ROUTINE_18,ROUTINE_19,ROUTINE_20
	DC.L	-1

SUCC2ROUTIN	DC.L	ROUTINE_20,ROUTINE_19,ROUTINE_18,ROUTINE_17,ROUTINE_16,ROUTINE_15
	DC.L	ROUTINE_14,ROUTINE_13,ROUTINE_12,ROUTINE_11,ROUTINE_10,ROUTINE_9
	DC.L	ROUTINE_8,ROUTINE_7,ROUTINE_6,ROUTINE_5,ROUTINE_4
	DC.L	ROUTINE_3,ROUTINE_2,ROUTINE_1
	DC.L	-1
TRAJET_1
N	SET	132
	REPT	128
	DC.W	132,N,160*28
N	SET	N-1	
	ENDR
N	SET	29
	REPT	143-28
	DC.W	5,5,160*N
N	SET	N+1
	ENDR
N	SET	6
	REPT	156
	DC.W	6,N,160*143
N	SET	N+1
	ENDR
	DC.L	-1

TRAJET_2
N	SET	132
	REPT	125
	DC.W	132,N,160*31
N	SET	N-1
	ENDR
N	SET	31
	REPT	140-30
	DC.W	8,8,160*N
N	SET	N+1
	ENDR
N	SET	9
	REPT	151
	DC.W	9,N,160*140
N	SET	N+1
	ENDR
	DC.L	-1

TRAJET_3
N	SET	188
	REPT	314-187
	DC.W	188,N,160*36
N	SET	N+1
	ENDR
N	SET	37
	REPT	143-36
	DC.W	314,314,160*N
N	SET	N+1
	ENDR
N	SET	313
	REPT	156
	DC.W	313,N,160*143
N	SET	N-1
	ENDR
	DC.L	-1

TRAJET_4	
N	SET	188
	REPT	311-187
	DC.W	188,N,160*39
N	SET	N+1
	ENDR
N	SET	40
	REPT	140-39
	DC.W	311,311,160*N
N	SET	N+1
	ENDR
N	SET	310
	REPT	155
	DC.W	310,N,160*140
N	SET	N-1
	ENDR
	DC.L	-1

CIRCULE
N	SET	50
	REPT	50
	DC.W	(N*4),32+(160*199)
N	SET	N-1
	ENDR
N	SET	199
	REPT	199-84-51
	DC.W	0,32+(160*N)
N	SET	N-1
	ENDR
	DC.L	-1

PNTS
N	SET	32768
	REPT	16
	DC.W	N
N	SET	N/2
	ENDR
	SECTION	BSS
ANC_PAL	DS.L	8
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
SAUV_SP	DS.L	1
SAUVEC	DS.W	1
IERA	DS.B	1
IERB	DS.B	1
ISRA	DS.B	1
ISRB	DS.B	1
IMRA	DS.B	1
IMRB	DS.B	1
VR	DS.B	1
TACR	DS.B	1
TBCR	DS.B	1
TCDCR	DS.B	1
TADR	DS.B	1
TBDR	DS.B	1
	DS.B	256
BUFFER	DS.L	16000
	DS.B	256
SCREEN1	DS.L	1
SCREEN2	DS.L	1
VBL_FLAG	DS.W	1
PNT_FADE	DS.L	1
PNT1TRAJET	DS.L	1
PNT2TRAJET	DS.L	1
PNT3TRAJET	DS.L	1
PNT4TRAJET	DS.L	1
	DS.L	2
TABLE_ADR	DS.L	201
	DS.L	2
TABLE2ADR	DS.L	101
	DS.L	2
TABLE3ADR	DS.L	51
ANNEX	DS.W	1
ANNEX2	DS.L	1
TABLE_DEC	DS.L	8
TABLE2DEC	DS.L	8
PNT1STARS	DS.L	1
PNT2STARS	DS.L	1
PNT3STARS	DS.L	1
BUF_DEST	DS.W	((2*NB_STARS)*200)
BUF2DEST	DS.W	((2*NB2STARS)*100)
BUF3DEST	DS.W	((2*NB3STARS)*50)
COD_FIGHT