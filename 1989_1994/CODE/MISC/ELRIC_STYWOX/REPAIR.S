NB_STARS	EQU	100

 	PEA	0.W
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1
	BSR	PREP_ECR

	BSR	CALCULE
	MOVE.L	#COURB_CENTER,PNT_CRB
	MOVE.L	#BUF_EFF,ADR_EFF
	MOVE.L	#BUF2EFF,ADR2EFF
	MOVE.W	#160,X_CENTER
	MOVE.W	#100,Y_CENTER

	MOVE.W	#1,VBL_FLAG

	ANDI.B	#%11111000,$484.W

	MOVE.L	$4D2.W,ADR_ZIK

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL

	MOVE.W 	#$FA00,A0
	MOVE.B	7(A0),IERA
	MOVE.B	9(A0),IERB
	MOVE.B	$F(A0),ISRA
	MOVE.B	$11(A0),ISRB
	MOVE.B	$13(A0),IMRA
	MOVE.B	$15(A0),IMRB
	MOVE.B	$17(A0),VR
	MOVE.B	$19(A0),TACR
	MOVE.B	$1B(A0),TBCR
	MOVE.B	$1D(A0),TCDCR
	MOVE.B	$1F(A0),TADR
	MOVE.B	$21(A0),TBDR
	
	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	BCLR	#3,$FFFFFA17.W
	MOVE.W	#$2300,SR

	JMP	IT_VBL

FIN	MOVE.W	#$2700,SR
	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W
	
	MOVE.W	#$FA00,A0
	MOVE.B	IERA,7(A0)
	MOVE.B	IERB,9(A0)
	MOVE.B	ISRA,$F(A0)
	MOVE.B	ISRB,$11(A0)
	MOVE.B	IMRA,$13(A0)
	MOVE.B	IMRB,$15(A0)
	MOVE.B	VR,$17(A0)
	MOVE.B	TACR,$19(A0)
	MOVE.B	TBCR,$1B(A0)
	MOVE.B	TCDCR,$1D(A0)
	MOVE.B	TADR,$1F(A0)
	MOVE.B	TBDR,$21(A0)
	MOVE.W	#$2300,SR

	MOVE.B	SAUVEC,$FFFF8201.W
	MOVE.B	SAUVEC+1,$FFFF8203.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	TST.L	ADR_ZIK
	BEQ.S	SET_484
	ORI.B	#%00000110,$484.W
	BRA.S	SET_484+6
SET_484	ORI.B	#%00000111,$484.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S    *******
VBLR	CLR.W	VBL_FLAG
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
	
TB0	RTE

IT_VBL	TST.W	VBL_FLAG
	BNE.S	IT_VBL

	MOVE.W	#$555,$FFFF8242.W

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	SUITV0
	ST	$FFFF8240.W

SUITV0	MOVE.L	ADR_EFF,A0
	MOVE.L	SCREEN2,A1
	MOVEQ	#0,D0
	REPT	NB_STARS
	MOVE.W	(A0)+,D1
	MOVE.W	D0,(A1,D1.W)
	ENDR

	MOVE.W	X_CENTER,D4
	MOVE.W	Y_CENTER,D5
	MOVE.W	#-16,D7
	LEA	PNTS,A1
	MOVE.L	SCREEN2,A2
	LEA	TABLE_DIV,A4
	LEA	TABLE_Y,A5
	LEA	BUF_COOR,A0
	MOVE.L	ADR_EFF,A6

	MOVE.W	#NB_STARS-1,D6
STARSR	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	MOVE.W	(A0),D2
	SUBQ.W	#8,D2
	CMPI.W	#-280,D2
	BLT	SUIV
	MOVE.W	D2,(A0)+
	MOVE.L	(A4,D2.W),D2
	EXT.L	D0
	EXT.L	D1
	ASL.L	#8,D0
	ASL.L	#8,D1
	DIVS	D2,D0
	DIVS	D2,D1
	ADD.W	D4,D0
	ADD.W	D5,D1
	ANDI.W	#$0FFF,D0
	TST.W	D0
	BLT	CLIP
	CMPI.W	#320,D0
	BGE	CLIP
	TST.W	D1
	BLT	CLIP
	CMPI.W	#200,D1
	BGE	CLIP
	MOVE.W	D0,D2
	ADD.W	D2,D2
	MOVE.W	(A1,D2.W),D2
	AND.W	D7,D0
	LSR	#1,D0
	ADD.W	D1,D1
	MOVE.W	(A5,D1.W),D1
	ADD.W	D0,D1
	MOVE.W	D1,(A6)+
	OR.W	D2,(A2,D1.W)
CLIP	DBRA	D6,STARSR
	SF	$FFFF8240.W

	CMPI.B	#$48,$FFFFFC02.W
	BNE.S	TEST_2
	SUBQ.W	#1,Y_CENTER
TEST_2	CMPI.B	#$50,$FFFFFC02.W
	BNE.S	TEST_3
	ADDQ.W	#1,Y_CENTER
TEST_3	CMPI.B	#$4B,$FFFFFC02.W
	BNE.S	TEST_4
	SUBQ.W	#1,X_CENTER
TEST_4	CMPI.B	#$4D,$FFFFFC02.W
	BNE.S	FIN_TEST
	ADDQ.W	#1,X_CENTER
FIN_TEST

	BSR	SWAPEC

	MOVE.W	#1,VBL_FLAG
	BRA	IT_VBL

SUIV	MOVE.W	#512*4,(A0)+
	MOVE.W	#0,(A6)+
	BRA	CLIP

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	MOVE.L	ADR_EFF,D0
	MOVE.L	ADR2EFF,ADR_EFF
	MOVE.L	D0,ADR2EFF
	RTS
	
PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
CALCULE	LEA	COOR_STARS,A0
	LEA	BUF_COOR,A1
	MOVE.W	#NB_STARS-1,D7
PRE_POZ	MOVE.L	(A0)+,(A1)+
	MOVE.W	(A0)+,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.W	D0,(A1)+
	DBRA	D7,PRE_POZ
	LEA	TABLE_DIV,A0
	LEA	-2048(A0),A0
	MOVE.W	#511,D7
	MOVE.W	#-2048,D0
RECOMPRECALC	MOVE.W	D0,D2
	SWAP	D2	;Z*(2^16)
	ASR.L	#7,D2	;Z=Z/256
	ADDI.L	#$20000,D2	;Z=Z+1
	ASR.L	#5,D2
	ASR.L	#4,D2
	TST.W	D2
	BNE.S	ZERO_Q
	MOVE.W	#1,D2
ZERO_Q	EXT.L	D2
	MOVE.L	D2,(A0)+
	ADDQ.W	#4,D0
	DBRA	D7,RECOMPRECALC
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D0	;Z
	MOVE.W	#1023,D7
RECOM_CALC	MOVE.L	D0,D2
	SWAP	D2	;Z*(2^16)
	ASR.L	#8,D2	;Z=Z/256
	ADDI.L	#$10000,D2	;Z=Z+1
	ASR.L	#8,D2
	TST.W	D2
	BNE.S	ZERO2Q
	MOVE.W	#1,D2
ZERO2Q	EXT.L	D2
	MOVE.L	D2,(A0)+
	ADDQ.W	#4,D0
	DBRA	D7,RECOM_CALC
	RTS
	
	SECTION	DATA
COOR_STARS	INCBIN	REPAIR.STR

COURB_CENTER	INCBIN	CRB_STAR.CRB
	DC.W	-1

	REPT	20
	DCB.W	16,0
	ENDR
PNTS	REPT	20
	DC.W	32768,16384,8192,4096,2048,1024,512,256,128,64,32,16,8,4,2,1
	ENDR
	REPT	1000
	DCB.W	16,0
	ENDR

	REPT	100
	DC.W	-160
	ENDR
TABLE_Y	
N	SET	0
	REPT	200
	DC.W	N*160
N	SET	N+1
	ENDR
	REPT	100
	DC.W	-160
	ENDR

	SECTION	BSS
ANC_PAL	DS.L	8
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
SAUV_SP	DS.L	1
SAUVEC	DS.W	1
IERA	DS.B	1
IERB	DS.B	1
ISRA	DS.B	1
ISRB	DS.B	1
IMRA	DS.B	1
IMRB	DS.B	1
VR	DS.B	1
TACR	DS.B	1
TBCR	DS.B	1
TCDCR	DS.B	1
TADR	DS.B	1
TBDR	DS.B	1
	DS.B	256+160
BUFFER	DS.L	16000
	DS.B	256+160
SCREEN1	DS.L	1
SCREEN2	DS.L	1
ADR_ZIK	DS.L	1
VBL_FLAG	DS.W	1
X_CENTER	DS.W	1
Y_CENTER	DS.W	1
PNT_CRB	DS.L	1
ADR_EFF	DS.L	1
ADR2EFF	DS.L	1
BUF_EFF	DS.W	NB_STARS
BUF2EFF	DS.W	NB_STARS
BUF_COOR	DS.W	3*NB_STARS
ADDIT	DS.W	1
	DS.L	512
TABLE_DIV	DS.L	1024