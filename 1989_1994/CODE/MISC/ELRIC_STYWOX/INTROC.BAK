NB_STARS	EQU	250
VITESSE	EQU	10
X_CENTER	EQU	160
Y_CENTER	EQU	100
;
NB1STARS	EQU	85
NB2STARS	EQU	50
NB3STARS	EQU	40

 	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP

	CLR.W	-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008
	MOVE.L	#FIN,$00C
	MOVE.L	#FIN,$010
	MOVE.L	#FIN,$014
	MOVE.L	#FIN,$018
	MOVE.L	#FIN,$01C
	MOVE.L	#FIN,$020

	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1
	BSR	PREP_ECR

	MOVE.W	#$000,COULEUR 

	BSR	PRP_QUART		;VA PREPARER LES DECALAGES DES 4 QUARTS DE CERLES

	MOVE.L	#COURB_1,PNT_1_CRB
	MOVE.L	#ROUTS_1,PNT_1_ROT
	MOVE.L	#COURB_Y,PNT_1_Y
	MOVE.L	#COURB_Y_2,PNT_2_Y
	MOVE.L	#COURB_2,PNT_2_CRB
	MOVE.L	#ROUTS_2,PNT_2_ROT

	MOVE.L	#PALET,PNT_FADE

	MOVE.L	#ROULO,PNT_ANIM
	MOVE.L	#DEPLAC_R,PNT_DEPR

	MOVE.W	#1,VBL_FLAG

	ANDI.B	#%11111000,$484.W

	MOVE.L	$4D2.W,ADR_ZIK

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	MOVEM.L	PALET,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.W 	#$FA00,A0
	MOVE.B	7(A0),IERA
	MOVE.B	9(A0),IERB
	MOVE.B	$F(A0),ISRA
	MOVE.B	$11(A0),ISRB
	MOVE.B	$13(A0),IMRA
	MOVE.B	$15(A0),IMRB
	MOVE.B	$17(A0),VR
	MOVE.B	$19(A0),TACR
	MOVE.B	$1B(A0),TBCR
	MOVE.B	$1D(A0),TCDCR
	MOVE.B	$1F(A0),TADR
	MOVE.B	$21(A0),TBDR
	
	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	BCLR	#3,$FFFFFA17.W
	MOVE.W	#$2300,SR

	CLR.W	TEMPO
	JMP	INTRO_VBL

FIN	MOVE.W	#$2700,SR
	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W
	
	MOVE.W	#$FA00,A0
	MOVE.B	IERA,7(A0)
	MOVE.B	IERB,9(A0)
	MOVE.B	ISRA,$F(A0)
	MOVE.B	ISRB,$11(A0)
	MOVE.B	IMRA,$13(A0)
	MOVE.B	IMRB,$15(A0)
	MOVE.B	VR,$17(A0)
	MOVE.B	TACR,$19(A0)
	MOVE.B	TBCR,$1B(A0)
	MOVE.B	TCDCR,$1D(A0)
	MOVE.B	TADR,$1F(A0)
	MOVE.B	TBDR,$21(A0)
	MOVE.W	#$2300,SR

	MOVE.B	SAUVEC,$FFFF8201.W
	MOVE.B	SAUVEC+1,$FFFF8203.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	TST.L	ADR_ZIK
	BEQ.S	SET_484
	ORI.B	#%00000110,$484.W
	BRA.S	SET_484+6
SET_484	ORI.B	#%00000111,$484.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S    *******
VBLR	CLR.B	$FFFFFA1B.W
	CLR.W	VBL_FLAG
	CMPI.B	#1,$FFFFFC02.W
	BEQ	FIN
	RTE

TB0	MOVE.L	(A6)+,$FFFF8240.W
	MOVE.L	(A6)+,$FFFF8244.W
	MOVE.L	(A6)+,$FFFF8248.W
	MOVE.L	(A6)+,$FFFF824C.W
	MOVE.L	(A6)+,$FFFF8250.W
	MOVE.L	(A6)+,$FFFF8254.W
	MOVE.L	(A6)+,$FFFF8258.W
	MOVE.L	(A6)+,$FFFF825C.W
	MOVE.B	#88,$FFFFFA21.W
	MOVE.L	#TB1,$120.W
	RTE

TB1	MOVE.W	VALOR,$FFFF8242.W
	CLR.B	$FFFFFA21.W
	CLR.B	$FFFFFA1B.W
 	CLR.L	$120.W
	RTE

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

	*******	V B L  &  P R O C E D U R E S    *******
INTRO_VBL	TST.W	VBL_FLAG
	BNE.S	INTRO_VBL
	BSR	AFF_SPR_1
	BSR	AFF_SPR_2
	BSR	AFF_SPR_3
	BSR	AFF_SPR_4
	BSR	SWAPEC
	CMPI.W	#$4E71,TEMPO
	BNE.S	CONT_VBLI1
	MOVE.W	#2,TEMPO
	MOVE.W	#1,VBL_FLAG
	JMP	INTRO2VBL
CONT_VBLI1	SF	$FFFF8240.W
	MOVE.W	#1,VBL_FLAG
	BRA	INTRO_VBL
	
INTRO2VBL	TST.W	VBL_FLAG
	BNE.S	INTRO2VBL
	MOVE.W	COULEUR,$FFFF8240.W
	LEA	PIC_ROND,A0
	MOVE.L	SCREEN2,A2
	LEA	48+(160*38)(A2),A2
	MOVEQ	#0,D0
	REPT	80
	MOVE.L	D0,(A2)+
	ENDR
N	SET	0
	REPT	120
	CLR.L	N-8(A2)
	CLR.L	N-4(A2)
	MOVEM.L	(A0)+,D0-D7/A3-A5
	MOVEM.L	D0-D7/A3-A5,N(A2)
	MOVEM.L	(A0)+,D0-D4
	MOVEM.L	D0-D4,N+44(A2)
N	SET	N+160
	ENDR
	LEA	160*120(A2),A2
	MOVEQ	#0,D0
	REPT	80
	MOVE.L	D0,(A2)+
	ENDR
	BSR	SWAPEC
	SF	$FFFF8240.W
	SUBQ.W	#1,TEMPO
	TST.W	TEMPO
	BNE.S	SUIT_INTR2
	MOVE.L	#3,PNT_1_CRB
	MOVE.W	#1,VBL_FLAG
	JMP	INTRO3VBL
SUIT_INTR2	MOVE.W	#1,VBL_FLAG
	BRA	INTRO2VBL

INTRO3VBL	TST.W	VBL_FLAG
	BNE.S	INTRO3VBL
	TST.L	PNT_1_CRB
	BNE.S	SUITV3
	MOVE.L	#3,PNT_1_CRB
	MOVE.L	PNT_FADE,A0
	LEA	$FFFF8240.W,A1
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,(A1)
	MOVE.L	(A0),D0
	BPL.S	SUIT_FAD
	MOVE.W	#1,VBL_FLAG
	JMP	INTRO4VBL
SUIT_FAD	MOVE.L	A0,PNT_FADE
SUITV3	SUBQ.L	#1,PNT_1_CRB
	BSR	SWAPEC
	MOVE.W	#1,VBL_FLAG
	BRA	INTRO3VBL

INTRO4VBL	TST.W	VBL_FLAG
	BNE.S	INTRO4VBL
	MOVE.W	COULEUR,$FFFF8240.W
	MOVE.L	SCREEN2,A0
	MOVE.L	PNT_DEPR,A1
	SUBQ.L	#4,A1
	ADDA.W	(A1),A0
	LEA	48(A0),A0
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
N	SET	0
	REPT	120
	MOVEM.L	D0-D7,N(A0)
	MOVEM.L	D0-D7,N+32(A0)
N	SET	N+160
	ENDR
	BSR	ANIMATION
	BSR	SWAPEC
	SF	$FFFF8240.W
	MOVE.W	#1,VBL_FLAG
	BRA	INTRO4VBL

INTRO5VBL	TST.W	VBL_FLAG
	BNE.S	INTRO5VBL
	MOVE.W	COULEUR,$FFFF8240.W
	MOVE.L	PNT_2_CRB,A2
	SUBQ.L	#4,A2
	LEA	CODES2,A1
	MOVE.L	SCREEN2,A0
	LEA	24(A0),A0
	LEA	160*199(A0),A0
	ADDA.W	(A2)+,A1
	LEA	CODES2+202,A3
	ADDA.W	(A1),A3
	MOVEQ	#0,D0
	JSR	(A3)
CONT_EFF
	MOVE.L	PNT_2_CRB,A2
	LEA	CODES,A1
	MOVE.L	SCREEN2,A0
	LEA	24(A0),A0
	LEA	160*199(A0),A0
	ADDA.W	(A2)+,A1
	LEA	CODES+202,A3
	ADDA.W	(A1),A3
	JSR	(A3)
	BSR	SWAPEC

	MOVE.W	(A2),D0
	BPL	CONT_SCR
	LEA	PRESENT_GRF,A0
	MOVE.L	SCREEN1,A2
	LEA	64+(160*180)(A2),A2
N	SET	0
	REPT	21
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,N(A2)
N	SET	N+160
	ENDR
	MOVE.L	#ANIM_COUL,PNT_FADE
	MOVE.W	#3,PNT_1_CRB
	MOVE.W	#1,VBL_FLAG
	JMP	INTRO6VBL
CONT_SCR	MOVE.L	A2,PNT_2_CRB
	SF	$FFFF8240.W
	MOVE.W	#1,VBL_FLAG
	BRA	INTRO5VBL

INTRO6VBL	TST.W	VBL_FLAG
	BNE.S	INTRO6VBL
	MOVEM.L	PALET,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	CLR.B	$FFFFFA1B.W
	MOVE.B	#180,$FFFFFA21.W
	MOVE.L	#TB2,$120.W
	MOVE.L	PNT_FADE,A6
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.L	PNT_FADE,A0
	TST.W	PNT_1_CRB
	BNE.S	SUITV6
	MOVE.W	#3,PNT_1_CRB
	ADDQ.L	#4,A0
	MOVE.L	(A0),D0
	BPL.S	SUITV6
	MOVE.W	#1,VBL_FLAG
	JMP	INTRO7VBL
SUITV6	MOVE.L	A0,PNT_FADE
	SUBQ.W	#1,PNT_1_CRB
	MOVE.W	#1,VBL_FLAG
	BRA	INTRO6VBL

INTRO7VBL	TST.W	VBL_FLAG
	BNE.S	INTRO7VBL
	CLR.B	$FFFFFA1B.W
	CMPI.B	#$39,$FFFFFC02.W
	BNE	FIN_VBL
;	MOVE.L	#VBLR,$70.W
	MOVE.L	#PALET_1,PNT_FADE
	MOVE.W	#5,TEMPO
	MOVE.W	#1,VBL_FLAG
	JMP	VBL_FADE1
FIN_VBL	MOVE.W	#1,VBL_FLAG
	BRA	INTRO7VBL

VBL_FADE1	TST.W	VBL_FLAG
	BNE.S	VBL_FADE1
	MOVE.L	PNT_FADE,A0
	TST.W	TEMPO
	BNE.S	SUIT_FADE22
	MOVE.W	#5,TEMPO
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	CMPI.L	#-1,(A0)
	BNE.S	SUIT_FADE22
	JMP	PRP_PART_2
SUIT_FADE22	SUBQ.W	#1,TEMPO
	MOVE.L	A0,PNT_FADE
	MOVE.W	#1,VBL_FLAG
	BRA	VBL_FADE1

TB2	MOVE.W	(A6)+,$FFFF8254.W
	MOVE.W	(A6),$FFFF8258.W
	RTE

ANIMATION	MOVE.L	PNT_ANIM,A0
	MOVE.L	PNT_DEPR,A1
	MOVE.L	SCREEN2,A4
	ADDA.W	(A1)+,A4
	LEA	48(A4),A4
CONT_AFF	LEA	PIC_ROND,A1
	MOVE.L	A4,A2
	ADDA.W	(A0)+,A2
	ADDA.W	(A0)+,A1
	MOVEM.L	(A1)+,D0-D7/A3/A5
	MOVEM.L	D0-D7/A3/A5,(A2)
	MOVEM.L	(A1)+,D0-D5
	MOVEM.L	D0-D5,40(A2)
	MOVE.W	(A0),D0
	BPL.S	CONT_AFF

	ADDQ.L	#2,A0
	MOVE.W	(A0),D0
	BPL.S	SUIT_ROUL
	LEA	ROULO,A0
	MOVE.L	#STYWOX_SCR,PNT_2_CRB
	MOVE.W	#1,VBL_FLAG
	JMP	INTRO5VBL
SUIT_ROUL	MOVE.L	PNT_DEPR,A1
	ADDQ.L	#2,A1
	CMPI.W	#-1,(A1)
	BNE.S	FIN_ROULO
	SUBQ.L	#2,A1
FIN_ROULO	MOVE.L	A0,PNT_ANIM
	MOVE.L	A1,PNT_DEPR
	RTS

AFF_SPR_1	MOVE.L	PNT_1_CRB,A5
	MOVE.L	PNT_1_ROT,A4
	MOVE.L	PNT_1_Y,A6
	MOVE.L	SCREEN2,A1
	LEA	PACK_ADR,A2
	MOVE.W	(A5)+,D0
	ADDA.W	(A4),A1
	ADDA.W	(A6)+,A1
	MOVE.L	(A2,D0.W),A0
	MOVE.L	2(A4),A2
	JSR	(A2)

	RTS

AFF_SPR_2	MOVE.L	PNT_2_CRB,A5
	MOVE.L	PNT_2_ROT,A4
	MOVE.L	PNT_1_Y,A6
	MOVE.L	SCREEN2,A1
	ADDA.W	(A6),A1
	LEA	-320(A1),A1
	BSR	EFF_LIG
	MOVE.L	SCREEN2,A1
	LEA	PACK_ADR+16,A2
	MOVE.W	(A5)+,D0
	ADDA.W	(A4),A1
	ADDA.W	(A6)+,A1
	MOVE.L	(A2,D0.W),A0
	MOVE.L	2(A4),A2
	JSR	(A2)

	ADDQ.L	#2,PNT_1_Y
	MOVE.L	PNT_1_Y,A0
	CMPI.W	#-1,(A0)
	BNE.S	FIN_SPR_2

	MOVE.W	#$4E71,TEMPO

FIN_SPR_2	RTS

AFF_SPR_3	MOVE.L	PNT_1_CRB,A5
	MOVE.L	PNT_1_ROT,A4
	MOVE.L	PNT_2_Y,A6
	MOVE.L	SCREEN2,A1
	LEA	PACK_ADR+32,A2
	MOVE.W	(A5)+,D0
	ADDA.W	(A4),A1
	ADDA.W	(A6)+,A1
	MOVE.L	(A2,D0.W),A0
	MOVE.L	2(A4),A2
	JSR	(A2)

	CMPI.W	#-1,(A5)
	BNE.S	SUIT_1
	LEA	COURB_1,A5
	ADDQ.L	#6,A4
	CMPI.L	#-1,(A4)
	BNE.S	SUIT_1
	SUBQ.L	#6,A4
	;ADDQ.L	#6,A5
SUIT_1	MOVE.L	A5,PNT_1_CRB
	MOVE.L	A4,PNT_1_ROT

	RTS

AFF_SPR_4	MOVE.L	PNT_2_CRB,A5
	MOVE.L	PNT_2_ROT,A4
	MOVE.L	PNT_2_Y,A6
	MOVE.L	SCREEN2,A1
	ADDA.W	(A6),A1
	LEA	(160*60)(A1),A1
	BSR	EFF_LIG
	MOVE.L	SCREEN2,A1
	LEA	PACK_ADR+48,A2
	MOVE.W	(A5)+,D0
	ADDA.W	(A4),A1
	ADDA.W	(A6)+,A1
	MOVE.L	(A2,D0.W),A0
	MOVE.L	2(A4),A2
	JSR	(A2)

	CMPI.W	#-1,(A6)
	BNE.S	TEST_4
	SUBQ.L	#2,A6
TEST_4	CMPI.W	#-1,(A5)
	BNE.S	SUIT_4
	SUBQ.L	#8,A5
	ADDQ.L	#6,A4
	CMPI.L	#-1,(A4)
	BNE.S	SUIT_4
	SUBQ.L	#6,A4
	;ADDQ.L	#6,A5
SUIT_4	MOVE.L	A6,PNT_2_Y 
	MOVE.L	A5,PNT_2_CRB
	MOVE.L	A4,PNT_2_ROT

	RTS

EFF_LIG	MOVEQ	#0,D7
	REPT	80
	MOVE.L	D7,(A1)+
	ENDR
	RTS

	*** DIFFERENTES ROUTINES D'AFFICHAGES ( CLIPPING ) !!! ***

	*** CLIPPING GAUCHE !!! ***
AFFICH1
N	SET	0
	REPT	60
	LEA	32(A0),A0
	MOVE.L	(A0)+,N(A1)
	MOVE.L	(A0)+,N+4(A1)
N	SET	N+160
	ENDR
	RTS

AFFICH2
N	SET	0
	REPT	60
	LEA	24(A0),A0
	MOVE.L	(A0)+,N(A1)
	MOVE.L	(A0)+,N+4(A1)
	MOVE.L	(A0)+,N+8(A1)
	MOVE.L	(A0)+,N+12(A1)
N	SET	N+160
	ENDR
	RTS

AFFICH3
N	SET	0
	REPT	60
	LEA	16(A0),A0
	MOVEM.L	(A0)+,D0-D5
	MOVEM.L	D0-D5,N(A1)
N	SET	N+160
	ENDR
	RTS

AFFICH4
N	SET	0
	REPT	60
	ADDQ.L	#8,A0
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,N(A1)
N	SET	N+160
	ENDR
	RTS

AFFICH5
N	SET	0
	REPT	60
	CLR.L	N-8(A1)
	CLR.L	N-4(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A3
	MOVEM.L	D0-D7/A2-A3,N(A1)
N	SET	N+160
	ENDR
	RTS

	*** CLIPPING DROIT !!! ***

AFFICH21
N	SET	0
	REPT	60
	MOVE.L	(A0)+,N(A1)
	MOVE.L	(A0)+,N+4(A1)
	LEA	32(A0),A0
N	SET	N+160
	ENDR
	RTS

AFFICH22
N	SET	0
	REPT	60
	MOVE.L	(A0)+,N(A1)
	MOVE.L	(A0)+,N+4(A1)
	MOVE.L	(A0)+,N+8(A1)
	MOVE.L	(A0)+,N+12(A1)
	LEA	24(A0),A0
N	SET	N+160
	ENDR
	RTS

AFFICH23
N	SET	0
	REPT	60
	MOVEM.L	(A0)+,D0-D5
	MOVEM.L	D0-D5,N(A1)
	LEA	16(A0),A0
N	SET	N+160
	ENDR
	RTS

AFFICH24
N	SET	0
	REPT	60
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,N(A1)
	ADDQ.L	#8,A0
N	SET	N+160
	ENDR
	RTS

AFFICH25
N	SET	0
	REPT	60
	MOVEM.L	(A0)+,D0-D7/A2-A3
	MOVEM.L	D0-D7/A2-A3,N(A1)
N	SET	N+160
	ENDR
	RTS

	*** SOUS-ROUTINES D'INITS ***
PRP_QUART	LEA	PACK_ADR,A6
	LEA	PIC_ROND,A0
	LEA	BUF_CERCLE,A1
	BSR	PREDECAL
	LEA	PIC_ROND+32,A0
	BSR	PREDECAL
	LEA	PIC_ROND+(64*60),A0
	BSR	PREDECAL
	LEA	PIC_ROND+(64*60)+32,A0
	BSR	PREDECAL
	RTS

PREDECAL	MOVE.W	#59,D6	;AFFICHE LE QUART DE CERCLE EN MEMOIRE
PREAF2	MOVE.W	#7,D7
PREAF1	MOVE.L	(A0)+,(A1)+
	DBRA	D7,PREAF1
	CLR.L	(A1)+
	CLR.L	(A1)+
	LEA	32(A0),A0
	DBRA	D6,PREAF2

	LEA	-(40*60)(A1),A0	;ROUTINE DE PREDECALAGES
	MOVE.L	A0,(A6)+

	MOVE.W	#2,D3
RECOM_DEC	MOVE.L	A1,(A6)+
	MOVE.W	#(10*60)-1,D7
RECOPY1	MOVE.L	(A0)+,(A1)+
	DBRA	D7,RECOPY1
	MOVE.W	#3,D4		;DECALAGES DE 4-->4 DECALAGES
NB_DECAL	MOVE.W	#59,D5
DECALY	MOVE.W	#3,D6
DECAL2	MOVE.W	#4,D7
DECAL1	ROXR	(A0)
	ADDQ.L	#8,A0
	DBRA	D7,DECAL1
	LEA	-38(A0),A0
	DBRA	D6,DECAL2
	LEA	32(A0),A0
	DBRA	D5,DECALY
	LEA	-(40*60)(A0),A0
	DBRA	D4,NB_DECAL
	DBRA	D3,RECOM_DEC
	RTS

ULT_1_VBL	TST.W	VBL_FLAG
	BNE.S	ULT_1_VBL

	BSR	AFF_TRAJET

	MOVE.L	PNT_FADE,A0
	TST.W	ANNEX
	BNE.S	FIN_FD1
	MOVE.W	#2,ANNEX
	MOVE.W	#$8240,A1
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,(A1)

	CMPA.L	#FIN_FADE,A0
	BNE.S	END_2
	LEA	PALET_BLANCS,A0
END_2	CMPI.L	#-1,(A0)
	BNE.S	END
	MOVE.W	#64,ANNEX
	MOVE.L	#SUCC_ROUTIN,ANNEX2
	MOVE.W	#1,VBL_FLAG
	BRA	ULT_2_VBL
END	MOVE.L	A0,PNT_FADE
FIN_FD1	SUBQ.W	#1,ANNEX

	MOVE.W	#1,VBL_FLAG
	BRA	ULT_1_VBL

ULT_2_VBL	TST.W	VBL_FLAG
	BNE.S	ULT_2_VBL

	BSR	AFF_TRAJET

	MOVE.L	PNT1STARS,A0
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	TABLE_DEC,A0
	MOVE.L	(A0,D0.W),A0
	LSR	#3,D1
	ADD.W	D1,D1
	ADD.W	D1,D1
	LEA	SUCC_ROUTIN,A1
	MOVE.L	(A1,D1.W),A2
	MOVE.L	SCREEN1,A1
	LEA	160*43(A1),A1
	JSR	(A2)

	MOVE.L	PNT2STARS,A0
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	TABLE2DEC,A0
	MOVE.L	(A0,D0.W),A0
	SUBA.W	D1,A0
	MOVE.W	D1,D2
	LSR	#3,D1
	ADD.W	D1,D1
	ADD.W	D1,D1
	LEA	SUCC2ROUTIN,A1
	MOVE.L	(A1,D1.W),A2
	MOVE.L	SCREEN1,A1
	LEA	160*64(A1),A1
	ADDA.W	D2,A1
	JSR	(A2)

	;160*43,160*64

	ADDQ.L	#4,PNT1STARS
	ADDQ.L	#4,PNT2STARS
 	MOVE.L	PNT2STARS,A0
	CMPI.L	#-1,(A0)
	BNE.S	PAS_FIN_ROUT
	MOVE.W	#1,VBL_FLAG
	BRA	ULT_3_VBL

PAS_FIN_ROUT	MOVE.W	#1,VBL_FLAG
	BRA	ULT_2_VBL

ULT_3_VBL	TST.W	VBL_FLAG
	BNE.S	ULT_3_VBL

	BSR	AFF_TRAJET

	SUBQ.W	#1,ANNEX
	TST.W	ANNEX
	BNE.S	SUITTT

	MOVE.L	#CIRCULE,PNT1STARS
	MOVE.W	#1,VBL_FLAG
	BRA	ULT_4_VBL

SUITTT	MOVE.W	#1,VBL_FLAG
	BRA	ULT_3_VBL

ULT_4_VBL	TST.W	VBL_FLAG
	BNE.S	ULT_4_VBL

	BSR	AFF_TRAJET

	MOVE.L	PNT1STARS,A2
	LEA	TABLE_ADR,A0
	ADDA.W	(A2)+,A0
	MOVE.L	(A0),A1
	MOVE.L	SCREEN1,A0
	ADDA.W	(A2)+,A0
	JSR	(A1)

	CMPI.L	#-1,(A2)
	BNE.S	FIGHT_OK
	SUBQ.W	#4,A2
FIGHT_OK	MOVE.L	A2,PNT1STARS

	MOVE.W	#1,VBL_FLAG
	BRA	ULT_4_VBL

ROUTINE_1
N	SET	0
	REPT	21
	LEA	160(A0),A0
	MOVE.L	(A0)+,N(A1)
	MOVE.L	(A0)+,N+4(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_2
N	SET	0
	REPT	21
	LEA	152(A0),A0
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,N(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_3
N	SET	0
	REPT	21
	LEA	144(A0),A0
	MOVEM.L	(A0)+,D0-D5
	MOVEM.L	D0-D5,N(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_4
N	SET	0
	REPT	21
	LEA	136(A0),A0
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,N(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_5
N	SET	0
	REPT	21
	LEA	128(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A3
	MOVEM.L	D0-D7/A2-A3,N(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_6
N	SET	0
	REPT	21
	LEA	120(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_7
N	SET	0
	REPT	21
	LEA	112(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVE.L	(A0)+,N+48(A1)
	MOVE.L	(A0)+,N+52(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_8
N	SET	0
	REPT	21
	LEA	104(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,N+48(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_9
N	SET	0
	REPT	21
	LEA	96(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D5
	MOVEM.L	D0-D5,N+48(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_10
N	SET	0
	REPT	21
	LEA	88(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,N+48(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_11
N	SET	0
	REPT	21
	LEA	80(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A3
	MOVEM.L	D0-D7/A2-A3,N+48(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_12
N	SET	0
	REPT	21
	LEA	72(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_13
N	SET	0
	REPT	21
	LEA	64(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVE.L	(A0)+,N+96(A1)
	MOVE.L	(A0)+,N+100(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_14
N	SET	0
	REPT	21
	LEA	56(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,N+96(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_15
N	SET	0
	REPT	21
	LEA	48(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D5
	MOVEM.L	D0-D5,N+96(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_16
N	SET	0
	REPT	21
	LEA	40(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,N+96(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_17
N	SET	0
	REPT	21
	LEA	32(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A3
	MOVEM.L	D0-D7/A2-A3,N+96(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_18
N	SET	0
	REPT	21
	LEA	24(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+96(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_19
N	SET	0
	REPT	21
	LEA	16(A0),A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+96(A1)
	MOVE.L	(A0)+,N+144(A1)
	MOVE.L	(A0)+,N+148(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_20
N	SET	0
	REPT	21
	ADDQ.W	#8,A0
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+96(A1)
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,N+144(A1)
N	SET	N+160
	ENDR
	RTS

ROUTINE_21
N	SET	0
	REPT	21
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+48(A1)
	MOVEM.L	(A0)+,D0-D7/A2-A5
	MOVEM.L	D0-D7/A2-A5,N+96(A1)
	MOVEM.L	(A0)+,D0-D3
	MOVEM.L	D0-D3,N+144(A1)
	ADDQ.W	#8,A0
N	SET	N+160
	ENDR
	RTS

IT_VBL	TST.W	VBL_FLAG
	BNE.S	IT_VBL

	MOVE.L	PNT_FADE,A0
	TST.W	TEMPO
	BNE.S	CONT_STARS
	MOVE.W	#5,TEMPO
	MOVEM.W	(A0)+,D0-D2
	MOVEM.W	D0-D2,$FFFF8242.W
	CMPI.L	#-1,(A0)
	BNE.S	CONT_STARS
	SUBQ.W	#6,A0
CONT_STARS	SUBQ.W	#1,TEMPO	
	MOVE.L	A0,PNT_FADE

	BSR	AFF_V_STARS

	BSR	SWAPEC
	CMPI.B	#$39,$FFFFFC02.W
	BNE	FIN_IT_VBL
	MOVE.L	#PALET_3,PNT_FADE
	MOVE.W	#5,TEMPO
	MOVE.W	#1,VBL_FLAG
	BRA	IT_FADE_VBL
FIN_IT_VBL	MOVE.W	#1,VBL_FLAG
	BRA	IT_VBL

IT_FADE_VBL	TST.W	VBL_FLAG
	BNE.S	IT_FADE_VBL

	MOVE.L	PNT_FADE,A0
	TST.W	TEMPO
	BNE.S	NO_FADE_VBL
	MOVE.W	#5,TEMPO
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	CMPI.L	#-1,(A0)
	BNE.S	NO_FADE_VBL
	JMP	MAK_3D_STARS
NO_FADE_VBL	SUBQ.W	#1,TEMPO
	MOVE.L	A0,PNT_FADE
	
	BSR	AFF_V_STARS

	BSR	SWAPEC

	MOVE.W	#1,VBL_FLAG
	BRA	IT_FADE_VBL

AFF_V_STARS	MOVE.L	PNT1STARS,A0
	MOVE.L	-8(A0),A0
	MOVE.L	SCREEN2,A1
	REPT	NB1STARS
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	NOT.W	D0
	AND.W	D0,(A1,D1.W)
	ENDR
	MOVE.L	PNT2STARS,A0
	MOVE.L	-8(A0),A0
	MOVE.L	SCREEN2,A1
	ADDQ.W	#2,A1
	REPT	NB2STARS
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	NOT.W	D0
	AND.W	D0,(A1,D1.W)
	ENDR
	MOVE.L	PNT3STARS,A0
	MOVE.L	-8(A0),A0
	MOVE.L	SCREEN2,A1
	MOVE.L	SCREEN2,A2
	ADDQ.W	#2,A2
	REPT	NB3STARS
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	NOT.W	D0
	AND.W	D0,(A1,D1.W)
	AND.W	D0,(A2,D1.W)
	ENDR
	MOVE.L	PNT1STARS,A0
	MOVE.L	(A0),A0
	MOVE.L	SCREEN2,A1
	REPT	NB1STARS
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	OR.W	D0,(A1,D1.W)
	ENDR
	MOVE.L	PNT2STARS,A0
	MOVE.L	(A0),A0
	MOVE.L	SCREEN2,A1
	ADDQ.W	#2,A1
	REPT	NB2STARS
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	OR.W	D0,(A1,D1.W)
	ENDR
	MOVE.L	PNT3STARS,A0
	MOVE.L	(A0),A0
	MOVE.L	SCREEN2,A1
	MOVE.L	SCREEN2,A2
	ADDQ.W	#2,A2
	REPT	NB3STARS
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	OR.W	D0,(A1,D1.W)
	OR.W	D0,(A2,D1.W)
	ENDR

	ADDQ.L	#4,PNT1STARS
	MOVE.L	PNT1STARS,A0
	CMPI.L	#-1,(A0)
	BNE.S	SUIT_11
	MOVE.L	#TABLE_ADR,PNT1STARS
SUIT_11	ADDQ.L	#4,PNT2STARS
	MOVE.L	PNT2STARS,A0
	CMPI.L	#-1,(A0)
	BNE.S	SUIT_2
	MOVE.L	#TABLE2ADR,PNT2STARS
SUIT_2	ADDQ.L	#4,PNT3STARS
	MOVE.L	PNT3STARS,A0
	CMPI.L	#-1,(A0)
	BNE.S	SUIT_3
	MOVE.L	#TABLE3ADR,PNT3STARS
SUIT_3	RTS

AFF_TRAJET	MOVE.L	PNT1TRAJET,A1
	MOVE.W	(A1)+,D0
	MOVE.W	(A1)+,D1
	MOVE.W	(A1)+,D2
	MOVE.L	SCREEN1,A0
	ADDQ.W	#6,A0
	JSR	REMPLIT
	MOVE.L	PNT2TRAJET,A1
	MOVE.W	(A1)+,D0
	MOVE.W	(A1)+,D1
	MOVE.W	(A1)+,D2
	MOVE.L	SCREEN1,A0
	ADDQ.W	#6,A0
	JSR	REMPLIT
	MOVE.L	PNT3TRAJET,A1
	MOVE.W	(A1)+,D0
	MOVE.W	(A1)+,D1
	MOVE.W	(A1)+,D2
	MOVE.L	SCREEN1,A0
	ADDQ.W	#6,A0
	JSR	REMPLIT
	MOVE.L	PNT4TRAJET,A1
	MOVE.W	(A1)+,D0
	MOVE.W	(A1)+,D1
	MOVE.W	(A1)+,D2
	MOVE.L	SCREEN1,A0
	ADDQ.W	#6,A0
	JSR	REMPLIT

	ADDQ.L	#6,PNT1TRAJET
	ADDQ.L	#6,PNT2TRAJET
	ADDQ.L	#6,PNT3TRAJET
	ADDQ.L	#6,PNT4TRAJET
	MOVE.L	PNT1TRAJET,A1
	CMPI.L	#-1,(A1)
	BNE.S	CONT1TRAJET
	MOVE.W	#1,VBL_FLAG
	JMP	PREP_ETOIL_V

CONT1TRAJET	MOVE.L	A1,PNT1TRAJET
	MOVE.L	PNT2TRAJET,A1
	CMPI.L	#-1,(A1)
	BNE.S	CONT2TRAJET
	SUBQ.W	#6,A1

CONT2TRAJET	MOVE.L	A1,PNT2TRAJET 
	MOVE.L	PNT3TRAJET,A1
	CMPI.L	#-1,(A1)
	BNE.S	CONT3TRAJET
	SUBQ.W	#6,A1

CONT3TRAJET	MOVE.L	A1,PNT3TRAJET 
	MOVE.L	PNT4TRAJET,A1
	CMPI.L	#-1,(A1)
	BNE.S	CONT4TRAJET
	SUBQ.W	#6,A1

CONT4TRAJET	MOVE.L	A1,PNT4TRAJET 
	RTS

ETOIL_VBL	TST.W	VBL_FLAG
	BNE.S	ETOIL_VBL

	MOVE.L	PNT_FADE,A0
	TST.W	TEMPO
	BNE.S	SUIT_ETOIL_3D
	MOVE.W	#5,TEMPO
	MOVEM.L	(A0)+,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	CMPI.L	#-1,(A0)
	BNE.S	SUIT_ETOIL_3D
	LEA	-32(A0),A0
SUIT_ETOIL_3D	SUBQ.W	#1,TEMPO
	MOVE.L	A0,PNT_FADE

	MOVE.L	SCREEN2,A0
	MOVE.L	PNT_STR,A1
	LEA	-NB_STARS*6*2(A1),A1
	REPT	NB_STARS
	MOVE.L	(A1)+,D0
	MOVE.W	(A1)+,D1
	NOT.W	D0
	SWAP	D0
	MOVE.W	D0,D2
	NOT.W	D2
	SWAP	D0
	AND.W	D0,2(A0,D1.W)
	AND.W	D2,(A0,D1.W)
	ENDR

	MOVE.L	SCREEN2,A0
	MOVE.L	PNT_STR,A1
	REPT	NB_STARS
	MOVE.L	(A1)+,D0
	MOVE.W	(A1)+,D1
	OR.L	D0,(A0,D1.W)
	ENDR

	ADDI.L	#NB_STARS*6,PNT_STR
	MOVE.L	PNT_STR,A0
	CMPI.L	#-1,(A0)
	BNE.S	SUIT
	MOVE.L	#BUF_DEST,PNT_STR

SUIT	;MOVE.L	#0,A1
	;LEA	OPTI,A2
	;MOVE.L	SCREEN2,A0
	;LEA	32(A0),A0
	;JSR	CODING
	;LEA	OPTI2,A2
	;MOVE.L	SCREEN2,A0
	;LEA	160*161(A0),A0
	;JSR	CODING2
	JSR	SWAPEC

	SF	$FFFF8240.W

	MOVE.W	#1,VBL_FLAG
	BRA	ETOIL_VBL

PRP_PART_2 	LEA	PIC+34,A0
	MOVE.L	SCREEN2,A1
	JSR	decomp_pc1

	MOVE.L	SCREEN1,A0
	MOVE.W	#7999,D7
EFF_SCRIN2	CLR.L	(A0)+
	DBRA	D7,EFF_SCRIN2

	MOVE.L	#FADE_ULT,PNT_FADE
	MOVE.L	#TRAJET_1,PNT1TRAJET
	MOVE.L	#TRAJET_2,PNT2TRAJET
	MOVE.L	#TRAJET_3,PNT3TRAJET
	MOVE.L	#TRAJET_4,PNT4TRAJET

	MOVE.L	SCREEN2,A0
	MOVE.L	SCREEN1,A1
	LEA	56+(160*28)(A1),A1
	MOVEQ	#11,D6
PREAFFTHE2	MOVEQ	#4,D7
PREAFFTHE1	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,PREAFFTHE1
	LEA	120(A0),A0
	LEA	120(A1),A1
	DBRA	D6,PREAFFTHE2

	LEA	TABLE_DEC,A6
	MOVE.L	SCREEN2,A0
	LEA	160*43(A0),A0
	LEA	BUF_DEST,A1
	MOVE.W	#21-1,D6	;HAUTEUR
AFF_ULT12	MOVEQ	#19,D7
AFF_ULT1	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,AFF_ULT1
	CLR.L	(A1)+
	CLR.L	(A1)+
	DBRA	D6,AFF_ULT12
	LEA	BUF_DEST,A0
	MOVE.L	A0,(A6)+
	MOVEQ	#14,D3
DEC_GEN	MOVE.L	A1,(A6)+
	MOVE.W	#(21*21)-1,D7
AFF_ULT2	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,AFF_ULT2
;	MOVEQ	#1,D4
DEC_ULT4	MOVEQ	#20,D5
DEC_ULT3	MOVEQ	#3,D6
DEC_ULT2	MOVEQ	#20,D7
DEC_ULT1	ROXR	(A0)
	ADDQ.W	#8,A0
	DBRA	D7,DEC_ULT1
	LEA	-166(A0),A0
	DBRA	D6,DEC_ULT2
	LEA	160(A0),A0
	DBRA	D5,DEC_ULT3
	LEA	-(21*168)(A0),A0
;	DBRA	D4,DEC_ULT4
	DBRA	D3,DEC_GEN

	LEA	TABLE2DEC,A6
	MOVE.L	SCREEN2,A0
	LEA	160*64(A0),A0
	MOVE.W	#21-1,D6	;HAUTEUR
AFF2ULT12	MOVEQ	#19,D7
AFF2ULT1	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,AFF2ULT1
	CLR.L	(A1)+
	CLR.L	(A1)+
	DBRA	D6,AFF2ULT12
	LEA	-(168*21)(A1),A0
	MOVE.L	A0,(A6)+
	MOVEQ	#14,D3
DEC2GEN	MOVE.L	A1,(A6)+
	MOVE.W	#(21*21)-1,D7
AFF2ULT2	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,AFF2ULT2
;	MOVEQ	#1,D4
DEC2ULT4	MOVEQ	#20,D5
DEC2ULT3	MOVEQ	#3,D6
DEC2ULT2	MOVEQ	#20,D7
DEC2ULT1	ROXR	(A0)
	ADDQ.W	#8,A0
	DBRA	D7,DEC2ULT1
	LEA	-166(A0),A0
	DBRA	D6,DEC2ULT2
	LEA	160(A0),A0
	DBRA	D5,DEC2ULT3
	LEA	-(21*168)(A0),A0
;	DBRA	D4,DEC2ULT4
	DBRA	D3,DEC2GEN

	BSR	MAK_COD_FIGHT

	MOVE.L	#SINUS_1,PNT1STARS
	MOVE.L	#SINUS_2,PNT2STARS

	MOVE.W	#2,ANNEX

	JMP	ULT_1_VBL

MAK_COD_FIGHT	MOVE.L	SCREEN2,A0
	LEA	32+(160*85)+(160*51)(A0),A0
	LEA	COD_FIGHT,A1
	LEA	TABLE_ADR,A6
	MOVEQ	#50,D6
MAKE_2	MOVE.L	A1,(A6)+
	MOVEQ	#23,D7
MAKE_1	MOVE.W	#$20FC,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,MAKE_1
	MOVE.W	#$41E8,(A1)+
	MOVE.W	#-(96+160),(A1)+
	LEA	-(160+96)(A0),A0
	DBRA	D6,MAKE_2
	MOVE.W	#$4E75,(A1)
	RTS

PREP_ETOIL_V	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
	MOVE.W	#(20*200)-1,D7
EFF_SCR	MOVE.L	(A1)+,(A2)+
	MOVE.L	(A1)+,(A2)+
	DBRA	D7,EFF_SCR

	BSR	ETOIL_V
	MOVE.L	#TABLE_ADR,PNT1STARS
	MOVE.L	#TABLE2ADR,PNT2STARS
	MOVE.L	#TABLE3ADR,PNT3STARS

	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
	MOVE.W	#(20*200)-1,D7
EFF2SCR	MOVE.L	(A1)+,(A2)+
	MOVE.L	(A1)+,(A2)+
	DBRA	D7,EFF2SCR

	JSR	SWAPEC

	CLR.L	$FFFF8242.W
	CLR.W	$FFFF8246.W
	MOVE.L	#PALET_STARS,PNT_FADE
	MOVE.W	#5,TEMPO

	MOVE.W	#1,VBL_FLAG
	BRA	IT_VBL

ETOIL_V	MOVE.L	SCREEN2,A1
	MOVE.W	#(20*200)-1,D7
MAK_MASK_SCR	MOVEM.W	(A1),D0-D3
	OR.W	D1,D0
	OR.W	D2,D0
	OR.W	D3,D0
	NOT.W	D0
	MOVE.W	D0,(A1)+
	CLR.W	(A1)+
	CLR.L	(A1)+
	DBRA	D7,MAK_MASK_SCR
	LEA	PNTS,A4
	LEA	CORES,A5
	LEA	BUF_DEST,A2
	
	LEA	TABLE_ADR,A6
	MOVE.W	#199,D6	;NB DE PHASES-1 (200,100 OU 50)
	MOVE.W	#NB1STARS-1,D5	;NB D'ETOILES-1
	LEA	COOR_ETOIL_V,A3
	MOVE.W	#160,ANNEX
	MOVE.L	#TABLE_ADR,ANNEX2
	BSR	STARS_DEC2
	LEA	TABLE2ADR,A6
	MOVE.W	#99,D6	;NB DE PHASES-1 (200,100 OU 50)
	MOVE.W	#NB2STARS-1,D5	;NB D'ETOILES-1
	LEA	COOR_ETOIL_V+(NB1STARS*6),A3
	MOVE.W	#160*2,ANNEX
	MOVE.L	#TABLE2ADR,ANNEX2
	BSR	STARS_DEC2
	LEA	TABLE3ADR,A6
	MOVE.W	#49,D6	;NB DE PHASES-1 (200,100 OU 50)
	MOVE.W	#NB3STARS-1,D5	;NB D'ETOILES-1
	LEA	COOR_ETOIL_V+((NB1STARS+NB2STARS)*6),A3
	MOVE.W	#160*4,ANNEX
	MOVE.L	#TABLE3ADR,ANNEX2
	BSR	STARS_DEC2
	RTS

STARS_DEC2	MOVE.L	A2,(A6)+
	MOVE.W	D5,D7
	MOVE.L	A3,A0
STARS_DEC1	MOVE.W	(A0)+,D0	;X AND 15
	MOVE.W	(A0)+,D1	;(X AND -16)/2
	MOVE.W	(A0),D2	;Y*160
	SUB.W	ANNEX,D2
	CMPI.W	#-160,D2
	BNE.S	NO_DEP
	MOVE.W	#160*199,D2
NO_DEP	CMPI.W	#-160*2,D2
	BNE.S	NO2DEP
	MOVE.W	#160*198,D2
NO2DEP	CMPI.W	#-160*3,D2
	BNE.S	NO3DEP
	MOVE.W	#160*197,D2
NO3DEP	CMPI.W	#-160*4,D2
	BNE.S	NO4DEP
	MOVE.W	#160*196,D2
NO4DEP	MOVE.W	D2,(A0)+
 	MOVE.L	SCREEN2,A1
	ADD.W	D1,A1
	ADD.W	D2,A1
	MOVE.W	(A1),D3
	MOVE.W	D0,D4
	ADD.W	D4,D4
	MOVE.W	(A5,D4.W),D4
	BTST	D4,D3
	BNE.S	AFFICH
	MOVE.L	#0,(A2)+
CONT_SUIT	DBRA	D7,STARS_DEC1
	DBRA	D6,STARS_DEC2
	MOVE.L	ANNEX2,A0
	MOVE.L	-8(A6),-8(A0)
	MOVE.L	-4(A6),-4(A0)
	MOVE.L	#-1,(A6)
	RTS

AFFICH	ADD.W	D0,D0
	MOVE.W	(A4,D0.W),(A2)+
	ADD.W	D2,D1
	MOVE.W	D1,(A2)+
	BRA	CONT_SUIT

MAK_3D_STARS	LEA	PIC_TETE+34,A0
	MOVE.L	SCREEN1,A1
	BSR	decomp_pc1
	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	MOVE.W	#(20*200)-1,D7
MAK_3D_MASK	MOVEM.W	(A0),D0-D3
	OR.W	D1,D0
	OR.W	D2,D0
	OR.W	D3,D0
	NOT.W	D0
	MOVE.W	D0,(A1)
	CLR.L	2(A1)
	CLR.W	6(A1)
	ADDQ.W	#8,A0
	ADDQ.W	#8,A1
	DBRA	D7,MAK_3D_MASK

	BSR	CALCULE

	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	MOVE.W	#7999,D7
AAAAA	MOVE.L	(A0)+,(A1)+
	DBRA	D7,AAAAA

	MOVE.L	#PALET_4,PNT_FADE
	MOVE.W	#1,VBL_FLAG
	JMP	ETOIL_VBL

CALCULE	LEA	BUF_DEST,A5
	MOVE.W	#(768/VITESSE)-1,D7	;NOMBRE DE PHASE

RECOM_CALC	LEA	STAR,A0

	MOVE.W	#NB_STARS-1,D6

RECOM2	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	MOVE.W	(A0),D2

;	MOVE.W	#$700,$FFFF8240.W
	SUB.W	#VITESSE,D2
	CMPI.W	#-255,D2
	BGT.S	OK
	ADD.W	#512+255,D2
OK	MOVE.W	D2,(A0)+
	MOVE.W	D2,D3
	EXT.L	D2
	SWAP	D2
	ASR.L	#8,D2
	ADDI.L	#$10000,D2
	ASR.L	#5,D2
	ASR.L	#4,D2

	EXT.L	D0
	ASL.W	#7,D0
	TST.W	D2
	BNE.S	DIVIS_1
	MOVEQ	#1,D2
DIVIS_1	DIVS.W	D2,D0

	EXT.L	D1
	ASL.W	#7,D1
	DIVS.W	D2,D1

	ADDI.W	#X_CENTER,D0
	ADDI.W	#Y_CENTER,D1

	BSR	CALC_ECRAN
;	MOVE.W	#$000,$FFFF8240.W

	DBRA	D6,RECOM2

	DBRA	D7,RECOM_CALC

	LEA	-NB_STARS*6*2(A5),A5
	MOVE.L	#BUF_DEST,PNT_STR

	LEA	BUF_DEST,A0
	LEA	-NB_STARS*6*2(A0),A0
	MOVE.W	#(NB_STARS*2)-1,D7
FINAL	MOVE.L	(A5)+,(A0)+
	MOVE.W	(A5)+,(A0)+
	DBRA	D7,FINAL

	MOVE.L	#-1,(A5)

	RTS

CALC_ECRAN	CMPI.W	#320,D0
	BGE	CLIP
	TST.W	D0
	BLT	CLIP
	CMPI.W	#195,D1
	BGE.S	CLIP
	TST.W	D1
	BLT.S	CLIP
	LEA	PNTS,A3
	MOVE.W	D0,D2
	ANDI.W	#15,D0
	MOVEQ	#0,D5
	ADD.W	D0,D0
	MOVE.W	D0,D5	;TEST
	MOVE.W	(A3,D0.W),D0

	ANDI.W	#-16,D2
	LSR	#1,D2
	MULU.W	#160,D1
	ADD.W	D2,D1

	MOVE.L	SCREEN2,A3
	ADDA.W	D1,A3
	MOVE.W	(A3),D4
	LEA	CORES,A4
	MOVE.W	(A4,D5.W),D5
	BTST	D5,D4
	BEQ	CLIP

	CMPI.W	#120,D3
	BLT	PLAN_1
	CMPI.W	#310,D3
	BLT	PLAN_2
	
	MOVE.W	D0,D2
	SWAP	D0
	MOVE.W	D2,D0
	MOVE.L	D0,(A5)+
	MOVE.W	D1,(A5)+
	RTS

PLAN_1	SWAP	D0
	CLR.W	D0
	SWAP	D0
	MOVE.L	D0,(A5)+
	MOVE.W	D1,(A5)+
	RTS

PLAN_2	SWAP	D0
	CLR.W	D0
	MOVE.L	D0,(A5)+
	MOVE.W	D1,(A5)+
	RTS

CLIP	MOVE.L	#0,(A5)+
	MOVE.W	#0,(A5)+
	RTS

	INCLUDE	DEC_PC1.COD

	INCLUDE	REMPLISS.S
	
	SECTION	DATA
PIC	INCBIN	ULTIMAT2.PC1

PIC_TETE	INCBIN	003.PC1

STAR	INCBIN	STARDEMI.STR

CODES	INCBIN	STYWOX.BIN

CODES2	INCBIN	STYWOX.EFF

PIC_ROND	INCBIN	ROND_STY.PIC

COOR_ETOIL_V	INCBIN	STARS_V.STR

SINUS_1	INCBIN	SINUS_1.ULT
	DC.L	-1

SINUS_2	INCBIN	SINUS_2.ULT
	DC.L	-1

CORES	DC.W	15,14,13,12,11,10,9,8,7,6,5,4,3,2,1,0

FADE_ULT	DC.W	0,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000
	DC.W	0,$000,$000,$000,$110,$110,$110,$110,$000,$100,$000,$000,$000,$100,$000,$111
	DC.W	0,$111,$000,$000,$220,$220,$220,$220,$000,$200,$100,$000,$000,$210,$000,$222
	DC.W	0,$222,$111,$000,$331,$330,$330,$330,$000,$310,$200,$100,$000,$320,$000,$333
	DC.W	0,$333,$222,$000,$442,$440,$440,$440,$000,$420,$310,$200,$100,$430,$100,$444
	DC.W	0,$444,$333,$111,$553,$551,$550,$550,$100,$530,$420,$310,$200,$540,$200,$555
	DC.W	0,$555,$444,$222,$664,$662,$661,$660,$200,$640,$530,$420,$310,$650,$300,$666
FIN_FADE	DC.L	-1

PALET_BLANCS	DC.W	0,$666,$555,$333,$775,$773,$772,$770,$310,$750,$640,$530,$420,$760,$400,$777
	DC.W	0,$777,$666,$444,$776,$774,$773,$771,$421,$761,$751,$641,$531,$771,$511,$777
	DC.W	0,$777,$777,$555,$777,$775,$774,$772,$532,$772,$762,$752,$642,$772,$622,$777
	DC.W	0,$777,$777,$666,$777,$776,$775,$773,$643,$773,$773,$763,$753,$773,$733,$777
	DC.W	0,$777,$777,$777,$777,$777,$776,$774,$754,$774,$774,$774,$764,$774,$744,$777
	DC.W	0,$777,$777,$777,$777,$777,$777,$775,$765,$775,$775,$775,$775,$775,$755,$777
	DC.W	0,$777,$777,$777,$777,$777,$777,$776,$776,$776,$776,$776,$776,$776,$766,$777
	DC.W	0,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777

	DC.W	0,$777,$777,$777,$777,$777,$777,$776,$776,$776,$776,$776,$776,$776,$766,$777
	DC.W	0,$777,$777,$777,$777,$777,$777,$775,$765,$775,$775,$775,$775,$775,$755,$777
	DC.W	0,$777,$777,$777,$777,$777,$776,$774,$754,$774,$774,$774,$764,$774,$744,$777
	DC.W	0,$777,$777,$666,$777,$776,$775,$773,$643,$773,$773,$763,$753,$773,$733,$777
	DC.W	0,$777,$777,$555,$777,$775,$774,$772,$532,$772,$762,$752,$642,$772,$622,$777
	DC.W	0,$777,$666,$444,$776,$774,$773,$771,$421,$761,$751,$641,$531,$771,$511,$777
	DC.W	0,$666,$555,$333,$775,$773,$772,$770,$310,$750,$640,$530,$420,$760,$400,$777
	DC.L	-1

SUCC_ROUTIN	DC.L	ROUTINE_1,ROUTINE_2,ROUTINE_3,ROUTINE_4,ROUTINE_5,ROUTINE_6
	DC.L	ROUTINE_7,ROUTINE_8,ROUTINE_9,ROUTINE_10,ROUTINE_11,ROUTINE_12
	DC.L	ROUTINE_13,ROUTINE_14,ROUTINE_15,ROUTINE_16,ROUTINE_17
	DC.L	ROUTINE_18,ROUTINE_19,ROUTINE_20
	DC.L	-1

SUCC2ROUTIN	DC.L	ROUTINE_20,ROUTINE_19,ROUTINE_18,ROUTINE_17,ROUTINE_16,ROUTINE_15
	DC.L	ROUTINE_14,ROUTINE_13,ROUTINE_12,ROUTINE_11,ROUTINE_10,ROUTINE_9
	DC.L	ROUTINE_8,ROUTINE_7,ROUTINE_6,ROUTINE_5,ROUTINE_4
	DC.L	ROUTINE_3,ROUTINE_2,ROUTINE_1
	DC.L	-1
TRAJET_1
N	SET	132
	REPT	128
	DC.W	132,N,160*28
N	SET	N-1	
	ENDR
N	SET	29
	REPT	143-28
	DC.W	5,5,160*N
N	SET	N+1
	ENDR
N	SET	6
	REPT	156
	DC.W	6,N,160*143
N	SET	N+1
	ENDR
	DC.L	-1

TRAJET_2
N	SET	132
	REPT	125
	DC.W	132,N,160*31
N	SET	N-1
	ENDR
N	SET	31
	REPT	140-30
	DC.W	8,8,160*N
N	SET	N+1
	ENDR
N	SET	9
	REPT	151
	DC.W	9,N,160*140
N	SET	N+1
	ENDR
	DC.L	-1

TRAJET_3
N	SET	188
	REPT	314-187
	DC.W	188,N,160*36
N	SET	N+1
	ENDR
N	SET	37
	REPT	143-36
	DC.W	314,314,160*N
N	SET	N+1
	ENDR
N	SET	313
	REPT	156
	DC.W	313,N,160*143
N	SET	N-1
	ENDR
	DC.L	-1

TRAJET_4	
N	SET	188
	REPT	311-187
	DC.W	188,N,160*39
N	SET	N+1
	ENDR
N	SET	40
	REPT	140-39
	DC.W	311,311,160*N
N	SET	N+1
	ENDR
N	SET	310
	REPT	155
	DC.W	310,N,160*140
N	SET	N-1
	ENDR
	DC.L	-1

CIRCULE
N	SET	50
	REPT	50
	DC.W	(N*4),32+(160*199)
N	SET	N-1
	ENDR
N	SET	199
	REPT	199-84-51
	DC.W	0,32+(160*N)
N	SET	N-1
	ENDR
	DC.L	-1

PALET	DC.W	0,$010,$021,$032,$043,$054,$065,$076,$276,$576,$700,$755,$500,$400,$377,$777
	DC.W	0,$121,$132,$143,$154,$165,$176,$177,$377,$677,$711,$766,$611,$511,$477,$777
	DC.W	0,$232,$243,$254,$265,$276,$277,$277,$477,$777,$722,$777,$722,$622,$577,$777
	DC.W	0,$343,$354,$365,$376,$377,$377,$377,$577,$777,$733,$777,$733,$733,$677,$777
	DC.W	0,$454,$465,$476,$477,$477,$477,$477,$677,$777,$744,$777,$744,$744,$777,$777
	DC.W	0,$565,$576,$577,$577,$577,$577,$577,$777,$777,$755,$777,$755,$755,$777,$777
	DC.W	0,$676,$677,$677,$677,$677,$677,$677,$777,$777,$766,$777,$766,$766,$777,$777
	DC.W	0,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777,$777
	DC.W	0,$676,$677,$677,$677,$677,$677,$677,$777,$777,$766,$777,$766,$766,$777,$777
	DC.W	0,$565,$576,$577,$577,$577,$577,$577,$777,$777,$755,$777,$755,$755,$777,$777
	DC.W	0,$454,$465,$476,$477,$477,$477,$477,$677,$777,$744,$777,$744,$744,$777,$777
	DC.W	0,$343,$354,$365,$376,$377,$377,$377,$577,$777,$733,$777,$733,$733,$677,$777
	DC.W	0,$232,$243,$254,$265,$276,$277,$277,$477,$777,$722,$777,$722,$622,$577,$777
	DC.W	0,$121,$132,$143,$154,$165,$176,$177,$377,$677,$711,$766,$611,$511,$477,$777
	DC.W	0,$010,$021,$032,$043,$054,$065,$076,$276,$576,$700,$755,$500,$400,$377,$777
	DC.L	-1

ROULO	INCBIN	ANIM_2.CRB
N	SET	0
N2	SET	0
	REPT	120
	DC.W	N,N2
N	SET	N+160
N2	SET	N2+64
	ENDR
	DC.W	-1
N	SET	0
N2	SET	0
	REPT	120
	DC.W	N,N2
N	SET	N+160
N2	SET	N2+64
	ENDR
	DC.W	-1
	DC.L	-1

ANIM_COUL	DC.L	0,$01110111,$02220222,$03330333,$04440444,$05550555,$06660666,$07770777
	DC.L	$07660666,$07550555,$07440544,$07330533,$07220522,$07110511,$07000500,-1

PRESENT_GRF	INCBIN	PRESENTS.PIC
	
	*** COURBE DU QUART DE CERCLE HAUT-GAUCHE ***
COURB_1	DC.W	0,4,8,12,-1

ROUTS_1	DC.W	0
	DC.L	AFFICH1
	DC.W	0
	DC.L	AFFICH2
	DC.W	0
	DC.L	AFFICH3
	DC.W	0
	DC.L	AFFICH4
N	SET	0
	REPT	6
	DC.W	N
	DC.L	AFFICH5
N	SET	N+8
	ENDR
	DC.L	-1

COURB_2	DC.W	12,8,4,0,-1

ROUTS_2	DC.W	152
	DC.L	AFFICH21
	DC.W	144
	DC.L	AFFICH22
	DC.W	136
	DC.L	AFFICH23
	DC.W	128
	DC.L	AFFICH24
N	SET	120
	REPT	6
	DC.W	N
	DC.L	AFFICH25
N	SET	N-8
	ENDR
	DC.L	-1

COURB_Y	
N	SET	0
	REPT	40
	DC.W	N*160
N	SET	N+1
	ENDR
	DC.W	-1

COURB_Y_2
N	SET	199-60
	REPT	40
	DC.W	N*160
N	SET	N-1
	ENDR
	DC.W	-1

	DC.W	0,40*160
DEPLAC_R
N	SET	40
	REPT	40
	DC.W	N*160
	DC.W	N*160
N	SET	N-1
	ENDR
	DC.W	0
	DC.W	0
	DC.W	0
	DC.W	-1

	DC.W	200
	DC.W	200
STYWOX_SCR	INCBIN	ARR_STY.CRB
	DC.W	0
 	DC.W	-1

PALET_1	
	DC.W	0,$010,$021,$032,$043,$054,$065,$076,$276,$576,$700,$755,$500,$400,$377,$777
	DC.W	0,$000,$010,$021,$032,$043,$054,$065,$165,$465,$600,$644,$400,$300,$266,$666
	DC.W	0,$000,$000,$010,$021,$032,$043,$054,$054,$354,$500,$533,$300,$200,$155,$555
	DC.W	0,$000,$000,$000,$010,$021,$032,$043,$043,$243,$400,$422,$200,$100,$044,$444
	DC.W	0,$000,$000,$000,$000,$010,$021,$032,$032,$132,$300,$311,$100,$000,$033,$333
	DC.W	0,$000,$000,$000,$000,$000,$010,$021,$021,$021,$200,$200,$000,$000,$022,$222
	DC.W	0,$000,$000,$000,$000,$000,$000,$010,$010,$010,$100,$100,$000,$000,$011,$111
	DC.W	0,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000
	DC.L	-1

PALET_2	DC.W	0,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000
	DC.W	0,$000,$000,$000,$110,$110,$110,$110,$100,$100,$000,$000,$000,$000,$000,$111
	DC.W	0,$000,$000,$000,$220,$220,$220,$220,$210,$200,$100,$000,$000,$000,$000,$222
	DC.W	0,$000,$000,$000,$331,$330,$330,$330,$320,$310,$200,$100,$000,$000,$000,$333
	DC.W	0,$000,$000,$000,$442,$440,$440,$440,$430,$420,$310,$200,$100,$000,$100,$444
	DC.W	0,$000,$000,$000,$553,$551,$550,$550,$540,$530,$420,$310,$200,$100,$200,$555
	DC.W	0,$000,$000,$000,$664,$662,$661,$660,$650,$640,$530,$420,$310,$200,$300,$666
	DC.W	0,$000,$000,$000,$775,$773,$772,$770,$760,$750,$640,$530,$420,$310,$400,$777
	DC.L	-1

PALET_3	DC.W	0,$222,$444,$777,$775,$773,$772,$770,$310,$750,$640,$530,$420,$760,$400,$777
	DC.W	0,$111,$333,$666,$664,$662,$661,$660,$200,$640,$530,$420,$310,$650,$300,$666
	DC.W	0,$000,$222,$555,$553,$551,$550,$550,$100,$530,$420,$310,$200,$540,$200,$555
	DC.W	0,$000,$111,$444,$442,$440,$440,$440,$000,$420,$310,$200,$100,$430,$100,$444
	DC.W	0,$000,$000,$333,$331,$330,$330,$330,$000,$310,$200,$100,$000,$320,$000,$333
	DC.W	0,$000,$000,$222,$220,$220,$220,$220,$000,$200,$100,$000,$000,$210,$000,$222
	DC.W	0,$000,$000,$111,$110,$110,$110,$110,$000,$100,$000,$000,$000,$100,$000,$111
	DC.W	0,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000
	DC.L	-1

PALET_4	DC.W	0,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000,$000
	DC.W	0,$000,$000,$000,$000,$000,$100,$000,$000,$000,$000,$000,$000,$000,$100,$111
	DC.W	0,$000,$011,$000,$000,$100,$210,$000,$000,$000,$000,$000,$000,$000,$210,$222
	DC.W	0,$000,$122,$000,$100,$210,$321,$100,$000,$011,$000,$000,$000,$000,$320,$333
	DC.W	0,$011,$233,$000,$210,$321,$432,$210,$000,$122,$000,$000,$100,$000,$431,$444
	DC.W	0,$122,$344,$000,$321,$432,$543,$320,$000,$233,$100,$011,$210,$000,$542,$555
	DC.W	0,$233,$455,$011,$432,$543,$654,$431,$100,$344,$210,$122,$321,$000,$653,$666
	DC.W	0,$344,$566,$122,$543,$654,$765,$542,$210,$455,$321,$233,$432,$011,$764,$777
	DC.L	-1

PALET_STARS	DC.W	$000,$000,$000
	DC.W	$000,$000,$111
	DC.W	$000,$000,$222
	DC.W	$000,$000,$333
	DC.W	$000,$111,$444
	DC.W	$000,$222,$555
	DC.W	$111,$333,$666
	DC.W	$222,$444,$777
	DC.L	-1

PNTS
N	SET	32768
	REPT	16
	DC.W	N
N	SET	N/2
	ENDR
	SECTION	BSS
ANC_PAL	DS.L	8
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
SAUV_SP	DS.L	1
SAUVEC	DS.W	1
IERA	DS.B	1
IERB	DS.B	1
ISRA	DS.B	1
ISRB	DS.B	1
IMRA	DS.B	1
IMRB	DS.B	1
VR	DS.B	1
TACR	DS.B	1
TBCR	DS.B	1
TCDCR	DS.B	1
TADR	DS.B	1
TBDR	DS.B	1
	DS.B	256+160
BUFFER	DS.L	16000
	DS.B	256+160
SCREEN1	DS.L	1
SCREEN2	DS.L	1
ADR_ZIK	DS.L	1
VBL_FLAG	DS.W	1

PNT1TRAJET	DS.L	1
PNT2TRAJET	DS.L	1
PNT3TRAJET	DS.L	1
PNT4TRAJET	DS.L	1
	DS.L	2
TABLE_ADR	DS.L	201
	DS.L	2
TABLE2ADR	DS.L	101
	DS.L	2
TABLE3ADR	DS.L	51
ANNEX	DS.W	1
ANNEX2	DS.L	1
TABLE_DEC	DS.L	16
TABLE2DEC	DS.L	16
PNT1STARS	DS.L	1
PNT2STARS	DS.L	1
PNT3STARS	DS.L	1

PNT_1_CRB	DS.L	1
PNT_1_ROT	DS.L	1
PNT_2_CRB	DS.L	1
PNT_2_ROT	DS.L	1
PNT_1_Y	DS.L	1
PNT_2_Y	DS.L	1
PNT_ANIM	DS.L	1
PNT_DEPR	DS.L	1
COULEUR	DS.W	1
PACK_ADR	DS.L	4*4

VALOR	DS.W	1
PNT_FADE	DS.L	1
TEMPO	DS.W	1
X1	DS.W	1
X2	DS.W	1
PNT_STR	DS.L	1
	DS.B	NB_STARS*6*2
BUF_CERCLE
BUF_DEST	DS.W	((2*NB1STARS)*200)	;85;68000
BUF2DEST	DS.W	((2*NB2STARS)*100)	;50;20000
BUF3DEST	DS.W	((2*NB3STARS)*50)	;40;8000
	DS.B	17000
COD_FIGHT	DS.B	7546
