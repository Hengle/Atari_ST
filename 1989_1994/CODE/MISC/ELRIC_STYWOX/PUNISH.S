	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP
	MOVE.L	D0,SAUV_SP

	MOVE.B	#2,$FFFF820A.W
	CLR.B	$FFFF8260.W
	MOVE.W	#$2700,SR
	MOVE.L	$24.W,SAUV_24
	MOVE.B	$FFFFFA07.W,SAUV_07
	MOVE.B	$FFFFFA09.W,SAUV_09
	MOVE.L	$C.W,SAUV_C
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA07.W
	LEA	DATAS,A0
	MOVE.W	#$17F,D0
	MOVE.L	#$636F6361,$24.W	;$636F6361='coca'
	MOVE.L	#$636F6C61,D1		;$636F6C61='cola'
	MOVE.L	#$12345678,$C.W
	LEA	$FFFF8209.W,A1
	MOVEQ	#0,D3
	MOVE.W	#$2704,SR

L600D4	MOVE.B	(A1),D3
	CMP.W	#$40,D3
	BLT.S	L600D4
	SUB.W	#$40,D3
	NOT.W	D3
	LSR.W	D3,D3
L600E4	EOR.L	D1,(A0)
	ADD.L	A0,D1
	MOVE.B	(A1),D1
	ROR.L	#7,D1
	MOVE	SR,D1
	ADD.L	$24.W,D1
	NOT.L	D1
	ADD.L	$C.W,D1
	MOVE.B	$FFFFFA09.W,D1
	ADDQ.L	#4,A0
	ADDI.L	#$4E4D4521,$24.W	;$4E4D4521='NME!'
	ADDI.L	#$534C494D,$C.W	;$534C494D='SLYM'
	CLR.L	$426.W
	DBF	D0,L600E4

	MOVE.B	SAUV_07,$FFFFFA07.W
	MOVE.B	SAUV_09,$FFFFFA09.W
	MOVE.L	SAUV_24,$24.W
	MOVE.L	SAUV_C,$C.W

	MOVE.W	#$2300,SR

	CLR.W	-(SP)
	PEA	F_NAME
	MOVE.W	#$3C,-(SP)
	TRAP	#1
	ADDQ.W	#8,SP
	MOVE.W	D0,HANDLE
	
	PEA	DATAS
	MOVE.L	#$600,-(SP)
	MOVE.W	HANDLE,-(SP)
	MOVE.W	#$40,-(SP)
	TRAP	#1
	LEA	12(SP),SP

	MOVE.W	HANDLE,-(SP)
	MOVE.W	#$3E,-(SP)
	TRAP	#1
	ADDQ.W	#4,SP

	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	TRAP	#1
	ADDQ.W	#2,SP

	SECTION	DATA
F_NAME	DC.B	"DECODê.BIN",0
	EVEN

	DS.L	100
	DS.L	100
DATAS	INCBIN	DECOD.BIN
	DS.L	100
	DS.L	100
	SECTION	BSS
SAUV_SP	DS.L	1
SAUV_24	DS.L	1
SAUV_C	DS.L	1
SAUV_07	DS.B	1
SAUV_09	DS.B	1
HANDLE	DS.W	1