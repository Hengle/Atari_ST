DEBUT:	MOVEA.L  A7,A5
	LEA.L 	D_PILE,A7
	MOVE.L 	4(A5),A5
	MOVE.L 	$C(A5),D0
	ADD.L 	$14(A5),D0
	ADD.L 	$1C(A5),D0
	ADD.L 	#$100,D0		
	MOVE.L 	D0,-(SP)		
        MOVE.L 	A5,-(SP)
	MOVE 	#0,-(SP)
	MOVE 	#$4A,-(SP)
	TRAP 	#1
	LEA 	12(SP),SP
	
	CLR.L 	-(SP)
	MOVE 	#$20,-(SP)
	TRAP 	#1
	ADDQ.L 	#6,SP
	MOVE.L 	D0,SAUVE

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W

	MOVEA.L	#DANLOGO+34,A0
;	ADDA.L	#160*100,A0
	MOVE.L	A0,DEPART
	
	CLR.L	PLUS

			
	MOVE.B	$FFFFFA07.W,MFP1
	MOVE.B	$FFFFFA09.W,MFP2
	MOVE.B	$FFFFFA0F.W,MFP3
	MOVE.B	$FFFFFA13.W,MFP4
	MOVE.B	$FFFFFA1B.W,MFP5
	MOVE.B	$FFFFFA21.W,MFP6
	
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	
	MOVE	#4,-(SP)
	TRAP 	#14
	ADDQ.L	#2,SP
	MOVE	D0,RES
	
	MOVE	#2,-(SP)
	TRAP 	#14
	ADDQ.L 	#2,SP
	MOVE.L	D0,ANC_ECR
	
	CLR.W 	-(SP)
	MOVE.L	#$78000,-(SP)
	MOVE.L	#$78000,-(SP)
	MOVE 	#5,-(SP)
	TRAP 	#14
	LEA.L 	12(SP),SP


	LEA.L	$FFFF8240.W,A0
	MOVEM.L	(A0),D0-D7
	LEA.L	ANC_PAL,A0
	MOVEM.L	D0-D7,(A0)
	
        
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	#VBL_IR,$70.W
	MOVE.L	$120.W,ANC_TB
	MOVE.L	#TB0_IR,$120.W
	
	DC.W	$A00A
	MOVE.B	#$12,$FFFFFC02.W
	
	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W



BOUCLE	JMP	BOUCLE

FIN:	
	MOVE.B	#8,$FFFFFC02.W
	MOVE.B	MFP1,$FFFFFA07.W
	MOVE.B	MFP2,$FFFFFA09.W
	MOVE.B	MFP3,$FFFFFA0F.W
	MOVE.B	MFP4,$FFFFFA13.W
	MOVE.B	MFP5,$FFFFFA1B.W
	MOVE.B	MFP6,$FFFFFA21.W
	
	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TB,$120.W
	
	LEA.L	ANC_PAL,A0
	MOVEM.L	(A0),D0-D7
	LEA.L	$FFFF8240.W,A0
	MOVEM.L	D0-D7,(A0)
	
	MOVE 	RES,-(SP)	
	MOVE.L	ANC_ECR,-(SP)	
	MOVE.L	ANC_ECR,-(SP)	
	MOVE 	#5,-(SP)	
	TRAP 	#14		
	LEA.L 	12(SP),SP	
	
	MOVE.L	SAUVE,-(SP)	
	MOVE 	#$20,-(SP)	
	TRAP 	#1		
	ADDQ.L 	#6,SP		
	
;	PEA	CHUT_DA	   	
;	MOVE	#$20,-(A7) 	
;	TRAP	#14        	
;	ADDQ.L	#6,A7      	

	CLR.W 	-(SP)		
	TRAP	#1		
	





VBL_IR	

	CLR.B	$FFFFFA1B.W
	MOVE.B	#199,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W

	MOVEM.L	PAL_DAN,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	
	
	MOVEA.L	#$78000,A1
	ADDA.L	#160*100,A1
	
	MOVEA.L	DEPART,A0
	ADDA.L	PLUS,A0
	
	MOVE.L	PLUS,D0
	ADD.L	#160,D0
	CMPI.L	#16000+160,D0
	BNE.S	OK
	SUB.L	#160,D0
OK	MOVE.L	D0,PLUS

INC	SET	0
	REPT	100
	MOVEM.L	INC(A0),D0-D7/A2-A6
	MOVEM.L	D0-D7/A2-A6,INC(A1)
	
	MOVEM.L	INC+52(A0),D0-D7/A2-A6
	MOVEM.L	D0-D7/A2-A6,INC+52(A1)
	
	MOVEM.L	INC+104(A0),D0-D7/A2-A6
	MOVEM.L	D0-D7/A2-A6,INC+104(A1)
	
	MOVEM.L	INC+156(A0),D0
	MOVEM.L	D0,INC+156(A1)
	
INC	SET	INC+160
	ENDR


	MOVEA.L	#$78000,A1
	ADDA.L	#160*200,A1

	MOVEA.L	#$78000,A0
	ADDA.L	#160*199,A0

INC	SET	0
INC2	SET	0
	REPT	33
	MOVEM.L	INC(A0),D0-D7/A2-A6
	MOVEM.L	D0-D7/A2-A6,INC2(A1)
	
	MOVEM.L	INC+52(A0),D0-D7/A2-A6
	MOVEM.L	D0-D7/A2-A6,INC2+52(A1)
	
	MOVEM.L	INC+104(A0),D0-D7/A2-A6
	MOVEM.L	D0-D7/A2-A6,INC2+104(A1)
	
	MOVEM.L	INC+156(A0),D0
	MOVEM.L	D0,INC2+156(A1)
	
INC	SET	INC-320
INC2	SET	INC2+160
	ENDR




	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE


 


TB_IR	
	CLR.B	$FFFFFA1B.W
	MOVE.B	#1,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
;	MOVE.L	#TB0_IR,$120
	BCLR	#0,$FFFFFA0F.W
	RTE			




TB0_IR
	MOVE	#$001,$FFFF8240.W

     REPT 127-4
	NOP	
     ENDR
	CLR.B	$FFFF820A.W
     REPT 10
     	NOP			
     ENDR
	MOVE.B	#2,$FFFF820A.W
	CLR.B	$FFFFFA1B.W
;	MOVE.B	#1,$FFFFFA21.W
;	MOVE.B	#8,$FFFFFA1B.W
;	MOVE.L	#TB0_IR,$120
	BCLR	#0,$FFFFFA0F.W
	RTE			




	 
	section   data
DANLOGO	INCBIN	DAN_NATO.PI1

PAL_DAN	DC.W	$000,$100,$200,$300,$400,$500,$600,$700
	DC.W	$100,$210,$320,$420,$530,$640,$750,$762

PAL_ZAP	DC.W	$000,$101,$212,$323,$545,$767,$112,$001
	DC.W	$143,$022,$677,$347,$007,$222,$333,$777
	

;CHUT_DA	DC.B	7,$FF,6,0,8,0,9,0,$A,0

 	SECTION BSS
 	EVEN
DEPART	DS.L	1
PLUS	DS.L	1
F_PILE	DS.L 	128	
D_PILE	DS.L 	1	
SAUVE	DS.L	1	
ANC_VBL DS.L	1	
ANC_TB	DS.L	1	
ANC_ECR	DS.L	1	
RES	DS.W	1	
ANC_PAL	DS.W	16
MFP1	DS.B	1
MFP2	DS.B	1
MFP3	DS.B	1
MFP4	DS.B	1
MFP5	DS.B	1
MFP6	DS.B	1
