NB_STARS = 400
LIM_1 = 200
LIM_2 = 500
COUL_1 = $FFF
COUL_2 = $444
COUL_3 = $222

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	BSR	PREP_ECR
	BSR	PREP_STARS
	BSR	PREP_ZOOMS
	SF	FLAG_CONT_ZOOM
	MOVE.W	#COUL_1,$FFFF8242.W
	MOVE.W	#COUL_2,$FFFF8244.W
	MOVE.W	#COUL_1,$FFFF8246.W
	MOVE.W	#COUL_3,$FFFF8248.W
	MOVE.W	#COUL_1,$FFFF824A.W
	MOVE.W	#COUL_2,$FFFF824C.W
	MOVE.W	#COUL_1,$FFFF824E.W

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

IT_VBL	;BSR	VSYNC
	TST.B	FLAG_CONT_ZOOM
	BNE.S	.NO_CREATION
	BSR	PREP_NEXT_ZOOM
.NO_CREATION	BRA	IT_VBL

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

GEST_ZOOM	LEA	COLORS_ZOOMS,A0
	MOVEQ	#0,D0
	MOVE.W	NUMER,D0
	ADD.W	D0,D0
	MOVE.W	(A0,D0.W),COLOR_ACTU
	ADD.W	D0,D0
	LEA	ADR_ZOOMS,A1
	MOVE.L	(A1,D0.W),A1
	LEA	TABLE_COD_CARRêS,A2
	LEA	64*99(A2),A2
	LEA	TABLE_COD_CARRêS_CLIP_1,A5
	LEA	128*99(A5),A5
	LEA	TABLE_COD_CARRêS_CLIP_2,A6
	LEA	128*99(A6),A6
	LSL.L	#4,D0
	SUBA.L	D0,A2
	ADD.L	D0,D0
	SUBA.L	D0,A5
	SUBA.L	D0,A6
	MOVEQ	#7,D6
	MOVEQ	#0,D5
.RECOM_AFF_2	MOVE.L	SCREEN2,A4
	ADDQ.W	#6,A4
	ADDA.W	(A1)+,A4	;A1=ADRESSE ECRAN AU Y
	MOVE.W	(A1)+,D7	;NB DE CARRêS SUR LA LIGNE -1
	BLT.S	.CONTINUE
.RECOM_AFF_1	MOVE.L	A4,A0
	MOVEQ	#0,D5
	MOVE.B	(A1)+,D5	;DECALAGE
	ADD.W	D5,D5
	ADD.W	D5,D5
	BTST	#8,D5
	BEQ.S	.NO_1
	ANDI.W	#$FF,D5
	MOVE.L	(A5,D5.W),A3	;ADRESSE DE LA ROUTINE D'AFFICHAGE DU CARRê
	ADDQ.W	#1,A1
	BRA.S	.G
.NO_1	BTST	#9,D5
	BEQ.S	.NO_2
	ANDI.W	#$FF,D5
	MOVE.L	(A6,D5.W),A3	;ADRESSE DE LA ROUTINE D'AFFICHAGE DU CARRê
	ADDQ.W	#1,A1
	MOVE.W	#152,D1
	BRA.S	.H
.NO_2	MOVE.L	(A2,D5.W),A3	;ADRESSE DE LA ROUTINE D'AFFICHAGE DU CARRê
.F	MOVEQ	#0,D1
	MOVE.B	(A1)+,D1	;OFFSET ECRAN
.H	ADDA.W	D1,A0
.G	JSR	(A3)
	DBRA	D7,.RECOM_AFF_1
.CONTINUE	DBRA	D6,.RECOM_AFF_2

	MOVE.W	NUMER,D0
	ADDQ.W	#1,D0
	CMPI.W	#100,D0
	BLT.S	.FUCK
	SF	FLAG_CONT_ZOOM
	MOVEQ	#0,D0
.FUCK	MOVE.W	D0,NUMER
	RTS

GEST_STARS	MOVE.L	SCREEN2,A0
	MOVE.L	ADR_EFF_S,A1
	MOVE.W	(A1)+,D7
	SUBQ.W	#1,D7
	MOVEQ	#0,D0
.RECOM_EFF	ADDQ.W	#2,A1
	MOVE.W	(A1)+,D1
	MOVE.B	D0,(A0,D1.W)
	DBRA	D7,.RECOM_EFF

	LEA	TABLE_ADR_COD_STARS,A2
	MOVE.W	NO_ETAPE,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.L	(A2,D0.W),A2
	MOVE.L	A2,ADR_EFF_S
	MOVEQ	#$80,D7
	MOVEQ	#$40,D6
	MOVEQ	#$20,D5
	MOVEQ	#$10,D4
	MOVEQ	#8,D3
	MOVEQ	#4,D2
	MOVEQ	#2,D1
	MOVEQ	#1,D0
	ADDQ.W	#2,A2
	JSR	(A2)
	MOVE.W	NO_ETAPE,D0
	ADDQ.W	#1,D0
	CMPI.W	#96,D0
	BLT.S	.FUCK
	MOVEQ	#0,D0
.FUCK	MOVE.W	D0,NO_ETAPE

	LEA	ADR_EFF_S,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0),-(A0)
	MOVE.L	D0,4(A0)
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	MOVEM.L	D0-A6,-(SP)
	BSR	GEST_STARS
	MOVE.W	COLOR_ACTU,D0
	MOVE.W	#$8250,A0
	MOVEQ	#7,D7
.R	MOVE.W	D0,(A0)+
	DBRA	D7,.R
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
N	SET	6
	REPT	200
	MOVE.W	D0,N(A0)
	MOVE.W	D0,8+N(A0)
	MOVE.W	D0,16+N(A0)
	MOVE.W	D0,24+N(A0)
	MOVE.W	D0,32+N(A0)
	MOVE.W	D0,40+N(A0)
	MOVE.W	D0,48+N(A0)
	MOVE.W	D0,56+N(A0)
	MOVE.W	D0,64+N(A0)
	MOVE.W	D0,72+N(A0)
	MOVE.W	D0,80+N(A0)
	MOVE.W	D0,88+N(A0)
	MOVE.W	D0,96+N(A0)
	MOVE.W	D0,104+N(A0)
	MOVE.W	D0,112+N(A0)
	MOVE.W	D0,120+N(A0)
	MOVE.W	D0,128+N(A0)
	MOVE.W	D0,136+N(A0)
	MOVE.W	D0,144+N(A0)
	MOVE.W	D0,152+N(A0)
N	SET	N+160
	ENDR
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.G
	MOVE.W	#$300,$FFFF8240.W
.G	CMPI.B	#1,$FFFFFC02.W
	BEQ.S	.FFF+6
	TST.B	FLAG_CONT_ZOOM
	BEQ.S	.FFF
	BSR	GEST_ZOOM
.FFF	BSR	SWAPEC
	CLR.W	$FFFF8240.W
	MOVEM.L	(SP)+,D0-A6
	RTE

PREP_STARS	LEA	COOR_STARS(PC),A0
	MOVE.W	#NB_STARS-1,D7
.RECOM_1	ADDQ.W	#4,A0
	MOVE.W	(A0),D0
	ADD.W	D0,D0
	MOVE.W	D0,(A0)+
	DBRA	D7,.RECOM_1

	LEA	COEFF(PC),A1
	LEA	CORES_X(PC),A2
	LEA	CORES_Y(PC),A3
	LEA	SUITZ+1536(PC),A4
	LEA	TABLE_ADR_COD_STARS,A5
	LEA	BUF_COD_STARS,A6
	MOVE.W	#95,D6
.RECOM_3	LEA	COOR_STARS(PC),A0
	MOVE.L	A6,(A5)+
	MOVE.L	A6,-(SP)
	ADDQ.W	#2,A6
	CLR.W	INCREMENT
	MOVE.W	#NB_STARS-1,D7
.RECOM_2	MOVE.W	(A0)+,D0	;X
	MOVE.W	(A0)+,D1	;Y
	MOVE.W	(A0)+,D2	;Z
	MOVE.W	(A4,D2.W),D2	;OFFSET POUR LE COEFF
	MOVEQ	#0,D5
	CMPI.W	#LIM_1*2,D2
	BLE.S	.OK
	ADDQ.W	#2,D5
	CMPI.W	#LIM_2*2,D2
	BLE.S	.OK
	ADDQ.W	#2,D5
.OK	MOVE.W	(A1,D2.W),D2	;COEFF
	LSL.W	#4,D0
	LSL.W	#4,D1
	MULS.W	D2,D0
	MULS.W	D2,D1
	SWAP	D0
	SWAP	D1
	CMPI.W	#160,D0
	BGE.S	.NO_AFF
	CMPI.W	#-160,D0
	BLT.S	.NO_AFF
	CMPI.W	#100,D1
	BGE.S	.NO_AFF
	CMPI.W	#-100,D1
	BLT.S	.NO_AFF
	ADD.W	D0,D0
	ADD.W	D0,D0
	ADD.W	D1,D1
	MOVE.W	#$8128,D3	;CODE DU OR.B D0,d16(A0)
	MOVE.W	(A2,D0.W),D4
	ADD.W	D4,D4
	ADDQ.W	#1,D4
	LSL.W	#8,D4
	OR.W	D4,D3
	MOVE.W	(A3,D1.W),D1	;Y*160
	ADD.W	2(A2,D0.W),D1	;OFFSET ECRAN
	MOVE.W	D3,(A6)+	;LE OR.B Dn,d16(A0)
	ADD.W	D5,D1	;3 COULEURS POUR LES ETOILES
	MOVE.W	D1,(A6)+	;L'OFFSET ECRAN
	ADDQ.W	#1,INCREMENT
.NO_AFF	DBRA	D7,.RECOM_2
	MOVE.W	#$4E75,(A6)+
	LEA	-16(A4),A4
	MOVE.L	(SP)+,A0
INCREMENT = *+2
	MOVE.W	#$1234,(A0)
	DBRA	D6,.RECOM_3

	CLR.W	NO_ETAPE

	LEA	ADR_EFF_S,A0
	LEA	BUF_COD_STARS,A1
	MOVE.L	A1,(A0)+
	MOVE.L	A1,(A0)
	RTS

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	ADDI.L	#16*160,D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000+16*160,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

COOR_STARS	INCBIN	STARS.COR

COEFF	INCBIN	COEFF.3D

N	SET	0
SUITZ	REPT	768
	DC.W	N*2
N	SET	N+1
	ENDR
N	SET	0
	REPT	768
	DC.W	N*2
N	SET	N+1
	ENDR

N	SET	0
	REPT	100
	DC.W	N*160
N	SET	N+1
	ENDR
CORES_Y	REPT	100
	DC.W	N*160
N	SET	N+1
	ENDR

N	SET	0
	REPT	10
	DC.W	7,N,6,N,5,N,4,N,3,N,2,N,1,N,0,N,7,N+1,6,N+1,5,N+1,4,N+1,3,N+1,2,N+1,1,N+1,0,N+1
N	SET	N+8
	ENDR
CORES_X	REPT	10
	DC.W	7,N,6,N,5,N,4,N,3,N,2,N,1,N,0,N,7,N+1,6,N+1,5,N+1,4,N+1,3,N+1,2,N+1,1,N+1,0,N+1
N	SET	N+8
	ENDR

PREP_NEXT_ZOOM	MOVE.L	PT_MESS,A0
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0	;TYPE DE FONTE ( 0,1 )
	CMPI.B	#-1,D0
	BNE.S	.NOT_THE_END
	LEA	MESSAGE(PC),A0
	MOVE.B	(A0)+,D0
.NOT_THE_END	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	TYPES_FONTS(PC),A1
	MOVE.L	(A1,D0.W),A1	;ADRESSE DES FONTES
	MOVEQ	#0,D7
	MOVE.B	(A0)+,D7	;D7=NB DE LETTRES
	MOVE.L	D7,D2
	MULU.W	#256,D2	;*256=*8*32
	MOVE.W	#3072,D3	;3072=8*32*12
	SUB.W	D2,D3	;(8*12*32)-(N*32)
	LSR.W	#1,D3	;/2
	SUBI.W	#3072/2,D3	;ON RECENTRE
	;D3=X ORIGINE
	LEA	BUF_ANNEX,A4
	SUBQ.W	#1,D7
	MOVEQ	#-112,D5	;Y0
	CLR.W	ADDIT
	MOVEQ	#7,D6
.NEXT_Y	MOVE.W	D3,D4
	SUB.L	A5,A5
	LEA	4(A4),A3
	MOVE.W	D5,(A4)+	;ON POSE UNE FOIS LE Y
	MOVE.W	D7,D1	;NB DE LETTRES-1
	MOVE.L	A0,-(SP)
.NEXT_LETTRE	MOVE.W	D1,A6
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0	;CODE ASCII DE LA LETTRE
	SUBI.B	#32,D0	;-32
	LSL.W	#3,D0	;*8
	MOVE.L	A1,A2
	ADDA.W	D0,A2	;ADRESSE DE LA FONTE
ADDIT = *+2
	ADDA.W	#$1234,A2
	MOVE.B	(A2)+,D0
	MOVEQ	#7,D1
.RECOM_TST_BIT	BTST	D1,D0
	BEQ.S	.NOTHING
	MOVE.W	D4,(A3)+	;ON POSE LE X
	ADDQ.W	#1,A5
.NOTHING	ADDI.W	#32,D4	;X=X+32
	DBRA	D1,.RECOM_TST_BIT
	MOVE.W	A6,D1
	DBRA	D1,.NEXT_LETTRE
	MOVE.L	A0,PT_MESS
	ADDI.W	#32,D5	;Y=Y+32
	ADDQ.W	#1,ADDIT
	MOVE.L	(SP)+,A0
	SUBQ.W	#1,A5
	MOVE.W	A5,(A4)
	MOVE.L	A3,A4
	DBRA	D6,.NEXT_Y	

	LEA	BUF_ZOOMS,A1
	LEA	COEFF_ZOOM+1600(PC),A2
	LEA	BUF_LONG+198,A3
	LEA	ADR_ZOOMS,A4
	MOVEQ	#99,D6
.RECOM_3	LEA	BUF_ANNEX,A0
	MOVE.L	A1,(A4)+
	MOVE.W	(A2),D0	;COEFF 3D
	MOVEQ	#7,D5
.RECOM_2	MOVE.W	(A0)+,D1	;Y
	ADD.W	D1,D1
	MULS.W	D0,D1
	SWAP	D1
	ADDI.W	#100,D1	;Y ECRAN
	MULU.W	#160,D1
	MOVE.W	D1,(A1)+	;Y*160
	MOVE.W	(A0)+,D2	;(NB DE CARRêS SUR LE LIGNE)-1
	SUBA.L	A6,A6
	MOVE.L	A1,A5
	ADDQ.W	#2,A1
	TST.W	D2
	BLT.S	.CONTINUE
.RECOM_1	CLR.B	FLAG_CLIP
	MOVE.W	(A3),D1	;LONGUEUR
	MOVEQ	#0,D7
	MOVE.W	(A0)+,D3	;X
	ADD.W	D3,D3
	MULS.W	D0,D3
	SWAP	D3
	ADDI.W	#160,D3	;D3=X ECRAN
	CMPI.W	#320,D3
	BGE.S	.NEXT_PT
	MOVE.W	D3,D4
	ADD.W	D1,D4
;	SUBQ.W	#1,D4
	CMPI.W	#320,D4
	BLT.S	.PAS_CLIP_D
	MOVE.W	#$80,D7	;FLAG CLIP DROIT
	ST	FLAG_CLIP
.PAS_CLIP_D	TST.W	D3
	BGE.S	.PAS_CLIP_G
	MOVE.W	D3,D4
	ADD.W	D1,D4
	TST.W	D4
	BLT.S	.NEXT_PT
	MOVEQ	#$40,D7	;FLAG CLIP GAUCHE
	ST	FLAG_CLIP
.PAS_CLIP_G	MOVE.W	D3,D4
;	TST.B	FLAG_CLIP
;	BEQ.S	.NO_CLIP
;	SWAP	D3
;	CLR.W	D3
;	SWAP	D3
;	DIVU.W	#32,D3
;	SWAP	D3
;	BRA.S	.RETOUR
.NO_CLIP	ANDI.W	#15,D3
.RETOUR	OR.B	D7,D3
	MOVE.B	D3,(A1)+	;DECALAGE
	ANDI.W	#$FFF0,D4
	LSR.W	#1,D4
	MOVE.B	D4,(A1)+	;OFFSET ECRAN
	ADDQ.W	#1,A6
.NEXT_PT	DBRA	D2,.RECOM_1
.CONTINUE	SUBQ.W	#1,A6
	MOVE.W	A6,(A5)
	DBRA	D5,.RECOM_2
	LEA	-16(A2),A2
	SUBQ.W	#2,A3
	DBRA	D6,.RECOM_3
	ST	FLAG_CONT_ZOOM
 	RTS

PREP_ZOOMS	LEA	COEFF_ZOOM+16(PC),A0
	LEA	CARRêS(PC),A1
	LEA	BUF_COD_CARRêS,A2
	LEA	TABLE_COD_CARRêS,A3
	LEA	BUF_LONG,A4
	MOVE.W	#32,(A4)+
	MOVE.L	#$FFFFFFFF,D0
	MOVEQ	#32-1,D1
	BSR	MAK_COD_CARRê
	MOVE.W	#99-1,D6
.FUCK	MOVEQ	#32*2,D0
	MULU.W	(A0),D0
	SWAP	D0	;LONGUEUR
	MOVE.W	D0,(A4)+
	MOVE.W	D0,D1
	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.L	(A1,D0.W),D0	;MOTIF
;	SUBQ.W	#1,D1	;HAUTEUR-1
	BSR	MAK_COD_CARRê
	LEA	16(A0),A0	;TOUT LES 8 Zs
	DBRA	D6,.FUCK

	LEA	COEFF_ZOOM+16(PC),A0
	LEA	CARRêS(PC),A1
	LEA	TABLE_COD_CARRêS_CLIP_1,A3
	MOVEQ	#0,D0
	MOVE.L	#$FFFFFFFF,D0
	MOVEQ	#32-1,D1
	BSR	MAK_COD_CARRê_CLIP_1
	MOVE.W	#99-1,D6
.FUCK2	MOVEQ	#32*2,D0
	MULU.W	(A0),D0
	SWAP	D0	;LONGUEUR
	MOVE.W	D0,D1
	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.W	(A1,D0.W),D0	;MOTIF
	SWAP	D0
	CLR.W	D0
	SWAP	D0
;	SUBQ.W	#1,D1	;HAUTEUR-1
	BSR	MAK_COD_CARRê_CLIP_1
	LEA	16(A0),A0	;TOUT LES 8 Zs
	DBRA	D6,.FUCK2

	LEA	COEFF_ZOOM+16(PC),A0
	LEA	CARRêS(PC),A1
	LEA	TABLE_COD_CARRêS_CLIP_2,A3
	MOVEQ	#0,D0
	MOVE.W	#$FFFFFFFF,D0
	MOVEQ	#32-1,D1
	BSR	MAK_COD_CARRê_CLIP_2
	MOVE.W	#99-1,D6
.FUCK3	MOVEQ	#32*2,D0
	MULU.W	(A0),D0
	SWAP	D0	;LONGUEUR
	MOVE.W	D0,D1
	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.W	(A1,D0.W),D0	;MOTIF
	SWAP	D0
	CLR.W	D0
	SWAP	D0
;	SUBQ.W	#1,D1	;HAUTEUR-1
	BSR	MAK_COD_CARRê_CLIP_2
	LEA	16(A0),A0	;TOUT LES 8 Zs
	DBRA	D6,.FUCK3

	MOVE.L	#MESSAGE,PT_MESS
	ST	FLAG_CONT_ZOOM
	MOVE.W	#0,NUMER
	RTS

MAK_COD_CARRê	MOVEM.L	D5-D6,-(SP)
	MOVE.W	D0,D6
	CLR.W	D0
	SWAP	D0
	MOVEQ	#15,D7
.ONLY_TWO	MOVE.L	A2,(A3)+
	MOVE.W	#160,D4
	MOVE.W	D1,D2
	MOVE.W	#$303C,(A2)+	;MOVE.W #$Imm,D0
	MOVE.W	D0,(A2)+
	MOVE.W	#$323C,(A2)+	;MOVE.W #$Imm,D1
	MOVE.W	D6,(A2)+
	MOVE.W	#$8150,(A2)+	;OR.W D0,(A0)
	MOVE.W	#$8368,(A2)+	;OR.W D1,8(A0)
	MOVE.W	#8,(A2)+
	SUBQ.W	#1,D2
.F	MOVE.W	#$8168,(A2)+
	MOVE.W	D4,(A2)+
	MOVE.W	#$8368,(A2)+
	MOVE.W	D4,(A2)
	ADDQ.W	#8,(A2)+
	ADDI.W	#160,D4
	DBRA	D2,.F
	MOVE.W	#$4E75,(A2)+
	SUBQ.W	#1,D7
	BLT.S	.FFUUCCKK
	MOVEQ	#0,D5
.RECOM_DEC	ROR.L	#1,D0
	SWAP	D0
	ROR.L	#1,D6
	OR.W	D0,D6
	CLR.W	D0
	SWAP	D0
	SWAP	D6
	MOVE.W	D6,D3
	SWAP	D6
	TST.W	D3
	BEQ.S	.ONLY_TWO
	TST.W	D7
	BLT.S	.FFUUCCKK
	MOVE.L	A2,(A3)+
	MOVE.W	#160,D4
	MOVE.W	D1,D2
	MOVE.W	#$303C,(A2)+	;MOVE.W #$Imm,D0
	MOVE.W	D0,(A2)+
	MOVE.W	#$323C,(A2)+	;MOVE.W #$Imm,D1
	MOVE.W	D6,(A2)+
	MOVE.W	#$343C,(A2)+	;MOVE.W #$Imm,D2
	MOVE.W	D3,(A2)+
	MOVE.W	#$8150,(A2)+	;OR.W D0,(A0)
	MOVE.W	#$8368,(A2)+	;OR.W D1,8(A0)
	MOVE.W	#8,(A2)+
	MOVE.W	#$8568,(A2)+	;OR.W D2,16(A0)
	MOVE.W	#16,(A2)+
	SUBQ.W	#1,D2
.G	MOVE.W	#$8168,(A2)+	;OR.W D0,d16(A0)
	MOVE.W	D4,(A2)+
	MOVE.W	#$8368,(A2)+	;OR.W D1,d16(A0)
	MOVE.W	D4,(A2)
	ADDQ.W	#8,(A2)+
	MOVE.W	#$8568,(A2)+	;OR.W D2,d16(A0)
	MOVE.W	D4,(A2)
	ADDI.W	#16,(A2)+
	ADDI.W	#160,D4
	DBRA	D2,.G
	MOVE.W	#$4E75,(A2)+
	DBRA	D7,.RECOM_DEC
.FFUUCCKK	MOVEM.L	(SP)+,D5-D6
	RTS

MAK_COD_CARRê_CLIP_1
	MOVEM.L	D5-D6,-(SP)
	MOVEQ	#0,D5
	MOVEQ	#32-1,D7
.RECOM	TST.L	D5
	BNE.S	.PAS_NOTHING
	MOVE.L	#RTS,(A3)+
	BRA.S	.NOTHING
.PAS_NOTHING	TST.W	D5
	BEQ.S	.TWO_EXACTLY
	MOVE.L	A2,(A3)+
	MOVE.W	#160,D4
	MOVE.W	D1,D2
	SWAP	D5
	MOVE.W	#$303C,(A2)+	;MOVE.W #$Imm,D0
	MOVE.W	D5,(A2)+
	SWAP	D5
	MOVE.W	#$8150,(A2)+	;OR.W D0,(A0)
	SUBQ.W	#1,D2
.G	MOVE.W	#$8168,(A2)+	;OR.W D0,d16(A0)
	MOVE.W	D4,(A2)+
	ADDI.W	#160,D4
	DBRA	D2,.G
	MOVE.W	#$4E75,(A2)+
	BRA.S	.NOTHING
.TWO_EXACTLY	MOVE.L	A2,(A3)+
	MOVE.W	#160,D4
	MOVE.W	D1,D2
	SWAP	D5
	MOVE.W	#$303C,(A2)+	;MOVE.W #$Imm,D0
	MOVE.W	D5,(A2)+
	SWAP	D5
	MOVE.W	#$323C,(A2)+	;MOVE.W #$Imm,D1
	MOVE.W	D5,(A2)+
	MOVE.W	#$8150,(A2)+	;OR.W D0,(A0)
	MOVE.W	#$8368,(A2)+	;OR.W D1,8(A0)
	MOVE.W	#8,(A2)+
	SUBQ.W	#1,D2
.F	MOVE.W	#$8168,(A2)+	;OR.W D0,d16(A0)
	MOVE.W	D4,(A2)+
	MOVE.W	#$8368,(A2)+	;OR.W D1,d16(A0)
	MOVE.W	D4,(A2)
	ADDQ.W	#8,(A2)+
	ADDI.W	#160,D4
	DBRA	D2,.F
	MOVE.W	#$4E75,(A2)+
.NOTHING	ROXR.L	#1,D0
	ROXR.L	#1,D5
	DBRA	D7,.RECOM
	MOVEM.L	(SP)+,D5-D6
	RTS

MAK_COD_CARRê_CLIP_2
	MOVEQ	#31,D7
.RECOM	MOVE.L	A2,(A3)+
	MOVE.W	#160,D4
	MOVE.W	D1,D2
	SWAP	D0
	TST.W	D0
	BNE.S	.TWO_EXACTLY
	SWAP	D0
	MOVE.W	#$303C,(A2)+	;MOVE.W #$Imm,D0
	MOVE.W	D0,(A2)+
	MOVE.W	#$8150,(A2)+	;OR.W D0,(A0)
	SUBQ.W	#1,D2
.F	MOVE.W	#$8168,(A2)+	;OR.W D1,d16(A0)
	MOVE.W	D4,(A2)+
	ADDI.W	#160,D4
	DBRA	D2,.F
	BRA.S	.OKAY
.TWO_EXACTLY	MOVE.W	#$323C,(A2)+	;MOVE.W #$Imm,D1
	MOVE.W	D0,(A2)+
	SWAP	D0
	MOVE.W	#$303C,(A2)+	;MOVE.W #$Imm,D0
	MOVE.W	D0,(A2)+
	MOVE.W	#$8150,(A2)+	;OR.W D0,(A0)
	MOVE.W	#$8368,(A2)+	;OR.W D1,8(A0)
	MOVE.W	#8,(A2)+
	SUBQ.W	#1,D2
.G	MOVE.W	#$8168,(A2)+	;OR.W D0,d16(A0)
	MOVE.W	D4,(A2)+
	MOVE.W	#$8168,(A2)+	;OR.W D1,d16(A0)
	MOVE.W	D4,(A2)
	ADDQ.W	#8,(A2)+
	ADDI.W	#160,D4
	DBRA	D2,.G
.OKAY	MOVE.W	#$4E75,(A2)+
	LSR.L	#1,D0
	DBRA	D7,.RECOM
RTS	RTS

	INCLUDE	FONTS.S

MESSAGE	DC.B	1,9,"HOLOCAUST"
	DC.B	1,8,"PRESENTS"
	DC.B	1,3,"YOU"
	DC.B	1,9,"A FUCKING"
	DC.B	1,9,"NEW STUFF"
	DC.B	1,1," "
	DC.B	1,9,"LET US GO"
	DC.B	1,3,"NOW"
	DC.B	-1
	EVEN

COEFF_ZOOM	INCBIN	B:COEFF2.3D

N	SET	6
COLORS_ZOOMS
	DCB	N,$111
	DCB	N,$222
	DCB	N,$333
	DCB	N,$444
	DCB	N,$555
	DCB	N,$666
	DCB.W	9+13,$777
	DCB.W	N,$767
	DCB.W	N,$656
	DCB.W	N,$545
	DCB.W	N,$434
	DCB.W	N,$323
	DCB.W	N,$212
	DCB.W	N+3,$101

CARRêS	DC.W	$8000,0,$C000,0,$E000,0,$F000,0
	DC.W	$F800,0,$FC00,0,$FE00,0,$FF00,0
	DC.W	$FF80,0,$FFC0,0,$FFE0,0,$FFF0,0
	DC.W	$FFF8,0,$FFFC,0,$FFFE,0,$FFFF,0
	DC.W	$FFFF,$8000,$FFFF,$C000,$FFFF,$E000,$FFFF,$F000
	DC.W	$FFFF,$F800,$FFFF,$FC00,$FFFF,$FE00,$FFFF,$FF00
	DC.W	$FFFF,$FF80,$FFFF,$FFC0,$FFFF,$FFE0,$FFFF,$FFF0
	DC.W	$FFFF,$FFF8,$FFFF,$FFFC,$FFFF,$FFFE,$FFFF,$FFFF

TYPES_FONTS	DC.L	FONTS_1,FONTS_2

	SECTION	BSS
FLAG_CONT_ZOOM	DS.B	1
FLAG_CLIP	DS.B	1
COLOR_ACTU	DS.W	1
NUMER	DS.W	1
PT_MESS	DS.L	1
NB_VBL	DS.W	1
NO_ETAPE	DS.W	1
ADR_EFF_S	DS.L	2
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.B	64000+48*160
TABLE_ADR_COD_STARS	DS.L	96
BUF_COD_STARS	DS.B	148000
ADR_ZOOMS	DS.L	100
BUF_LONG	DS.W	100
BUF_ANNEX	DS.W	(12*8*8)+(2*8)	;1568 .B
TABLE_COD_CARRêS	DS.L	16*100
TABLE_COD_CARRêS_CLIP_1	DS.L	32*100
TABLE_COD_CARRêS_CLIP_2	DS.L	32*100
BUF_ZOOMS	DS.B	41000
BUF_COD_CARRêS	DS.B	94000