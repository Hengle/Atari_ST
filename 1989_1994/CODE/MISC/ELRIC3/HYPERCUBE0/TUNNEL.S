NB_PTS_CER = 65	;NB DE POINTS CONSTITUANT UN CERCLE

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	BSR	PREP_ECR
	BSR	PREPARE_ALL

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

IT_VBL	BSR	VSYNC
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.PAS_TAB
	MOVE.W	#$300,$FFFF8240.W
.PAS_TAB	BSR	GEST_CERCLES
	BSR	SWAPEC
	CLR.W	$FFFF8240.W
	BRA	IT_VBL

GEST_CERCLES	MOVE.L	ADR_EFF_CER,A1
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	JSR	(A1)

	MOVE.L	PT_TUNNEL,A1
	MOVE.W	(A1)+,D0	;Z DE DEPART
	LEA	TABLE_CERCLES,A6
	ADD.W	D0,D0
	ADD.W	D0,D0
	ADDA.W	D0,A6

	MOVE.L	ADR_EFF_CER,A5
	ADDQ.W	#2,A5

	REPT	20
	MOVE.L	(A6),A0
	LEA	CORES_X(PC),A2
	LEA	CORES_Y(PC),A3
	MOVE.L	SCREEN2,A4
	JSR	BUF_COD_CERCLES
	LEA	20*4(A6),A6
	ENDR

	MOVE.L	A1,PT_TUNNEL

	LEA	ADR_EFF_CER,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0),-(A0)
	MOVE.L	D0,4(A0)
	RTS

RT_EFF_1	REPT	1300
	MOVE.W	D0,0(A0)
	ENDR
	RTS

RT_EFF_2	REPT	1300
	MOVE.W	D0,0(A0)
	ENDR
	RTS

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	RTE

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREPARE_ALL	LEA	BUF_COD_CERCLES,A0
	MOVE.L	LITTLE_CODES,(A0)+
	MOVE.W	#NB_PTS_CER-1,D0
.RECOP_NB_RT	LEA	ROUTINE_PT_CERCLES(PC),A1
	MOVE.W	#LONG_RT_CIRCLES-1,D1
.RECOP_RT	MOVE.W	(A1)+,(A0)+
	DBRA	D1,.RECOP_RT
	DBRA	D0,.RECOP_NB_RT
	MOVE.W	#$4E75,(A0)

	LEA	COEFF_CERCLES(PC),A1
	LEA	BUF_COOR_CERCLES,A2
	LEA	TABLE_CERCLES,A3
	MOVE.W	#499,D6
.RECOM_CALC_ALL_Z
	MOVE.L	A2,(A3)+
	LEA	CIRCLE_COOR(PC),A0
	MOVE.W	(A1)+,D5	;COEFF 3D
	MOVEQ	#NB_PTS_CER-1,D7
.RECOM_CALC_ALL_PNTS
	MOVE.W	(A0)+,D0	;X
	MOVE.W	(A0)+,D1	;Y
	ADD.W	D0,D0
	ADD.W	D1,D1
	MULS.W	D5,D0
	MULS.W	D5,D1
	SWAP	D0
	SWAP	D1
	ADDI.W	#160,D0
	ADDI.W	#100,D1
	ADD.W	D0,D0
	ADD.W	D0,D0
	ADD.W	D1,D1
	MOVE.W	D0,(A2)+	;X
	MOVE.W	D1,(A2)+	;Y
	DBRA	D7,.RECOM_CALC_ALL_PNTS
	DBRA	D6,.RECOM_CALC_ALL_Z

	LEA	PARCOURS(PC),A0
	MOVE.L	A0,PT_TUNNEL
	MOVE.W	#998,D6
.S	ADDQ.W	#2,A0
	MOVEQ	#19,D7
.R	MOVE.W	(A0),D0	;C_X
	SUBI.W	#160,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.W	D0,(A0)+	;OFFSET C_X
	MOVE.W	(A0),D0	;C_Y
	SUBI.W	#100,D0
	ADD.W	D0,D0
	MOVE.W	D0,(A0)+	;OFFSET C_Y
	DBRA	D7,.R
	DBRA	D6,.S

	LEA	ADR_EFF_CER,A0
	LEA	RT_EFF_1,A1
	LEA	RT_EFF_2,A2
	MOVE.L	A1,(A0)+
	MOVE.L	A2,(A0)
	RTS
  
LITTLE_CODES	MOVE.W	(A1)+,D2	;D2=C_X*4
	MOVE.W	(A1)+,D3	;D3=C_Y*4

ROUTINE_PT_CERCLES
	MOVE.W	(A0)+,D0	;X*4 ,2
	MOVE.W	(A0)+,D1	;Y*2 ,2
	ADD.W	D2,D0	;+C_X*4 ,1
	ADD.W	D3,D1	;+C_Y*2 ,1
	MOVE.W	(A3,D1.W),D1	;Y*160 ,4
	ADD.W	2(A2,D0.W),D1	;+OFFSET ,4
	MOVE.W	(A2,D0.W),D0	;MOTIF ,4
	OR.W	D0,(A4,D1.W)	;AFFICHAGE ,5
	MOVE.W	D1,(A5)	;2
	ADDQ.W	#4,A5	;1
LONG_RT_CIRCLES = (*-ROUTINE_PT_CERCLES)/2
	;TOTAL=23 NOPS.

PROUT	DCB.W	100,0

	DCB.W	64*20,0
N	SET	0
CORES_X	REPT	20
	DC.W	32768,N,16384,N,8192,N,4096,N,2048,N,1024,N,512,N,256,N,128,N,64,N,32,N,16,N,8,N,4,N,2,N,1,N
N	SET	N+8
	ENDR
	DCB.W	64*20,0

N	SET	0
CORES_Y	REPT	200
	DC.W	N*160
N	SET	N+1
	ENDR
	DCB.W	200,0

COEFF_CERCLES	INCBIN	COEFF5.3D

CIRCLE_COOR	INCBIN	CERCLE.DAT

PARCOURS	INCBIN	CHEMIN.DAT

	SECTION	BSS
ADR_EFF_CER	DS.L	2
PT_TUNNEL	DS.L	1
NB_VBL	DS.W	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.B	64000
BUF_COD_CERCLES	DS.W	3+LONG_RT_CIRCLES*NB_PTS_CER
TABLE_CERCLES	DS.L	500
BUF_COOR_CERCLES
	DS.W	NB_PTS_CER*2*500