	;2b7c xxxxxxxx 0472
MAIN	PEA	DEBUT(PC)
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

RECOM	MOVE.W	#1,-(SP)
	TRAP	#1
	ADDQ.W	#2,SP
	CMPI.B	#32,D0
	BEQ	FIN
	CMPI.B	#'*',D0
	BNE.S	RECOM
	
	MOVE.W	#LONG_BOOT,D0
	MOVE.W	#1,-(SP)
	CLR.W	-(SP)
	CLR.W	-(SP)
	MOVE.W	#1,-(SP)
	CLR.W	-(SP)
	CLR.L	-(SP)
	PEA	BUF_BOOT
	MOVE.W	#8,-(SP)
	TRAP	#14
	LEA	20(SP),SP

	LEA	BUF_BOOT+2,A0
	LEA	BOOT+2,A1
	MOVEQ	#27,D7
.F	MOVE.B	(A0)+,(A1)+
	DBRA	D7,.F

	LEA	BOOT,A0
	MOVEQ	#0,D0
	MOVE.W	#254,D7
.R	ADD.W	(A0)+,D0
	DBRA	D7,.R
	MOVE.W	#$1234,D6
	SUB.W	D0,D6
	MOVE.W	D6,(A0)

	MOVE.W	#1,-(SP)
	CLR.W	-(SP)
	CLR.W	-(SP)
	MOVE.W	#1,-(SP)
	CLR.W	-(SP)
	CLR.L	-(SP)
	PEA	BOOT
	MOVE.W	#9,-(SP)
	TRAP	#14
	LEA	20(SP),SP

	BRA	MAIN

FIN	CLR.W	-(SP)
	TRAP	#1
	ADDQ.W	#2,SP

DEBUT	DC.B	27,"E* POUR POSER LE BOOT"
	DC.B	27,"Y",33,32,"<SPACE> POUR QUITTER"
	DC.B	27,"Y",34,32,"HOLOCAUST ANTI-VIRUS.",0
	EVEN

	;VALEURS A RECHERCHER : $263C000000D6  : MOVE.L #$D6,D3
BOOT	DC.W	$601C
	DC.B	"PROTEK"
	DCB.B	22,0
	PEA	MESSAGE(PC)
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP
	LEA	RT_DSK(PC),A0
	LEA	4.W,A1
	MOVE.L	(A1),A1
	MOVE.L	550(A1),6(A0)
	LEA	$472.W,A2
	LEA	$200.W,A1
	MOVE.L	A1,(A2)
	MOVE.W	#-1+LONG/2,D0
.COPY	MOVE.W	(A0)+,(A1)+
	DBRA	D0,.COPY
	MOVE.W	#10,D7
.G	MOVE.W	#$25,-(SP)
	TRAP	#14
	ADDQ.W	#2,SP
	DBRA	D7,.G
	RTS

RT_DSK	MOVE.L	(SP)+,$260.W
	JSR	$12345678
	MOVE.L	$260.W,-(SP)
	MOVEM.L	D0-A6,$260.W
	LEA	8.W,A0
	LEA	$2000,A1
.NEXT_L	CMPA.L	A1,A0
	BGE.S	.NO_VIRUS
	CMPI.L	#$263C0000,(A0)+
	BNE.S	.NEXT_L
	CMPI.W	#$D6,(A0)+
	BNE.S	.NEXT_L
	;SI ON ARRIVE ICI, LE GHOST EST PRESENT AVEC P=~1
	MOVE.W	#$8240,A0
	MOVE.W	(A0),D3
	MOVE.W	#-1+300*20,D7
.RETOUR	ADDI.W	#$123,(A0)
	MOVE.W	#49,D6
.FUCK	DBRA	D6,.FUCK
	DBRA	D7,.RETOUR
	MOVE.W	D3,(A0)
.NO_VIRUS	MOVEM.L	$260.W,D0-A6
	RTS
LONG = *-RT_DSK
MESSAGE	DC.B	27,"EHOLOCAUST ANTI-VIRUS. (c)1993 Elric."
	DC.B	27,"Y",33,32,"DORENAVANT, A CHAQUE FOIS"
	DC.B	27,"Y",34,32,"QU'UNE DISQUETTE VêROLêê"
	DC.B	27,"Y",35,32,"SERA INTRODUITE, IL SE PRODUIRA"
	DC.B	27,"Y",36,32,"UN FLASH DE COULEUR. CIAO.",0
	EVEN
LONG_BOOT = *-BOOT
	DS.B	512

BUF_BOOT	DS.B	512