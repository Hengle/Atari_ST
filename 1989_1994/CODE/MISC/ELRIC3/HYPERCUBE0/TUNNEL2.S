NB_PTS_CER = 43	;NB DE POINTS CONSTITUANT UN CERCLE
NB_1 = 10
NB_2 = 9
NB_3 = 6

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	BSR	PREP_ECR
	BSR	PREPARE_ALL

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

IT_VBL	BSR	VSYNC
	MOVE.W	#$666,$FFFF8242.W
	MOVE.W	#$444,$FFFF8244.W
	MOVE.W	#$666,$FFFF8246.W
	MOVE.W	#$222,$FFFF8248.W
	MOVE.W	#$666,$FFFF824A.W
	MOVE.W	#$444,$FFFF824C.W
	MOVE.W	#$666,$FFFF824E.W
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.PAS_TAB
	MOVE.W	#$300,$FFFF8240.W
.PAS_TAB	BSR	GEST_CERCLES
	BSR	SWAPEC
	CLR.W	$FFFF8240.W
	BRA	IT_VBL

GEST_CERCLES	MOVE.L	PT_TUNNEL,A0
	LEA	BUF_POZ_COOR_CENTRES,A1
	MOVE.W	#160,D1
	MOVE.W	#100,D2
	SUB.W	(A0)+,D1	;A
	SUB.W	(A0)+,D2	;B
	MOVE.L	A0,PT_TUNNEL
	MOVEQ	#NB_1+NB_2+NB_3-1,D7
	MOVE.L	PT_2,A0
.REPEAT	MOVE.W	(A0)+,D3	;X
	MOVE.W	(A0)+,D4	;Y
	ADD.W	D1,D3	;X+A
	ADD.W	D2,D4	;Y+B
	SUBI.W	#160,D3
	SUBI.W	#100,D4
	ADD.W	D3,D3
	ADD.W	D3,D3
	ADD.W	D4,D4
	MOVE.W	D3,(A1)+
	MOVE.W	D4,(A1)+
	LEA	12(A0),A0
	DBRA	D7,.REPEAT

	MOVE.L	PT_TUNNEL,A0
	CMPA.L	#FIN_PARCOURS,A0
	BNE.S	.FUCK2
	LEA	PARCOURS(PC),A0
	MOVE.L	A0,PT_2
	MOVE.L	A0,PT_TUNNEL
.FUCK2
	MOVE.L	ADR_EFF_CER,A3
	MOVE.L	SCREEN2,A0
	LEA	2(A0),A1
	LEA	2(A1),A2
	MOVEQ	#0,D0
	JSR	(A3)

	MOVE.W	Z_DEP,D1
	MOVE.W	D1,D0
	LEA	TABLE_CERCLES,A6
	ADD.W	D0,D0
	ADD.W	D0,D0
	ADDA.W	D0,A6
	SUBQ.W	#1,D1
	ANDI.W	#3,D1
	CMPI.W	#3,D1
	BNE.S	.FUCK
	ADDI.L	#16,PT_2
.FUCK	MOVE.W	D1,Z_DEP

	MOVE.L	ADR_EFF_CER,A5
	ADDQ.W	#2,A5

	LEA	BUF_POZ_COOR_CENTRES,A1
	LEA	CORES_X(PC),A2
	LEA	CORES_Y(PC),A3
	MOVE.L	SCREEN2,A4
	REPT	NB_1
	MOVE.L	(A6),A0
	JSR	BUF_COD_CERCLES
	LEA	4*4(A6),A6
	ENDR
	ADDQ.W	#2,A4
	REPT	NB_2
	MOVE.L	(A6),A0
	JSR	BUF_COD_CERCLES
	LEA	4*4(A6),A6
	ENDR
	ADDQ.W	#2,A4
	REPT	NB_3
	MOVE.L	(A6),A0
	JSR	BUF_COD_CERCLES
	LEA	4*4(A6),A6
	ENDR

	LEA	ADR_EFF_CER,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0),-(A0)
	MOVE.L	D0,4(A0)
	RTS

RT_EFF_1	REPT	NB_1*NB_PTS_CER
	MOVE.W	D0,0(A0)
	ENDR
	REPT	NB_2*NB_PTS_CER
	MOVE.W	D0,0(A1)
	ENDR
	REPT	NB_3*NB_PTS_CER
	MOVE.W	D0,0(A2)
	ENDR
	RTS

RT_EFF_2	REPT	NB_1*NB_PTS_CER
	MOVE.W	D0,0(A0)
	ENDR
	REPT	NB_2*NB_PTS_CER
	MOVE.W	D0,0(A1)
	ENDR
	REPT	NB_3*NB_PTS_CER
	MOVE.W	D0,0(A2)
	ENDR
	RTS

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	RTE

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	ADDI.L	#8*160,D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000+8*160,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREPARE_ALL	LEA	BUF_COD_CERCLES,A0
	MOVE.L	LITTLE_CODES,(A0)+
	MOVE.W	#NB_PTS_CER-1,D0
.RECOP_NB_RT	LEA	ROUTINE_PT_CERCLES(PC),A1
	MOVE.W	#LONG_RT_CIRCLES-1,D1
.RECOP_RT	MOVE.W	(A1)+,(A0)+
	DBRA	D1,.RECOP_RT
	DBRA	D0,.RECOP_NB_RT
	MOVE.W	#$4E75,(A0)

	LEA	COEFF_CERCLES(PC),A1
	LEA	BUF_COOR_CERCLES,A2
	LEA	TABLE_CERCLES,A3
	MOVE.W	#103,D6
.RECOM_CALC_ALL_Z
	MOVE.L	A2,(A3)+
	LEA	CIRCLE_COOR(PC),A0
	MOVE.W	(A1)+,D5	;COEFF 3D
	MOVEQ	#NB_PTS_CER-1,D7
.RECOM_CALC_ALL_PNTS
	MOVE.W	(A0)+,D0	;X
	MOVE.W	(A0)+,D1	;Y
	ADD.W	D0,D0
	ADD.W	D1,D1
	MULS.W	D5,D0
	MULS.W	D5,D1
	SWAP	D0
	SWAP	D1
	ADDI.W	#160,D0
	ADDI.W	#100,D1
	ADD.W	D0,D0
	ADD.W	D0,D0
	ADD.W	D1,D1
	ADD.W	#CORES_Y-CORES_X,D1
	MOVE.W	D0,(A2)+	;X
	MOVE.W	D1,(A2)+	;Y
	DBRA	D7,.RECOM_CALC_ALL_PNTS
	DBRA	D6,.RECOM_CALC_ALL_Z

	LEA	PARCOURS(PC),A0
	MOVE.L	A0,PT_TUNNEL
	MOVE.L	A0,PT_2
	LEA	FIN_PARCOURS,A1
	MOVE.W	#999,D7
.RECOP_FIN	MOVE.W	(A0)+,(A1)+
	DBRA	D7,.RECOP_FIN

	LEA	ADR_EFF_CER,A0
	LEA	RT_EFF_1,A1
	LEA	RT_EFF_2,A2
	MOVE.L	A1,(A0)+
	MOVE.L	A2,(A0)

	CLR.W	Z_DEP

	RTS
  
LITTLE_CODES	MOVE.W	(A1)+,D2	;D2=C_X*4
	MOVE.W	(A1)+,D3	;D3=C_Y*4

ROUTINE_PT_CERCLES
	MOVE.W	(A0)+,D0	;X*4 ,2
	MOVE.W	(A0)+,D1	;Y*2 ,2
	ADD.W	D2,D0	;+C_X*4 ,1
	ADD.W	D3,D1	;+C_Y*2 ,1
	MOVE.W	(A2,D1.W),D1	;Y*160 ,4
	ADD.W	2(A2,D0.W),D1	;+OFFSET ,4
	MOVE.W	(A2,D0.W),D0	;MOTIF ,4
	OR.W	D0,(A4,D1.W)	;AFFICHAGE ,5
	MOVE.W	D1,(A5)	;2
	ADDQ.W	#4,A5	;1
LONG_RT_CIRCLES = (*-ROUTINE_PT_CERCLES)/2
	;TOTAL=23 NOPS.

BUF_POZ_COOR_CENTRES
	DS.W	2*25

	DCB.W	64*20,0
N	SET	0
CORES_X	REPT	20
	DC.W	32768,N,16384,N,8192,N,4096,N,2048,N,1024,N,512,N,256,N,128,N,64,N,32,N,16,N,8,N,4,N,2,N,1,N
N	SET	N+8
	ENDR
	DCB.W	64*20,0

	DCB.W	300,-160
N	SET	0
CORES_Y	REPT	200
	DC.W	N*160
N	SET	N+1
	ENDR
	DCB.W	300,-160

COEFF_CERCLES	INCBIN	COEFF10.3D

CIRCLE_COOR	INCBIN	CERCLE2.DAT

PARCOURS	INCBIN	CHEMIN4.DAT
FIN_PARCOURS	DCB.W	1000,0

	SECTION	BSS
Z_DEP	DS.W	1
ADR_EFF_CER	DS.L	2
PT_TUNNEL	DS.L	1
PT_2	DS.L	1
NB_VBL	DS.W	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.B	64000+24*160
BUF_COD_CERCLES	DS.W	3+LONG_RT_CIRCLES*NB_PTS_CER
TABLE_CERCLES	DS.L	104
BUF_COOR_CERCLES
	DS.W	NB_PTS_CER*2*104