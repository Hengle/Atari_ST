Z_1 = 20
Z_2 = 60

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	BSR	PREP_ECR
	BSR	PREP_CUBE

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

IT_VBL	BSR	VSYNC
	MOVE.W	#$777,$FFFF8242.W
	MOVE.W	#$444,$FFFF8244.W
	MOVE.W	#$777,$FFFF8246.W
	MOVE.W	#$222,$FFFF8248.W
	MOVE.W	#$777,$FFFF824A.W
	MOVE.W	#$777,$FFFF824C.W
	MOVE.W	#$777,$FFFF824E.W
	CMPI.B	#1,$FFFFFC02.W
	BEQ.S	.F
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.PAS_TAB
	MOVE.W	#$300,$FFFF8240.W
.PAS_TAB	BSR	EFF_ECRAN
	BSR	GEST_CUBE
	BSR	SWAPEC
.F	CLR.W	$FFFF8240.W
	BRA	IT_VBL

GEST_CUBE	MOVE.L	PT_D,A0
	MOVE.W	(A0)+,D1
	ADD.W	D1,D1
	ADD.W	D1,D1
	CMPI.W	#$1234,(A0)
	BNE.S	.FUCK_2
	LEA	DEPLAC(PC),A0
.FUCK_2	MOVE.L	A0,PT_D
	MOVE.W	NUM,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	TABLE_ADR,A0
	MOVE.L	(A0,D0.W),A0
	LEA	CORES_X-160*4,A1
	ADDA.W	D1,A1
	MOVE.L	SCREEN2,A3
	MOVE.L	ADR_EFF,A4
	REPT	343
	MOVE.W	(A0)+,D0	;X*4 ( 2 NOPS )
	MOVE.W	(A0)+,D1	;Y*160 ( 2 NOPS )
	MOVE.B	1(A1,D0.W),D2	;MOTIF ( 4 NOPS)
	ADD.W	2(A1,D0.W),D1 	;( 4 NOPS )
	OR.B	D2,(A3,D1.W)	;( 5 NOPS )
	MOVE.W	D1,(A4)+
	ENDR
	MOVE.W	NUM,D0
	ADDQ.W	#1,D0
	CMPI.W	#120,D0
	BNE.S	.FUCK
	MOVEQ	#0,D0
.FUCK	MOVE.W	D0,NUM
	LEA	ADR_EFF,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0),-(A0)
	MOVE.L	D0,4(A0)
	RTS

EFF_ECRAN	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVE.L	ADR_EFF,A1
	REPT	343
	MOVE.W	(A1)+,D1
	MOVE.B	D0,(A0,D1.W)
	ENDR
	RTS

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	RTE

PREP_CUBE	LEA	SIN_COS(PC),A1	;SINUS
	LEA	60(A1),A2	;COSINUS
	LEA	COEFF_CUB_3D(PC),A3
	LEA	TABLE_ADR,A4
	LEA	BUF_COOR_CUBE,A5
	MOVE.W	#120-1,D7
.REPEAT_2	MOVE.L	A5,(A4)+
	LEA	COOR_CUBE(PC),A0
	MOVE.W	D7,GG
	MOVE.W	(A1)+,D3	;SINUS T
	MOVE.W	(A2)+,D4	;COSINUS T
	MOVE.W	#343-1,D7
.REPEAT	MOVE.W	D7,A6
	MOVE.W	(A0)+,D0	;X
	MOVE.W	(A0)+,D1	;Y
	MOVE.W	(A0)+,D2	;Z
	SUBI.W	#48,D2
	;ROTATION X
	MOVEQ	#0,D5
	MOVE.W	D1,D5
	ADD.W	D5,D5	;Y*2
	MULS.W	D4,D5	;D5=Y*COS T
	SWAP	D5
	MOVEQ	#0,D6
	MOVE.W	D2,D6
	ADD.W	D6,D6	;Z*2
	MULS.W	D3,D6	;D6=Z*SIN T
	SWAP	D6
	SUB.W	D6,D5	;D5=Y*COS T-Z*SIN T=Y
	MOVEQ	#0,D6
	MOVE.W	D1,D6
	ADD.W	D6,D6	;Y*2
	MULS.W	D3,D6	;D6=Y*SIN T
	SWAP	D6
	MOVEQ	#0,D7
	MOVE.W	D2,D7
	ADD.W	D7,D7	;Z*2
	MULS.W	D4,D7	;D7=Z*COS T
	SWAP	D7
	ADD.W	D7,D6	;D6=Y*SIN T+Z*COS T=Z
	MOVE.W	D5,D1	;NEW Y
	MOVE.W	D6,D2	;NEW Z

	;ROTATION Y
	MOVEQ	#0,D5
	MOVE.W	D0,D5
	ADD.W	D5,D5	;X*2
	MULS.W	D4,D5	;D5=X*COS T
	SWAP	D5
	MOVEQ	#0,D6
	MOVE.W	D2,D6
	ADD.W	D6,D6	;Z*2
	MULS.W	D3,D6	;D6=Z*SIN T
	SWAP	D6
	ADD.W	D6,D5	;D5=X*COS T+Z*SIN T=X
	MOVEQ	#0,D6
	MOVE.W	D0,D6
	ADD.W	D6,D6	;X*2
	MULS.W	D3,D6	;D6=X*SIN T
	SWAP	D6
	MOVEQ	#0,D7
	MOVE.W	D2,D7
	ADD.W	D7,D7	;Z*2
	MULS.W	D4,D7	;D7=Z*COS T
	SWAP	D7
	SUB.W	D6,D7	;D7=Z*COS T-X*SIN T=Z
	MOVE.W	D5,D0	;NEW X
	MOVE.W	D7,D2	;NEW Z

	;PROJECTION
	ADDI.W	#48,D2
	MOVEQ	#0,D7
	CMPI.W	#Z_1,D2
	BLT.S	.NO1
	ADDQ.W	#2,D7
.NO1	CMPI.W	#Z_2,D2
	BLT.S	.NO2
	ADDQ.W	#2,D7
.NO2	ADD.W	D2,D2
	MOVE.W	(A3,D2.W),D2	;COEFF
	ADD.W	D0,D0	;X*2
	ADD.W	D1,D1	;Y*2
	MULS.W	D2,D0
	MULS.W	D2,D1
	SWAP	D0
	SWAP	D1
	ADDI.W	#100,D1
	MULU.W	#160,D1
	ADD.W	D7,D1
	ADDI.W	#160,D0
	ADD.W	D0,D0
	ADD.W	D0,D0	;X*4
	MOVE.W	D0,(A5)+
	MOVE.W	D1,(A5)+
	MOVE.W	A6,D7
	DBRA	D7,.REPEAT
GG = *+2
	MOVE.W	#$1234,D7
	DBRA	D7,.REPEAT_2
	CLR.W	NUM

	LEA	ADR_EFF,A0
	LEA	BUF_EFF,A1
	LEA	343*2(A1),A2
	MOVE.L	A1,(A0)+
	MOVE.L	A2,(A0)

	MOVE.L	#DEPLAC,PT_D
	RTS

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

ROUTINE_AFF	MOVE.W	(A0)+,D0	;X*4 ( 2 NOPS )
	MOVE.W	(A0)+,D1	;Y*160 ( 2 NOPS )
	MOVE.B	(A1,D0.W),D2	;MOTIF ( 4 NOPS)
	ADD.W	2(A1,D0.W),D1 	;( 4 NOPS )
	OR.B	D2,(A3,D1.W)	;( 5 NOPS )
	MOVE.W	D1,(A4)+	;OFFSET
LONG_RT_AFF = *-ROUTINE_AFF

	DCB.W	32*10,0
N	SET	0
CORES_X	REPT	20
	DC.W	$80,N,$40,N,$20,N,$10,N,8,N,4,N,2,N,1,N,$80,N+1,$40,N+1,$20,N+1,$10,N+1,8,N+1,4,N+1,2,N+1,1,N+1
N	SET	N+8
	ENDR
	DCB.W	32*10,0

	DCB.W	30,$7FFF
COEFF_CUB_3D	INCBIN	COEFF5.3D

SIN_COS	INCBIN	SINCOSRT.DAT

COOR_CUBE	INCBIN	CUBE_1.DAT

DEPLAC	INCBIN	SINUSB.DAT
	DC.W	$1234

	SECTION	BSS
PT_D	DS.L	1
ADR_EFF	DS.L	2
BUF_EFF	DS.W	343*2
NUM	DS.W	1
NB_VBL	DS.W	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.B	64000
TABLE_ADR	DS.L	120
BUF_COOR_CUBE	DS.W	2*343*120