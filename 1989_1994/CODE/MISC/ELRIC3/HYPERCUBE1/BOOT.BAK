MAIN	MOVE.W	#1,-(SP)
	CLR.W	-(SP)
	CLR.W	-(SP)
	MOVE.W	#1,-(SP)
	CLR.W	-(SP)
	CLR.L	-(SP)
	PEA	BUF_BOOT
	MOVE.W	#8,-(SP)
	TRAP	#14
	LEA	20(SP),SP

	LEA	BUF_BOOT+2,A0
	LEA	BOOT,A1
	MOVEQ	#27,D7
.F	MOVE.B	(A0)+,(A1)+
	DBRA	D7,.F

	LEA	BOOT,A0
	MOVEQ	#0,D0
	MOVE.W	#254,D7
.R	ADD.W	(A0)+,D0
	DBRA	D7,.R
	MOVE.W	#$1234,D6
	SUB.W	D0,D6
	MOVE.W	D6,(A0)

	MOVE.W	#1,-(SP)
	CLR.W	-(SP)
	CLR.W	-(SP)
	MOVE.W	#1,-(SP)
	CLR.W	-(SP)
	CLR.L	-(SP)
	PEA	BOOT
	MOVE.W	#9,-(SP)
	TRAP	#14
	LEA	20(SP),SP

	ILLEGAL

BOOT	DC.W	$601C
	DCB.B	28,0
	LEA	$70000,A7
	MOVE.W	#$8240,A4
	MOVE.W	#$777,D4
.OK_FADE	MOVE.W	D4,(A4)
	BSR	VSYNC
	BSR	VSYNC
	BSR	VSYNC
	SUBI.W	#$111,D4
	BGE.S	.OK_FADE

	LEA	$10000,A5
	MOVEQ	#24,D4	;NB DE SECTEURS TOTAL
	MOVEQ	#1,D5	;No DE LA PISTE DE DEPART
	MOVEQ	#2,D7
.OK	MOVE.W	#10,-(SP)
	CLR.W	-(SP)	;FACE
	MOVE.W	D5,-(SP)	;PISTE
	MOVE.W	#1,-(SP)	;1er SECTEUR A LIRE
	CLR.W	-(SP)	;UNITê A
	CLR.L	-(SP)
	PEA	(A5)
	MOVE.W	#8,-(SP)
	TRAP	#14
	LEA	20(SP),SP
	LEA	512*10(A5),A5
	ADDQ.W	#1,D5
	DBRA	D7,.OK

	LEA	$10000,A0
	MOVE.L	A0,A1
	ADD.L	2(A0),A1
	ADD.L	6(A0),A1
	LEA	31(A1),A1
	MOVEQ	#0,D0
	
	LEA	$10000+28,A0
	MOVE.L	A0,D1
	
RELOCATION	MOVE.B	(A1)+,D0
	BEQ.S	RELOC_END
	CMP.W	#1,D0
	BNE.S	RELOC1
	ADD.W	#254,A0
	BRA.S	RELOCATION
RELOC1	ADD.W	D0,A0
	ADD.L	D1,(A0)
	BRA.S	RELOCATION
RELOC_END	JMP	$10000
VSYNC	MOVE.W	#$25,-(SP)
	TRAP	#14
	ADDQ.W	#2,SP
	RTS
	DS.B	512

BUF_BOOT	DS.B	512