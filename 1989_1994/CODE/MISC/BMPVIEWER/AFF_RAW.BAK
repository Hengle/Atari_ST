               ;VIEWER DE RAW EN COULEUR STE (POUR LE STF C'EST TOUT BETE!)

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	BSR	INIT_SYSTEME

	MOVE.L	SCREEN1,A0
	MOVE.W	#8000-1,D7
.EFF	CLR.L	(A0)+
	DBRA	D7,.EFF

	MOVE.W	IMG+6,D7	;2^D7=NB DE COULEURS
	MOVE.W	IMG+8,D0	;D0=LONGUEUR
	MOVE.W	D0,LONG_PIX
	LSR.W	#4,D0
	ADDQ.W	#1,D0
	LSL.W	#3,D0	;D0=LONGUEUR EN OCTETS
	MOVE.W	D0,LONG
	SUBI.W	#160,D0
	MOVE.W	D0,OFF
	MOVE.W	IMG+10,D1	;D1=HAUTEUR
	MULU.W	LONG_PIX,D1	;D1=NB TOTAL DE PIXEL
	MOVE.L	D1,TOTAL

	LEA	IMG+32,A0
;	ADDA.W	(A0)+,A0
;	SUBQ.W	#1,A0
	;A0=DEBUT DES COULEURS
	MOVE.W	#$8240,A1
	MOVE.L	#15*32768,D4	;15 BITS DE VIRGULE
	DIVU.W	#255,D4	;FACTEUR MULTIPLICATIF
	MOVEQ	#16-1,D7
MAKE_COLORS	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVE.B	(A0)+,D0	;COMPOSANTE BLEUE
	MOVE.B	(A0)+,D1	;COMPOSANTE VERTE
	MOVE.B	(A0)+,D2	;COMPOSANTE ROUGE
	ADD.W	D0,D0
	ADD.W	D1,D1
	ADD.W	D2,D2
	MULU.W	D4,D0
	MULU.W	D4,D1
	MULU.W	D4,D2
	SWAP	D0
	SWAP	D1
	SWAP	D2
	ADD.W	D0,D0
	ADD.W	D1,D1
	ADD.W	D2,D2
	LEA	TABLE_COLOR,A4
	MOVE.W	(A4,D0.W),D0
	MOVE.W	(A4,D1.W),D1
	MOVE.W	(A4,D2.W),D2
	LSL.W	#4,D0
	LSL.W	#4,D0
	LSL.W	#4,D1
	OR.W	D1,D0
	OR.W	D2,D0
	MOVE.W	D0,(A1)+
	DBRA	D7,MAKE_COLORS

	LEA	IMG+80,A0
	LEA	BUF_IMG,A3
	MOVE.L	A3,A1
	MOVEQ	#7,D4   ;NO DU PIXEL A ALLUMER(0-->15)
	MOVEQ	#0,D3    ;NO DU PIXEL SUR LA LIGNE COURANTE
	MOVEQ	#1,D6	;INCREMENT ECRAN
	MOVE.L	TOTAL,D7
AFF	
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	RTS(PC),A5
	MOVE.L	(A5,D0.W),A5
	JSR	(A5)
	SUBQ.W	#1,D4
	BGE.S	.OK
	MOVEQ	#7,D4
	ADD.W	D6,A1
	EORI.W	#6,D6
.OK	ADDQ.W	#1,D3
	CMP.W	LONG_PIX,D3
	BLT.S	.OK2
LONG = *+2
	LEA	$1234(A3),A3
	MOVE.L	A3,A1
	MOVEQ	#0,D3	
	MOVEQ	#7,D4
.OK2	
	SUBQ.L	#1,D7
	BGT.S	AFF

	MOVE.L	SCREEN1,A5
	LEA	BUF_IMG,A6
	REPT	200
	MOVEM.L	(A6)+,D0-A4
	MOVEM.L	D0-A4,(A5)
	MOVEM.L	(A6)+,D0-A4
	MOVEM.L	D0-A4,52(A5)
	MOVEM.L	(A6)+,D0-A4
	MOVEM.L	D0-A4,104(A5)
	MOVE.L	(A6)+,156(A5)
	LEA	160(A5),A5
	ADDA.W	OFF,A6
	ENDR

J	JMP	J	

TABLE_COLOR	DC.W	0,8,1,9,2,$A,3,$B,4,$C,5,$D,6,$E,7,$F

COL0	RTS
COL1	BSET	D4,(A1)
	RTS
COL2	BSET	D4,2(A1)
	RTS
COL3	BSET	D4,(A1)
	BSET	D4,2(A1)
	RTS
COL4	BSET	D4,4(A1)
	RTS
COL5	BSET	D4,4(A1)
	BSET	D4,(A1)
	RTS
COL6	BSET	D4,4(A1)
	BSET	D4,2(A1)
	RTS
COL7	BSET	D4,4(A1)
	BSET	D4,2(A1)
	BSET	D4,(A1)
	RTS
COL8	BSET	D4,6(A1)
	RTS
COL9	BSET	D4,(A1)
	BSET	D4,6(A1)
	RTS
COL10	BSET	D4,2(A1)
	BSET	D4,6(A1)
	RTS
COL11	BSET	D4,(A1)
	BSET	D4,2(A1)
	BSET	D4,6(A1)
	RTS
COL12	BSET	D4,4(A1)
	BSET	D4,6(A1)
	RTS
COL13	BSET	D4,4(A1)
	BSET	D4,(A1)
	BSET	D4,6(A1)
	RTS
COL14	BSET	D4,4(A1)
	BSET	D4,2(A1)
	BSET	D4,6(A1)
	RTS
COL15	BSET	D4,4(A1)
	BSET	D4,2(A1)
	BSET	D4,(A1)
	BSET	D4,6(A1)
	RTS

INIT_SYSTEME	CLR.B	$FFFF8260.W
	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

RTS	DC.L	COL0,COL1,COL2,COL3,COL4,COL5,COL6,COL7
	DC.L	COL8,COL9,COL10,COL11,COL12,COL13,COL14,COL15
	REPT	256-16
	DC.L	COL0
	ENDR

IMG	INCBIN	RANCE97.RAW

	SECTION	BSS
LONG_PIX	DS.W	1
TOTAL	DS.L	1
OFF	DS.W	1
	DS.B	256
BUFFER	DS.B	64000
SCREEN1	DS.L	1
SCREEN2	DS.L	1
BUF_IMG	DS.B	320*600