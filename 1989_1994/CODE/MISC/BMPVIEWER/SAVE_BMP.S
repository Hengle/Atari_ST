               ;VIEWER FULL DE BMP EN COULEUR STE (POUR LE STF C'EST TOUT BETE!)

BORD_GAUCHE	MACRO
	MOVE.W	A7,(A7)	;$8260
	MOVE.B	#0,(A7)
	ENDM

BORD_DROIT	MACRO
	MOVE.B	#0,(A6)	;$820A
	MOVE.W	A7,(A6)
	ENDM

STAB	MACRO
	MOVE.W	A7,(A7)	;$8260
	MOVE.B	#0,(A7)
	ENDM

NP	MACRO
	REPT	\1
	NOP
	ENDR
	ENDM

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	BSR	INIT_SYSTEME

	MOVE.L	SCREEN1,A0
	MOVE.W	#8000-1,D7
.EFF	CLR.L	(A0)+
	DBRA	D7,.EFF

;	MOVE.L	IMG+16,D7	;2^D7=NB DE COULEURS
	MOVE.L	IMG+16,D0	;D0=LONGUEUR
	ROL.W	#8,D0
	SUBQ.W	#1,D0
	LSR.W	#3,D0
	ADDQ.W	#1,D0
	LSL.W	#3,D0
	MOVE.W	D0,LONG_PIX
	LSR.W	#4,D0
	ADDQ.W	#1,D0
	LSL.W	#3,D0	;D0=LONGUEUR EN OCTETS
	MOVE.W	D0,LONG
	MOVE.W	D0,LONG2
	CMPI.W	#224,D0
	BGE.S	.OK
	CLR.W	OFF
	MOVE.W	#224,D1
	SUB.W	D0,D1
	MOVE.W	D1,OFF2
	LSR.W	#3,D0
	SUBQ.W	#1,D0
	MOVE.W	D0,NB_MOTS
	BRA.S	.OK2
.OK	MOVE.W	#28-1,NB_MOTS
	SUBI.W	#224,D0
	MOVE.W	D0,OFF
.OK2	MOVE.L	IMG+20,D1	;D1=HAUTEUR
	ROL.W	#8,D1
	MOVE.W	D1,HAUT
	MULU.W	LONG_PIX,D1	;D1=NB TOTAL DE PIXEL
	MOVE.L	D1,TOTAL

	LEA	IMG+54,A0
	;A0=DEBUT DES COULEURS
	MOVE.W	#$8240,A1
	MOVE.L	#15*32768,D4	;15 BITS DE VIRGULE
	DIVU.W	#255,D4	;FACTEUR MULTIPLICATIF
	MOVEQ	#16-1,D7
MAKE_COLORS	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVE.B	(A0)+,D2	;COMPOSANTE BLEUE
	MOVE.B	(A0)+,D1	;COMPOSANTE VERTE
	MOVE.B	(A0)+,D0	;COMPOSANTE ROUGE
	ADDQ.W	#1,A0
	ADD.W	D0,D0
	ADD.W	D1,D1
	ADD.W	D2,D2
	MULU.W	D4,D0
	MULU.W	D4,D1
	MULU.W	D4,D2
	SWAP	D0
	SWAP	D1
	SWAP	D2
	ADD.W	D0,D0
	ADD.W	D1,D1
	ADD.W	D2,D2
	LEA	TABLE_COLOR,A4
	MOVE.W	(A4,D0.W),D0
	MOVE.W	(A4,D1.W),D1
	MOVE.W	(A4,D2.W),D2
	LSL.W	#4,D0
	LSL.W	#4,D0
	LSL.W	#4,D1
	OR.W	D1,D0
	OR.W	D2,D0
	MOVE.W	D0,(A1)+
	DBRA	D7,MAKE_COLORS

	LEA	IMG+118,A0
	LEA	BUF_IMG,A3
	MOVE.W	LONG,D0
	MULU.W	HAUT,D0
	ADDA.L	D0,A3
	MOVE.L	A3,A1
	MOVEQ	#7,D4   ;NO DU PIXEL A ALLUMER(0-->15)
	MOVEQ	#0,D3    ;NO DU PIXEL SUR LA LIGNE COURANTE
	MOVEQ	#1,D6	;INCREMENT ECRAN
	MOVE.L	TOTAL,D7
AFF	
	MOVEQ	#0,D0
	MOVE.B	(A0),D0
	LSR.W	#4,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	RTS(PC),A5
	MOVE.L	(A5,D0.W),A5
	JSR	(A5)
	SUBQ.W	#1,D4
	BGE.S	.OK
	MOVEQ	#7,D4
	ADD.W	D6,A1
	EORI.W	#6,D6
.OK	ADDQ.W	#1,D3
	CMP.W	LONG_PIX,D3
	BLT.S	.OK2
LONG2 = *+2
	SUBA.W	#$1234,A3
	MOVE.L	A3,A1
	MOVEQ	#0,D3	;PIXEL DE DEPART
	MOVEQ	#7,D4	;BIT DE DEPART
	MOVEQ	#1,D6	;INCREMENT DE DEPART
.OK2	
	SUBQ.L	#1,D7
SECOND_QUARTET	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0
	ANDI.W	#15,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	RTS(PC),A5
	MOVE.L	(A5,D0.W),A5
	JSR	(A5)
	SUBQ.W	#1,D4
	BGE.S	.OK
	MOVEQ	#7,D4
	ADD.W	D6,A1
	EORI.W	#6,D6
.OK	ADDQ.W	#1,D3
	CMP.W	LONG_PIX,D3
	BLT.S	.OK2
LONG = *+2
	SUBA.W	#$1234,A3
	MOVE.L	A3,A1
	MOVEQ	#0,D3	;PIXEL DE DEPART
	MOVEQ	#7,D4	;BIT DE DEPART
	MOVEQ	#1,D6	;INCREMENT DE DEPART
.OK2	
	SUBQ.L	#1,D7
	BGT.S	AFF

	MOVE.L	SCREEN1,A5
	LEA	160(A5),A5
	MOVE.L	SCREEN2,A4
	LEA	160(A4),A4
	LEA	BUF_IMG+48+328*9,A6
	MOVE.W	#289-1,D7
.COPY_ALL	MOVE.W	NB_MOTS,D6
.AFF_X	MOVE.L	(A6),(A4)+
	MOVE.L	(A6)+,(A5)+
	MOVE.L	(A6),(A4)+
	MOVE.L	(A6)+,(A5)+
	DBRA	D6,.AFF_X
	ADDA.W	OFF2,A4
	ADDA.W	OFF2,A5
	ADDQ.W	#6,A4
	ADDQ.W	#6,A5
	ADDA.W	OFF,A6
	DBRA	D7,.COPY_ALL

	LEA	BUF_IMG+48+328*9,A0
	LEA	BUF_SOV,A1
	MOVE.W	#289-1,D6
.Y	MOVE.W	#28-1,D7
.COPY	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,.COPY
	ADDA.W	OFF,A0
	DBRA	D6,.Y
	LEA	BUF_SOV,A0
	SUBQ.W	#1,A1

	MOVE.W	#$2700,SR
	CLR.W	NB_VBL
	MOVE.L	#INTER_RTE,$68.W
	MOVE.L	#INTER_RTE,$120.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

J	JMP	J	

TABLE_COLOR	DC.W	0,8,1,9,2,$A,3,$B,4,$C,5,$D,6,$E,7,$F

COL0	RTS
COL1	BSET	D4,(A1)
	RTS
COL2	BSET	D4,2(A1)
	RTS
COL3	BSET	D4,(A1)
	BSET	D4,2(A1)
	RTS
COL4	BSET	D4,4(A1)
	RTS
COL5	BSET	D4,4(A1)
	BSET	D4,(A1)
	RTS
COL6	BSET	D4,4(A1)
	BSET	D4,2(A1)
	RTS
COL7	BSET	D4,4(A1)
	BSET	D4,2(A1)
	BSET	D4,(A1)
	RTS
COL8	BSET	D4,6(A1)
	RTS
COL9	BSET	D4,(A1)
	BSET	D4,6(A1)
	RTS
COL10	BSET	D4,2(A1)
	BSET	D4,6(A1)
	RTS
COL11	BSET	D4,(A1)
	BSET	D4,2(A1)
	BSET	D4,6(A1)
	RTS
COL12	BSET	D4,4(A1)
	BSET	D4,6(A1)
	RTS
COL13	BSET	D4,4(A1)
	BSET	D4,(A1)
	BSET	D4,6(A1)
	RTS
COL14	BSET	D4,4(A1)
	BSET	D4,2(A1)
	BSET	D4,6(A1)
	RTS
COL15	BSET	D4,4(A1)
	BSET	D4,2(A1)
	BSET	D4,(A1)
	BSET	D4,6(A1)
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	MOVE.B	#0,$FFFFFA19.W
	MOVE.B	#99,$FFFFFA1F.W       ; BORDER HAUT
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W
	;ICI 33 LIGNES DE LIBRES

INTER_RTE	RTE

INTER_TMA:	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE	#$2100,SR

	MOVEQ	#16,D0
	LEA	$FFFF8209.W,A0
	LEA	$FFFF820A.W,A6
	LEA	$FFFF8260.W,A3

	MOVEQ	#2,D3
	MOVEQ	#0,D4

	STOP	#$2100

	MOVE	#$2700,SR
	MOVE.W	#$2300,(SP)

	MOVEQ	#29,D2
SYNCHROA:	DBF	D2,SYNCHROA
	NOP

	MOVE.B	D4,(A6)	;820A
	REPT	6
	NOP
	ENDR
	MOVE.B	D3,(A6)	;820A

	;SYNCHRO ICI...
.SYNCHRO	MOVE.B	(A0),D2
	BEQ.S	.SYNCHRO
	SUB.B	D2,D0
	LSL.W	D0,D0

	DCB.W	97-9,$4E71
	MOVE.L	A7,AUTO_MOD1	;5
	MOVE.W	#$8260,A7	;2
	MOVE.W	#$820A,A6	;2

	BORD_GAUCHE
	NP	88
	BORD_DROIT
	NP	13-4
	MOVE.W	#226-1,D7
	NOP
.FULL	NOP
	STAB
	NP	12
	BORD_GAUCHE
	NP	88
	BORD_DROIT
	NP	13-4
	DBRA	D7,.FULL

	STAB
	NP	12
	BORD_GAUCHE
	NP	88
	BORD_DROIT
	NP	13
	STAB
	NP	4
	MOVE.B	#0,(A6)	;$820A
	NP	5
	BORD_GAUCHE
	MOVE.W	A7,(A6)
	NP	88-2
	BORD_DROIT
	NP	13-4
	MOVE.W	#45-1,D7
	NOP
.FULL2	NOP
	STAB
	NP	12
	BORD_GAUCHE
	NP	88
	BORD_DROIT
	NP	13-4
	DBRA	D7,.FULL2

AUTO_MOD1 = *+2
	LEA	$12345678,A7

	BSR	SWAPEC

	RTE

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

INIT_SYSTEME	CLR.B	$FFFF8260.W
	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#64000+3*1280,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

RTS	DC.L	COL0,COL1,COL2,COL3,COL4,COL5,COL6,COL7
	DC.L	COL8,COL9,COL10,COL11,COL12,COL13,COL14,COL15
	REPT	256-16
	DC.L	COL0
	ENDR

IMG	INCBIN	RANCE03.BMP

	SECTION	BSS
	DS.L	256
NEW_PILE	DS.L	256
NB_VBL	DS.W	1
LONG_PIX	DS.W	1
TOTAL	DS.L	1
HAUT	DS.W	1
OFF	DS.W	1
OFF2	DS.W	1
NB_MOTS	DS.W	1
	DS.B	256
BUFFER	DS.B	128000+9*1280*2
SCREEN1	DS.L	1
SCREEN2	DS.L	1
BUF_IMG	DS.B	328*600
BUF_SOV	DS.B	224*300