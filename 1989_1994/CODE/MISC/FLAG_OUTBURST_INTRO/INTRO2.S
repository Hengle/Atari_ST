MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	BSR	PREP_ECR
	BSR	PREP_ALL

	MOVEM.L	PALETTE,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

IT_VBL	BSR	VSYNC
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.F
	MOVE.W	#$300,$FFFF8240.W
.F	BSR	GEST_SCROLL
	BSR	GEST_PIERRES
	BSR	SWAPEC
	LEA	ADR_EFF_P,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0),-(A0)
	MOVE.L	D0,4(A0)
	CLR.W	$FFFF8240.W
	BRA	IT_VBL

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	RTE

GEST_SCROLL	MOVE.W	CNT_LG,D0
	CMPI.W	#400,D0
	BEQ.S	.FUCK
	ADDQ.W	#1,D0
	SUB.L	#160,SCREEN2
	SUB.L	#160,ANNEX_2
.FUCK	MOVE.W	D0,CNT_LG
	RTS

GEST_PIERRES	MOVE.L	ADR_EFF_P,A0
	MOVE.W	(A0)+,D0	;NB DE PIERRES A EFFACER
	BEQ	.NIB_ONE
.REPEAT_PIERRES	MOVE.L	SCREEN2,A1
	MOVE.L	ANNEX_2,A2
	MOVE.W	(A0)+,D1	;OFFSET ECRAN
	ADDA.W	D1,A1
	ADDA.W	D1,A2
N	SET	0
	REPT	34
	MOVEM.L	(A2)+,D1-D6
	MOVEM.L	D1-D6,N(A1)
	LEA	136(A2),A2
N	SET	N+160
	ENDR
	DBRA	D0,.REPEAT_PIERRES
.NIB_ONE	MOVEQ	#0,D5	;NB DE PIERRES AFFICHêES
	MOVE.L	ADR_EFF_P,A4
	LEA	2(A4),A5
	LEA	BUF_COOR,A2
	MOVEQ	#15,D7
.RECOM_PIERRE	MOVEQ	#0,D0
	MOVE.L	(A2),D0	;Y ACTUEL
	MOVEQ	#0,D1
	MOVE.W	4(A2),D1
	SUB.L	D1,D0
	MOVE.L	D0,D3
	LSR.L	#8,D3	;NEW Y ACTUEL
	CMPI.W	#-32,D3
	BGT.S	.LET_S_GO
	MOVE.L	#220*256,D0
	MOVE.W	#220,D3
.LET_S_GO	MOVE.L	D0,(A2)+
	MOVEQ	#8,D6
	CMPI.W	#200,D3
	BGE.S	.NO_AFF
	MULS.W	#160,D3
	ADDQ.W	#2,A2
	ADD.W	(A2)+,D3	;+OFFSET X
	MOVE.L	(A2)+,A1	;ADRESSE DE LA ROUTINE
	MOVE.L	SCREEN2,A0
	ADD.W	D3,A0
	MOVE.W	D3,(A5)+
	JSR	(A1)
	ADDQ.W	#1,D5
	MOVEQ	#0,D6
.NO_AFF	ADDA.W	D6,A2
	DBRA	D7,.RECOM_PIERRE
	MOVE.W	D5,(A4)
	RTS

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	MOVE.B	SCREEN1+3,$FFFF820D.W
	MOVE.L	ANNEX_1,D0
	MOVE.L	ANNEX_2,ANNEX_1
	MOVE.L	D0,ANNEX_2
	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	ADDI.L	#32*160,D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#64000+32*160,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	MOVE.B	SCREEN1+3,$FFFF820D.W
	RTS

PREP_ALL	LEA	GRAF,A0
	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
	LEA	GRAF_ANNEX,A3
	MOVE.W	#8000-1-40,D7
.R	MOVE.L	(A0),(A1)+
	MOVE.L	(A0),(A2)+
	MOVE.L	(A0)+,(A3)+
	DBRA	D7,.R
	LEA	160(A0),A0
	MOVE.W	#8000-1,D7
.S	MOVE.L	(A0),(A1)+
	MOVE.L	(A0),(A2)+
	MOVE.L	(A0)+,(A3)+
	DBRA	D7,.S
	ADDI.L	#32000,SCREEN1
	ADDI.L	#32000,SCREEN2
	LEA	GRAF_ANNEX+32000,A0
	MOVE.L	A0,ANNEX_1
	MOVE.L	A0,ANNEX_2
	CLR.W	CNT_LG
	LEA	COOR_PIERRES(PC),A0
	LEA	BUF_COOR,A1
	LEA	COEFFS(PC),A4
	MOVEQ	#2,D6
.FUCK_2	MOVE.W	(A4)+,D5
	MOVE.W	(A4)+,D4
	LEA	CODS(PC),A2
	ADDA.W	D4,A2
	MOVEQ	#5,D7
.FUCK	MOVE.W	(A0)+,D0	;No DE LA PIERRE(0 OU 1)
	MOVE.W	(A0)+,D1	;X
	MOVEQ	#0,D2
	MOVE.W	(A0)+,D2	;Y
	ANDI.W	#-16,D1
	LSR.W	#1,D1
;	MOVE.W	D2,D3
;	MULU.W	#160,D3
;	ADD.W	D3,D1	;OFFSET ECRAN
	LSL.L	#8,D2
	MOVE.L	D2,(A1)+	;ON RANGE LE Y ACTUEL
	MOVE.W	D5,(A1)+	;VITESSE *2^8
	MOVE.W	D1,(A1)+	;ON RANGE L'OFFSET
	LEA	CODS(PC),A3
	ADDA.W	(A2)+,A3
	MOVE.L	A3,(A1)+	;ADRESSE DE LA ROUTINE D'AFFICHAGE DE LA PIERRE CORRESPONDANTE
	DBRA	D7,.FUCK
	DBRA	D6,.FUCK_2
	LEA	ADR_EFF_P,A0
	LEA	BUF_EFF_P,A1
	LEA	19*2(A1),A2
	MOVE.L	A1,(A0)+
	MOVE.L	A2,(A0)
	RTS

COEFFS	DC.W	250,12,394,0,502,24

COOR_PIERRES	INCBIN	PIERRES.COR

CODS	INCBIN	PIERRES.COD

PALETTE	INCBIN	PALETTE.PAL

GRAF	INCBIN	GOKOU.TOT

	SECTION	BSS
CNT_LG	DS.W	1
NB_VBL	DS.W	1
BUF_COOR	DS.W	6*18
ADR_EFF_P	DS.L	2
BUF_EFF_P	DS.W	19*2
ANNEX_1	DS.L	1
ANNEX_2	DS.L	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.B	128000+96*160
GRAF_ANNEX	DS.B	400*160