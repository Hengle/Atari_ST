	;147 LIGNES POUR LA FABRICATION DES ANDS 
	;REMARQUE ( AVANT : MOINS DE 40 LIGNES )

	;CETTE VERSION COMPREND : LA GENERATION DES ANDS COMPLETE
	;	           ( REGISTRES + MODES D'ADRESSAGE )
	;	           LA GENERATION DES ORS-MOVES
	;	           ( REGISTRES )
	;	           LA TABLE D'OPT:OR PUIS MOVE
	;	           (PAS OR-MOVE,OR-MOVE,OR,OR...)

	;RESTE A FAIRE : - L'OPTIMISATION DES MODES D'ADRESSAGES SUR
	;	    LES MOVES & LES ORS ( IRREALISTE !! )
	;	  - LES MOVEMS, TRES IMPORTANT !!!

	;v0.8A

	PEA	0.W
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP
	MOVE.L	D0,SAUV_SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	BSR	PREP_ECR
	BSR	MAKE_CODE_GêNêRê

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	MOVEM.L	PALETTE,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
;	MOVE.W	#$745,$FFFF825E.W

	MOVE.L	SCREEN1,A0
	MOVE.W	#(40*20)-1,D7
EFF_ECR	CLR.L	(A0)+
	DBRA	D7,EFF_ECR
	MOVEQ	#0,D0
	MOVE.W	#(40*180)-1,D7
ECR_1	MOVE.L	D0,(A0)+
	DBRA	D7,ECR_1

	MOVE.L	SCREEN1,A0
	LEA	160*50(A0),A0
	LEA	SPR,A1
	MOVEQ	#79,D7
.RECOP_Y	MOVEM.L	(A1)+,D0-D6/A2-A6
	MOVEM.L	D0-D6/A2-A6,(A0)
	MOVE.L	(A1)+,48(A0)
	MOVE.L	(A1)+,52(A0)
	MOVE.L	(A1)+,56(A0)
	MOVE.L	(A1)+,60(A0)
	LEA	160(A0),A0
	DBRA	D7,.RECOP_Y

VBL	;MOVE.W	#$2700,SR
	;MOVEQ	#24,D0
	;MOVE.W	#$8209,A0
SYNCHRO	;TST.B	(A0)
	;BEQ.S	SYNCHRO
	;SUB.B	(A0),D0
	;LSL	D0,D0
	ST	$FFFF8240.W
	MOVE.L	SCREEN1,A0
	LEA	160*20(A0),A0
	MOVE.W	BUF_COD,D0
	LEA	BUF_COD,A1
	JSR	(A1,D0.W)
	SF	$FFFF8240.W
	;MOVE.W	#$2300,SR
	MOVE.W	#$25,-(SP)
	TRAP	#14
	ADDQ.W	#2,SP
	CMPI.B	#$39,$FFFFFC02.W
	BNE.S	VBL

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	TRAP	#1

PALETTE	INCBIN	LVL1.PAL
ANC_PAL	DS.L	8

VAR_1	DS.L	1
VAR_2	DS.L	1
START_CODE	DS.L	1

MAKE_CODE_GêNêRê
	LEA	OPT,A5
	MOVE.L	#ANIM_A_FAIRE,PT_ANIM
	MOVEQ	#5,D7
REPEAT_SPR	MOVE.W	D7,NB_SPR
	LEA	BUF_COD,A6
	ADDQ.W	#2,A6
	MOVEM.L	A5/A6,-(SP)
	MOVE.W	(A5)+,D0	;NB DE SEPARATIONS POUR LES ANDS
	SUBQ.W	#1,D0
.RECOP_OPT	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	DBRA	D0,.RECOP_OPT
	MOVE.L	A6,VAR_1	;VAR_1=DEBUT DES OPT DES ORS
	MOVE.W	(A5)+,D0	;NB DE SEPARATIONS ORS
	MOVE.W	(A5)+,D1	;NB DE SEPARATIONS MOVES
	SUBQ.W	#1,D0
.RECOP_OPT_2	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	DBRA	D0,.RECOP_OPT_2
	MOVE.L	A6,VAR_2	;VAR_2=DEBUT DES OPT DES MOVES
	SUBQ.W	#1,D1
.RECOP_OPT_3	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	MOVE.L	(A5)+,(A6)+
	DBRA	D1,.RECOP_OPT_3

	MOVE.L	(SP)+,A5
	MOVE.L	(SP)+,D0
	MOVE.L	A6,START_CODE
	MOVE.L	A6,D1
	SUB.L	D0,D1
	ADDQ.W	#2,D1	;OFFSET
	NEG.W	D1
	MOVE.W	#$43FA,(A6)+	;LEA $12345678(PC),A1
	MOVE.W	D1,(A6)+
	MOVE.L	D0,A0
	SUBQ.W	#2,A0
	MOVE.L	A6,A1
	SUBQ.W	#4,A1
	SUB.L	A0,A1
	MOVE.W	A1,(A0)

	MOVE.L	PT_ANIM,A4
	MOVE.W	(A4)+,D0	;X
	ANDI.W	#15,D0	;D0=DECALAGE (0-->15)
	MOVE.W	D0,AFF_X+2	;MISE EN PLACE DU DECALAGE
	ADD.W	D0,D0
	ADD.W	D0,D0	;DEC*4
	LEA	TABLE_MSK,A3
	MOVE.L	(A3,D0.W),D4
	MOVE.W	(A4)+,D1	;NO_SPR
	MULU.W	#12,D1
	MOVE.L	A4,PT_ANIM
	LEA	INF+4,A0
	ADDA.W	D1,A0	;A0=ADRESSE DE L'INFO-SPRITE
	LEA	SPR,A1
	ADDA.L	(A0)+,A1	;ADRESSE DU SPRITE
	MOVE.W	(A0)+,D0	;LONGUEUR-1
	MOVE.W	(A0)+,D1	;HAUTEUR-1
	ADDQ.W	#4,A0	;ON SAUTE 4 .B DE MERDE !
	ANDI.W	#$FFF0,D0
	LSR	#4,D0	;NB DE BLOK16 -1
	ADDQ.W	#1,D0
	MOVE.W	D0,LONG
	;ADDQ.W	#1,LONG
	MOVE.W	D1,HAUTS
	ADDQ.W	#1,HAUTS
	LEA	HAUTS,A4
	MOVE.W	(A4),-2(A4)
	MOVE.L	SCREEN1,A2
	MOVE.L	SCREEN2,A3
	MOVE.W	D0,AUTO_MODIF+2
	MOVE.W	#160,D5
	ADDQ.W	#1,D0
	LSL	#3,D0
	SUB.W	D0,D5
	MOVE.W	D5,OFFSET_FIN_LIG
AFF_Y	MOVE.W	D1,-(SP)
AUTO_MODIF	MOVE.W	#$1234,D7
	SUBQ.W	#1,D7
	MOVEQ	#0,D5
	MOVEQ	#0,D6
AFF_X	MOVE.W	#$1234,D3
	MOVE.L	(A1)+,D0	;PLAN 1&2
	MOVE.L	(A1)+,D1	;PLAN 3&4
	ROR.L	D3,D0
	ROR.L	D3,D1
	MOVE.L	D0,D2
	MOVE.L	D1,D3
	AND.L	D4,D2	;BITS A CONSERVER
	AND.L	D4,D3
	EOR.L	D2,D0	;PARTIE A AFFICHER
	EOR.L	D3,D1
	SWAP	D2
	SWAP	D3
	OR.L	D5,D0
	OR.L	D6,D1
	MOVE.L	D0,(A3)+
	MOVE.L	D1,(A3)+
	MOVE.L	D2,D5
	MOVE.L	D3,D6
	OR.L	D0,D1
	MOVE.L	D1,D0
	SWAP	D0
	OR.L	D1,D0
	NOT.L	D0
	MOVE.L	D0,(A2)+
	MOVE.L	D0,(A2)+
	DBRA	D7,AFF_X
	MOVE.L	D5,(A3)+
	MOVE.L	D6,(A3)+
	OR.L	D5,D6
	MOVE.L	D6,D5
	SWAP	D5
	OR.L	D6,D5
	NOT.L	D5
	MOVE.L	D5,(A2)+
	MOVE.L	D5,(A2)+
	ADDA.W	OFFSET_FIN_LIG,A2
	ADDA.W	OFFSET_FIN_LIG,A3
	MOVE.W	(SP)+,D1
	DBRA	D1,AFF_Y

	LEA	HAUTS,A0
	MOVE.W	(A0),D7
	MOVEQ	#0,D6
	BTST	#0,D7
	BEQ.S	.PAIR_1
	MOVEQ	#1,D6
.PAIR_1	LSR	#1,D7
	ADD.W	D6,D7
	MOVE.W	D7,2(A0)
	MOVEQ	#0,D6
	BTST	#0,D7
	BEQ.S	.PAIR_2
	MOVEQ	#1,D6
.PAIR_2	LSR	#1,D7
	ADD.W	D6,D7
	MOVE.W	D7,6(A0)
	MOVEQ	#0,D6
	BTST	#0,D7
	BEQ.S	.PAIR_3
	MOVEQ	#1,D6
.PAIR_3	LSR	#1,D7
	ADD.W	D6,D7
	MOVE.W	D7,14(A0)
	BSR	MAKE_ANDS
	BSR	MAKE_ORS
	MOVE.L	PT_ANIM,A4
	MOVE.L	(A4)+,A3
	MOVE.L	A4,PT_ANIM
	CLR.W	-(SP)
	PEA	(A3)
	MOVE.W	#$3C,-(SP)
	TRAP	#1
	ADDQ.W	#8,SP
	MOVE.W	D0,HANDLE
	SUBA.L	#BUF_COD,A6
	PEA	BUF_COD
	MOVE.L	A6,-(SP)
	MOVE.W	HANDLE,-(SP)
	MOVE.W	#$40,-(SP)
	TRAP	#1
	LEA	12(SP),SP
	MOVE.W	HANDLE,-(SP)
	MOVE.W	#$3E,-(SP)
	TRAP	#1
	ADDQ.W	#4,SP

	MOVE.W	NB_SPR,D7
	DBRA	D7,REPEAT_SPR
	RTS

HANDLE	DS.W	1

MAKE_ANDS	MOVE.W	#$4E60,(A6)+
	MOVE.W	(A5)+,D0	;D0=1,2,4,8=NB D'ITêRATIONS
	LEA	HAUTS,A0
	ADD.W	D0,D0
	MOVE.W	-2(A0,D0.W),D7	;D7=HAUTEUR
	LSR	D0
	MOVE.L	SCREEN1,A0	;MASK
	SUBQ.W	#1,D7	;D7=HAUTEUR-1
	MOVEQ	#0,D5	;D5=0=OFFSET ECRAN DE DEPART
	SUBQ.W	#1,D0	;D0=NB D'ITêRATIONS-1
.RECOD_ITER	MOVE.L	#$4CD900FF,(A6)+ ;ON POSE LE MOVEM ...,D0-D7	
	MOVE.W	D7,D4	;D4=HAUTEUR-1
.CODE_ANDS_Y	MOVE.W	LONG,D6
	LEA	BUF_CONJ,A4
.CODE_ANDS_X	TST.L	(A0)	;MASK VIDE=MOVE=NO ANDS ?
	BEQ.S	.NO_ANDS
	CMPI.W	#-1,(A0)	;MASK PLEIN=RIEN=NO ANDS ?
	BEQ.S	.NO_ANDS
	MOVE.L	(A0),D2	;D2=LE MASK
	MOVE.L	#$02A8C1A8,D3	;CODE DU ANDI+AND Dn
	LEA	.RETOUR_S,A1
	BRA	SEARCH_M
.NO_ANDS	ADDQ.W	#8,D5	;PROCHAIN OFFSET ECRAN
.RETOUR_S	ADDQ.W	#8,A0	;PROCHAIN BLOK16
	DBRA	D6,.CODE_ANDS_X
	BSR	OPTIMIZE_1
	MOVE.W	OFFSET_FIN_LIG,D6
	ADDA.W	D6,A0
	ADD.W	D6,D5
	DBRA	D4,.CODE_ANDS_Y
	LEA	32(A5),A5
	DBRA	D0,.RECOD_ITER
	RTS

OPTIMIZE_1	MOVEM.L	D0-D4/D6-D7/A0-A3/A5-A6,-(SP)
	LEA	BUF_CONJ,A5
START_2	LEA	BUF_ADR,A2
	LEA	ADR_OPT,A3
	MOVEQ	#0,D7	;D7=COMPTEUR DE AND DONT LES OFFSETS SONT DE 4 EN 4
	CMPI.W	#$2A8,(A5)
	BNE.S	.PAS_IMM
	MOVE.W	6(A5),D0	;D0=OFFSET ECRAN DE DEPART(Imm)
	SUBQ.W	#4,D0
	BRA.S	START
.PAS_IMM	MOVE.W	2(A5),D0	;D0=OFFSET ECRAN DE DEPART(Dn)
	SUBQ.W	#4,D0
START	CMPA.L	A4,A5
	BEQ.S	FIN_OPTI
	CMPI.W	#$2A8,(A5)
	BNE.S	.PAS_IMM
	MOVE.W	6(A5),D1	;SECOND OFFSET A COMPARER AU PREMIER
	MOVEQ	#8,D3	;LONGUEUR D'UN ANDI.L=8.B
	BRA.S	.IMM
.PAS_IMM	MOVE.W	2(A5),D1	;""            ""            ""
	MOVEQ	#4,D3	;LONGUEUR D'UN AND.L=4.B
.IMM	MOVE.W	D1,D4
	SUB.W	D0,D1	;DIFFERENCE ENTRE LE DEUXIEME ET LE PREMIER
	SUBQ.W	#4,D1
	BNE.S	.PAS_SUIVI
	MOVE.L	A5,(A3)+
	MOVE.W	D3,(A2)+
	ADDQ.W	#1,D7
	ADDA.W	D3,A5	;AND SUIVANT
	MOVE.W	D4,D0
	BRA.S	START
.PAS_SUIVI	LEA	RETOUR_OPTI,A6
	CMPI.W	#4,D7
	BGE.S	MAK_OPTI
	SUBQ.W	#2,A5	;ON REVIENT TRAITER LE MEME AND
	BRA.S	START_2
RETOUR_OPTI	MOVEQ	#0,D7
	LEA	BUF_ADR,A2
	ADDA.W	D3,A5	;AND SUIVANT
	MOVE.W	D4,D0
	BRA.S	START
FIN_OPTI	LEA	RETOUR2OPTI,A6
	CMPI.W	#4,D7
	BGE.S	MAK_OPTI
RETOUR2OPTI	MOVEM.L	(SP)+,D0-D4/D6-D7/A0-A3/A5-A6
	LEA	BUF_CONJ,A3
.CONT_RECOP	CMPA.L	A3,A4
	BEQ.S	.FIN_RECOP
	MOVE.W	(A3)+,(A6)+	;ON RECOPIE LE CODE OPTIMISê
	BRA.S	.CONT_RECOP
.FIN_RECOP	RTS

MAK_OPTI	SUBQ.W	#1,D7
	MOVE.L	ADR_OPT,A1	;ADRESSE DU AND A OPTIMISER
	CMPI.W	#$2A8,(A1)
	BEQ.S	.IMM
	MOVE.W	2(A1),D0
	BRA.S	.PAS_IMM
.IMM	MOVE.W	6(A1),D0
.PAS_IMM	ADDQ.W	#4,A4
	MOVE.L	A4,A0
	ADDQ.W	#4,A1
.RECOPY_INV	CMPA.L	A1,A0
	BEQ.S	.FIN_INV
	MOVE.W	-6(A0),-(A0)
	BRA.S	.RECOPY_INV
.FIN_INV	MOVE.W	#$41E8,-4(A1)
	MOVE.W	D0,-2(A1)	;LE LEA !!!
	SUB.W	D0,D5	;L'OFFSET ECRAN EST DIMINUê A CAUSE DU LEA !!!
	LEA	BUF_ADR,A0
.RECOM_OPTI	MOVE.W	(A0)+,D0	;LONGUEUR DU AND A OPTIMISER
	MOVE.W	(A1),D1	;CODE DU AND
	ANDI.W	#$FFC7,D1	;ON VIRE LE MODE d16(A0)
	ORI.W	#$18,D1	;ON REMPLACE PAR (A0)+
	MOVE.W	D1,(A1)	;ON POSE LE NOUVEAU AND
	SUBQ.W	#2,D0
	ADD.W	D0,A1	;ON VA SUR L'OFFSET
	MOVE.L	A1,-(SP)
	LEA	2(A1),A3	;A3=INSTRUCTION SUIVANTE
.RECOPY	CMPA.L	A4,A3
	BEQ.S	.FIN_RECOPY
	MOVE.W	(A3)+,(A1)+	;ON ECRASE L'OFFSET
	BRA.S	.RECOPY
.FIN_RECOPY	SUBQ.W	#2,A4	;ADRESSE DE FIN -2
	MOVE.L	(SP)+,A1
	SUBQ.W	#4,D5
	DBRA	D7,.RECOM_OPTI
	JMP	(A6)

SEARCH_M	MOVE.L	D7,-(SP)
	MOVE.L	A5,A3
	MOVEQ	#7,D7
.TRY_SEARCH	CMP.L	(A3)+,D2	;EST-CE QUE LE MASK FIGURE DANS LES OPTIMISATIONS POSSIBLES ?
	BEQ.S	.FIND_REGISTER
	ADDI.W	#$200,D3
	DBRA	D7,.TRY_SEARCH
	SWAP	D3
	MOVE.W	D3,(A4)+	;CODE DU ANDI.L #...,XXX(A0)
	MOVE.L	D2,(A4)+	;LE MASK EN IMMêDIAT
	MOVE.W	D5,(A4)+	;L'OFFSET ECRAN
	ADDQ.W	#4,D5
	MOVE.W	D3,(A4)+	;CODE DU ANDI.L #...,XXX(A0)
	MOVE.L	D2,(A4)+	;LE MASK EN IMMêDIAT
	MOVE.W	D5,(A4)+	;L'OFFSET ECRAN
	ADDQ.W	#4,D5
	MOVE.L	(SP)+,D7
	JMP	(A1)
.FIND_REGISTER	MOVE.W	D3,(A4)+	;AND.L Dn,XXX(A0)
	MOVE.W	D5,(A4)+	;L'OFFSET ECRAN
	ADDQ.W	#4,D5
	MOVE.W	D3,(A4)+	;AND.L Dn,XXX(A0)
	MOVE.W	D5,(A4)+	;L'OFFSET ECRAN
	ADDQ.W	#4,D5
	MOVE.L	(SP)+,D7
	JMP	(A1)

MAKE_ORS	MOVE.W	#$4E68,(A6)+
	MOVE.L	SCREEN1,A0	;MASQUE
	MOVE.L	SCREEN2,A1	;SPRITE
	MOVE.W	(A5)+,D5	;NB DE SEPARATIONS DES ORS
	MOVE.W	(A5)+,D1	;NB DE SEPARATIONS DES MOVES
	MOVE.W	D5,-(SP)
       	MOVE.L	A5,A3
	LSL	#5,D5
	ADDA.W	D5,A3	;A5=OPT DES ORS,A3=OPT DES MOVES
	MOVE.W	(SP)+,D5
	MOVEM.L	D0-A5,-(SP)
	MOVE.L	VAR_1,A0
	MOVE.L	A6,D0
	SUB.L	A0,D0
	ADDQ.W	#2,D0
	NEG.W	D0
	MOVE.W	#$43FA,(A6)+
	MOVE.W	D0,(A6)+
	MOVE.L	VAR_2,A0
	MOVE.L	A6,D0
	SUB.L	A0,D0
	ADDQ.W	#2,D0
	NEG.W	D0
	MOVE.W	#$4DFA,(A6)+
	MOVE.W	D0,(A6)+
	MOVEM.L	(SP)+,D0-A5
	LEA	HAUTS,A2
	ADD.W	D5,D5
	ADD.W	D1,D1
	MOVE.W	-2(A2,D5.W),D2
	MOVE.W	-2(A2,D1.W),D3
	MOVE.W	D2,LABEL_1+2
	MOVE.W	D3,LABEL_2+2
	MOVEQ	#0,D4	;D4=OFFSET ECRAN
	MOVE.L	#$4CD900FF,(A6)+
	MOVE.L	#$4CDE3C00,(A6)+
	MOVE.W	(A2),D5
	SUBQ.W	#1,D5
CODE_Y	LEA	BUF_CONJ,A4
	MOVE.W	LONG,D7
CODE_X	CMPI.L	#-1,(A0)
	BEQ	RIEN_A_AFFICHER
	TST.L	(A0)
	BEQ	AFFICH_MOVE

AFFICH_OR	TST.L	(A1)
	BEQ	OR34
	TST.L	2(A1)
	BEQ	OR14
	TST.L	4(A1)
	BEQ	OR12
	TST.W	(A1)
	BEQ	OR234
	TST.W	2(A1)
	BEQ	OR134
	TST.W	4(A1)
	BEQ	OR124
	TST.W	6(A1)
	BEQ	OR123

OR1234	MOVE.L	#$A881A8,D0	;ORI.L #$Imm,d(A0)
	MOVE.L	(A1),D1	;Imm
	BSR	SEARCH_L
	MOVE.L	#$A881A8,D0	;ORI.L #$Imm,d(A0)
	MOVE.L	4(A1),D1	;Imm
	BSR	SEARCH_L
	BRA	FIN_AFFICH

OR123	TST.W	(A1)
	BEQ	OR23
	TST.W	2(A1)
	BEQ	OR13
	TST.W	4(A1)
	BEQ	OR12
	MOVE.L	#$A881A8,D0	;ORI.L #$Imm,d(A0)
	MOVE.L	(A1),D1	;Imm
	BSR	SEARCH_L
	MOVE.L	#$688168,D0	;ORI.W #$Imm,d(A0)
	MOVE.W	4(A1),D1	;Imm
	BSR	SEARCH_W
	ADDQ.W	#2,D4
	BRA	FIN_AFFICH

OR124	TST.W	(A1)
	BEQ	OR24
	TST.W	2(A1)
	BEQ	OR14
	TST.W	6(A1)
	BEQ	OR12
	MOVE.L	#$A881A8,D0	;ORI.L #$Imm,d(A0)
	MOVE.L	(A1),D1	;Imm
	BSR	SEARCH_L
	ADDQ.W	#2,D4	
	MOVE.L	#$688168,D0	;ORI.W #$Imm,d(A0)
	MOVE.W	6(A1),D1	;Imm
	BSR	SEARCH_W
	BRA	FIN_AFFICH

OR134	TST.W	(A1)
	BEQ	OR34
	TST.W	4(A1)
	BEQ	OR14
	TST.W	6(A1)
	BEQ	OR13
	MOVE.L	#$688168,D0	;ORI.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_W
	ADDQ.W	#2,D4
	MOVE.L	#$A881A8,D0	;ORI.L #$Imm,d(A0)
	MOVE.L	4(A1),D1	;Imm
	BSR	SEARCH_L
	BRA	FIN_AFFICH

OR234	TST.W	2(A1)
	BEQ	OR34
	TST.W	4(A1)
	BEQ	OR24
	TST.W	6(A1)
	BEQ	OR23
	MOVE.L	#$A881A8,D0	;ORI.L #$Imm,d(A0)
	MOVE.L	2(A1),D1	;Imm
	ADDQ.W	#2,D4
	BSR	SEARCH_L
	MOVE.L	#$688168,(A4)+	;ORI.W #$Imm,d(A0)
	MOVE.W	6(A1),D1	;Imm
	BSR	SEARCH_W
	BRA	FIN_AFFICH

OR12	TST.W	(A1)
	BEQ	OR2
	TST.W	2(A1)
	BEQ	OR1
	MOVE.L	#$A881A8,D0	;ORI.L #$Imm,d(A0)
	MOVE.L	(A1),D1	;Imm
	BSR	SEARCH_L
	ADDQ.W	#4,D4
	BRA	FIN_AFFICH

OR13	TST.W	(A1)
	BEQ	OR3
	TST.W	4(A1)
	BEQ	OR1
	MOVE.L	#$688168,D0	;ORI.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_W
	ADDQ.W	#2,D4
	MOVE.L	#$688168,D0	;ORI.W #$Imm,d(A0)
	MOVE.W	4(A1),D1	;Imm
	BSR	SEARCH_W
	ADDQ.W	#2,D4
	BRA	FIN_AFFICH

OR23	TST.W	2(A1)
	BEQ	OR3
	TST.W	4(A1)
	BEQ	OR2
	ADDQ.W	#2,D4
	MOVE.L	#$A881A8,D0	;ORI.L #$Imm,d(A0)
	MOVE.L	2(A1),D1	;Imm
	BSR	SEARCH_L
	ADDQ.W	#2,D4
	BRA	FIN_AFFICH

OR24	TST.W	2(A1)
	BEQ	OR2
	TST.W	6(A1)
	BEQ	OR4
	ADDQ.W	#2,D4
	MOVE.L	#$688168,D0	;ORI.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_W
	ADDQ.W	#2,D4
	MOVE.L	#$688168,D0	;ORI.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_W
	BRA	FIN_AFFICH
	
OR14	TST.W	(A1)
	BEQ	OR4
	TST.W	6(A1)
	BEQ	OR1
	MOVE.L	#$688168,D0	;ORI.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_W
	ADDQ.W	#4,D4
	MOVE.L	#$688168,D0	;ORI.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_W
	BRA	FIN_AFFICH

OR34	TST.W	4(A1)
	BEQ	OR4
	TST.W	6(A1)
	BEQ.S	OR3
	ADDQ.W	#4,D4
	MOVE.L	#$A881A8,D0	;ORI.L #$Imm,d(A0)
	MOVE.L	4(A1),D1	;Imm
	BSR	SEARCH_L
	BRA	FIN_AFFICH

OR4	ADDQ.W	#6,D4
	MOVE.L	#$688168,D0	;ORI.W #$Imm,d(A0)
	MOVE.W	6(A1),D1	;Imm
	BSR	SEARCH_W
	BRA	FIN_AFFICH

OR3	ADDQ.W	#4,D4
	MOVE.L	#$688168,D0	;ORI.W #$Imm,d(A0)
	MOVE.W	4(A1),D1	;Imm
	BSR	SEARCH_W
	ADDQ.W	#2,D4
	BRA	FIN_AFFICH

OR2	ADDQ.W	#2,D4
	MOVE.L	#$688168,D0	;ORI.W #$Imm,d(A0)
	MOVE.W	2(A1),D1	;Imm
	BSR	SEARCH_W
	ADDQ.W	#4,D4
	BRA	FIN_AFFICH

OR1	MOVE.L	#$688168,D0	;ORI.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_W
	ADDQ.W	#6,D4
	BRA	FIN_AFFICH

AFFICH_MOVE	TST.L	(A1)
	BEQ	MOVE34
	TST.L	2(A1)
	BEQ	MOVE14
	TST.L	4(A1)
	BEQ	MOVE12
	TST.W	(A1)
	BEQ	MOVE234
	TST.W	2(A1)
	BEQ	MOVE134
	TST.W	4(A1)
	BEQ	MOVE124
	TST.W	6(A1)
	BEQ	MOVE123

MOVE1234	MOVE.L	#$217C214A,D0	;MOVE.L #$Imm,d(A0)
	MOVE.L	(A1),D1	;Imm
	BSR	SEARCH_LMV
	MOVE.L	#$217C214A,D0	;MOVE.L #$Imm,d(A0)
	MOVE.L	4(A1),D1	;Imm
	BSR	SEARCH_LMV
	BRA	FIN_AFFICH

MOVE123	TST.W	(A1)
	BEQ	MOVE23
	TST.W	2(A1)
	BEQ	MOVE13
	TST.W	4(A1)
	BEQ	MOVE12
	MOVE.L	#$217C214A,D0	;MOVE.L #$Imm,d(A0)
	MOVE.L	(A1),D1	;Imm
	BSR	SEARCH_LMV
	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	4(A1),D1	;Imm
	BSR	SEARCH_WMV
	MOVE.W	#$4268,(A4)+	;CLR.W
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	BRA	FIN_AFFICH

	;$4268 : CLR.W $1234(A0)
	;$42A8 : CLR.L $1234(A0)

MOVE124	TST.W	(A1)
	BEQ	MOVE24
	TST.W	2(A1)
	BEQ	MOVE14
	TST.W	6(A1)
	BEQ	MOVE12
	MOVE.L	#$217C214A,D0	;MOVE.L #$Imm,d(A0)
	MOVE.L	(A1),D1	;Imm
	BSR	SEARCH_LMV
	MOVE.W	#$4268,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	6(A1),D1	;Imm
	BSR	SEARCH_WMV
	BRA	FIN_AFFICH

MOVE134	TST.W	(A1)
	BEQ	MOVE34
	TST.W	4(A1)
	BEQ	MOVE14
	TST.W	6(A1)
	BEQ	MOVE13
	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_WMV
	MOVE.W	#$4268,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	MOVE.L	#$217C214A,D0	;MOVE.L #$Imm,d(A0)
	MOVE.L	4(A1),D1	;Imm
	BSR	SEARCH_LMV
	BRA	FIN_AFFICH

MOVE234	TST.W	2(A1)
	BEQ	MOVE34
	TST.W	4(A1)
	BEQ	MOVE24
	TST.W	6(A1)
	BEQ	MOVE23
	MOVE.W	#$4268,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	MOVE.L	#$217C214A,D0	;MOVE.L #$Imm,d(A0)
	MOVE.L	2(A1),D1	;Imm
	BSR	SEARCH_LMV
	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	6(A1),D1	;Imm
	BSR	SEARCH_WMV
	BRA	FIN_AFFICH

MOVE12	TST.W	(A1)
	BEQ	MOVE2
	TST.W	2(A1)
	BEQ	MOVE1
	MOVE.L	#$217C214A,D0	;MOVE.L #$Imm,d(A0)
	MOVE.L	(A1),D1	;Imm
	BSR	SEARCH_LMV
	MOVE.W	#$42A8,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#4,D4
	BRA	FIN_AFFICH

MOVE13	TST.W	(A1)
	BEQ	MOVE3
	TST.W	4(A1)
	BEQ	MOVE1
	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_WMV
	MOVE.W	#$4268,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	4(A1),D1	;Imm
	BSR	SEARCH_WMV
	MOVE.W	#$4268,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	BRA	FIN_AFFICH

MOVE23	TST.W	2(A1)
	BEQ	MOVE3
	TST.W	4(A1)
	BEQ	MOVE2
	MOVE.W	#$4268,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	MOVE.L	#$217C214A,D0	;MOVE.L #$Imm,d(A0)
	MOVE.L	2(A1),D1	;Imm
	BSR	SEARCH_LMV
	MOVE.W	#$4268,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	BRA	FIN_AFFICH

MOVE24	TST.W	2(A1)
	BEQ	MOVE2
	TST.W	6(A1)
	BEQ	MOVE4
	MOVE.W	#$4268,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_WMV
	MOVE.W	#$4268,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_WMV
	BRA	FIN_AFFICH
	
MOVE14	TST.W	(A1)
	BEQ	MOVE4
	TST.W	6(A1)
	BEQ	MOVE1
	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_WMV
	MOVE.W	#$42A8,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#4,D4
	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_WMV
	BRA	FIN_AFFICH

MOVE34	TST.W	4(A1)
	BEQ	MOVE4
	TST.W	6(A1)
	BEQ.S	MOVE3
	MOVE.W	#$42A8,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#4,D4
	MOVE.L	#$217C214A,D0	;MOVE.L #$Imm,d(A0)
	MOVE.L	4(A1),D1	;Imm
	BSR	SEARCH_LMV
	BRA	FIN_AFFICH

MOVE4	MOVE.W	#$42A8,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#4,D4
	MOVE.W	#$4268,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	6(A1),D1	;Imm
	BSR	SEARCH_WMV
	BRA	FIN_AFFICH

MOVE3	MOVE.W	#$42A8,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#4,D4
	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	4(A1),D1	;Imm
	BSR	SEARCH_WMV
	MOVE.W	#$4268,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	BRA	FIN_AFFICH

MOVE2	MOVE.W	#$4268,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	2(A1),D1	;Imm
	BSR	SEARCH_WMV
	MOVE.W	#$42A8,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#4,D4
	BRA	FIN_AFFICH

MOVE1	MOVE.L	#$317C314A,D0	;MOVE.W #$Imm,d(A0)
	MOVE.W	(A1),D1	;Imm
	BSR	SEARCH_WMV
	MOVE.W	#$4268,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	MOVE.W	#$42A8,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#4,D4
	BRA	FIN_AFFICH
RIEN_A_AFFICHER
	ADDQ.W	#8,D4
FIN_AFFICH	ADDQ.W	#8,A0
	ADDQ.W	#8,A1
	DBRA	D7,CODE_X
	BSR	OPTIMIZE_ORS_MOVES
	LEA	BUF_CONJ,A2
.CONT_RECOP	CMPA.L	A4,A2
	BEQ.S	.FIN_RECOP
	MOVE.W	(A2)+,(A6)+
	BRA.S	.CONT_RECOP
.FIN_RECOP	ADDA.W	OFFSET_FIN_LIG,A0
	ADDA.W	OFFSET_FIN_LIG,A1
	ADD.W	OFFSET_FIN_LIG,D4
	SUBQ.W	#1,D2	;1 LIGNE OR EN MOINS
	SUBQ.W	#1,D3	;1 LIGNE MOVE EN MOINS
	TST.W	D2
	BNE.S	OR_P
LABEL_1	MOVE.W	#$1234,D2
	MOVE.L	#$4CD900FF,(A6)+
	LEA	32(A5),A5
OR_P	TST.W	D3
	BNE.S	MOV_P
LABEL_2	MOVE.W	#$1234,D3
	MOVE.L	#$4CDE3C00,(A6)+
	LEA	16(A3),A3
MOV_P	DBRA	D5,CODE_Y
	;SUBQ.W	#8,A6
	MOVE.W	#$4E75,(A6)+	;'RTS'
	MOVE.L	A3,A5
	RTS

OPTIMIZE_ORS_MOVES
	RTS

SEARCH_L	MOVEM.L	D7/A5,-(SP)
	MOVEQ	#7,D7
.SEARCH	CMP.L	(A5)+,D1
	BEQ.S	TROUVê_1
	ADDI.W	#$200,D0
	DBRA	D7,.SEARCH
	SWAP	D0
	MOVE.W	D0,(A4)+
	MOVE.L	D1,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#4,D4
	BRA.S	FIN_SEARCH_L
TROUVê_1	MOVE.W	D0,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#4,D4
FIN_SEARCH_L	MOVEM.L	(SP)+,D7/A5
	RTS

SEARCH_W	MOVEM.L	D7/A5,-(SP)
	MOVEQ	#7,D7
.SEARCH	CMP.W	2(A5),D1
	BEQ.S	TROUVê_2
	ADDI.W	#$200,D0
	ADDQ.W	#4,A5
	DBRA	D7,.SEARCH
	SWAP	D0
	MOVE.W	D0,(A4)+
	MOVE.W	D1,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	BRA.S	FIN_SEARCH_W
TROUVê_2	MOVE.W	D0,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
FIN_SEARCH_W	MOVEM.L	(SP)+,D7/A5
	RTS

SEARCH_LMV	MOVEM.L	D7/A3,-(SP)
	MOVEQ	#3,D7
.SEARCH	CMP.L	(A3)+,D1
	BEQ.S	TROUVê_3
	ADDQ.W	#1,D0
	DBRA	D7,.SEARCH
	SWAP	D0
	MOVE.W	D0,(A4)+
	MOVE.L	D1,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#4,D4
	BRA.S	FIN_SEARCH_LMV
TROUVê_3	MOVE.W	D0,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#4,D4
FIN_SEARCH_LMV	MOVEM.L	(SP)+,D7/A3
	RTS

SEARCH_WMV	MOVEM.L	D7/A3,-(SP)
	MOVEQ	#3,D7
.SEARCH	CMP.W	2(A3),D1
	BEQ.S	TROUVê_4
	ADDQ.W	#1,D0
	ADDQ.W	#4,A3
	DBRA	D7,.SEARCH
	SWAP	D0
	MOVE.W	D0,(A4)+
	MOVE.W	D1,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
	BRA.S	FIN_SEARCH_WMV
TROUVê_4	MOVE.W	D0,(A4)+
	MOVE.W	D4,(A4)+
	ADDQ.W	#2,D4
FIN_SEARCH_WMV	MOVEM.L	(SP)+,D7/A3
	RTS

BUF_CONJ	DS.W	100	;BUFFER OU LA LIGNE SERA PRECODêE
BUF_ADR	DS.W	100	;BUFFER OU LES OFFSETS ECRAN SERONT ENTREPOSêS PUIS CONTROLêS
ADR_OPT	DS.L	10

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

INF	INCBIN	A_DEAD.INF
SPR	INCBIN	A_DEAD.SPR

NB_AND	DS.W	1

OPT	;INCBIN	EX.OPT
	INCBIN	A_DEAD.OPT

TABLE_MSK	DC.L	0,$80008000,$C000C000,$E000E000,$F000F000
	DC.L	$F800F800,$FC00FC00,$FE00FE00,$FF00F00
	DC.L	$FF80FF80,$FFC0FFC0,$FFE0FFE0,$FFF0FFF0
	DC.L	$FFF8FFF8,$FFFCFFFC,$FFFEFFFE,$FFFFFFFF

ANIM_A_FAIRE	DC.W	39,13
	DC.L	FIC_1
	DC.W	121,12
	DC.L	FIC_2
	DC.W	26,20
	DC.L	FIC_3
	DC.W	146,25
	DC.L	FIC_4
	DC.W	48,0
	DC.L	FIC_5
	DC.W	74,1
	DC.L	FIC_6

FIC_1	DC.B	"JAMBE1_G.BIN",0
FIC_2	DC.B	"JAMBE1_D.BIN",0
FIC_3	DC.B	"CUISSE1G.BIN",0
FIC_4	DC.B	"CUISSE1D.BIN",0
FIC_5	DC.B	"TETE.BIN",0
FIC_6	DC.B	"MACHE_1.BIN",0

	SECTION	BSS
SAUV_SP	DS.L	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.L	16000
PT_ANIM	DS.L	1
NB_SPR	DS.W	1
LONG	DS.W	1
OFFSET_FIN_LIG	DS.W	1
	DS.W	1
HAUTS	DS.W	10
BUF_COD	DS.L	10000