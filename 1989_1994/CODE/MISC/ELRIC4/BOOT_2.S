;BOOT SPREADPOINT V.1
;CODE BY ELRIC FROM HOLOCAUST
;ON THE 7-12-92.

NB_VITESSES = 15
NB_CHANGE = 15

	MOVE.L	#R2,D0
	MOVE.L	#LONG_BOOT,D0

	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP
	MOVE.L	D0,SAUV_SP

	PEA	CHAIN(PC)
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP
RECOMMENCE	MOVE.W	#7,-(SP)
	TRAP	#1
	ADDQ.W	#2,SP
	CMPI.B	#32,D0
	BEQ.S	QUIT
	CMPI.B	#"*",D0
	BNE.S	RECOMMENCE

	MOVE.W	#1,-(SP)
	CLR.W	-(SP)
	CLR.W	-(SP)
	MOVE.W	#1,-(SP)
	CLR.W	-(SP)
	CLR.L	-(SP)
	PEA	BUF_BOOT(PC)
	MOVE.W	#8,-(SP)
	TRAP	#14
	LEA	20(SP),SP
	LEA	BUF_BOOT+2(PC),A0
	LEA	BOOT+2(PC),A1
	MOVEQ	#13,D7
.RECOP	MOVE.W	(A0)+,(A1)+
	DBRA	D7,.RECOP

	LEA	BOOT(PC),A0
	MOVEQ	#0,D0
	MOVE.W	#254,D7
.COMPT	ADD.W	(A0)+,D0
	DBRA	D7,.COMPT
	MOVE.W	#$1234,D1
	SUB.W	D0,D1
	MOVE.W	D1,(A0)

	MOVE.W	#1,-(SP)
	CLR.W	-(SP)
	CLR.W	-(SP)
	MOVE.W	#1,-(SP)
	CLR.W	-(SP)
	CLR.L	-(SP)
	PEA	BOOT(PC)
	MOVE.W	#9,-(SP)
	TRAP	#14
	LEA	20(SP),SP

	BRA	RECOMMENCE

QUIT	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	TRAP	#1

SAUV_SP	DS.L	1

CHAIN	DC.B	27,"ETHIS IS THE SPREADPOINT-BOOT"
	DC.B	27,"Y",33,32,"ALL CODES ARE FROM ELRIC OF HOLOCAUST"
	DC.B	27,"Y",34,32,"PRESS * IF YOU WANNA HAVE IT ON DISK"
	DC.B	27,"Y",35,32,"OF SPACE TO QUIT.",0
	EVEN

BUF_BOOT	DS.B	512

DEB_BOOT = *
BOOT	DC.W	$601C
	DS.B	28
	LEA	$40000,A6
	DC.W	$A000
	MOVE.L	4(A1),A0
	MOVE.L	76(A0),A0	;A0=ADRESSE DES FONTES 'SYSTEME'
	MOVE.L	A6,A2
	LEA	40(A2),A2
	LEA	MESSAGE(PC),A4
	MOVEQ	#R2-1,D6
.R2	MOVE.L	A2,A3
	MOVE.L	A0,A1
	MOVEQ	#0,D0
	MOVE.B	(A4)+,D0
	ADDA.W	D0,A1	;A1=ADRESSE DE LA FONTE
	MOVEQ	#7,D7
.R	MOVE.B	(A1),(A3)
	LEA	256(A1),A1
	LEA	CONST_1(A3),A3
	DBRA	D7,.R
	ADDQ.W	#1,A2
	DBRA	D6,.R2

	MOVE.L	A6,A0
	LEA	CONST_1(A0),A0
	MOVEQ	#1,D4
	MOVEQ	#14,D5
.DEC_3	MOVEQ	#7,D6
.DEC_2	LEA	CONST_1*8(A0),A1
	MOVEQ	#(CONST_1/4)-1,D7
	MOVEQ	#0,D2
.DEC_1	MOVE.L	-(A0),D0
	ROL.L	#1,D0
	MOVE.L	D0,D1
	AND.L	D4,D1	;BIT A CONSERVER
	EOR.L	D1,D0	;PARTIE A AFFICHER
	OR.L	D2,D0
	MOVE.L	D0,-(A1)
	MOVE.L	D1,D2
	DBRA	D7,.DEC_1
	LEA	CONST_1*2(A0),A0
	DBRA	D6,.DEC_2
	DBRA	D5,.DEC_3

	LEA	BUF_SCROLL(A6),A0
	MOVEQ	#1,D0	;VITESSE DE DEPART
	MOVEQ	#50/((NB_VITESSES*2)-1),D6
.POS_3	MOVEQ	#1,D5
.RECOM_POS	MOVEQ	#NB_VITESSES-2,D7
.POS	MOVE.W	D0,(A0)+
	CLR.W	(A0)+
	ADD.W	D5,D0
	DBRA	D7,.POS
	NEG.W	D5
	BLT.S	.RECOM_POS
	DBRA	D6,.POS_3

	LEA	ROUT_AFF(A6),A5
	MOVE.L	A5,A4
	MOVEQ	#7,D6
.ECRIT_2	MOVEQ	#19,D7
.ECRIT	MOVE.L	#$329A5049,(A4)+
	DBRA	D7,.ECRIT
	MOVE.W	#$45EA,(A4)+
	MOVE.W	#CONST_1-40,(A4)+
	DBRA	D6,.ECRIT_2
	MOVE.W	#$4E75,(A4)

	LEA	COURBE_Y(A6),A0
	MOVEQ	#1,D5
	MOVEQ	#NB_CHANGE*8,D0
.RECOM	MOVEQ	#(NB_CHANGE*8)-1,D6
.E	MOVE.W	D0,(A0)+
	SUB.W	D5,D0
	DBRA	D6,.E
	NEG.W	D5
	BLT.S	.RECOM

	MOVE.W	#$825C,A0
	CLR.W	-$1C(A0)
	MOVE.W	#$310,-$1A(A0)
	MOVE.W	#$337,(A0)+
	MOVE.W	#$337,(A0)
	PEA	MESSAGE_2(PC)
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	;A5=ADRESSE DU CODE GENERE POUR AFFICHER UN SCROLL
	MOVEQ	#0,D6
.VBL	LEA	BUF_SCROLL(A6),A0
	MOVE.L	$44E.W,A1
	LEA	-160*8(A1),A1
	LEA	COURBE_Y(A6),A4
	ADDA.W	D6,A4
	ADDQ.W	#2,D6
	CMPI.W	#2*NB_CHANGE*2*8,D6
	BLT.S	.NOT_YET
	MOVEQ	#0,D6
.NOT_YET	MOVE.W	(A4),D0
	MOVE.W	D0,D1
	ANDI.W	#$FFF8,D1
	LSR	#1,D1
	ADDA.W	D1,A0
	ANDI.W	#7,D0
	MULU.W	#160,D0
	SUBA.W	D0,A1

	MOVEQ	#26,D7
.AFF_SCR	ADDQ.W	#2,A0
	MOVE.W	(A0)+,D0	;D0=X DU SCROLL
	MOVE.W	D0,D1
	ANDI.W	#15,D1
	MULU.W	#CONST_1*8,D1	;D1=OFFSET
	ANDI.W	#-16,D0
	LSR	#3,D0
	ADD.W	D0,D1
	LEA	(A6,D1.W),A2	;A2=ADRESSE DU SCROLL
	JSR	(A5)
	DBRA	D7,.AFF_SCR

	LEA	BUF_SCROLL(A6),A0
	MOVEQ	#79,D7
.FUCK_2	MOVE.W	(A0)+,D0	;VITESSE
	ADD.W	(A0),D0	;X+VITESSE
	CMPI.W	#(CONST_1*8)-320,D0
	BLT.S	.FUCK
	SUBI.W	#(CONST_1*8)-320,D0
.FUCK	MOVE.W	D0,(A0)
	ADDQ.W	#2,A0
	DBRA	D7,.FUCK_2

	MOVE.W	#$25,-(SP)
	TRAP	#14
	ADDQ.W	#2,SP
	CMPI.B	#$39,$FFFFFC02.W
	BNE.S	.VBL

	RTS

MESSAGE_2	DC.B	27,"Y",44,32,"HOLOCAUST CODE IT IN LESS THAN 480.B !!!",0
	EVEN

R = *
MESSAGE	DC.B	"FUCK YOU, YOU HAVE NO VIRUS ON YOUR BOOT... HOLOCAUST RULES FOREVER!"
R2 = *-R

LONG_BOOT = *-DEB_BOOT

	DS.B	200

	RSRESET
BUF_MESSAGE	RS.B	(40+R2+40)*8*16
BUF_SCROLL	RS.B	4*80
ROUT_AFF	RS.W	(4*20*8)+1
COURBE_Y	RS.W	8*(NB_CHANGE*2)
LONG_BSS	RS.B	1

CONST_1 = 40+R2+40

BSS_BOOT	DS.B	LONG_BSS