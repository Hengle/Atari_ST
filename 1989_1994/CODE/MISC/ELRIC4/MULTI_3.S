;Premiäre partie : Intro.

NB_ZOOM = 20
;
NB_PASSES = 55		;TEMPS EN VBL POUR LA TRANSFORMATION
TEMPO_INTER_TRANSFO = 22	;TEMPS D'ATTENTE ENTRE CHAQUE TRANSFO
;
NB_PASSES_D = 47	;TEMPS D'UNE LETTRE POUR ALLER DE Z=0 A 666
;
NB_PASSES_Z = 60	;TEMPS EN VBL POUR LE ZOOM 'HOLOCAUST'

	RSRESET
X	RS.L	1
Y	RS.L	1
PAS_X	RS.W	1
PAS_Y	RS.W	1
X_DEF	RS.W	1
Y_DEF	RS.W	1
LONG_1	RS.B	1
	RSRESET
X_D	RS.L	1
Y_D	RS.L	1
Z_D	RS.L	1
PAS_X_D	RS.W	1
PAS_Y_D	RS.W	1
PAS_Z_D	RS.W	1
X_DEF_D	RS.W	1
Y_DEF_D	RS.W	1
LONG_2	RS.B	1
	RSRESET
PT_COOR	RS.W	1
PT_DEST	RS.L	1
ETAP_TRANSFO	RS.W	1
NB_ETAP_D	RS.W	1
TEMPO	RS.W	1
FLAG_TRANSFO	RS.B	1
FLAG_D_ZOOM	RS.B	1
FLAG_PART_1	RS.B	1
FLAG_PART_2	RS.B	1
FLAG_PART_3	RS.B	1
FLAG_FIN_PRESENTS	RS.B	1
ADR_1	RS.L	2
ADR_2	RS.L	2
	;X(.L),Y(.L),PAS_X(.W),PAS_Y(.W),X_DEF(.W),Y_DEF(.W)
BUF_COOR_BALLS	RS.W	LONG_1*21
BUF_COOR_D_ZOOM	RS.W	1+(LONG_2*21)
TABLE_ADR_BALL	RS.W	((4*16)+1)*NB_ZOOM
BUF_COOR_EFF_1	RS.W	21*2*2
BUF_BALLS	RS.B	38000
BUF_SOV_CARRE	RS.B	12+(64*112*2)
PT_COOR_2	RS.W	1
BUF_PRESENTS	RS.W	48*6
TABLE_ANNEX	RS.W	3*21*9
FIN_TABLE	RS.B	1
ADR_ZOOMS	RS.W	4*NB_PASSES_Z
BUFFER_ZOOMS	RS.B	280000
LONG_BSS_PART_1	RS.B	1

LINK = 0	;1 POUR LINKER
DEBUG = 0	;1 POUR DEBUGGER SANS SE FAIRE CHIER, 0 POUR EXECUTER

WAIT	MACRO
	MOVE.W	#\1,D7
\@BCL	DBRA	D7,\@BCL
	ENDM

CPU	MACRO
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	\@PAS_TOUCHE
	MOVE.W	#\1,$FFFF8240.W
\@PAS_TOUCHE	
	ENDM

COUL	MACRO
	MOVE.W	#\2,$FFFF8240+(\1*2).W
	ENDM

	IFEQ	LINK
	 CLR.L	-(SP)
	 MOVE.W	#$20,-(SP)
	 TRAP	#1
	 ADDQ.L	#6,SP
	 MOVE.L	D0,SAUV_SP

	 IFEQ	DEBUG
	  CLR.W	-(SP)
	  MOVE.L	#-1,-(SP)
	  MOVE.L	#-1,-(SP)
	  MOVE.W	#5,-(SP)
	  TRAP	#14
	  LEA	12(SP),SP

	  LEA	FIN,A0
	  MOVE.W	#8,A1
	  MOVE.L	A0,(A1)+
	  MOVE.L	A0,(A1)+
	  MOVE.L	A0,(A1)+
	  MOVE.L	A0,(A1)+
	  MOVE.L	A0,(A1)+
	  MOVE.L	A0,(A1)+
	  MOVE.L	A0,(A1)+
	 ENDC
	ENDC

	BSR	PREP_ECR
	BSR	PREPAR_BOULES
	BSR	PREPAR_PRESENTS

	IFNE	DEBUG
	 BRA	IT_VBL
	ENDC

	CLR.B	DATA_JOY
	CLR.B	DATA2JOY

	IFEQ	LINK
	 ANDI.B	#%11111000,$484.W
	ENDC

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL

	MOVE.W	#$333,$FFFF8242.W
	MOVE.W	#$444,$FFFF8244.W
	MOVE.W	#$555,$FFFF8246.W
	MOVE.W	#$333,$FFFF8248.W
	MOVE.W	#$333,$FFFF824A.W
	MOVE.W	#$333,$FFFF824C.W
	MOVE.W	#$333,$FFFF824E.W
	MOVE.W	#$444,$FFFF8250.W
	MOVE.W	#$444,$FFFF8252.W
	MOVE.W	#$444,$FFFF8254.W
	MOVE.W	#$444,$FFFF8256.W
	MOVE.W	#$555,$FFFF8258.W
	MOVE.W	#$555,$FFFF825A.W
	MOVE.W	#$555,$FFFF825C.W
	MOVE.W	#$555,$FFFF825E.W

	MOVE.W 	#$FA00,A0
	MOVE.B	7(A0),IERA
	MOVE.B	9(A0),IERB
	MOVE.B	$F(A0),ISRA
	MOVE.B	$11(A0),ISRB
	MOVE.B	$13(A0),IMRA
	MOVE.B	$15(A0),IMRB
	MOVE.B	$17(A0),VR
	MOVE.B	$19(A0),TACR
	MOVE.B	$1B(A0),TBCR
	MOVE.B	$1D(A0),TCDCR
	MOVE.B	$1F(A0),TADR
	MOVE.B	$21(A0),TBDR
	MOVE.B	$23(A0),TCDR
	MOVE.B	$25(A0),TDDR
	
	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	$118.W,ANC_CLAV
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	MOVE.L	#IT_CLAV,$118.W
	JSR	MUZAXX
	BSET	#0,$FFFFFA07.W		TB AUTORISê
;	BSET	#6,$FFFFFA09.W		IT CLAVIER OK
	MOVE.B	$FFFFFA07.W,$FFFFFA13.W
	MOVE.B	$FFFFFA09.W,$FFFFFA15.W
	MOVE.B	#$40,$FFFFFA17.W
	MOVE.W	#$2300,SR

	CLR.W	NB_VBL
	JMP	PREPARE_ZOOM

MUZAXX	INCBIN	LEVEL5.MUS
	EVEN
	DS.B	6000

FIN	MOVE.W	#$2700,SR
	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W
	MOVE.L	ANC_CLAV,$118.W
	
	MOVE.W	#$FA00,A0
	MOVE.B	IERA,7(A0)
	MOVE.B	IERB,9(A0)
	MOVE.B	ISRA,$F(A0)
	MOVE.B	ISRB,$11(A0)
	MOVE.B	IMRA,$13(A0)
	MOVE.B	IMRB,$15(A0)
	MOVE.B	VR,$17(A0)
	MOVE.B	TACR,$19(A0)
	MOVE.B	TBCR,$1B(A0)
	MOVE.B	TCDCR,$1D(A0)
	MOVE.B	TADR,$1F(A0)
	MOVE.B	TBDR,$21(A0)
	MOVE.B	#$B0,$23(A0)
	MOVE.B	TDDR,$25(A0)
	MOVE.W	#$2300,SR

	MOVE.B	SAUVEC,$FFFF8201.W
	MOVE.B	SAUVEC+1,$FFFF8203.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	IFEQ	LINK
	 ORI.B	#%00000111,$484.W

	 MOVE.L	SAUV_SP,-(SP)
	 MOVE.W	#$20,-(SP)
	 TRAP	#1
	 ADDQ.L	#6,SP
	
	 CLR.W	-(SP)
	 TRAP	#1
	 ADDQ.L	#2,SP
	ELSE
	 ILLEGAL
	ENDC
	
	*******	V B L  &  P R O C E D U R E S    *******
VBLR	ADDQ.W	#1,NB_VBL
	MOVEM.L	D0-A6,-(SP)
	JSR	MUZAXX+8
	BSR	DEMO
	BSR	SWAPEC
	MOVEM.L	(SP)+,D0-A6
	RTE
	
TB0	RTE

IT_CLAV	MOVE.L	D0,-(SP)
	MOVEQ	#0,D0
	MOVE.B	$FFFFFC02.W,D0
	CMPI.B	#$FF,D0
	BNE.S	JOY_2
	MOVE.L	#CLAV_1,$118.W
	MOVE.L	(SP)+,D0
	RTE
JOY_2	CMPI.B	#$FE,D0
	BNE.S	PAS_JOY
	MOVE.L	#CLAV_2,$118.W
	MOVE.L	(SP)+,D0
	RTE
PAS_JOY	MOVE.L	(SP)+,D0
	RTE

CLAV_1	MOVE.B	$FFFFFC02.W,DATA_JOY
	MOVE.L	#IT_CLAV,$118.W
	RTE

CLAV_2	MOVE.B	$FFFFFC02.W,DATA2JOY
	MOVE.L	#IT_CLAV,$118.W
	RTE

IT_VBL

Z_ACT	DS.L	1
VIT_Z	DS.W	1
P	DS.W	1

PREPARE_ZOOM	LEA	BUFFER_ZOOMS+BSS_DEMO,A4
	LEA	ADR_ZOOMS+BSS_DEMO,A6
	MOVE.W	#666,D0	;Z_DEP
	EXT.L	D0
	ASL.L	#8,D0
	MOVE.L	D0,D1	;D1=Z_ACT (.L)
	DIVS.W	#NB_PASSES_Z,D0	;D0=VITESSE EN Z (.L)
	EXT.L	D0
	SUB.L	D0,D1
	MOVE.L	D1,Z_ACT
	MOVE.W	D0,VIT_Z

.REPEAT_ETAP	MOVE.L	Z_ACT,D1
	ASR.L	#8,D1
 
	LEA	COEFF_3D,A1
	LEA	CORES_ZOOM,A2
	MOVE.L	D1,D2
	MOVE.W	D2,D5	;D5=Z
	ADD.W	D5,D5	;Z*2
	MOVE.W	(A2,D5.W),D5	;D5=No DU ZOOM
	ADD.W	D2,D2
	MOVE.W	(A1,D2.W),D2	;D2=COEFFICIENT 3D
	MOVE.W	#116*2,D6
	MOVE.W	#-64*2,D3
	MULU.W	D2,D6
	MULS.W	D2,D3
	SWAP	D6	;D6=LONGUEUR PROJETêE
	SWAP	D3
	ADDI.W	#97,D3
	MOVE.W	D6,MOD_HAUT
	SUBQ.W	#1,D6
	MOVE.W	D6,(A6)+	;HAUTEUR-1
	MOVE.W	D3,(A6)+	;Y
	MOVE.L	A4,(A6)+
	LEA	COOR_DEST,A0
	MOVEQ	#8,D6
	LEA	TABLE_ANNEX+BSS_DEMO,A3
	MOVE.L	#0,A5
	MOVE.W	D5,(A3)+	;No DU ZOOM GENERAL
.RECOM_LET	MOVE.W	(A0)+,D7	;NB DE BOULES
	SUBQ.W	#1,D7
	ADDQ.W	#4,A0
.RECOM_BALL	MOVE.W	(A0)+,D3	;X
	MOVE.W	(A0)+,D4	;Y
	ADD.W	D3,D3	;X*2
	ADD.W	D4,D4	;Y*2
	MULS.W	D2,D3
	MULS.W	D2,D4
	SWAP	D3
	SWAP	D4
	ADDI.W	#160,D3	;D3=X ECRAN
	ADDI.W	#100,D4	;D4=Y ECRAN
	MOVE.W	D3,(A3)+
	MOVE.W	D4,(A3)+
	ADDQ.W	#1,A5
	DBRA	D7,.RECOM_BALL
	DBRA	D6,.RECOM_LET
	SUBQ.W	#1,A5
	;A5=NB DE BOULES -1
	LEA	TABLE_ANNEX+BSS_DEMO+2,A0
	MOVE.W	#1000,D1	;PAR EXEMPLE...
.CONT_TEST	CMPA.L	A3,A0
	BEQ.S	.FIN_TEST
	MOVE.L	(A0)+,D0
	CMP.W	D0,D1
	BLT.S	.DEJA_PETIT
	MOVE.W	D0,D1
.DEJA_PETIT	BRA.S	.CONT_TEST
.FIN_TEST	LEA	TABLE_ANNEX+BSS_DEMO+2+2,A0
	MOVE.W	A5,D7
.SUB_Y	SUB.W	D1,(A0)+
	ADDQ.W	#2,A0
	DBRA	D7,.SUB_Y
	LEA	TABLE_ANNEX+BSS_DEMO,A0
	MOVE.W	(A0)+,D0	;No DU ZOOM
	LEA	TABLE_ADR_BALL+BSS_DEMO,A1
	MOVE.W	D0,D1
	ADD.W	D1,D1
	MOVE.W	D1,D2
	LSL	#6,D2
	ADD.W	D2,D1
	ADDA.W	D1,A1	;A1=ADR INFO DU ZOOM ACTUEL
	MOVE.W	A5,D6
.REPEAT_BALLS	MOVE.W	(A0)+,D0	;X
	MOVE.W	(A0)+,D1	;Y
	MULU.W	#80,D1
	MOVE.W	D0,D2
	ANDI.W	#15,D2
	ADD.W	D2,D2
	ADD.W	D2,D2	;D2=(X AND 15)*4
;	ADD.W	D1,D3	;D3=OFFSET ECRAN
	MOVE.L	A4,A3
	ADDA.W	D1,A3	;A3=ADRESSE DESTINATION
	MOVE.W	(A1),D7	;D7=HAUTEUR-1
	TST.W	D0
	BGE.S	.X_POSITIF
	CMPI.W	#-16,D0
	BLE	.NEXT_BALLS
	MOVE.L	2(A1,D2.W),A2
.AFF_CLP_G	MOVE.L	4(A2),D5
	OR.L	D5,(A3)
	LEA	80(A3),A3
	ADDQ.W	#8,A2
	DBRA	D7,.AFF_CLP_G
	BRA.S	.NEXT_BALLS
.X_POSITIF	CMPI.W	#320,D0
	BGE.S	.NEXT_BALLS
	MOVE.W	D0,D3
	ANDI.W	#$FFF0,D3
	LSR	#2,D3
	ADDA.W	D3,A3
	CMPI.W	#305,D0
	BLT.S	.NO_CLP_D
	MOVE.L	2(A1,D2.W),A2
.AFF_CLP_D	MOVE.L	(A2),D5
	OR.L	D5,(A3)
	LEA	80(A3),A3
	ADDQ.W	#8,A2
	DBRA	D7,.AFF_CLP_D
	BRA.S	.NEXT_BALLS
.NO_CLP_D	TST.W	D2
	BNE.S	.DEC_PLUS
	MOVE.L	2(A1),A2	;A2=ADRESSE DU SPRITE 0
.AFF_ALL_0	MOVE.L	(A2)+,D5
	OR.L	D5,(A3)
	LEA	80(A3),A3
	DBRA	D7,.AFF_ALL_0
	BRA.S	.NEXT_BALLS
.DEC_PLUS	MOVE.L	2(A1,D2.W),A2	;A2=ADRESSE DU SPRITE
.AFF_ALL	MOVE.L	(A2)+,D5
	MOVE.L	(A2)+,D4
	OR.L	D5,(A3)
	OR.L	D4,4(A3)
	LEA	80(A3),A3
	DBRA	D7,.AFF_ALL
.NEXT_BALLS	DBRA	D6,.REPEAT_BALLS
MOD_HAUT = *+2
	MOVE.W	#$1234,D0
	MULU.W	#80,D0
	ADDA.W	D0,A4
	MOVEQ	#0,D0
	MOVE.W	VIT_Z,D0
	SUB.L	D0,Z_ACT
	TST.W	Z_ACT
	BGT	.REPEAT_ETAP
	TST.W	Z_ACT
	BLT.S	J
	CLR.W	Z_ACT
	BRA	.REPEAT_ETAP
J	JMP	J
	ILLEGAL

DEMO	TST.B	FLAG_PART_1+BSS_DEMO
	BEQ	PART_1
	TST.B	FLAG_FIN_PRESENTS+BSS_DEMO
	BEQ	PART_2
	BRA.S	PART_3
	TST.B	FLAG_PART_3+BSS_DEMO
	BEQ	PART_3
	RTS

PART_3	LEA	ANIM_PAL(PC),A0
	ADDA.W	PT_COOR+BSS_DEMO,A0
	MOVE.W	#$8240,A1
	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)
	LEA	ADR_ZOOMS+BSS_DEMO,A0
	ADDA.W	PT_COOR+BSS_DEMO,A0
	MOVE.L	SCREEN2,A1
	MOVE.W	(A0)+,D7
	MOVE.W	(A0)+,D6
	MULU.W	#160,D6
	ADDA.W	D6,A1
	MOVE.L	A1,A2
	MOVEQ	#0,D0
	MOVE.W	D7,D5
.EFF	MOVE.L	D0,(A2)
	MOVE.L	D0,8(A2)
	MOVE.L	D0,16(A2)
	MOVE.L	D0,24(A2)
	MOVE.L	D0,32(A2)
	MOVE.L	D0,40(A2)
	MOVE.L	D0,48(A2)
	MOVE.L	D0,56(A2)
	MOVE.L	D0,64(A2)
	MOVE.L	D0,72(A2)
	MOVE.L	D0,80(A2)
	MOVE.L	D0,88(A2)
	MOVE.L	D0,96(A2)
	MOVE.L	D0,104(A2)
	MOVE.L	D0,112(A2)
	MOVE.L	D0,120(A2)
	MOVE.L	D0,128(A2)
	MOVE.L	D0,136(A2)
	MOVE.L	D0,144(A2)
	MOVE.L	D0,152(A2)
	LEA	160(A2),A2
	DBRA	D5,.EFF
	TST.B	FLAG_PART_3+BSS_DEMO
	BEQ.S	.CONT
	RTS
.CONT	MOVE.L	(A0)+,A0
.AFF	MOVE.L	(A0)+,(A1)
	MOVE.L	(A0)+,8(A1)
	MOVE.L	(A0)+,16(A1)
	MOVE.L	(A0)+,24(A1)
	MOVE.L	(A0)+,32(A1)
	MOVE.L	(A0)+,40(A1)
	MOVE.L	(A0)+,48(A1)
	MOVE.L	(A0)+,56(A1)
	MOVE.L	(A0)+,64(A1)
	MOVE.L	(A0)+,72(A1)
	MOVE.L	(A0)+,80(A1)
	MOVE.L	(A0)+,88(A1)
	MOVE.L	(A0)+,96(A1)
	MOVE.L	(A0)+,104(A1)
	MOVE.L	(A0)+,112(A1)
	MOVE.L	(A0)+,120(A1)
	MOVE.L	(A0)+,128(A1)
	MOVE.L	(A0)+,136(A1)
	MOVE.L	(A0)+,144(A1)
	MOVE.L	(A0)+,152(A1)
	LEA	160(A1),A1
	DBRA	D7,.AFF
	ADDQ.W	#8,PT_COOR+BSS_DEMO
	CMPI.W	#8*NB_PASSES_Z,PT_COOR+BSS_DEMO
	BNE.S	.FUCK
	ST	FLAG_PART_3+BSS_DEMO
.FUCK	RTS

PART_2	MOVE.L	SCREEN2,A0	
	ADDQ.W	#4,A0
	MOVEQ	#0,D0
	MOVE.W	#199,D7
.EFF	MOVE.W	D0,(A0)
	MOVE.W	D0,8(A0)
	MOVE.W	D0,16(A0)
	MOVE.W	D0,24(A0)
	MOVE.W	D0,32(A0)
	MOVE.W	D0,40(A0)
	MOVE.W	D0,48(A0)
	MOVE.W	D0,56(A0)
	MOVE.W	D0,64(A0)
	MOVE.W	D0,72(A0)
	MOVE.W	D0,80(A0)
	MOVE.W	D0,88(A0)
	MOVE.W	D0,96(A0)
	MOVE.W	D0,104(A0)
	MOVE.W	D0,112(A0)
	MOVE.W	D0,120(A0)
	MOVE.W	D0,128(A0)
	MOVE.W	D0,136(A0)
	MOVE.W	D0,144(A0)
	MOVE.W	D0,152(A0)
	LEA	160(A0),A0
	DBRA	D7,.EFF

	TST.B	FLAG_FIN_PRESENTS+BSS_DEMO
	BNE	.FUCK
	LEA	BUF_PRESENTS+BSS_DEMO,A0
	LEA	COOR_PRESENTS(PC),A1
	ADDA.W	PT_COOR_2+BSS_DEMO,A1
	ADDI.W	#24,PT_COOR_2+BSS_DEMO
	LEA	TABLE_160(PC),A2
	LEA	MOTIFS_1_PLAN(PC),A3
	MOVEQ	#5,D7
.AFF_ALL_LIG	MOVE.W	D7,A6
	MOVE.W	(A0)+,D7
	MOVE.W	(A1)+,D4	;Y
	MOVE.W	(A1)+,D5	;HAUTEUR
	TST.W	D4
	BGE.S	.Y_POSITIF
	NEG.W	D4	;NB DE LIGNES EN DEHORS (HAUT)
	SUB.W	D4,D5	;D5=NOUVELLE HAUTEUR
	MOVEQ	#0,D4	;Y=0
.Y_POSITIF	CMPI.W	#200,D4
	BGE.S	.PAS_AFF_LIG
	MOVE.W	D4,D3
	ADD.W	D5,D3
	SUBQ.W	#1,D3	;Y_BAS...
	CMPI.W	#200,D3
	BLT.S	.PAS_CLIP_BAS
	SUBI.W	#199,D3	;NB DE LIGNES EN DEHORS (BAS)
	SUB.W	D3,D5
.PAS_CLIP_BAS	ADD.W	D4,D4
	SUBQ.W	#1,D5
	BLT.S	.PAS_AFF_LIG
	MOVE.L	SCREEN2,A4
	ADDQ.W	#4,A4
	ADDA.W	(A2,D4.W),A4
.AFF_ALL_CARRE	MOVE.W	(A0)+,D6	;X
	MOVE.W	D6,D3
	ANDI.W	#$FFF0,D6
	LSR	D6
	ANDI.W	#15,D3
	ADD.W	D3,D3
	ADD.W	D3,D3
	MOVE.W	(A3,D3.W),D0
	MOVE.W	2(A3,D3.W),D1
	MOVE.L	A4,A5
	ADDA.W	D6,A5
	MOVE.W	D5,D2
.AFF_CARRE	OR.W	D0,(A5)
	OR.W	D1,8(A5)
	LEA	160(A5),A5
	DBRA	D2,.AFF_CARRE
	DBRA	D7,.AFF_ALL_CARRE
.RETOUR	MOVE.W	A6,D7
	DBRA	D7,.AFF_ALL_LIG
	CMPI.W	#$1234,(A1)
	BNE.S	.FUCK
	ST	FLAG_FIN_PRESENTS+BSS_DEMO
	CLR.W	PT_COOR+BSS_DEMO
.FUCK	RTS
.PAS_AFF_LIG	ADDQ.W	#1,D7
	ADD.W	D7,D7
	ADDA.W	D7,A0	;PROCHAINE LIGNE
	BRA.S	.RETOUR

PART_1	CMPI.B	#$12,FLAG_TRANSFO+BSS_DEMO
	BNE.S	.GO_DEMO
	BSR	AFF_H_DEP
	RTS
.GO_DEMO	BSR	DEMO_1
	LEA	ADR_1+BSS_DEMO,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0),-(A0)
	MOVE.L	D0,4(A0)

	LEA	ADR_2+BSS_DEMO,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0),-(A0)
	MOVE.L	D0,4(A0)
	RTS

AFF_H_DEP	LEA	CRB_DEP(PC),A0
	ADDA.W	PT_COOR+BSS_DEMO,A0
	MOVE.W	#111,D6
	LEA	BUF_SOV_CARRE+6+BSS_DEMO,A1
	MOVE.W	(A0)+,D7	;Y
	BGE.S	.Y_POSITIF
	NEG.W	D7	;NB DE LIGNES QUI SORTENT
	SUB.W	D7,D6	;D6=NOUVELLE HAUTEUR
	LSL	#6,D7
	ADDA.W	D7,A1
	MOVEQ	#0,D7
.Y_POSITIF	ADDQ.W	#2,PT_COOR+BSS_DEMO
	CMPI.W	#$1234,(A0)
	BNE.S	.OK
	MOVE.B	#1,FLAG_TRANSFO+BSS_DEMO
	MOVE.W	#21*4,PT_COOR+BSS_DEMO
.OK	MULU.W	#160,D7
	MOVE.L	SCREEN2,A0
	LEA	$30(A0),A0
	ADDA.W	D7,A0
	MOVEQ	#0,D5
N	SET	-640
	REPT	4
	MOVE.L	D5,N(A0)
	MOVE.L	D5,N+8(A0)
	MOVE.L	D5,N+16(A0)
	MOVE.L	D5,N+24(A0)
	MOVE.L	D5,N+32(A0)
	MOVE.L	D5,N+40(A0)
	MOVE.L	D5,N+48(A0)
	MOVE.L	D5,N+56(A0)
N	SET	N+160
	ENDR
.AFF_H_DEP	MOVEM.L	(A1)+,D0-D5/D7/A2-A6
	MOVEM.L	D0-D5/D7/A2-A6,(A0)
	MOVE.L	(A1)+,48(A0)
	MOVE.L	(A1)+,52(A0)
	MOVE.L	(A1)+,56(A0)
	MOVE.L	(A1)+,60(A0)
	LEA	160(A0),A0
	DBRA	D6,.AFF_H_DEP
	CMPI.B	#1,FLAG_TRANSFO+BSS_DEMO
	BNE	.FUCK
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	LEA	BUF_SOV_CARRE+6+BSS_DEMO,A0
N	SET	0
	REPT	112
	MOVEM.L	D0-D7,N(A0)
	MOVEM.L	D0-D7,N+32(A0)
N	SET	N+64
	ENDR	
	MOVE.W	#(160*44)+$30,-6(A0)
	MOVE.W	#8*4,-4(A0)
	MOVE.W	#111,-2(A0)
	MOVE.W	#(160*44)+$30,(64*112)(A0)
	MOVE.W	#8*4,2+(64*112)(A0)
	MOVE.W	#111,4+(64*112)(A0)	
.FUCK	RTS

DEMO_1	BSR	EFFACING
	BSR	MK_D_ZOOM

	CMPI.B	#-1,FLAG_TRANSFO+BSS_DEMO
	BNE.S	.PAS_FIN
	CMPI.B	#-1,FLAG_D_ZOOM+BSS_DEMO
	BNE.S	.KKK
	ST	FLAG_PART_1+BSS_DEMO ;PARTIE 1 : FINIE !
	MOVE.W	#$556,$FFFF8248.W
	MOVE.W	#$556,$FFFF824A.W
	MOVE.W	#$556,$FFFF824C.W
	MOVE.W	#$556,$FFFF824E.W
.KKK	RTS
.PAS_FIN	CMPI.B	#3,FLAG_TRANSFO+BSS_DEMO
	BNE.S	.F
	BSR	AFF_BALLS_D
	SUBQ.W	#1,TEMPO+BSS_DEMO
	BGT.S	.NOT_NOW
	MOVE.B	#1,FLAG_TRANSFO+BSS_DEMO
	MOVE.B	#1,FLAG_D_ZOOM+BSS_DEMO
.NOT_NOW	RTS
.F	CMPI.B	#1,FLAG_TRANSFO+BSS_DEMO
	BNE.S	.PAS_MK_TRANSFO
	LEA	COOR_LET+2(PC),A0
	ADDA.W	PT_COOR+BSS_DEMO,A0 ;A0=ADRESSE DES COORDONNêES PROCHAINE
	LEA	BUF_COOR_BALLS+BSS_DEMO,A1
	MOVEQ	#20,D7
.RECOM_PRP	MOVE.W	X_DEF(A1),D0
	MOVE.W	D0,(A1)+
	CLR.W	(A1)+
	MOVE.W	(A0)+,D1
	SUB.W	D1,D0	;DX
	EXT.L	D0
	ASL.L	#8,D0
	DIVS.W	#NB_PASSES,D0
	MOVE.W	Y_DEF-4(A1),D2
	MOVE.W	D2,(A1)+
	CLR.W	(A1)+
	MOVE.W	(A0)+,D3
	SUB.W	D3,D2	;DY
	EXT.L	D2
	ASL.L	#8,D2
	DIVS.W	#NB_PASSES,D2
	MOVE.W	D0,(A1)+
	MOVE.W	D2,(A1)+
	MOVE.W	D1,(A1)+
	MOVE.W	D3,(A1)+
	DBRA	D7,.RECOM_PRP
	ADDI.W	#21*4,PT_COOR+BSS_DEMO
	CMPI.W	#(21*4)*10,PT_COOR+BSS_DEMO
	BLT.S	.OK
	MOVE.B	#-1,FLAG_TRANSFO+BSS_DEMO
	BRA.S	.PAS_MK_TRANSFO
.OK	CLR.B	FLAG_TRANSFO+BSS_DEMO
.PAS_MK_TRANSFO	CMPI.B	#2,FLAG_TRANSFO+BSS_DEMO
	BNE.S	.PAS_AFF_LAST
	BSR	AFF_BALLS_D
	MOVE.B	#3,FLAG_TRANSFO+BSS_DEMO
	MOVE.W	#TEMPO_INTER_TRANSFO,TEMPO+BSS_DEMO
	RTS

.PAS_AFF_LAST	BSR	AFF_BALLS
	BSR	INCREMENTE
	RTS

INCREMENTE	LEA	BUF_COOR_BALLS+BSS_DEMO,A0
	MOVEQ	#20,D7
.R	MOVEQ	#0,D0
	MOVE.W	PAS_X(A0),D0
	EXT.L	D0
	ASL.L	#8,D0
	SUB.L	D0,(A0)+
	MOVEQ	#0,D0
	MOVE.W	PAS_Y-4(A0),D0
	EXT.L	D0
	ASL.L	#8,D0
 	SUB.L	D0,(A0)
	LEA	LONG_1-4(A0),A0
	DBRA	D7,.R
	ADDQ.W	#1,ETAP_TRANSFO+BSS_DEMO
	CMPI.W	#NB_PASSES,ETAP_TRANSFO+BSS_DEMO
	BNE.S	.FUCK
	CLR.W	ETAP_TRANSFO+BSS_DEMO
	MOVE.B	#2,FLAG_TRANSFO+BSS_DEMO
.FUCK	RTS

AFF_BALLS	LEA	BUF_COOR_BALLS+BSS_DEMO,A0
	LEA	TABLE_160,A1
	MOVE.L	SCREEN2,A2
	ADDQ.W	#4,A2
	MOVE.L	ADR_1+BSS_DEMO,A6
	MOVEQ	#20,D7
.RECOM_AFF	LEA	TABLE_ADR_BALL+2+BSS_DEMO,A5
	MOVE.W	X(A0),D0	;X
	MOVE.W	Y(A0),D1	;Y
	LEA	LONG_1(A0),A0
	MOVE.W	D0,D2
	ANDI.W	#$FFF0,D2
	LSR	#1,D2
	ADD.W	D1,D1
	ADD.W	(A1,D1.W),D2
	MOVE.L	A2,A3
	MOVE.W	D2,(A6)+
	ADDA.W	D2,A3	;A3=ADRESSE ECRAN+OFFSET
	ANDI.W	#15,D0	;DECALAGE
	BNE	.DECALAGE_P
.DECALAGE_0	MOVE.L	(A5),A4	;ADRESSE DU SPRITE
	MOVE.L	64(A5),A5	;ADRESSE DU MASK
N	SET	0
	REPT	16
	MOVE.L	(A5)+,D0
	AND.L	D0,N(A3)
	MOVE.L	(A4)+,D0
	OR.L	D0,N(A3)
N	SET	N+160
	ENDR
	BRA	.FIN_AFF
.DECALAGE_P	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.L	(A5,D0.W),A4	;ADRESSE DU SPRITE
	MOVE.L	64(A5,D0.W),A5	;ADRESSE DU MASK
	MOVE.W	D7,-(SP)
N	SET	0
	REPT	4
	MOVEM.L	(A5)+,D0-D7
	AND.L	D0,N(A3)
	AND.L	D1,N+8(A3)
	AND.L	D2,N+160(A3)
	AND.L	D3,N+168(A3)
	AND.L	D4,N+320(A3)
	AND.L	D5,N+328(A3)
	AND.L	D6,N+480(A3)
	AND.L	D7,N+488(A3)
	MOVEM.L	(A4)+,D0-D7
	OR.L	D0,N(A3)
	OR.L	D1,N+8(A3)
	OR.L	D2,N+160(A3)
	OR.L	D3,N+168(A3)
	OR.L	D4,N+320(A3)
	OR.L	D5,N+328(A3)
	OR.L	D6,N+480(A3)
	OR.L	D7,N+488(A3)
N	SET	N+640
	ENDR
	MOVE.W	(SP)+,D7
.FIN_AFF	DBRA	D7,.RECOM_AFF
	RTS

AFF_BALLS_D	LEA	BUF_COOR_BALLS+BSS_DEMO,A0
	LEA	TABLE_160,A1
	MOVE.L	SCREEN2,A2
	ADDQ.W	#4,A2
	MOVE.L	ADR_1+BSS_DEMO,A6
	MOVEQ	#20,D7
.RECOM_AFF	LEA	TABLE_ADR_BALL+2+BSS_DEMO,A5
	MOVE.W	X_DEF(A0),D0	;X
	MOVE.W	Y_DEF(A0),D1	;Y
	LEA	LONG_1(A0),A0
	MOVE.W	D0,D2
	ANDI.W	#$FFF0,D2
	LSR	#1,D2
	ADD.W	D1,D1
	ADD.W	(A1,D1.W),D2
	MOVE.L	A2,A3
	MOVE.W	D2,(A6)+
	ADDA.W	D2,A3	;A3=ADRESSE ECRAN+OFFSET
	ANDI.W	#15,D0	;DECALAGE
	BNE	.DECALAGE_P
.DECALAGE_0	MOVE.L	(A5),A4	;ADRESSE DU SPRITE
	MOVE.L	64(A5),A5	;ADRESSE DU MASK
N	SET	0
	REPT	16
	MOVE.L	(A5)+,D0
	AND.L	D0,N(A3)
	MOVE.L	(A4)+,D0
	OR.L	D0,N(A3)
N	SET	N+160
	ENDR
	BRA	.FIN_AFF
.DECALAGE_P	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.L	(A5,D0.W),A4	;ADRESSE DU SPRITE
	MOVE.L	64(A5,D0.W),A5	;ADRESSE DU MASK
	MOVE.W	D7,-(SP)
N	SET	0
	REPT	4
	MOVEM.L	(A5)+,D0-D7
	AND.L	D0,N(A3)
	AND.L	D1,N+8(A3)
	AND.L	D2,N+160(A3)
	AND.L	D3,N+168(A3)
	AND.L	D4,N+320(A3)
	AND.L	D5,N+328(A3)
	AND.L	D6,N+480(A3)
	AND.L	D7,N+488(A3)
	MOVEM.L	(A4)+,D0-D7
	OR.L	D0,N(A3)
	OR.L	D1,N+8(A3)
	OR.L	D2,N+160(A3)
	OR.L	D3,N+168(A3)
	OR.L	D4,N+320(A3)
	OR.L	D5,N+328(A3)
	OR.L	D6,N+480(A3)
	OR.L	D7,N+488(A3)
N	SET	N+640
	ENDR
	MOVE.W	(SP)+,D7
.FIN_AFF	DBRA	D7,.RECOM_AFF
	RTS

EFFACING	MOVE.L	ADR_1+BSS_DEMO,A0
	MOVE.L	SCREEN2,A2
	ADDQ.W	#4,A2
	MOVEQ	#20,D7
	MOVEQ	#0,D2
.R	MOVE.L	A2,A3
	ADDA.W	(A0)+,A3
N	SET	0
	REPT	16
	MOVE.L	D2,N(A3)
	MOVE.L	D2,N+8(A3)
N	SET	N+160
	ENDR
	DBRA	D7,.R
	RTS

MK_D_ZOOM	CMPI.B	#5,FLAG_D_ZOOM+BSS_DEMO
	BNE.S	.PAS_DEF
	BSR	PUT_CARRE
	BSR	AFF_D_ZOOM_D
	BSR	SOV_CARRE
	ADDQ.W	#1,NB_ETAP_D+BSS_DEMO
	CMPI.W	#6,NB_ETAP_D+BSS_DEMO
	BNE.S	.FUCK
	MOVE.B	#-1,FLAG_D_ZOOM+BSS_DEMO
.FUCK	RTS
.PAS_DEF	CMPI.B	#-1,FLAG_D_ZOOM+BSS_DEMO
	BNE.S	.PAS_QUIT
	RTS
.PAS_QUIT	CMPI.B	#1,FLAG_D_ZOOM+BSS_DEMO
	BNE	.OK_D_ZOOM
	MOVE.L	PT_DEST+BSS_DEMO,A0
	MOVE.W	(A0)+,D0	;NB DE BOULES.
	SUBQ.W	#1,D0
	LEA	BUF_COOR_D_ZOOM+BSS_DEMO,A1
	MOVE.W	D0,(A1)+
	LEA	BUF_COOR_BALLS+BSS_DEMO,A2

	MOVEQ	#0,D7
	MOVE.W	#666,D7
	ASL.L	#8,D7
	DIVU.W	#NB_PASSES_D,D7	;D7=PAS_Z
	ADDI.W	#5,D7

	MOVE.W	#-56,D1
	MOVE.W	D1,(A1)+
	CLR.W	(A1)+
	MOVE.W	(A0)+,D2	;X AU Z=666.
	SUB.W	D2,D1	;dX.
;	ADD.W	D1,D1
;	MOVE.W	(A6,D1.W),D1
	EXT.L	D1
	ASL.L	#8,D1
	DIVS.W	#NB_PASSES_D,D1
	MOVE.W	#-56,D3	;Y AU Z=0.
	MOVE.W	D3,(A1)+
	CLR.W	(A1)+
	MOVE.W	(A0)+,D4	;Y AU Z=666.
	ADDI.W	#156,D4
	SUB.W	D4,D3	;dY.
;	ADD.W	D3,D3
;	MOVE.W	(A6,D3.W),D3
	EXT.L	D3
	ASL.L	#8,D3
	DIVS.W	#NB_PASSES_D,D3
	CLR.L	(A1)+	;Z_DEP=0.
	MOVE.W	D1,(A1)+	;PAS_X.
	MOVE.W	D3,(A1)+	;PAS_Y.
	MOVE.W	D7,(A1)+	;PAS_Z
	MOVE.W	D2,(A1)+	;X_DEF.
	MOVE.W	D4,(A1)+	;Y_DEF.
	
.RECOM_PRP	MOVE.W	X_DEF(A2),D1	;X AU Z=0.
	SUBI.W	#160,D1
	MOVE.W	D1,(A1)+
	CLR.W	(A1)+
	MOVE.W	(A0)+,D2	;X AU Z=666.
	SUB.W	D2,D1	;dX.
;	ADD.W	D1,D1
;	MOVE.W	(A6,D1.W),D1
	EXT.L	D1
	ASL.L	#8,D1
	DIVS.W	#NB_PASSES_D,D1
	MOVE.W	Y_DEF(A2),D3	;Y AU Z=0.
	SUBI.W	#100,D3
	MOVE.W	D3,(A1)+
	CLR.W	(A1)+
	MOVE.W	(A0)+,D4	;Y AU Z=666.
	ADDI.W	#156,D4
	SUB.W	D4,D3	;dY.
;	ADD.W	D3,D3
;	MOVE.W	(A6,D3.W),D3
	EXT.L	D3
	ASL.L	#8,D3
	DIVS.W	#NB_PASSES_D,D3
	CLR.L	(A1)+	;Z_DEP=0.
	MOVE.W	D1,(A1)+	;PAS_X.
	MOVE.W	D3,(A1)+	;PAS_Y.
	MOVE.W	D7,(A1)+	;PAS_Z
	MOVE.W	D2,(A1)+	;X_DEF.
	MOVE.W	D4,(A1)+	;Y_DEF.
	LEA	LONG_1(A2),A2
	DBRA	D0,.RECOM_PRP
	MOVE.L	A0,PT_DEST+BSS_DEMO
	CLR.B	FLAG_D_ZOOM+BSS_DEMO
	CLR.W	NB_ETAP_D+BSS_DEMO
	RTS
.OK_D_ZOOM	BSR	PUT_CARRE
	BSR	INCREMENTE_D
	BSR	SOV_CARRE
	BSR	AFF_D_ZOOM
	RTS

PUT_CARRE	MOVE.L	SCREEN2,A0
	MOVE.L	ADR_2+BSS_DEMO,A1
	ADDA.W	(A1)+,A0
	MOVE.W	(A1)+,D1
	MOVE.W	(A1)+,D0
	LEA	TABLE_2_RT,A2
	MOVE.L	(A2,D1.W),A2
	JMP	(A2)

SOV_CARRE	LEA	TABLE_160,A0
	LEA	BUF_COOR_D_ZOOM+2+BSS_DEMO,A1
	MOVE.W	X_D(A1),D0
	MOVE.W	Y_D(A1),D1
	MOVE.W	Z_D(A1),D2
	MOVE.W	#112,D3
	ADD.W	D0,D0
	ADD.W	D1,D1
	ADD.W	D2,D2
	ADD.W	D3,D3
	LEA	COEFF_3D,A1
	MOVE.W	(A1,D2.W),D2
	MULS.W	D2,D0	;X_P
	MULS.W	D2,D1	;Y_P
	MULU.W	D2,D3	;LONGUEUR PROJETêE
	SWAP	D0
	SWAP	D1
	SWAP	D3
	ADDI.W	#160,D0
	ADDI.W	#100,D1
	ADD.W	D1,D1
	ANDI.W	#$FFF0,D0
	LSR	#1,D0	;OFFSET X
	ADD.W	(A0,D1.W),D0
	MOVE.L	SCREEN2,A1
	MOVE.L	ADR_2+BSS_DEMO,A0
	SUBI.W	#160,D0
	ADDA.W	D0,A1
	MOVE.W	D0,(A0)+
	MOVE.W	D3,D0	;D0=HAUTEUR
	ADDQ.W	#2,D0
	LSR	#4,D3
	ADDQ.W	#2,D3
	ADD.W	D3,D3
	ADD.W	D3,D3
	MOVE.W	D3,(A0)+	;LONGUEUR EN B16 -1
	MOVE.W	D0,(A0)+
	LEA	TABLE_ADR(PC),A4
	MOVE.L	(A4,D3.W),A4
	JMP	(A4)

TABLE_ADR	DC.L	RT_1,RT_1,RT_2,RT_3,RT_4,RT_5,RT_6,RT_7
	DC.L	RT_8,RT_9,RT_10,RT_11,RT_12

RT_1
.A	MOVE.L	(A1),(A0)+
	LEA	160(A1),A1
	DBRA	D0,.A
	RTS

RT_2
.A	MOVE.L	(A1),(A0)+
	MOVE.L	8(A1),(A0)+
	LEA	160(A1),A1
	DBRA	D0,.A
	RTS
RT_3
.A	MOVE.L	(A1),(A0)+
	MOVE.L	8(A1),(A0)+
	MOVE.L	16(A1),(A0)+
	LEA	160(A1),A1
	DBRA	D0,.A
	RTS

RT_4
.A	MOVE.L	(A1),(A0)+
	MOVE.L	8(A1),(A0)+
	MOVE.L	16(A1),(A0)+
	MOVE.L	24(A1),(A0)+
	LEA	160(A1),A1
	DBRA	D0,.A
	RTS

RT_5
.A	MOVE.L	(A1),(A0)+
	MOVE.L	8(A1),(A0)+
	MOVE.L	16(A1),(A0)+
	MOVE.L	24(A1),(A0)+
	MOVE.L	32(A1),(A0)+
	LEA	160(A1),A1
	DBRA	D0,.A
	RTS

RT_6
.A	MOVE.L	(A1),(A0)+
	MOVE.L	8(A1),(A0)+
	MOVE.L	16(A1),(A0)+
	MOVE.L	24(A1),(A0)+
	MOVE.L	32(A1),(A0)+
	MOVE.L	40(A1),(A0)+
	LEA	160(A1),A1
	DBRA	D0,.A
	RTS

RT_7
.A	MOVE.L	(A1),(A0)+
	MOVE.L	8(A1),(A0)+
	MOVE.L	16(A1),(A0)+
	MOVE.L	24(A1),(A0)+
	MOVE.L	32(A1),(A0)+
	MOVE.L	40(A1),(A0)+
	MOVE.L	48(A1),(A0)+
	LEA	160(A1),A1
	DBRA	D0,.A
	RTS

RT_8
.A	MOVE.L	(A1),(A0)+
	MOVE.L	8(A1),(A0)+
	MOVE.L	16(A1),(A0)+
	MOVE.L	24(A1),(A0)+
	MOVE.L	32(A1),(A0)+
	MOVE.L	40(A1),(A0)+
	MOVE.L	48(A1),(A0)+
	MOVE.L	56(A1),(A0)+
	LEA	160(A1),A1
	DBRA	D0,.A
	RTS

RT_9
.A	MOVE.L	(A1),(A0)+
	MOVE.L	8(A1),(A0)+
	MOVE.L	16(A1),(A0)+
	MOVE.L	24(A1),(A0)+
	MOVE.L	32(A1),(A0)+
	MOVE.L	40(A1),(A0)+
	MOVE.L	48(A1),(A0)+
	MOVE.L	56(A1),(A0)+
	MOVE.L	64(A1),(A0)+
	LEA	160(A1),A1
	DBRA	D0,.A
	RTS

RT_10
.A	MOVE.L	(A1),(A0)+
	MOVE.L	8(A1),(A0)+
	MOVE.L	16(A1),(A0)+
	MOVE.L	24(A1),(A0)+
	MOVE.L	32(A1),(A0)+
	MOVE.L	40(A1),(A0)+
	MOVE.L	48(A1),(A0)+
	MOVE.L	56(A1),(A0)+
	MOVE.L	64(A1),(A0)+
	MOVE.L	72(A1),(A0)+
	LEA	160(A1),A1
	DBRA	D0,.A
	RTS

RT_11
.A	MOVE.L	(A1),(A0)+
	MOVE.L	8(A1),(A0)+
	MOVE.L	16(A1),(A0)+
	MOVE.L	24(A1),(A0)+
	MOVE.L	32(A1),(A0)+
	MOVE.L	40(A1),(A0)+
	MOVE.L	48(A1),(A0)+
	MOVE.L	56(A1),(A0)+
	MOVE.L	64(A1),(A0)+
	MOVE.L	72(A1),(A0)+
	MOVE.L	80(A1),(A0)+
	LEA	160(A1),A1
	DBRA	D0,.A
	RTS

RT_12
.A	MOVE.L	(A1),(A0)+
	MOVE.L	8(A1),(A0)+
	MOVE.L	16(A1),(A0)+
	MOVE.L	24(A1),(A0)+
	MOVE.L	32(A1),(A0)+
	MOVE.L	40(A1),(A0)+
	MOVE.L	48(A1),(A0)+
	MOVE.L	56(A1),(A0)+
	MOVE.L	64(A1),(A0)+
	MOVE.L	72(A1),(A0)+
	MOVE.L	80(A1),(A0)+
	MOVE.L	88(A1),(A0)+
	LEA	160(A1),A1
	DBRA	D0,.A
	RTS

TABLE_2_RT	DC.L	RT_2_1,RT_2_1,RT_2_2,RT_2_3,RT_2_4,RT_2_5
	DC.L	RT_2_6,RT_2_7,RT_2_8,RT_2_9,RT_2_10
	DC.L	RT_2_11,RT_2_12

RT_2_1
.A	MOVE.L	(A1)+,(A0)
	LEA	160(A0),A0
	DBRA	D0,.A
	RTS

RT_2_2
.A	MOVE.L	(A1)+,(A0)
	MOVE.L	(A1)+,8(A0)
	LEA	160(A0),A0
	DBRA	D0,.A
	RTS

RT_2_3
.A	MOVE.L	(A1)+,(A0)
	MOVE.L	(A1)+,8(A0)
	MOVE.L	(A1)+,16(A0)
	LEA	160(A0),A0
	DBRA	D0,.A
	RTS

RT_2_4
.A	MOVE.L	(A1)+,(A0)
	MOVE.L	(A1)+,8(A0)
	MOVE.L	(A1)+,16(A0)
	MOVE.L	(A1)+,24(A0)
	LEA	160(A0),A0
	DBRA	D0,.A
	RTS

RT_2_5
.A	MOVE.L	(A1)+,(A0)
	MOVE.L	(A1)+,8(A0)
	MOVE.L	(A1)+,16(A0)
	MOVE.L	(A1)+,24(A0)
	MOVE.L	(A1)+,32(A0)
	LEA	160(A0),A0
	DBRA	D0,.A
	RTS

RT_2_6
.A	MOVE.L	(A1)+,(A0)
	MOVE.L	(A1)+,8(A0)
	MOVE.L	(A1)+,16(A0)
	MOVE.L	(A1)+,24(A0)
	MOVE.L	(A1)+,32(A0)
	MOVE.L	(A1)+,40(A0)
	LEA	160(A0),A0
	DBRA	D0,.A
	RTS

RT_2_7
.A	MOVE.L	(A1)+,(A0)
	MOVE.L	(A1)+,8(A0)
	MOVE.L	(A1)+,16(A0)
	MOVE.L	(A1)+,24(A0)
	MOVE.L	(A1)+,32(A0)
	MOVE.L	(A1)+,40(A0)
	MOVE.L	(A1)+,48(A0)
	LEA	160(A0),A0
	DBRA	D0,.A
	RTS

RT_2_8
.A	MOVE.L	(A1)+,(A0)
	MOVE.L	(A1)+,8(A0)
	MOVE.L	(A1)+,16(A0)
	MOVE.L	(A1)+,24(A0)
	MOVE.L	(A1)+,32(A0)
	MOVE.L	(A1)+,40(A0)
	MOVE.L	(A1)+,48(A0)
	MOVE.L	(A1)+,56(A0)
	LEA	160(A0),A0
	DBRA	D0,.A
	RTS

RT_2_9
.A	MOVE.L	(A1)+,(A0)
	MOVE.L	(A1)+,8(A0)
	MOVE.L	(A1)+,16(A0)
	MOVE.L	(A1)+,24(A0)
	MOVE.L	(A1)+,32(A0)
	MOVE.L	(A1)+,40(A0)
	MOVE.L	(A1)+,48(A0)
	MOVE.L	(A1)+,56(A0)
	MOVE.L	(A1)+,64(A0)
	LEA	160(A0),A0
	DBRA	D0,.A
	RTS

RT_2_10
.A	MOVE.L	(A1)+,(A0)
	MOVE.L	(A1)+,8(A0)
	MOVE.L	(A1)+,16(A0)
	MOVE.L	(A1)+,24(A0)
	MOVE.L	(A1)+,32(A0)
	MOVE.L	(A1)+,40(A0)
	MOVE.L	(A1)+,48(A0)
	MOVE.L	(A1)+,56(A0)
	MOVE.L	(A1)+,64(A0)
	MOVE.L	(A1)+,72(A0)
	LEA	160(A0),A0
	DBRA	D0,.A
	RTS

RT_2_11
.A	MOVE.L	(A1)+,(A0)
	MOVE.L	(A1)+,8(A0)
	MOVE.L	(A1)+,16(A0)
	MOVE.L	(A1)+,24(A0)
	MOVE.L	(A1)+,32(A0)
	MOVE.L	(A1)+,40(A0)
	MOVE.L	(A1)+,48(A0)
	MOVE.L	(A1)+,56(A0)
	MOVE.L	(A1)+,64(A0)
	MOVE.L	(A1)+,72(A0)
	MOVE.L	(A1)+,80(A0)
	LEA	160(A0),A0
	DBRA	D0,.A
	RTS

RT_2_12
.A	MOVE.L	(A1)+,(A0)
	MOVE.L	(A1)+,8(A0)
	MOVE.L	(A1)+,16(A0)
	MOVE.L	(A1)+,24(A0)
	MOVE.L	(A1)+,32(A0)
	MOVE.L	(A1)+,40(A0)
	MOVE.L	(A1)+,48(A0)
	MOVE.L	(A1)+,56(A0)
	MOVE.L	(A1)+,64(A0)
	MOVE.L	(A1)+,72(A0)
	MOVE.L	(A1)+,80(A0)
	MOVE.L	(A1)+,88(A0)
	LEA	160(A0),A0
	DBRA	D0,.A
	RTS

AFF_D_ZOOM	LEA	BUF_COOR_D_ZOOM+BSS_DEMO,A0
	LEA	CORES_ZOOM,A1
	LEA	COEFF_3D,A2
	LEA	TABLE_160,A3
	MOVE.W	(A0)+,D7
	LEA	LONG_2(A0),A0
	MOVE.W	Z_D(A0),D2	;Z
	ADD.W	D2,D2
	MOVE.W	(A1,D2.W),D3	;D3=No DU ZOOM A AFFICHER
	MOVE.W	(A2,D2.W),D2	;D2=COEFFICIENT 3D
	LEA	TABLE_ADR_BALL+BSS_DEMO,A6
	MOVE.W	D3,D4
	ADD.W	D4,D4
	LSL.W	#7,D3
	ADD.W	D4,D3
	ADDA.W	D3,A6	;A6=ADRs DES DECA. DU ZOOM

.RECOM_ALL_BALL	MOVE.W	(A0),D0	;X
	MOVE.W	Y_D(A0),D1	;Y
	LEA	LONG_2(A0),A0
	ADD.W	D0,D0
	ADD.W	D1,D1
	MULS.W	D2,D0
	MULS.W	D2,D1
	SWAP	D0
	SWAP	D1
	ADDI.W	#160,D0
	ADDI.W	#100,D1
	MOVE.W	(A6),D6	;HAUTEUR DU SPRITE
	MOVE.W	D0,D3
	ANDI.W	#$FFF0,D3
	LSR.W	#1,D3
	ADD.W	D1,D1
	ADD.W	(A3,D1.W),D3
	MOVE.L	SCREEN2,A4
	ADDA.W	D3,A4	;A4=ADRESSE ECRAN+offset
	ANDI.W	#15,D0
	BNE	.DECALAGES_P
.DECALAGE_0	MOVE.L	2(A6),A1	;A5=ADRESSE DU SPRITE
	MOVE.L	64+2(A6),A2	;A6=ADRESSE DU MASK
	SUBQ.W	#1,D6
.G	MOVE.L	(A1)+,D0
	OR.L	D0,(A4)
	LEA	160(A4),A4
	DBRA	D6,.G
	BRA	.FIN_AFF_D
.DECALAGES_P	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.L	2(A6,D0.W),A1	;A5=ADRESSE DU SPRITE
	MOVE.L	64+2(A6,D0.W),A2 ;A6=ADRESSE DU MASK
	SUBQ.W	#1,D6
.H	MOVE.L	(A1)+,D0
	OR.L	D0,(A4)
	MOVE.L	(A1)+,D0
	OR.L	D0,8(A4)
	LEA	160(A4),A4
	DBRA	D6,.H
.FIN_AFF_D	DBRA	D7,.RECOM_ALL_BALL
	RTS

AFF_D_ZOOM_D	LEA	BUF_COOR_D_ZOOM+BSS_DEMO,A0
	LEA	CORES_ZOOM,A1
	LEA	COEFF_3D,A2
	LEA	TABLE_160,A3
	MOVE.W	(A0)+,D7
	LEA	LONG_2(A0),A0
.RECOM_ALL_BALL	MOVE.W	X_DEF_D(A0),D0	;X
	MOVE.W	Y_DEF_D(A0),D1	;Y
	MOVE.W	Z_D(A0),D2	;Z
	LEA	LONG_2(A0),A0
	ADD.W	D0,D0
	ADD.W	D1,D1
	ADD.W	D2,D2
	MOVE.W	(A1,D2.W),D3	;D3=No DU ZOOM A AFFICHER
	MOVE.W	(A2,D2.W),D2	;D2=COEFFICIENT 3D
	MULS.W	D2,D0
	MULS.W	D2,D1
	SWAP	D0
	SWAP	D1
	ADDI.W	#160,D0
	ADDI.W	#100,D1
	LEA	TABLE_ADR_BALL+BSS_DEMO,A6
	MOVE.W	D3,D4
	ADD.W	D4,D4
	LSL.W	#7,D3
	ADD.W	D4,D3
	ADDA.W	D3,A6	;A6=ADRs DES DECA. DU ZOOM
	MOVE.W	(A6)+,D6	;HAUTEUR DU SPRITE
	MOVE.W	D0,D2
	ANDI.W	#$FFF0,D2
	LSR.W	#1,D2
	ADD.W	D1,D1
	ADD.W	(A3,D1.W),D2
	MOVE.L	SCREEN2,A4
	ADDA.W	D2,A4	;A4=ADRESSE ECRAN+offset
	ANDI.W	#15,D0
	BNE	.DECALAGES_P
.DECALAGE_0	MOVE.L	(A6),A5	;A5=ADRESSE DU SPRITE
	MOVE.L	64(A6),A6	;A6=ADRESSE DU MASK
	SUBQ.W	#1,D6
.G	MOVE.L	(A6)+,D0
	AND.L	D0,(A4)
	MOVE.L	(A5)+,D0
	OR.L	D0,(A4)
	LEA	160(A4),A4
	DBRA	D6,.G
	BRA	.FIN_AFF_D
.DECALAGES_P	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.L	(A6,D0.W),A5	;A5=ADRESSE DU SPRITE
	MOVE.L	64(A6,D0.W),A6	;A6=ADRESSE DU MASK
	SUBQ.W	#1,D6
.H	MOVE.L	(A6)+,D0
	AND.L	D0,(A4)
	MOVE.L	(A6)+,D0
	AND.L	D0,8(A4)
	MOVE.L	(A5)+,D0
	OR.L	D0,(A4)
	MOVE.L	(A5)+,D0
	OR.L	D0,8(A4)
	LEA	160(A4),A4
	DBRA	D6,.H
.FIN_AFF_D	DBRA	D7,.RECOM_ALL_BALL
	RTS

INCREMENTE_D	LEA	BUF_COOR_D_ZOOM+BSS_DEMO,A0
	MOVE.W	(A0)+,D7
	ADDQ.W	#1,D7
	MOVE.W	PAS_Z_D(A0),D1
	EXT.L	D1
	ASL.L	#8,D1
.R	MOVE.W	PAS_X_D(A0),D0
	EXT.L	D0
	ASL.L	#8,D0
	SUB.L	D0,(A0)+
	MOVE.W	PAS_Y_D-4(A0),D0
	EXT.L	D0
	ASL.L	#8,D0
	SUB.L	D0,(A0)+
	ADD.L	D1,(A0)+
	LEA	LONG_2-12(A0),A0
	DBRA	D7,.R
	ADDQ.W	#1,NB_ETAP_D+BSS_DEMO
	CMPI.W	#NB_PASSES_D,NB_ETAP_D+BSS_DEMO
	BNE.S	.F
	MOVE.B	#5,FLAG_D_ZOOM+BSS_DEMO
	CLR.W	NB_ETAP_D+BSS_DEMO
.F	RTS

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
PREP_ECR	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1
	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREPAR_BOULES	LEA	BALL(PC),A0
	LEA	HAUTEURS(PC),A1
	LEA	TABLE_ADR_BALL+BSS_DEMO,A2
	LEA	BUF_BALLS+BSS_DEMO,A3
	MOVE.L	#$80008000,D2	;MASQUE.

	MOVE.W	#NB_ZOOM-1,D7
.RECOM_ALL_ZOOM	MOVE.W	D7,MOD

	MOVE.W	(A1)+,D7
	SUBQ.W	#1,D7	;HAUTEUR-1.
	MOVE.W	D7,(A2)+
	MOVE.L	A0,(A2)+	;ADRESSE DE LA BOULE, DEC. 0.

	MOVE.L	D7,D6
	MOVE.L	A3,(A2)+	;ADRESSE DU SPRITE.
	MOVE.L	A3,A4
.RECOM_MK_DECA	MOVE.L	(A0)+,D0	;PLAN 1 & 2.
	ROR.L	#1,D0
	MOVE.L	D0,D1
	AND.L	D2,D1	;D1=BITS QUI SORTENT.
	EOR.L	D1,D0	;D0=PARTIE A AFFICHER.
	MOVE.L	D0,(A3)+
	MOVE.L	D1,(A3)+
	DBRA	D6,.RECOM_MK_DECA

	MOVEQ	#13,D5
.RECOM_ALL_DECA	MOVE.W	D7,D6
	MOVE.L	A3,(A2)+	;ADRESSE DU SPRITE
	MOVE.L	A3,A5
.RECOM_MK_DECA2	MOVE.L	(A4)+,D0
	MOVE.L	(A4)+,D1
	ROR.L	#1,D0
	ROR.L	#1,D1
	MOVE.L	D0,D3
	AND.L	D2,D3	;PARTIE A CONSERVER
	EOR.L	D3,D0	;PARTIE A AFFICHER
	SWAP	D3
	OR.L	D3,D1
	MOVE.L	D0,(A3)+
	MOVE.L	D1,(A3)+
	DBRA	D6,.RECOM_MK_DECA2
	MOVE.L	A5,A4
	DBRA	D5,.RECOM_ALL_DECA

	LEA	-64(A2),A6
	MOVE.L	(A6)+,A4
	MOVE.L	A3,(A2)+	;ADRESSE DU MSK, DECA. 0.
	MOVE.W	D7,D6
.RECOM_MK_MSK_0	MOVE.W	(A4)+,D0
	MOVE.W	(A4)+,D1
	OR.W	D1,D0
	NOT.W	D0
	MOVE.W	D0,(A3)+
	MOVE.W	D0,(A3)+
	DBRA	D6,.RECOM_MK_MSK_0

	MOVEQ	#14,D5
.RECOM_ALL_MSK	MOVE.L	A3,(A2)+	;ADRESSE DU MASK.
	MOVE.L	(A6)+,A4
	MOVE.W	D7,D6
.RECOM_MK_MSK	MOVE.L	(A4)+,D0
	MOVE.L	D0,D1
	SWAP	D1
	OR.L	D1,D0
	NOT.L	D0
	MOVE.L	D0,(A3)+
	MOVE.L	(A4)+,D0
	MOVE.L	D0,D1
	SWAP	D1
	OR.L	D1,D0
	NOT.L	D0
	MOVE.L	D0,(A3)+
	DBRA	D6,.RECOM_MK_MSK
	DBRA	D5,.RECOM_ALL_MSK
MOD = *+2
	MOVE.W	#$1234,D7
	DBRA	D7,.RECOM_ALL_ZOOM
	MOVE.B	#1,FLAG_TRANSFO+BSS_DEMO
	MOVE.B	#1,FLAG_D_ZOOM+BSS_DEMO
	LEA	COOR_LET+2(PC),A0
	LEA	BUF_COOR_BALLS+BSS_DEMO,A1
	MOVEQ	#20,D7
.R	MOVE.W	(A0)+,X_DEF(A1)
	MOVE.W	(A0)+,Y_DEF(A1)
	LEA	LONG_1(A1),A1
	DBRA	D7,.R
;	MOVE.W	#21*4,PT_COOR
	CLR.W	PT_COOR+BSS_DEMO
	MOVE.L	#COOR_DEST,PT_DEST+BSS_DEMO

	LEA	BUF_COOR_EFF_1+BSS_DEMO,A0
	LEA	ADR_1+BSS_DEMO,A1
	MOVE.L	A0,(A1)+
	LEA	21*2*2(A0),A0
	MOVE.L	A0,(A1)+

	LEA	BUF_SOV_CARRE+BSS_DEMO,A0
	LEA	ADR_2+BSS_DEMO,A1
	MOVE.L	A0,(A1)+
	LEA	6+(64*112)(A0),A0
	MOVE.L	A0,(A1)+

	LEA	COOR_LET(PC),A0
	LEA	BUF_SOV_CARRE+6+BSS_DEMO,A1
	LEA	TABLE_ADR_BALL+2+BSS_DEMO,A3
	MOVE.W	(A0)+,D7
	SUBQ.W	#1,D7
.RECOM_BALL	MOVE.W	(A0)+,D0	;X
	MOVE.W	(A0)+,D1	;Y
	SUBI.W	#104-8,D0
	SUBI.W	#$2C,D1
	LSL	#6,D1	;Y*64
	MOVE.W	D0,D2
	ANDI.W	#$FFF0,D2
	LSR	#1,D2
	ADD.W	D1,D2
	MOVE.L	A1,A2
	ADDA.W	D2,A2	;A2=ADRESSE DESTINATION
	ANDI.W	#15,D0
	BNE	.PAS_DEC_0
.DEC_0	MOVE.L	(A3),A4
	MOVEQ	#15,D6
.R1	MOVE.L	(A4)+,D0
	OR.L	D0,(A2)
	LEA	64(A2),A2
	DBRA	D6,.R1
	BRA.S	.NEXT_BALL
.PAS_DEC_0	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.L	(A3,D0.W),A4
	MOVEQ	#15,D6
.R2	MOVE.L	(A4)+,D0
	OR.L	D0,(A2)
	MOVE.L	(A4)+,D0
	OR.L	D0,8(A2)
	LEA	64(A2),A2
	DBRA	D6,.R2
.NEXT_BALL	DBRA	D7,.RECOM_BALL
	MOVE.B	#$12,FLAG_TRANSFO+BSS_DEMO
	SF	FLAG_PART_1+BSS_DEMO
	SF	FLAG_PART_2+BSS_DEMO
	SF	FLAG_PART_3+BSS_DEMO
	RTS

PREPAR_PRESENTS	LEA	PRESENTS_MOTIF(PC),A0
	LEA	BUF_PRESENTS+BSS_DEMO,A1
	MOVEQ	#5,D6
.LOOP_3	MOVE.L	A1,A2
	ADDQ.W	#2,A1
	MOVEQ	#-1,D7
	MOVEQ	#16,D2	;X_DEP=16
	MOVEQ	#2,D3
.LOOP_2	MOVEQ	#15,D1
	MOVE.W	(A0)+,D0
.LOOP_1	BTST	D1,D0
	BEQ.S	.NIB
	MOVE.W	D2,(A1)+
	ADDQ.W	#1,D7	;1 BLOC DE PLUS
.NIB	ADDQ.W	#6,D2	;X=X+6
	DBRA	D1,.LOOP_1
	DBRA	D3,.LOOP_2
	MOVE.W	D7,(A2)
	DBRA	D6,.LOOP_3
	CLR.W	PT_COOR+BSS_DEMO
	SF	FLAG_FIN_PRESENTS+BSS_DEMO
	RTS

COOR_LET	INCBIN	TRANSFOR.TRS

BALL	INCBIN	BALL_1.ZOM

HAUTEURS	INCBIN	HAUTBALL.DAT

COOR_DEST	INCBIN	DEST_TIR.DAT

CORES_ZOOM	INCBIN	CORESZOM.DAT

COEFF_3D	INCBIN	COEFF.DAT

CRB_DEP	INCBIN	SIN_DEP.CRB
	DC.W	$1234

COOR_PRESENTS	INCBIN	PRESENTS.MOV
	DC.W	$1234

	;LES CARRES FONT 6 PIXELS DE LARGEURS
MOTIFS_1_PLAN	DC.W	$FC00,0
	DC.W	$7E00,$0000,$3F00,$0000
	DC.W	$1F80,$0000,$0FC0,$0000
	DC.W	$07E0,$0000,$03F0,$0000
	DC.W	$01F8,$0000,$00FC,$0000
	DC.W	$007E,$0000,$003F,$0000
	DC.W	$001F,$8000,$000F,$C000
	DC.W	$0007,$E000,$0003,$F000
	DC.W	$0001,$F800

PRESENTS_MOTIF	DC.W	%1111001111001111,%1001111011111010,%0001011111001111
	DC.W	%1000101000101000,%0010000010000011,%0001000100010000
	DC.W	%1111001111001110,%0001110011100010,%1001000100001110
	DC.W	%1000001010001000,%0000001010000010,%0101000100000001
	DC.W	%1000001001001000,%0000001010000010,%0011000100000001
	DC.W	%1000001000101111,%1011110011111010,%0001000100011110

ANIM_PAL	REPT	1+NB_PASSES_Z-(5*2)
	DC.W	0,$333,$444,$555
	ENDR
	DC.W	0,$222,$333,$444
	DC.W	0,$222,$333,$444
	DC.W	0,$111,$222,$333
	DC.W	0,$111,$222,$333
	DC.W	0,0,$111,$222
	DC.W	0,0,$111,$222
	DC.W	0,0,0,$111
	DC.W	0,0,0,$111
	DC.W	0,0,0,0
	DC.W	0,0,0,0
	
N	SET	0
TABLE_160	REPT	200
	DC.W	N*160
N	SET	N+1
	ENDR

	SECTION	BSS
ANC_PAL	DS.L	8
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
ANC_CLAV	DS.L	1
SAUV_SP	DS.L	1
SAUVEC	DS.W	1
IERA	DS.B	1
IERB	DS.B	1
ISRA	DS.B	1
ISRB	DS.B	1
IMRA	DS.B	1
IMRB	DS.B	1
VR	DS.B	1
TACR	DS.B	1
TBCR	DS.B	1
TCDCR	DS.B	1
TADR	DS.B	1
TBDR	DS.B	1
TCDR	DS.B	1
TDDR	DS.B	1
	DS.B	256+640
BUFFER	DS.L	16000
SCREEN1	DS.L	1
SCREEN2	DS.L	1
DATA_JOY	DS.B	1
DATA2JOY	DS.B	1
NB_VBL	DS.W	1
;
BSS_DEMO	DS.B	LONG_BSS_PART_1