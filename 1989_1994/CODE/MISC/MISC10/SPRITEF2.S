	*****************************************
	*   ROUTINE DE CODE GENERE DE SPRITES   *
	*   v.4.20   02-08-91    2H04MN         *
	*       LE NOMBRE DE DECALAGES EST      *
	*        PARAMETRABLE ( 8 OU 16 )       *
	*****************************************

NB_SPRITES	EQU	5
OFFSET	EQU	52

	PEA	0
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAUV_SP

	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1
	BSR	PREP_ECR
	BSR	PRP_COD_GEN
	MOVE.L	#COURB_SPR,PNT_SPR
	LEA	COURB_SPR,A0
	LEA	1571*4(A0),A1
	MOVE.L	-8(A1),-8(A0)
	MOVE.L	-4(A1),-4(A0)
	MOVE.W	#((OFFSET/4)*NB_SPRITES)-2,D7
RECOMDEB	MOVE.L	(A0)+,(A1)+
	DBRA	D7,RECOMDEB
	MOVE.W	#-1,(A1)

	CLR.W	-(SP)
	PEA	-1
	PEA	-1
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008
	MOVE.L	#FIN,$00C
	MOVE.L	#FIN,$010
	MOVE.L	#FIN,$014
	MOVE.L	#FIN,$018
	MOVE.L	#FIN,$01C
	MOVE.L	#FIN,$020

	ANDI.B	#%11111000,$484.W

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	MOVEM.L	SPRITES+2,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	MOVE.W	#$300,$FFFF8250.W
	MOVE.L	$FFFF8242.W,$FFFF8252.W
	MOVE.L	$FFFF8246.W,$FFFF8256.W
	MOVE.L	$FFFF824A.W,$FFFF825A.W
	MOVE.W	$FFFF824E.W,$FFFF825E.W

	MOVEQ	#1,D0
	JSR	MUZAXX

	MOVE.B	$FFFFFA07.W,MFP
	MOVE.B	$FFFFFA09.W,MFP+1
	MOVE.B	$FFFFFA0F.W,MFP+2
	MOVE.B	$FFFFFA13.W,MFP+3
	MOVE.B	$FFFFFA1B.W,MFP+4
	MOVE.B	$FFFFFA21.W,MFP+5

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W

LOOP	JMP	LOOP

FIN	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W

	MOVE.W	#$22,-(SP)
	TRAP	#14
	ADDQ.L	#2,SP
	MOVE.L	D0,A0
	CLR.L	$6C(A0)

	MOVE.B	MFP,$FFFFFA07.W
	MOVE.B	MFP+1,$FFFFFA09.W
	MOVE.B	MFP+2,$FFFFFA0F.W
	MOVE.B	MFP+3,$FFFFFA13.W
	MOVE.B	MFP+4,$FFFFFA1B.W
	MOVE.B	MFP+5,$FFFFFA21.W

	MOVE.B	SAUVEC,$FFFF8201.W
	MOVE.B	SAUVEC+1,$FFFF8203.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	
	ORI.B	#%00000111,$484.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	MOVE.L	SAUV_SP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR.W	-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	*******	V B L  &  P R O C E D U R E S    *******
VBLR	CLR	$FFFF8240.W
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	SUITV0
	ST	$FFFF8240.W
SUITV0	
	BSR	EFF_SPR
	
	
full	
	MOVE.L	#$FFFF8209,A3	
	MOVE.L	#$FFFF8260,A1	
	MOVE.L	#$FFFF820A,A2
	MOVEQ	#0,D1		
	MOVEQ	#0,D4
	MOVEQ	#2,D3
	MOVEQ	#$10,D6
	MOVE	#$2700,SR

SYNCHRO	MOVE.B	(A3),D7     * SYNCHRO
	BEQ.S	SYNCHRO
	SUB.W	D7,D6		
	LSL.W	D6,D1		

	REPT	97-5
	NOP	
	ENDR
	

	JSR	AFF_SPR
	
	BSR	SWAPEC
	SF	$FFFF8240.W
	JSR	MUZAXX+6
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
	
TB0	BCLR	#0,$FFFFFA0F.W
	RTE

EFF_SPR	MOVEQ	#0,D0
	MOVE.L	PNT_SPR,A5
	SUBQ.L	#8,A5

	LEA	CORES_SPR+256,A4	;C
	MOVE.W	(A5),D1
	MOVE.L	4(A4,D1.W),A3
	MOVE.L	SCREEN2,A0
	ADDA.W	2(A5),A0
	JSR	(A3)
	LEA	CORES_SPR+192,A4	;I
	MOVE.W	OFFSET(A5),D1
	MOVE.L	4(A4,D1.W),A3
	MOVE.L	SCREEN2,A0
	ADDA.W	OFFSET+2(A5),A0
	JSR	(A3)
	LEA	CORES_SPR+128,A4	;R
	MOVE.W	(OFFSET*2)(A5),D1
	MOVE.L	4(A4,D1.W),A3
	MOVE.L	SCREEN2,A0
	ADDA.W	(OFFSET*2)+2(A5),A0
	JSR	(A3)
	LEA	CORES_SPR+64,A4	;L
	MOVE.W	(OFFSET*3)(A5),D1
	MOVE.L	4(A4,D1.W),A3
	MOVE.L	SCREEN2,A0
	ADDA.W	(OFFSET*3)+2(A5),A0
	JSR	(A3)
	LEA	CORES_SPR,A4	E
	MOVE.W	(OFFSET*4)(A5),D1
	MOVE.L	4(A4,D1.W),A3
	MOVE.L	SCREEN2,A0
	ADDA.W	(OFFSET*4)+2(A5),A0
	JSR	(A3)

	RTS	

AFF_SPR	
	DCB	100/5,$2090
	MOVE.L	PNT_SPR,A5	5
	LEA	CORES_SPR+256,A4	C3
	MOVE.W	(A5),D1	2
	MOVE.L	(A4,D1.W),A3	5
	MOVE.L	SCREEN2,A0	5
	ADDA.W	2(A5),A0	4
	JSR	(A3)	4

	DCB	104,$4E71
	LEA	CORES_SPR+192,A4	I3
	MOVE.W	OFFSET(A5),D1		3
	MOVE.L	(A4,D1.W),A3		5
	MOVE.L	SCREEN2,A0		5
	ADDA.W	OFFSET+2(A5),A0		4
	JSR	(A3)			4
	
	DCB	104,$4E71
	LEA	CORES_SPR+128,A4	R3
	MOVE.W	OFFSET*2(A5),D1		3
	MOVE.L	(A4,D1.W),A3		5
	MOVE.L	SCREEN2,A0		5
	ADDA.W	(OFFSET*2)+2(A5),A0	4
	JSR	(A3)			4
	
	DCB	104,$4E71
	LEA	CORES_SPR+64,A4		L3
	MOVE.W	OFFSET*3(A5),D1		3
	MOVE.L	(A4,D1.W),A3		5
	MOVE.L	SCREEN2,A0		5
	ADDA.W	(OFFSET*3)+2(A5),A0	4
	JSR	(A3)			4
	
	DCB	104,$4E71
	LEA	CORES_SPR,A4		E3
	MOVE.W	OFFSET*4(A5),D1		3
	MOVE.L	(A4,D1.W),A3		5
	MOVE.L	SCREEN2,A0		5
	ADDA.W	(OFFSET*4)+2(A5),A0	4
	JSR	(A3)			4

	ADDQ.L	#4,PNT_SPR
	MOVE.L	PNT_SPR,A0
	CMPI.W	#-1,(OFFSET*NB_SPRITES)-4(A0)
	BNE.S	FIN_SPR
	MOVE.L	#COURB_SPR,PNT_SPR
FIN_SPR	RTS	

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

	*	ROUTINE DE CODE GENERE           *

PRP_COD_GEN	LEA	SPRITES+34,A0
	LEA	BUF_SPR,A1
	LEA	BUF_COD,A5
	LEA	BUFFER,A2
	LEA	CORES_SPR,A6
	BSR	MAK_COD
	LEA	SPRITES+34+32,A0
	LEA	BUF_SPR,A1
	LEA	BUFFER,A2
	BSR	MAK_COD
	LEA	SPRITES+34+64,A0
	LEA	BUF_SPR,A1
	LEA	BUFFER,A2
	BSR	MAK_COD
	LEA	SPRITES+34+96,A0
	LEA	BUF_SPR,A1
	LEA	BUFFER,A2
	BSR	MAK_COD
	LEA	SPRITES+34+128,A0
	LEA	BUF_SPR,A1
	LEA	BUFFER,A2
	BSR	MAK_COD
	RTS
	
MAK_COD	MOVE.W	#59,D6
PRPSPR2	MOVE.W	#7,D7
PRPSPR1	MOVE.L	(A0)+,(A1)+	
	DBRA	D7,PRPSPR1
	CLR.L	(A1)+
	CLR.L	(A1)+
	LEA	128(A0),A0
	DBRA	D6,PRPSPR2
	LEA	BUF_SPR,A0
	LEA	BUF_MSK,A1
	MOVE.W	#59,D6
PRPMSK2	MOVE.W	#3,D7
PRPMSK1	MOVEM.W	(A0),D0-D3
	OR.W	D1,D0
	OR.W	D2,D0
	OR.W	D3,D0
	NOT.W	D0
	MOVE.W	D0,(A1)+
	ADDQ.L	#8,A0
	DBRA	D7,PRPMSK1
	MOVE.W	#-1,(A1)+
	ADDQ.L	#8,A0
	DBRA	D6,PRPMSK2
	LEA	BUFFER,A2

	MOVE.L	A5,(A6)+
	BSR	COD_GEN
	BSR	INCRUSTE
	MOVE.L	A5,(A6)+
	BSR	MAK_EFF

	REPT	7
	BSR	DECAL
	LEA	BUFFER,A2
	MOVE.L	A5,(A6)+
	BSR	COD_GEN
	BSR	INCRUSTE
	MOVE.L	A5,(A6)+
	BSR	MAK_EFF
	ENDR

	RTS

DECAL	MOVE.W	#1,D4
RECOMANC	LEA	BUF_SPR,A0
	MOVE.W	#59,D5
DECALG	MOVE.W	#3,D6
DECAL2	MOVE.W	#4,D7
DECAL1	ROXR	(A0)
	ADDQ.L	#8,A0
	DBRA	D7,DECAL1
	LEA	-38(A0),A0
	DBRA	D6,DECAL2
	LEA	32(A0),A0
	DBRA	D5,DECALG
	DBRA	D4,RECOMANC
	LEA	BUF_SPR,A0
	LEA	BUF_MSK,A1
	MOVE.W	#59,D6
MSK2	MOVE.W	#4,D7
MSK1	MOVEM.W	(A0),D0-D3
	OR.W	D1,D0
	OR.W	D2,D0
	OR.W	D3,D0
	NOT.W	D0
	MOVE.W	D0,(A1)+
	ADDQ.L	#8,A0
	DBRA	D7,MSK1
	DBRA	D6,MSK2
	RTS

COD_GEN	LEA	BUF_SPR,A0
	LEA	BUF_MSK,A1
	MOVEQ	#0,D4
	MOVEQ	#4,D5
	MOVE.W	#59,D6
RECOMSPR2	MOVE.W	#4,D7
RECOMSPR1	BSR	AND1
	BSR	ORI1
	ADDQ.L	#8,A0
	ADDQ.L	#2,A1
	ADDQ.W	#8,D4
	ADDQ.W	#8,D5
	DBRA	D7,RECOMSPR1
	ADDI.W	#120,D4
	ADDI.W	#120,D5
	DBRA	D6,RECOMSPR2
	MOVE.W	#$4E75,(A2)+
	RTS

AND1	TST.W	(A1)
	BEQ	NO_MSK
	CMPI.W	#-1,(A1)
	BEQ	NO_MSK
	TST.L	(A0)
	BEQ	MSKE12
	TST.L	2(A0)
	BEQ	MSKE1
;	TST.W	(A0)
;	BEQ	MSKE1
;	TST.W	2(A0)
;	BEQ	MSKE2
	TST.W	4(A0)
	BEQ	MSKE3
	MOVE.W	#$02A8,(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	D4,(A2)+
	MOVE.W	#$0268,(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	D5,(A2)+
	BRA	NO_MSK
MSKE3	MOVE.W	#$0268,(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	D5,(A2)+
	BRA	NO_MSK
MSKE2	ADDQ.W	#2,D4
	MOVE.W	#$0268,(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	D4,(A2)+
	SUBQ.W	#2,D4
	BRA	NO_MSK
MSKE1	MOVE.W	#$0268,(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	D4,(A2)+
	BRA	NO_MSK
MSKE12	MOVE.W	#$02A8,(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	D4,(A2)+
	BRA	NO_MSK
MSKE23	ADDQ.W	#2,D4
	MOVE.W	#$02A8,(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	D4,(A2)+
	SUBQ.W	#2,D4
	BRA	NO_MSK
MSKE13	MOVE.W	#$0268,(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	D4,(A2)+
	MOVE.W	#$0268,(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	D5,(A2)+
NO_MSK	RTS

ORI1	TST.W	(A1)
	BEQ	AFF_MOVE
	CMPI.W	#-1,(A1)
	BEQ	NO_AFF
	TST.L	(A0)
	BEQ	OR3
	TST.L	2(A0)
	BEQ	OR1
	TST.W	(A0)
	BEQ	OR23
	TST.W	2(A0)
	BEQ	OR13
	TST.W	4(A0)
	BEQ	OR12
	MOVE.W	#$00A8,(A2)+
	MOVE.L	(A0),(A2)+
	MOVE.W	D4,(A2)+
	MOVE.W	#$0068,(A2)+
	MOVE.W	4(A0),(A2)+
	MOVE.W	D5,(A2)+
	BRA	NO_AFF
OR1	MOVE.W	#$0068,(A2)+
	MOVE.W	(A0),(A2)+
	MOVE.W	D4,(A2)+
	BRA	NO_AFF
OR2	ADDQ.W	#2,D4
	MOVE.W	#$0068,(A2)+
	MOVE.W	2(A0),(A2)+
	MOVE.W	D4,(A2)+
	SUBQ.W	#2,D4
	BRA	NO_AFF
OR3	MOVE.W	#$0068,(A2)+
	MOVE.W	4(A0),(A2)+
	MOVE.W	D5,(A2)+
	BRA	NO_AFF
OR12	MOVE.W	#$00A8,(A2)+
	MOVE.L	(A0),(A2)+
	MOVE.W	D4,(A2)+
	BRA	NO_AFF
OR23	ADDQ.W	#2,D4
	MOVE.W	#$00A8,(A2)+
	MOVE.L	2(A0),(A2)+
	MOVE.W	D4,(A2)+
	SUBQ.W	#2,D4
	BRA	NO_AFF
OR13	MOVE.W	#$0068,(A2)+
	MOVE.W	(A0),(A2)+
	MOVE.W	D4,(A2)+
	MOVE.W	#$0068,(A2)+
	MOVE.W	4(A0),(A2)+
	MOVE.W	D5,(A2)+
	BRA	NO_AFF
AFF_MOVE	TST.L	(A0)
	BEQ	MOV3
	TST.L	2(A0)
	BEQ	MOV1
	TST.W	(A0)
	BEQ	MOV23
	TST.W	2(A0)
	BEQ	MOV13
	TST.W	4(A0)
	BEQ	MOV12
	MOVE.W	#$217C,(A2)+
	MOVE.L	(A0),(A2)+
	MOVE.W	D4,(A2)+
	MOVE.W	#$317C,(A2)+
	MOVE.W	4(A0),(A2)+
	MOVE.W	D5,(A2)+
	BRA	NO_AFF
MOV1	MOVE.W	#$317C,(A2)+
	MOVE.W	(A0),(A2)+
	MOVE.W	D4,(A2)+
	ADDQ.W	#2,D4
;	MOVE.W	#$2140,(A2)+
;	MOVE.W	D4,(A2)+
	SUBQ.W	#2,D4
	BRA	NO_AFF
MOV2	ADDQ.W	#2,D4
	MOVE.W	#$317C,(A2)+
	MOVE.W	2(A0),(A2)+
	MOVE.W	D4,(A2)+
	SUBQ.W	#2,D4
;	MOVE.W	#$3140,(A2)+
;	MOVE.W	D4,(A2)+
;	MOVE.W	#$3140,(A2)+
;	MOVE.W	D5,(A2)+
	BRA	NO_AFF
MOV3	MOVE.W	#$317C,(A2)+
	MOVE.W	4(A0),(A2)+
	MOVE.W	D5,(A2)+
;	MOVE.W	#$2140,(A2)+
;	MOVE.W	D4,(A2)+
	BRA	NO_AFF
MOV12	MOVE.W	#$217C,(A2)+
	MOVE.L	(A0),(A2)+
	MOVE.W	D4,(A2)+
;	MOVE.W	#$3140,(A2)+
;	MOVE.W	D5,(A2)+
	BRA	NO_AFF
MOV23	ADDQ.W	#2,D4
	MOVE.W	#$217C,(A2)+
	MOVE.L	2(A0),(A2)+
	MOVE.W	D4,(A2)+
	SUBQ.W	#2,D4
;	MOVE.W	#$3140,(A2)+
;	MOVE.W	D4,(A2)+
	BRA	NO_AFF
MOV13	MOVE.W	#$317C,(A2)+
	MOVE.W	(A0),(A2)+
	MOVE.W	D4,(A2)+
	MOVE.W	#$317C,(A2)+
	MOVE.W	4(A0),(A2)+
	MOVE.W	D5,(A2)+
	ADDQ.W	#2,D4
;	MOVE.W	#$3140,(A2)+
;	MOVE.W	D4,(A2)+
	SUBQ.W	#2,D4
NO_AFF	RTS

MAK_EFF	LEA	BUF_SPR,A0
	LEA	BUF_MSK,A1
	MOVEQ	#0,D4
	MOVEQ	#4,D5
	MOVE.W	#59,D6
RECOMEF2	MOVE.W	#4,D7
RECOMEF1	CMPI.W	#-1,(A1)
	BEQ	NO_EFF
	TST.L	(A0)
	BEQ	EFF3
	TST.L	2(A0)
	BEQ	EFF1
	TST.W	(A0)
	BEQ	EFF23
	TST.W	2(A0)
	BEQ	EFF13
	TST.W	4(A0)
	BEQ	EFF12
	MOVE.W	#$2140,(A5)+
	MOVE.W	D4,(A5)+
	MOVE.W	#$3140,(A5)+
	MOVE.W	D5,(A5)+
	BRA	NO_EFF
EFF1	MOVE.W	#$3140,(A5)+
	MOVE.W	D4,(A5)+
	BRA	NO_EFF
EFF2	ADDQ.W	#2,D4
	MOVE.W	#$3140,(A5)+
	MOVE.W	D4,(A5)+
	SUBQ.W	#2,D4
	BRA	NO_EFF
EFF3	MOVE.W	#$3140,(A5)+
	MOVE.W	D5,(A5)+
	BRA	NO_EFF
EFF12	MOVE.W	#$2140,(A5)+
	MOVE.W	D4,(A5)+
	BRA	NO_EFF
EFF23	ADDQ.W	#2,D4
	MOVE.W	#$2140,(A5)+
	MOVE.W	D4,(A5)+
	SUBQ.W	#2,D4
	BRA	NO_EFF
EFF13	MOVE.W	#$3140,(A5)+
	MOVE.W	D4,(A5)+
	MOVE.W	#$3140,(A5)+
	MOVE.W	D5,(A5)+
NO_EFF	ADDQ.W	#8,D4
	ADDQ.W	#8,D5
	ADDQ.L	#2,A1
	ADDQ.L	#8,A0
	DBRA	D7,RECOMEF1
	ADDI.W	#120,D4
	ADDI.W	#120,D5
	DBRA	D6,RECOMEF2
	MOVE.W	#$4E75,(A5)+
	RTS

PREP_ECR	MOVE.L	#BUFFERS,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
INCRUSTE
	MOVEM.L	D0-A4,SAVER
	CLR.L	LIGNES
	CLR.L	TM
	CLR.L	D0
	
	LEA	BUFFER,A0
	LEA	INSTRUCTIONS,A1
;	LEA	BUF_COD,A5
BOUCLE	MOVE	(A1),D2
	MOVE	2(A1),D1
	MOVE	4(A1),D0
	CMP	(A0),D0
	BEQ.S	THATS_IT
	CMPI	#$4E75,(A0)
	BEQ	THE_END
	CMPI	#$ABCD,D0
	BEQ	PROBLEME_NUMERO_UN
	LEA	6*2(A1),A1
	BRA.S	BOUCLE
THATS_IT
	CMPI.L	#0,TM
	BNE.S	PAS_OVER_GAUCHE
	MOVE.L	#$12834E71,(A5)+
	MOVE	#$1283,(A5)+
	MOVE.L	#5,TM
PAS_OVER_GAUCHE
	CMPI.L	#94,TM
	BLE	PARTIE_UN
	CMPI.L	#111,TM
	BLE.S	PARTIE_DEUX
	CMPI.L	#128,TM
	BLE	PARTIE_TROIS


PARTIE_TROIS
	MOVE.L	TM,D4
	ADD.L	D1,D4
	CMPI.L	#128,D4
	BGT.S	TOUJOURS_TROP_GRAND	SI=ALORS CA RENTRE
	BRA	CA_RENTRE
TOUJOURS_TROP_GRAND
	MOVE.L	#128,D4
	MOVE.L	TM,D5
	SUB.L	D5,D4
	CMPI.L	#0,D4
	BEQ.S	DECIDEMENT_PAS_DE_NOP
	SUBQ.L	#1,D4
NOPZ	MOVE	#$4E71,(A5)+
	DBF	D4,NOPZ
DECIDEMENT_PAS_DE_NOP	
	MOVE.L	#$12834E71,(A5)+
	MOVE	#$1284,(A5)+
	MOVE.L	#5,TM
	ADD.L	#1,LIGNES
	BRA	CA_RENTRE

PARTIE_DEUX
	MOVE.L	TM,D4
	ADD.L	D1,D4
	CMPI.L	#111,D4
	BGT.S	ENCORE_TROP_GRAND	SI=ALORS CA RENTRE
	BRA	CA_RENTRE
ENCORE_TROP_GRAND
	MOVE.L	#111,D4
	MOVE.L	TM,D5
	SUB.L	D5,D4
	CMPI.L	#0,D4
	BEQ.S	TOUJOURS_PAS_DE_NOP
	SUBQ.L	#1,D4
NOPX	MOVE	#$4E71,(A5)+
	DBF	D4,NOPX
TOUJOURS_PAS_DE_NOP	
	MOVE.L	#$12834E71,(A5)+
	MOVE	#$1284,(A5)+
	MOVE.L	#116,TM
	BRA.S	CA_RENTRE
	
PARTIE_UN
	MOVE.L	TM,D4
	ADD.L	D1,D4
	CMPI.L	#94,D4
	BGT.S	TROP_GRAND	SI=ALORS CA RENTRE
	BRA	CA_RENTRE
TROP_GRAND
	MOVE.L	#94,D4
	MOVE.L	TM,D5
	SUB.L	D5,D4
	CMPI.L	#0,D4
	BEQ.S	PAS_DE_NOP
	SUBQ.L	#1,D4
NOPY	MOVE	#$4E71,(A5)+
	DBF	D4,NOPY
PAS_DE_NOP	
	MOVE.L	#$14831484,(A5)+
	MOVE.L	#98,TM
CA_RENTRE	
	SUBQ	#1,D2
ICI	MOVE	(A0)+,(A5)+
	DBF	D2,ICI
	ADD.L	D1,TM
	LEA	INSTRUCTIONS,A1
	BRA	BOUCLE


FINI
	MOVEM.L	SAVER,D0-A4
	RTS



THE_END
	JSR	NOPS_QUI_RESTENT
	
	MOVE.L	#TEXTE2,-(SP)
	MOVE	#$9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
;	MOVE	#$7,-(SP)
;	TRAP	#1
;	ADDQ.L	#2,SP
	BRA.S	FINI

PROBLEME_NUMERO_UN
	MOVE.L	#TEXTE1,-(SP)
	MOVE	#$9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE	#$7,-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	BRA.S	FINI

PROBLEME_NUMBER_TWO
	MOVE.L	#TEXTE3,-(SP)
	MOVE	#$9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE	#$7,-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	BRA	FIN

NOPS_QUI_RESTENT
	CMPI.L	#124,TM
	BGT.S	PROBLEME_NUMBER_TWO

	CMPI.L	#94,TM
	BEQ.S	MILIEU
	CMPI.L	#111,TM
	BEQ.S	OUT_DR
	CMPI.L	#124,TM
	BEQ.S	OUT
	MOVE	#$4E71,(A5)+
	ADD.L	#1,TM
	BRA.S	NOPS_QUI_RESTENT
MILIEU	MOVE.L	#$14841483,(A5)+
	ADD.L	#4,TM
	BRA.S	NOPS_QUI_RESTENT
OUT_DR	MOVE.L	#$12834E71,(A5)+
	MOVE	#$1284,(A5)+
	ADD.L	#5,TM
	BRA.S	NOPS_QUI_RESTENT
OUT	MOVE	#$4E75,(A5)+
	RTS

	
;	CLR.L	-(SP)
;	TRAP	#1

	SECTION	DATA
SPRITES	INCBIN	ELRIC2.PI1
	
	DS.L	2
COURB_SPR	INCBIN	CRB1SPR.CRB
	DS.L	800

MUZAXX	INCBIN	BIZAR.MUS

TEXTE1	DC.B	'INSTRUCTION NON RECONNUE.',0
TEXTE2	DC.B	'CA ROULE.',0
TEXTE3	DC.B	'PRB DS LES NOPS',0
	EVEN
INSTRUCTIONS
 DC	4,6,$217C,$FFFF,$FFFF,$0008	;MOVE.L	#$FFFFFFFF,8(A0)
 DC	3,4,$317C,$FFFF,$0008,$0000	;MOVE	#$FFFF,8(A0)
 DC	3,4,$117C,$00FF,$0008,$0000	;MOVE.B	#$FF,8(A0)
 DC	3,5,$20BC,$FFFF,$FFFF,$0000	;MOVE.L	#$FFFFFFFF,(A0)
 DC	2,3,$30BC,$FFFF,$0000,$0000	;MOVE	#$FFFF,(A0)
 DC	2,3,$10BC,$00FF,$0000,$0000	;MOVE.B	#$FF,(A0)

 DC	4,8,$00A8,$FFFF,$FFFF,$0008     ;ORI.L   #$FFFFFFFF,8(A0)
 DC	3,5,$0068,$FFFF,$0008,$0000           ;ORI     #$FFFF,8(A0)
 DC	3,5,$0028,$00FF,$0008,$0000     ;ORI.B   #$FF,8(A0)
 DC	3,7,$0090,$FFFF,$FFFF,$0000           ;ORI.L   #$FFFFFFFF,(A0)
 DC	2,4,$0050,$FFFF,$0000,$0000                 ;ORI     #$FFFF,(A0)
 DC	2,4,$0010,$00FF,$0000,$0000                 ;ORI.B   #$FF,(A0)

 DC	4,8,$02A8,$FFFF,$FFFF,$0008     ;ANDI.L  #$FFFFFFFF,8(A0)
 DC	3,5,$0268,$FFFF,$0008,$0000           ;ANDI    #$FFFF,8(A0)
 DC	3,5,$0228,$00FF,$0008,$0000           ;ANDI.B  #$FF,8(A0)
 DC	3,7,$0290,$FFFF,$FFFF,$0000           ;ANDI.L  #$FFFFFFFF,(A0)
 DC	2,4,$0250,$FFFF,$0000,$0000                 ;ANDI    #$FFFF,(A0)
 DC	2,4,$0210,$00FF,$0000,$0000                 ;ANDI.B  #$FF,(A0)


 DC	9,9,$ABCD			;FIN


	SECTION	BSS
ANC_PAL	DS.L	8
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
SAUV_SP	DS.L	1
SAUVEC	DS.W	1
MFP	DS.B	8
SAVER	DS.L	8
	DS.B	256
BUFFERS	DS.L	16000
	DS.B	256
SCREEN1	DS.L	1
SCREEN2	DS.L	1
PNT_SPR	DS.L	1
CORES_SPR	DS.L	32*5
BUF_SPR	DS.L	10*60
BUF_MSK	DS.W	5*60
LIGNES	DS.L	1
TM	DS.L	1	
BUFFER	DS.B	10000
	EVEN
BUF_COD	DS.B	232122
	EVEN